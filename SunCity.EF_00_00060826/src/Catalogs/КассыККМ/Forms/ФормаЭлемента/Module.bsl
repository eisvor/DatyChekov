
&НаКлиенте
&Вместо("ОбновитьПараметрыВыбораКассы")
Процедура EF_00_00060826_ОбновитьПараметрыВыбораКассы()
	
	ПараметрыВыбораЭлемента = Новый Массив;
	Если Объект.ТипКассы = ПредопределенноеЗначение("Перечисление.ТипыКассККМ.ФискальныйРегистратор") Тогда
		Значения = Новый Массив;
		Значения.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ФискальныйРегистратор"));
		Значения.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ПринтерЧеков"));
		Значения.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККТ"));
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипОборудования", Значения);
		ПараметрыВыбораЭлемента.Добавить(НовыйПараметр);
		НастройкаШаблонов = Истина;
	Иначе
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипОборудования", ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККМОфлайн"));
		ПараметрыВыбораЭлемента.Добавить(НовыйПараметр);
		НастройкаШаблонов = Ложь;
	КонецЕсли; 
	
	НовыйПараметр = Новый ПараметрВыбора("Отбор.УстройствоИспользуется", Истина);
	ПараметрыВыбораЭлемента.Добавить(НовыйПараметр);
	
	Элементы.ПодключаемоеОборудование.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
	
КонецПроцедуры

&НаСервере
&Вместо("УстановитьВидимостьПодключаемогоОборудования")
Процедура EF_00_00060826_УстановитьВидимостьПодключаемогоОборудования()
	
	ЭтоПодключаемоеОборудование = ЗначениеЗаполнено(Объект.РабочееМесто) И НЕ Объект.ИспользоватьБезПодключенияОборудования;
	
	Элементы.ПодключаемоеОборудование.Видимость = ЭтоПодключаемоеОборудование;
	
	Если ЭтоПодключаемоеОборудование Тогда
		УстановитьДоступностьПодключаемогоОборудования();
	КонецЕсли;
	
	Элементы.ПодключаемоеОборудование.АвтоОтметкаНезаполненного = НЕ Объект.ИспользоватьБезПодключенияОборудования;
	
	Элементы.РаспределениеВыручки.АвтоОтметкаНезаполненного =
		Объект.ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор;
	
	УстановитьВидимостьДопСвойств();
	
КонецПроцедуры

&НаСервере
&Вместо("УстановитьДоступностьПодключаемогоОборудования")
Процедура EF_00_00060826_УстановитьДоступностьПодключаемогоОборудования()
	
	Элементы.ПодключаемоеОборудование.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Объект.РабочееМесто) ИЛИ Объект.ИспользоватьБезПодключенияОборудования;
	
	Элементы.ПодключаемоеОборудование.АвтоОтметкаНезаполненного = НЕ Объект.ИспользоватьБезПодключенияОборудования;
	
	Элементы.РаспределениеВыручки.АвтоОтметкаНезаполненного     = Объект.ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор;
	
КонецПроцедуры
