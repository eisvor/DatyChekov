
&НаКлиенте 
#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
	Перем _ЛокКонтекст Экспорт;
#КонецЕсли

&НаКлиенте
Перем ТекущиеПользователи Экспорт,
	ПользователиMSАдрес Экспорт;

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	
	ЗагрузитьДеревоПользователейНаСервере(ЭтаФорма.ПользователиMSАдрес);
	
	Если ЭтаФорма.ТекущиеПользователи <> "" И ВРег(СокрЛП(ЭтаФорма.ТекущиеПользователи)) <> ВРег(СокрЛП("Никому конкретно")) Тогда		 
		МассивПользователейMS = _ЛокКонтекст.ЛокЯдро_РазложитьСтрокуВМассивПодстрок(ЭтаФорма.ТекущиеПользователи, "|");
		ПроставитьФлагиВыгружать(МассивПользователейMS);	
	КонецЕсли;
	Если ОдиночныйРежимВыделения Тогда
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			Элементы.ПользователиMS.РежимВыделения 	= РежимВыделенияТабличногоПоля.Одиночный;
			Элементы.УстройстваMS.РежимВыделения 	= РежимВыделенияТабличногоПоля.Одиночный;
		#Иначе
			Элементы.ПользователиMS.РежимВыделения 	= РежимВыделенияТаблицы.Одиночный;
			Элементы.УстройстваMS.РежимВыделения 	= РежимВыделенияТаблицы.Одиночный;
		#КонецЕсли
	КонецЕсли;
	
	ДобавитьИзменитьЭлементы();
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗагрузитьДеревоПользователейНаСервере(Адрес)
	
	ДеревоПользователейMS = ПолучитьИзВременногоХранилища(Адрес);	
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если СпособНазначения = "Устройству" Тогда
			УстройстваMS = ДеревоПользователейMS;
		Иначе
			ПользователиMS = ДеревоПользователейMS;
		КонецЕсли;
	#Иначе
		Если СпособНазначения = "Устройству" Тогда
			ЗначениеВРеквизитФормы(ДеревоПользователейMS, "УстройстваMS");
		Иначе
			ЗначениеВРеквизитФормы(ДеревоПользователейMS, "ПользователиMS");
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПроставитьФлагиВыгружать(МассивПользователейMS)
	        	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если СпособНазначения = "Устройству" Тогда
			пПользователиMS = УстройстваMS;
		Иначе
			пПользователиMS = ПользователиMS;
		КонецЕсли;
	#Иначе
		Если СпособНазначения = "Устройству" Тогда
			пПользователиMS = РеквизитФормыВЗначение("УстройстваMS");
		Иначе
			пПользователиMS = РеквизитФормыВЗначение("ПользователиMS");
		КонецЕсли;
	#КонецЕсли
	
	Для каждого Элемент Из МассивПользователейMS Цикл
		НайденнаяСтрока = пПользователиMS.Строки.Найти(Элемент, "ИдГруппыПользователя", Истина);
		Если НайденнаяСтрока <> Неопределено Тогда			
			НайденнаяСтрока.Выгружать = Истина;			
		КонецЕсли;
	КонецЦикла;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если СпособНазначения = "Устройству" Тогда
			УстройстваMS = пПользователиMS;
		Иначе
			ПользователиMS = пПользователиMS;
		КонецЕсли;		
	#Иначе
		Если СпособНазначения = "Устройству" Тогда
			ЗначениеВРеквизитФормы(пПользователиMS, "УстройстваMS");
		Иначе
			ЗначениеВРеквизитФормы(пПользователиMS, "ПользователиMS");
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОчиститьВыделение(Команда)
	
	ИзменитьФлажки(Ложь);	
	РазвернутьДерево(Элементы.ПользователиMS);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	СтрокиИсключения = Новый Массив;
	СтрокиИсключения.Добавить("Выгружать всем");
	СтрокиИсключения.Добавить("Спрашивать при выгрузке");	
	
	ИзменитьФлажки(Истина, Неопределено, СтрокиИсключения);	
	РазвернутьДерево(Элементы.ПользователиMS);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиMSВыгружатьПриИзменении(Элемент)	
	
	ТекДанные = Элементы.ПользователиMS.ТекущиеДанные;		
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 	
	
	Если ТекДанные.Выгружать Тогда		
		
		Если Найти(ВРег(СокрЛП("Выгружать всем/Спрашивать при выгрузке")), ВРег(СокрЛП(ТекДанные.ИдГруппыПользователя))) > 0 Тогда			
			СтрокиИсключения = Новый Массив;
			СтрокиИсключения.Добавить(ТекДанные.ИдГруппыПользователя);		
			СтрокиДляИзменения = Неопределено;
		Иначе
			СтрокиДляИзменения = Новый Массив;
			СтрокиДляИзменения.Добавить("Выгружать всем");
			СтрокиДляИзменения.Добавить("Спрашивать при выгрузке");
			СтрокиИсключения = Неопределено;
		КонецЕсли;			
		
		ИзменитьФлажки(Ложь, СтрокиДляИзменения, СтрокиИсключения);			
		РазвернутьДерево(Элементы.ПользователиMS);		
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстройстваMSВыгружатьПриИзменении(Элемент)
	
	ТекДанные = Элементы.УстройстваMS.ТекущиеДанные;		
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 	
	
	Если ТекДанные.Выгружать Тогда		
		
		Если СпособНазначения = "Устройству" Тогда
		    СтрокиИсключения = Новый Массив;
			СтрокиИсключения.Добавить(ТекДанные.ИдГруппыПользователя);		
		КонецЕсли;
		
		ИзменитьФлажки(Ложь,, СтрокиИсключения);			
		РазвернутьДерево(Элементы.УстройстваMS);		
		
	КонецЕсли;

КонецПроцедуры

#Область ВспомогательныеМеханизмы

&НаКлиенте
Процедура ИзменитьФлажки(ЗначениеФлажка, СтрокиДляИзменения = Неопределено, СтрокиИсключения = Неопределено)		
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если СпособНазначения = "Устройству" Тогда
			УстановитьЗначениеФлажкаВСтрокахДерева(УстройстваMS.Строки, ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);
		Иначе
			УстановитьЗначениеФлажкаВСтрокахДерева(ПользователиMS.Строки, ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);
		КонецЕсли;
	#Иначе
		Если СпособНазначения = "Устройству" Тогда
			УстановитьЗначениеФлажкаВСтрокахДерева(УстройстваMS.ПолучитьЭлементы(), ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);
		Иначе
			УстановитьЗначениеФлажкаВСтрокахДерева(ПользователиMS.ПолучитьЭлементы(), ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);
		КонецЕсли;
	#КонецЕсли				
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеФлажкаВСтрокахДерева(СтрокиДерева, ЗначениеФлажка, СтрокиДляИзменения = Неопределено, СтрокиИсключения = Неопределено)
	
	Для каждого Строка Из СтрокиДерева Цикл
		
		Если СтрокиИсключения <> Неопределено И СтрокиИсключения.Найти(Строка.ИдГруппыПользователя) <> Неопределено Тогда
			Продолжить;
		Иначе
			Если СтрокиДляИзменения = Неопределено Тогда
				Строка.Выгружать = ЗначениеФлажка;				
			Иначе
				Если СтрокиДляИзменения.Найти(Строка.ИдГруппыПользователя) <> Неопределено Тогда	
					Строка.Выгружать = ЗначениеФлажка;
				Иначе
					Продолжить;
				КонецЕсли;	
			КонецЕсли;				
		КонецЕсли;	
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			УстановитьЗначениеФлажкаВСтрокахДерева(Строка.Строки, ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);
		#Иначе
			УстановитьЗначениеФлажкаВСтрокахДерева(Строка.ПолучитьЭлементы(), ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);
		#КонецЕсли
		
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево(ТабличноеПоле)
	
	Строка = ТабличноеПоле.ТекущаяСтрока;
	Если Строка <> Неопределено Тогда
		ТабличноеПоле.Развернуть(Строка, Истина);
	КонецЕсли;
        
КонецПроцедуры

&НаКлиенте
Процедура СформироватьМассивПользователейMS(СтрокиДереваЗначений, МассивПользователейMS)
	
	Если РежимВводаПинКода Тогда
		
		СтрокаВыбораЗаполнена = Ложь;
		Если ТипЗнч(СтрокиДереваЗначений) = Тип("ДанныеФормыЭлементДерева") Тогда
			СтрокаВыбораЗаполнена = СтрокиДереваЗначений.Свойство("ИдГруппыПользователя");
		ИначеЕсли ТипЗнч(СтрокиДереваЗначений) = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
			СтрокаВыбораЗаполнена = СтрокиДереваЗначений.Количество();
		КонецЕсли;
		
		Если СтрокаВыбораЗаполнена Тогда
			
			СтруктураПользователя = Новый Структура; 
			СтруктураПользователя.Вставить("Ид",  СтрокиДереваЗначений.ИдГруппыПользователя);
			СтруктураПользователя.Вставить("Имя", СтрокиДереваЗначений.ГруппаПользователь);			
			
			МассивПользователейMS.Добавить(СтруктураПользователя);			
			
			#Если ТолстыйКлиентОбычноеПриложение Тогда
				СформироватьМассивПользователейMS(СтрокиДереваЗначений.Строки, МассивПользователейMS);
			#Иначе
				СформироватьМассивПользователейMS(СтрокиДереваЗначений.ПолучитьЭлементы(), МассивПользователейMS);
			#КонецЕсли
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Для Каждого Строка из СтрокиДереваЗначений Цикл
		
		Если Строка.Выгружать Тогда
			СтруктураПользователя = Новый Структура; 
			СтруктураПользователя.Вставить("Ид", 	Строка.ИдГруппыПользователя);
			СтруктураПользователя.Вставить("Имя", 	Строка.ГруппаПользователь);			
			МассивПользователейMS.Добавить(СтруктураПользователя);			
		КонецЕсли;
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			СформироватьМассивПользователейMS(Строка.Строки, МассивПользователейMS);
		#Иначе
		 	СформироватьМассивПользователейMS(Строка.ПолучитьЭлементы(), МассивПользователейMS);
		#КонецЕсли
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОсновныеДействияВыбрать(Команда)
	
	Если РежимВводаПинКода Тогда
		ВыборУстройстваMSВРежимеВводаПинКода(Элементы.УстройстваMS.ТекущиеДанные);
		Возврат;
	КонецЕсли;
	
	МассивПользователейMS = Новый Массив;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если СпособНазначения = "Устройству" Тогда
			СформироватьМассивПользователейMS(УстройстваMS.Строки, МассивПользователейMS);
		Иначе
			СформироватьМассивПользователейMS(ПользователиMS.Строки, МассивПользователейMS);
		КонецЕсли;
	
		Если НЕ ОписаниеОповещенияОЗакрытии = Неопределено Тогда	
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, МассивПользователейMS);
		КонецЕсли;
	#Иначе
		Если СпособНазначения = "Устройству" Тогда
			СформироватьМассивПользователейMS(УстройстваMS.ПолучитьЭлементы(), МассивПользователейMS);
		Иначе 
			СформироватьМассивПользователейMS(ПользователиMS.ПолучитьЭлементы(), МассивПользователейMS);
		КонецЕсли;
	#КонецЕсли
	
	Закрыть(МассивПользователейMS);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзменитьЭлементы()

	ЭлементПользователиMS = ЭтаФорма.Элементы.Найти("ПользователиMS");
	Если ЭлементПользователиMS <> Неопределено Тогда
		ЭлементПользователиMS.Видимость = СпособНазначения <> "Устройству";
	КонецЕсли;
	ЭлементУстройстваMS = ЭтаФорма.Элементы.Найти("УстройстваMS");
	Если ЭлементУстройстваMS <> Неопределено Тогда
		ЭлементУстройстваMS.Видимость = СпособНазначения = "Устройству";
	КонецЕсли;		
	ЭлементУстройстваMSВыгружать = ЭтаФорма.Элементы.Найти("УстройстваMSВыгружать");
	Если ЭлементУстройстваMSВыгружать <> Неопределено Тогда
		ЭлементУстройстваMSВыгружать.Видимость = Не РежимВводаПинКода;
	КонецЕсли;		
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстройстваMSВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не РежимВводаПинКода Тогда
		Возврат;
	КонецЕсли;
	
	ВыборУстройстваMSВРежимеВводаПинКода(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборУстройстваMSВРежимеВводаПинКода(ВыбраннаяСтрокаУстройства)
	
	МассивПользователейMS = Новый Массив;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		СформироватьМассивПользователейMS(ВыбраннаяСтрокаУстройства, МассивПользователейMS);
	
		Если НЕ ОписаниеОповещенияОЗакрытии = Неопределено Тогда	
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, МассивПользователейMS);
		КонецЕсли;
	#Иначе
		СформироватьМассивПользователейMS(ВыбраннаяСтрокаУстройства, МассивПользователейMS);
	#КонецЕсли
	
	Закрыть(МассивПользователейMS);
	
КонецПроцедуры

#КонецОбласти

#Если ТолстыйКлиентОбычноеПриложение Тогда
	ПолноеИмяОбъекта = ЭтотОбъект.Метаданные().ПолноеИмя();
	Элементы = ЭлементыФормы;
	Объект = ЭтотОбъект;
#КонецЕсли