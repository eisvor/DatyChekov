&НаКлиенте 
#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
	Перем _ЛокКонтекст Экспорт;
#КонецЕсли

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии()	
	
	ЭтаФорма.ВНовыйДокумент = Истина;	
	ИмяКонфигурации1С = _ЛокКонтекст.ЛокЯдро_ПолучитьИзКэшаНастройкуSMARTS("ПодключеннаяБазаИмяКонфигурации1С");
	ЕстьРеквизитНомер = _ЛокКонтекст.СтруктураМетаданных.Документы1С[ТипДокумента1С].Реквизиты.НайтиПоЗначению("Номер")<> Неопределено; //25.02.20 У документа Упаковочный лист нет реквизита "номер"
	СоздатьТаблицуДокументы1С(ИмяКонфигурации1С);		
	УстановитьДоступностьТаблицыДокументы1С();	
	
	// Восстановление изначальных размеров формы, если она была изменена пользователем (программное нажатие Alt+Shift+R)
	#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
		Попытка
			WSHShell=Новый COMObject("WScript.Shell");;
			WSHShell.SendKeys("%+(R)"); 	
		Исключение
			// Ничего не делаем...
		КонецПопытки;	
	#Иначе                        
		// Для УФ данное свойство не отрабатывает почему-то, так что разделим код
		ЭтаФорма.КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Документы1СВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	ТекущиеДанные = Элементы.Документы1С.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Кнопка)
	
	ОбработатьВыборДокументаДляЗагрузки(Элементы.Документы1С.ТекущиеДанные);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВНовыйДокументПриИзменении(Элемент)
	
	УстановитьДоступностьТаблицыДокументы1С();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьТаблицуДокументы1С(ИмяКонфигурации1С)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		
		МассивТиповДокументов = Новый Массив;
		МассивТиповДокументов.Добавить(Тип("ДокументСписок." + ТипДокумента1С));
		
		Элементы.Документы1С.ТипЗначения = Новый ОписаниеТипов(МассивТиповДокументов);			
		Элементы.Документы1С.СоздатьКолонки();
		
		КолонкаДляУдаления = Элементы.Документы1С.Колонки.Проведен;
		Элементы.Документы1С.Колонки.Удалить(КолонкаДляУдаления);
		
		КолонкаДляУдаления = Элементы.Документы1С.Колонки.Картинка;
		Элементы.Документы1С.Колонки.Удалить(КолонкаДляУдаления);
		
		Элементы.Документы1С.Колонки.Добавить("Название");			
		Элементы.Документы1С.Колонки.Название.УстановитьЭлементУправления(Тип("ПолеВвода"));
		Элементы.Документы1С.Колонки.Название.ЭлементУправления.ТипЗначения = Новый ОписаниеТипов("Строка");
		
		Элементы.Документы1С.Колонки.Добавить("КоличествоСтрок", "Кол-во строк");			
		Элементы.Документы1С.Колонки.КоличествоСтрок.УстановитьЭлементУправления(Тип("ПолеВвода"));
		Элементы.Документы1С.Колонки.КоличествоСтрок.ЭлементУправления.ТипЗначения = Новый ОписаниеТипов("Строка");	
		
		Элементы.Документы1С.Колонки.Дата.Ширина 				= 15;
		Элементы.Документы1С.Колонки.Номер.Ширина 				= 15;
		Элементы.Документы1С.Колонки.Название.Ширина 			= 60;
		Элементы.Документы1С.Колонки.КоличествоСтрок.Ширина 	= 10;	
		
		Элементы.Документы1С.ОбновитьСтроки();
		
	#Иначе
		МассивДобавляемыхРеквизитов = Новый Массив;
		
		МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("Документы1С", Новый ОписаниеТипов("ДинамическийСписок"), , "Документы 1С"));	
		
		ЭтаФорма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);		
		ЕстьТЧ1С = ЗначениеЗаполнено(ИмяТабличнойЧасти1С);
		
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ИмяТипаДокументаДокумент.Дата,"
		+?(ЕстьРеквизитНомер,"ИмяТипаДокументаДокумент.Номер,","")+"
		|	ИмяТипаДокументаДокумент.Ссылка,
		|	"+?(ЕстьТЧ1С,"МАКСИМУМ(ЕСТЬNULL(ИмяТипаДокументаТовары.НомерСтроки, 0))","0")+" КАК КоличествоСтрок
		|ИЗ
		|	Документ.ИмяТипаДокумента КАК ИмяТипаДокументаДокумент"+?(ЕстьТЧ1С,"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИмяТипаДокумента."+ИмяТабличнойЧасти1С+" КАК ИмяТипаДокументаТовары
		|		ПО (ИмяТипаДокументаТовары.Ссылка = ИмяТипаДокументаДокумент.Ссылка)
		|","") +"
		|СГРУППИРОВАТЬ ПО
		|	ИмяТипаДокументаДокумент.Дата,"
		+?(ЕстьРеквизитНомер,"ИмяТипаДокументаДокумент.Номер,","")+"
		|	ИмяТипаДокументаДокумент.Ссылка";		
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяТипаДокумента", ТипДокумента1С);			
		
		ЭтаФорма.Документы1С.ТекстЗапроса = ТекстЗапроса;
		
		ЭтаФорма.Документы1С.ОсновнаяТаблица = "Документ." + ТипДокумента1С;
		ЭтаФорма.Документы1С.ДинамическоеСчитываниеДанных = Истина;	
		
		ЭлементПорядка 						= ЭтаФорма.Документы1С.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ЭлементПорядка.Использование 		= Истина;
		ЭлементПорядка.Поле					= Новый ПолеКомпоновкиДанных("Дата");
		ЭлементПорядка.ТипУпорядочивания	= НаправлениеСортировкиКомпоновкиДанных.Убыв;
		
		Если ЕстьРеквизитНомер Тогда
			ЭлементПорядка 						= ЭтаФорма.Документы1С.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
			ЭлементПорядка.Использование 		= Истина;
			ЭлементПорядка.Поле					= Новый ПолеКомпоновкиДанных("Номер");
			ЭлементПорядка.ТипУпорядочивания	= НаправлениеСортировкиКомпоновкиДанных.Убыв;	
		КонецЕсли;
		
		НоваяТаблица 							= Элементы.Добавить("Документы1С", Тип("ТаблицаФормы"));
		НоваяТаблица.ПутьКДанным 				= "Документы1С";	
		НоваяТаблица.Видимость					= Истина;	
		НоваяТаблица.ПоложениеЗаголовка 		= ПоложениеЗаголовкаЭлементаФормы.Нет;
		НоваяТаблица.ЧередованиеЦветовСтрок 	= Истина;
		НоваяТаблица.Отображение				= ОтображениеТаблицы.Список;
		НоваяТаблица.РежимВыделения				= РежимВыделенияТаблицы.Одиночный;
		НоваяТаблица.НачальноеОтображениеСписка	= НачальноеОтображениеСписка.Начало;
		НоваяТаблица.КоманднаяПанель.Видимость	= Истина;
		НоваяТаблица.ВысотаВСтрокахТаблицы		= 10;
		
		НоваяТаблица.УстановитьДействие("Выбор", "Документы1СВыбор");
		
		НоваяКолонка 				= Элементы.Добавить("Документы1СДата", Тип("ПолеФормы"), Элементы.Документы1С); 			
		НоваяКолонка.ПутьКДанным 	= "Документы1С.Дата";
		НоваяКолонка.Вид 			= ВидПоляФормы.ПолеВвода; 
		НоваяКолонка.Заголовок		= "Дата"; 	
		НоваяКолонка.Ширина			= 9;
		Если ЕстьРеквизитНомер Тогда
			НоваяКолонка 				= Элементы.Добавить("Документы1СНомер", Тип("ПолеФормы"), Элементы.Документы1С); 			
			НоваяКолонка.ПутьКДанным 	= "Документы1С.Номер";
			НоваяКолонка.Вид 			= ВидПоляФормы.ПолеВвода; 
			НоваяКолонка.Заголовок		= "Номер"; 	
			НоваяКолонка.Ширина			= 14;
		КонецЕсли;
		
		НоваяКолонка 				= Элементы.Добавить("Документы1СНазвание", Тип("ПолеФормы"), Элементы.Документы1С); 			
		НоваяКолонка.ПутьКДанным 	= "Документы1С.Ссылка";
		НоваяКолонка.Вид 			= ВидПоляФормы.ПолеВвода; 
		НоваяКолонка.Заголовок		= "Название";	
		НоваяКолонка.Ширина			= 50;
		
		НоваяКолонка 				= Элементы.Добавить("Документы1СКоличествоСтрок", Тип("ПолеФормы"), Элементы.Документы1С); 			
		НоваяКолонка.ПутьКДанным 	= "Документы1С.КоличествоСтрок";
		НоваяКолонка.Вид 			= ВидПоляФормы.ПолеВвода; 
		НоваяКолонка.Заголовок		= "Кол-во строк";		
		НоваяКолонка.Ширина			= 12;
		
		ЭтаФорма.Ширина = 92;
		ЭтаФорма.Высота = 33;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДокументаДляЗагрузки(ВыбраннаяСтрока)
	
	РезультатВыбора = Неопределено;
	
	Если НЕ ЭтаФорма.ВНовыйДокумент Тогда
		Если ВыбраннаяСтрока <> Неопределено Тогда
			РезультатВыбора = ВыбраннаяСтрока.Ссылка;
		Иначе
			Сообщить("Пожалуйста, выберите документ для загрузки данных из ТСД");
			Возврат;
		КонецЕсли;
	Иначе
		РезультатВыбора = Ложь;	
	КонецЕсли;	
	
	// Перенесено в ПриЗакрытии(), иначе отрабатывает не в том порядке
	//#Если ТолстыйКлиентОбычноеПриложение Тогда
	//	Если НЕ ОписаниеОповещенияОЗакрытии = Неопределено Тогда	
	//		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии,РезультатВыбора);
	//	КонецЕсли;
	//#КонецЕсли
	
	ЭтаФорма.Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьТаблицыДокументы1С()
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если НЕ ЭтаФорма.ВНовыйДокумент Тогда
			Элементы.Документы1С.Доступность = Истина;
			Элементы.Документы1С.ЦветТекстаПоля = WebЦвета.Черный;
		Иначе
			Элементы.Документы1С.Доступность = Ложь;
			Элементы.Документы1С.ЦветТекстаПоля = WebЦвета.Серый;
		КонецЕсли;	
	#Иначе

		Если НЕ ЭтаФорма.ВНовыйДокумент Тогда
			Элементы.Документы1С.Доступность = Истина;
			Элементы.Документы1С.ЦветТекста = WebЦвета.Черный;
		Иначе
			Элементы.Документы1С.Доступность = Ложь;
			Элементы.Документы1С.ЦветТекста = WebЦвета.Серый;
		КонецЕсли;	
	#КонецЕсли	
КонецПроцедуры

#КонецОбласти

#Если ТолстыйКлиентОбычноеПриложение Тогда
	
	Процедура Документы1СПриПолученииДанных(Элемент, ОформленияСтрок)
		
		Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
			
			ОформлениеСтроки.Ячейки.Название.Значение = СокрЛП(ОформлениеСтроки.ДанныеСтроки.Ссылка); 		
			
			Если ЗначениеЗаполнено(ИмяТабличнойЧасти1С) Тогда
				КоличествоТоваров = ОформлениеСтроки.ДанныеСтроки.Ссылка[ИмяТабличнойЧасти1С].Количество();
				Если КоличествоТоваров = 0 Тогда
					ОформлениеСтроки.Ячейки.КоличествоСтрок.Значение = "";	
				Иначе
					ОформлениеСтроки.Ячейки.КоличествоСтрок.Значение = СокрЛП(КоличествоТоваров);	
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
		
	КонецПроцедуры

	Процедура ПриЗакрытии()
	
		// Обработка оповещения о закрытии для обычных форм
		//Если НЕ ЭтаФорма.ВНовыйДокумент Тогда
		//	ВыбраннаяСтрока = Элементы.Документы1С.ТекущиеДанные;
		//	Если ВыбраннаяСтрока <> Неопределено Тогда
		//		РезультатВыбора = ВыбраннаяСтрока.Ссылка;
		//	Иначе
		//		Сообщить("Пожалуйста, выберите документ для загрузки данных из ТСД");
		//		Отказ = Истина;
		//	КонецЕсли;
		//Иначе
		//	РезультатВыбора = Ложь;	
		//КонецЕсли;
		
		Если НЕ ОписаниеОповещенияОЗакрытии = Неопределено Тогда	
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, РезультатВыбора);
		КонецЕсли;	
		//\\
	
	КонецПроцедуры

ПолноеИмяОбъекта = ЭтотОбъект.Метаданные().ПолноеИмя();
Элементы = ЭлементыФормы;
Объект = ЭтотОбъект;
#КонецЕсли