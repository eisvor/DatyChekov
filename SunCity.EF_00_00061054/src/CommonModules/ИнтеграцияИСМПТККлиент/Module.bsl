
&Вместо("ЗагрузитьКодыМаркировкиИзФайлаЗавершение")
Процедура EF_00_00061054_ЗагрузитьКодыМаркировкиИзФайлаЗавершение(РезультатВыполнения, СтруктураПараметров)
	
	ПараметрыСканирования = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПараметрыСканирования(ЭтотОбъект);
	ТокенАвторизации = ?(ЗначениеЗаполнено(РезультатВыполнения), РезультатВыполнения, Неопределено);
	
	УникальныйИдентификатор = СтруктураПараметров.ФормаОбъекта.УникальныйИдентификатор;
	МассивОшибок = СтруктураПараметров.МассивОшибок;
	СведенияОВыбранныхДокументах = СтруктураПараметров.СведенияОВыбранныхДокументах;
	Если СтруктураПараметров.Свойство("НеПроверятьКорректностьКМ") Тогда
		НеПроверятьКорректностьКМ = СтруктураПараметров.НеПроверятьКорректностьКМ;
	Иначе
		НеПроверятьКорректностьКМ = Ложь;
	КонецЕсли;
	ПараметрыДобавленияКодов = Новый Структура("УникальныйИдентификатор, МассивОшибок, СведенияОВыбранныхДокументах", УникальныйИдентификатор, МассивОшибок, СведенияОВыбранныхДокументах);
	
	ДлительнаяОперация = РаботаСДокументамиИСМПТКВызовСервера.ВыполнитьДобавлениеКМИзФайлаВФоне(ПараметрыДобавленияКодов, ПараметрыСканирования, ТокенАвторизации, НеПроверятьКорректностьКМ);
	
	ПараметрыОжидания = ОбщегоНазначенияИСМПТККлиентПереопределяемый.ПараметрыОжидания(СтруктураПараметров.ФормаОбъекта);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;
	
	ОбщегоНазначенияИСМПТККлиентПереопределяемый.ОжидатьЗавершение(ДлительнаяОперация, Новый ОписаниеОповещения("ВыполнитьПроцедуруФоновоВыполнено", СтруктураПараметров.ФормаОбъекта), ПараметрыОжидания);
	
КонецПроцедуры

&Вместо("ЗавершениеПолучитьДанныеПоГрупповомуКодуМаркировки_ПродолжитьПослеПроверки")
Процедура EF_00_00061054_ЗавершениеПолучитьДанныеПоГрупповомуКодуМаркировки_ПродолжитьПослеПроверки(Форма, РезультатВыполнения, Параметры, ДанныеПоГрупповомуКоду, КодИдентификации, ВыполнялосьПреобразованиеКМ, ФорматBase64)
	
	Объект = Форма.Объект;
	СтруктураКода = ИнтеграцияИСМПТКВызовСервера.ПолучитьИнформациюПоГрупповомуКоду(ДанныеПоГрупповомуКоду, КодИдентификации, Параметры.ВидУпаковки, ФорматBase64);
	
	//Заменяем данные в ТЧ по результату разбора данных сервера
	Параметры.GTIN 		   = СтруктураКода.GTIN;
	Параметры.GTINВерхнегоУровня = ?(ЗначениеЗаполнено(СтруктураКода.GTINВерхнегоУровня), СтруктураКода.GTINВерхнегоУровня, Параметры.GTINВерхнегоУровня);
	Параметры.EAN 		   = СтруктураКода.EAN;
	Параметры.Количество   = СтруктураКода.Количество;
	Параметры.ВидПродукции = ?(ЗначениеЗаполнено(СтруктураКода.ВидПродукции), СтруктураКода.ВидПродукции, Параметры.ВидПродукции);
	ЭтоГрупповаяУпаковка   = Параметры.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая");
	
	Номенклатура = СтруктураКода.Номенклатура;
	
	Параметры.Вставить("Номенклатура",	   Номенклатура);
	Параметры.Вставить("КодИдентификации", КодИдентификации); 
	Параметры.Вставить("ВыполнялосьПреобразованиеКМ", ВыполнялосьПреобразованиеКМ);
	Форма.ДобавитьКодМаркировкиВДерево(Номенклатура, Параметры);
	
	НоваяСтрока = Объект.Марки.Добавить();
	
	НоваяСтрока.Номенклатура = Параметры.Номенклатура;
	НоваяСтрока.Количество 	 = Параметры.Количество;
	НоваяСтрока.ВидУпаковки	 = Параметры.ВидУпаковки;
	НоваяСтрока.ВидПродукции = Параметры.ВидПродукции;
	НоваяСтрока.GTINВерхнегоУровня	= ?(ЭтоГрупповаяУпаковка, Параметры.GTINВерхнегоУровня, "");
	НоваяСтрока.GTIN = ?(ЗначениеЗаполнено(Параметры.GTINВерхнегоУровня) И Не ЭтоГрупповаяУпаковка, Параметры.GTINВерхнегоУровня, Параметры.GTIN);
	
	Если ВыполнялосьПреобразованиеКМ Тогда
		НоваяСтрока.КодМаркировки    = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодВBase64(КодИдентификации);
		НоваяСтрока.КодИдентификации = КодИдентификации;
		НоваяСтрока.EAN 			 = КодИдентификации;
	Иначе
		НоваяСтрока.КодМаркировки    = Параметры.КодМаркировки;
		НоваяСтрока.КодИдентификации = Параметры.КодИдентификации;
		НоваяСтрока.EAN 			 = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(НоваяСтрока.GTIN);
	КонецЕсли;
	
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() Тогда
		ДополнительныеПараметры = Новый Структура("КодИдентификации", НоваяСтрока.КодИдентификации);
		Форма.ТокенАвторизацииВрем = Неопределено;
		//отложено: вывод сообщения с результатом запроса статуса
		ЗавершениеПроверитьСостояниеКМПриДобавленииНаСервере = Новый ОписаниеОповещения("ЗавершениеПроверитьСостояниеКМПриДобавленииНаСервере", Форма, ДополнительныеПараметры);
		
		СтруктурныеЕдиницы = Новый Соответствие;
		СтруктурныеЕдиницы.Вставить(Объект.Организация, "");
		//предварительный запрос токена
		ПолучитьКлючАвторизации(ЗавершениеПроверитьСостояниеКМПриДобавленииНаСервере, СтруктурныеЕдиницы, Истина);
		
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	Форма.СформироватьДеревоКодовМаркировки();

КонецПроцедуры
