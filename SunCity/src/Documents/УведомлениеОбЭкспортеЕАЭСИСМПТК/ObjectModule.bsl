#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
			ДокументОснованиеДляЗаполнения = ДанныеЗаполнения.ДокументОснование;
		Иначе
	        ДокументОснованиеДляЗаполнения = Неопределено;
		КонецЕсли;
	Иначе
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ДокументОснованиеДляЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	МассивДокументов = Новый Массив();
	МассивДокументов.Добавить(ДокументОснованиеДляЗаполнения);
	
	ПараметрыЗаполнения = РаботаСДокументамиИСМПТК.ПолучитьПараметрыЗаполненияДокументовИСМПТПоУмолчанию();
	ПараметрыЗаполнения.Вставить("МассивДокументов", МассивДокументов);
		
	ТипДокументаИСМПТ = Документы.УведомлениеОбЭкспортеЕАЭСИСМПТК.ПустаяСсылка();
	МассивДанныхДляЗаполнения = РаботаСДокументамиИСМПТК.ПодготовитьДанныеДляЗаполненияДокументаИСМПТ(ПараметрыЗаполнения, ТипДокументаИСМПТ);
	
	Если ТипЗнч(МассивДанныхДляЗаполнения) = Тип("Массив") И МассивДанныхДляЗаполнения.Количество() > 0 Тогда
		
		Для Каждого СтруктураДокумента Из МассивДанныхДляЗаполнения Цикл
			
			Если МассивДанныхДляЗаполнения.Найти(СтруктураДокумента) > 0 Тогда
				НовыйДокумент = Документы.УведомлениеОбЭкспортеЕАЭСИСМПТК.СоздатьДокумент();
			Иначе
				НовыйДокумент = ЭтотОбъект;
			КонецЕсли; 
		
			//Заполняем шапку документа
			ЗаполнитьЗначенияСвойств(НовыйДокумент, СтруктураДокумента.Реквизиты[0]);
			
			СтруктураПереопределяемыхРеквизитов = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОбЭкспортеИСМПТ(МассивДокументов);
			ЗаполнитьЗначенияСвойств(НовыйДокумент, СтруктураПереопределяемыхРеквизитов);
			
			НовыйДокумент.Состояние = Перечисления.СостоянияДокументовИСМПТК.Сформирован;
			НовыйДокумент.Статус    = Перечисления.СтатусыДокументовИСМПТК.Черновик;
			
			ПерезаполнятьИИНБИНПолучателя = Ложь;
			НалоговыйНомер = Неопределено;
			ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьДанныеКонтрагентаИзЕАЭС(ПерезаполнятьИИНБИНПолучателя, НалоговыйНомер, НовыйДокумент.Получатель);
			Если ПерезаполнятьИИНБИНПолучателя Тогда
				НовыйДокумент.ПолучательИдентификационныйНомер = НалоговыйНомер;
			КонецЕсли;
			
			Если СтруктураДокумента.Свойство("Марки") 
				И Не СтруктураДокумента.Марки = Неопределено
				И РаботаСДокументамиИСМПТКПереопределяемый.ВДокументеНужноЗаполнятьМаркиИзОснованияИСМПТ(ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбЭкспортеЕАЭСИСМПТК(), ДокументОснованиеДляЗаполнения) Тогда
				Для Каждого СтрокаТЧ Из СтруктураДокумента.Марки Цикл
					НоваяСтрокаТЧ = НовыйДокумент.Марки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаТЧ);
				КонецЦикла;
			КонецЕсли;
			Если СтруктураДокумента.Свойство("Товары") 
				И Не СтруктураДокумента.Товары = Неопределено Тогда
				Для Каждого СтрокаТЧ Из СтруктураДокумента.Товары Цикл
					НоваяСтрокаТЧ = НовыйДокумент.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаТЧ);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Состояние = РаботаСДокументамиИСМПТК.СостояниеДокументаИСМПТ(ЭтотОбъект);
		
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Автор) Тогда
		ЭтотОбъект.Автор = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь();
	КонецЕсли;
	
	ЭтотОбъект.Контрагент = ЭтотОбъект.Получатель;
		
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Статус) Тогда
		ЭтотОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик"); 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.ДокументОснование = Неопределено; 
	ЭтотОбъект.Контрагент = Неопределено;
	ЭтотОбъект.Автор = Неопределено;
	ЭтотОбъект.Статус = Перечисления.СтатусыДокументовИСМПТК.Черновик;  
	ЭтотОбъект.Состояние = Неопределено;
	ЭтотОбъект.НомерИСМПТ = Неопределено;
	ЭтотОбъект.ДатаВИСМПТ = Неопределено;
	ЭтотОбъект.Идентификатор = Неопределено;
	ЭтотОбъект.Номер = Неопределено;
	ЭтотОбъект.НомерДокументаТрансграна = Неопределено; 
	ЭтотОбъект.ДатаДокументаТрансграна = Неопределено; 
	ЭтотОбъект.ДатаВремяОтгрузки = Неопределено; 
	
	ЭтотОбъект.Марки.Очистить();
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.ДокументыСоответствия.Очистить();
	ЭтотОбъект.Ошибки.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//В общем случае не проверяем заполненность реквизитов при записи
	//проверка необходимых реквизитов проходит при отправке документа
	//в случае необходимости можно указать реквизиты проверки отдельно
	//в процедуре ДокументЭкспортЕАЭС_ОбработкаПроверкиЗаполнения
	ПроверяемыеРеквизиты.Очистить();
	МассивНепроверяемыхРеквизитов = Новый Массив();
	РаботаСДокументамиИСМПТКПереопределяемый.ДокументЭкспортЕАЭС_ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	РаботаСДокументамиИСМПТКПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
		
КонецПроцедуры

#КонецЕсли