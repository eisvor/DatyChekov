#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения.ДокументОснование;
	Иначе
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения;
	КонецЕсли;	
	
	Если ДокументОснованиеДляЗаполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("Массив") 
		И ДокументОснованиеДляЗаполнения.Количество() <> 0 Тогда
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения[0]);
	Иначе
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения);
	КонецЕсли;
	
	//Если создается документ без основания
	Если ТипДокумента = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = РаботаСДокументамиИСМПТК.ПолучитьПараметрыЗаполненияДокументовИСМПТПоУмолчанию();
	ПараметрыЗаполнения.Вставить("МассивДокументов", ДокументОснованиеДляЗаполнения);
	
	ТипДокументаИСЦЭДМ = Документы.АктПриемаПередачиИСЦЭДМ.ПустаяСсылка();
	МассивДанныхДляЗаполнения = РаботаСДокументамиИСМПТК.ПодготовитьДанныеДляЗаполненияДокументаИСЦЭДМ_Общая(ПараметрыЗаполнения, ТипДокументаИСЦЭДМ);
	
	Если ТипЗнч(МассивДанныхДляЗаполнения) = Тип("Массив") И МассивДанныхДляЗаполнения.Количество() > 0 Тогда
		
		Для Каждого СтруктураДокумента Из МассивДанныхДляЗаполнения Цикл
			
			Если МассивДанныхДляЗаполнения.Найти(СтруктураДокумента) > 0 Тогда
				НовыйДокументАПП = Документы.АктПриемаПередачиИСЦЭДМ.СоздатьДокумент();
			Иначе
				НовыйДокументАПП = ЭтотОбъект;
			КонецЕсли; 
		
			ЗаполнитьЗначенияСвойств(НовыйДокументАПП, СтруктураДокумента.Реквизиты[0]);
			//Заполняем шапку документа дополнительными сведениями
			СтруктураПереопределяемыхРеквизитов = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьЗначенияПоУмолчаниюДляСозданияАктаПриемаПередачиИСЦЭДМ(ПараметрыЗаполнения.МассивДокументов);
			ЗаполнитьЗначенияСвойств(НовыйДокументАПП, СтруктураПереопределяемыхРеквизитов);
			Если ОбщегоНазначенияИСМПТКВызовСервера.ПроверитьЗначениеКонстанты("ВестиУчетПоСкладамИСЦЭДМ") Тогда
				СкладЦЭДМПоСоответствию = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(НовыйДокументАПП.Склад, НовыйДокументАПП.Организация);
				Если Не СкладЦЭДМПоСоответствию = Неопределено Тогда
					НовыйДокументАПП.СкладЦЭДМ = СкладЦЭДМПоСоответствию.СкладЦЭДМ;
					НовыйДокументАПП.ПоставщикСкладЦЭДМИдентификационныйНомер = СкладЦЭДМПоСоответствию.ИдентификаторСклада;
				КонецЕсли;
			КонецЕсли;

			НовыйДокументАПП.Статус = Перечисления.СтатусыДокументовИСЦЭДМ.НеОпределен;  		
			
			Для Каждого СтрокаТЧ Из СтруктураДокумента.Товары Цикл
				НоваяСтрокаТЧ = НовыйДокументАПП.Товары.Добавить();
				НоваяСтрокаТЧ.Номенклатура = СтрокаТЧ.Номенклатура;
				НоваяСтрокаТЧ.КоличествоИсточник = СтрокаТЧ.КоличествоИсточник;
				НоваяСтрокаТЧ.ВидПродукции = СтрокаТЧ.ВидПродукции;
			КонецЦикла;

		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Автор) Тогда
		ЭтотОбъект.Автор = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь();
	КонецЕсли;
	
	Если ЭтотОбъект.Направление = Перечисления.НаправленияДокументовИСМПТК.Исходящий Тогда
		ЭтотОбъект.Контрагент = ЭтотОбъект.Получатель;
	Иначе
		ЭтотОбъект.Контрагент = ЭтотОбъект.Поставщик;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Статус)
		И ЭтотОбъект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		ЭтотОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСЦЭДМ.НеОпределен");
		ЭтотОбъект.ЭтапОформления = ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Номер)
		И ЭтотОбъект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		ЭтотОбъект.Номер = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьНомерНаПечать(ЭтотОбъект);
	КонецЕсли;
	
	Ошибки.Очистить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	ЭтотОбъект.ДокументОснование = Неопределено; 
	ЭтотОбъект.Идентификатор = Неопределено;
	ЭтотОбъект.НомерИСЦЭДМ = Неопределено;
	ЭтотОбъект.ДатаВИСЦЭДМ = Неопределено;
	ЭтотОбъект.Контрагент = Неопределено;
	ЭтотОбъект.Статус = Перечисления.СтатусыДокументовИСЦЭДМ.НеОпределен;  	
	ЭтотОбъект.РегНомерНаБумажномНосителе = Неопределено;
	ЭтотОбъект.ДатаВыпискиНаБумажномНосителе = Неопределено;
    ЭтотОбъект.Автор = Неопределено;
	ЭтотОбъект.ЦеныПереданыУспешно = Неопределено;
	ЭтотОбъект.ЭтапОформления = Перечисления.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе;
	ЭтотОбъект.СтатусСверкиТоваров = Неопределено;
	ЭтотОбъект.ИсходноеУведомлениеОРасхожденияхИдентификатор = Неопределено;
	
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.Марки.Очистить();
	ЭтотОбъект.Ошибки.Очистить();
	ЭтотОбъект.Итоги.Очистить();
	ЭтотОбъект.РезультатСверки.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//В общем случае не проверяем заполненность реквизитов при записи
	//проверка необходимых реквизитов проходит при отправке документа
	//в случае необходимости можно указать реквизиты проверки отдельно
	//в процедуре ДокументАПП_ОбработкаПроверкиЗаполнения
	ПроверяемыеРеквизиты.Очистить();
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	РаботаСДокументамиИСМПТКПереопределяемый.ДокументАПП_ОбработкаПроверкиЗаполненияИСЦЭДМ(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	РаботаСДокументамиИСМПТКПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Если ЭтотОбъект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда
		Префикс = "В";
	ИначеЕсли ЭтотОбъект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		Префикс = "И";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли


