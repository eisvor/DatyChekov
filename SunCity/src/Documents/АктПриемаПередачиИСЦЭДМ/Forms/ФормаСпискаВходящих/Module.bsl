////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		Организация = Параметры.Отбор.Организация;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		ЭтаФорма.Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	СобытияФормИСМПТКПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Т.к. видимость команд взаимодействия с сервером зависит от наличия настройки обена для пользователя, 
	//а не только от статуса документа, проверяем доступность команд после заполнения организации.
	ИзменитьКоманднуюПанельДокумента();
	
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСклады", 
				"Видимость", ОбщегоНазначенияИСМПТКВызовСервера.ПроверитьЗначениеКонстанты("ВестиУчетПоСкладамИСЦЭДМ"));
	
	СобытияФормИСМПТККлиентПереопределяемый.ПриОткрытии(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияЗаписьАктПриемаПередачиИСЦЭДМ() Тогда
		
		Элементы.Список.Обновить();
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если Не ТекущиеДанные = Неопределено Тогда
			ИзменитьКоманднуюПанельДокумента(ТекущиеДанные);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)	
	
	УстановитьБыстрыйОтбор("Организация", Организация);
	//Т.к. видимость команд взаимодействия с сервером зависит от наличия настройки обена для пользователя, 
	//а не только от статуса документа, проверяем доступность команд после заполнения организации.
	ИзменитьКоманднуюПанельДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	УстановитьБыстрыйОтбор("Контрагент", Контрагент);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УстановитьБыстрыйОтбор(ВидЭлемента, ЗначениеЭлемента)
	
	ОбщегоНазначенияИСМПТККлиентСервер.ИзменитьЭлементОтбораСписка(Список, ВидЭлемента, ЗначениеЭлемента, ЗначениеЗаполнено(ЗначениеЭлемента));
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКоманднуюПанельДокумента(ТекущиеДанные = Неопределено, МножественныйВыбор = Ложь)
	
	Если МножественныйВыбор Тогда
		//Выводим все команды, доступность их для каждого документа отдельно быдет проверяться уже при выполнении
		МожноОбновить 	 = Истина;
		МожноПодтвердить = Истина;
		МожноОтклонить 	 = Истина;
		УПользователяЕстьНастройкаОбмена = Истина;		
	Иначе
		Если Не ТекущиеДанные = Неопределено Тогда 
				
			МожноОбновить = Не ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСЦЭДМ.НеОпределен");
			МожноПодтвердить = ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСЦЭДМ.ОжидаетПодтверждение");
			МожноОтклонить = ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСЦЭДМ.ОжидаетПодтверждение");
					
			УПользователяЕстьНастройкаОбмена = ЗначениеЗаполнено(ТекущиеДанные.Организация) 
						И ИнтерфейсИСМПТК.ПроверитьНаличиеНастройкиПодключенияУПользователя(ТекущиеДанные.Организация);
		Иначе
			
			МожноОбновить    = Ложь;
			МожноПодтвердить = Ложь;
			МожноОтклонить   = Ложь;
			УПользователяЕстьНастройкаОбмена = Ложь;
			
		КонецЕсли;
	КонецЕсли;
				
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументАППОбновить",
																"Видимость", УПользователяЕстьНастройкаОбмена И МожноОбновить);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументАППОтклонить",
																"Видимость", УПользователяЕстьНастройкаОбмена И МожноОтклонить);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументАПППодтвердить",
																"Видимость", УПользователяЕстьНастройкаОбмена И МожноПодтвердить);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	МножественныйВыбор = Элементы.Список.ВыделенныеСтроки.Количество() > 1 ;
	ИзменитьКоманднуюПанельДокумента(ТекущиеДанные, МножественныйВыбор);
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ОбщегоНазначенияИСМПТККлиентПереопределяемый.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	
	ОбщегоНазначенияИСМПТКПереопределяемый.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти