#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения.ДокументОснование;
	Иначе
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения;
	КонецЕсли;	
	
	Если ДокументОснованиеДляЗаполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("Массив") 
		И ДокументОснованиеДляЗаполнения.Количество() <> 0 Тогда
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения[0]);
	Иначе
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения);
	КонецЕсли;
	
	//Если создается документ без основания
	Если ТипДокумента = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = РаботаСДокументамиИСМПТК.ПолучитьПараметрыЗаполненияДокументовИСМПТПоУмолчанию();
	ПараметрыЗаполнения.Вставить("МассивДокументов", ДокументОснованиеДляЗаполнения);
	
	ТипДокументаИСЦЭДМ = Документы.АктВнутреннегоПеремещенияИСЦЭДМ.ПустаяСсылка();
	МассивДанныхДляЗаполнения = РаботаСДокументамиИСМПТК.ПодготовитьДанныеДляЗаполненияДокументаИСЦЭДМ_Перемещение(ПараметрыЗаполнения, ТипДокументаИСЦЭДМ);
	
	Если ТипЗнч(МассивДанныхДляЗаполнения) = Тип("Массив") И МассивДанныхДляЗаполнения.Количество() > 0 Тогда
		
		Для Каждого СтруктураДокумента Из МассивДанныхДляЗаполнения Цикл
			
			Если МассивДанныхДляЗаполнения.Найти(СтруктураДокумента) > 0 Тогда
				НовыйДокумент = Документы.АктВнутреннегоПеремещенияИСЦЭДМ.СоздатьДокумент();
			Иначе
				НовыйДокумент = ЭтотОбъект;
			КонецЕсли; 
			
			РеквизитыДокумента = СтруктураДокумента.Реквизиты[0];
			ЗаполнитьЗначенияСвойств(НовыйДокумент, РеквизитыДокумента);
			//Заполняем шапку документа дополнительными сведениями
			СтруктураПереопределяемыхРеквизитов = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьЗначенияПоУмолчаниюДляСозданияАктаВнутреннегоПеремещенияИСЦЭДМ(ПараметрыЗаполнения.МассивДокументов);
			ЗаполнитьЗначенияСвойств(НовыйДокумент, СтруктураПереопределяемыхРеквизитов);
			
			СкладОтправительПоОснованию = РеквизитыДокумента.СкладОтправитель;
			СкладПолучательПоОснованию  = РеквизитыДокумента.СкладПолучатель;
			НовыйДокумент.СкладПоставщик  = СкладОтправительПоОснованию;
			НовыйДокумент.СкладПолучатель = СкладПолучательПоОснованию;
			//Пробуем найти соответствие складов ЦЭДМ
			СкладЦЭДМОтправитель = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(СкладОтправительПоОснованию, РеквизитыДокумента.Организация);
			СкладЦЭДМПолучатель  = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(СкладПолучательПоОснованию,  РеквизитыДокумента.Организация);
			Если Не СкладЦЭДМОтправитель = Неопределено Тогда
				НовыйДокумент.Поставщик = СкладЦЭДМОтправитель.СкладЦЭДМ;
				НовыйДокумент.ПоставщикНаименование = СкладЦЭДМОтправитель.Представление;
				НовыйДокумент.ПоставщикИдентификационныйНомер = СкладЦЭДМОтправитель.ИдентификаторСклада;
			Иначе
				//заполняем данными организации
				НовыйДокумент.Поставщик = РеквизитыДокумента.Организация;
				НовыйДокумент.ПоставщикНаименование = РеквизитыДокумента.ОрганизацияНаименование;
				НовыйДокумент.ПоставщикИдентификационныйНомер = РеквизитыДокумента.ОрганизацияИдентификационныйНомер;
			КонецЕсли;
			Если Не СкладЦЭДМПолучатель = Неопределено Тогда
				НовыйДокумент.Получатель = СкладЦЭДМПолучатель.СкладЦЭДМ;
				НовыйДокумент.ПолучательНаименование = СкладЦЭДМПолучатель.Представление;
				НовыйДокумент.ПолучательИдентификационныйНомер = СкладЦЭДМПолучатель.ИдентификаторСклада;
			Иначе
				//заполняем данными организации
				НовыйДокумент.Получатель = РеквизитыДокумента.Организация;
				НовыйДокумент.ПолучательНаименование = РеквизитыДокумента.ОрганизацияНаименование;
				НовыйДокумент.ПолучательИдентификационныйНомер = РеквизитыДокумента.ОрганизацияИдентификационныйНомер;
			КонецЕсли;
			
			НовыйДокумент.Статус = Перечисления.СтатусыДокументовИСЦЭДМ.НеОпределен;  		
			
			Для Каждого СтрокаТЧ Из СтруктураДокумента.Товары Цикл
				НоваяСтрокаТЧ = НовыйДокумент.Товары.Добавить();
				НоваяСтрокаТЧ.Номенклатура = СтрокаТЧ.Номенклатура;
				НоваяСтрокаТЧ.КоличествоИсточник = СтрокаТЧ.КоличествоИсточник;
				НоваяСтрокаТЧ.ЕдиницаИзмерения = СтрокаТЧ.ЕдиницаИзмерения;
			КонецЦикла;

		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Автор) Тогда
		ЭтотОбъект.Автор = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь(); 
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.Статус = Перечисления.СтатусыДокументовИСЦЭДМ.НеОпределен;
	ЭтотОбъект.Идентификатор = Неопределено;
	ЭтотОбъект.НомерИСЦЭДМ = Неопределено;
	ЭтотОбъект.ДатаВИСЦЭДМ = Неопределено;
	ЭтотОбъект.РегНомерНаБумажномНосителе = Неопределено;
	ЭтотОбъект.ДатаВыпискиНаБумажномНосителе = Неопределено;
	ЭтотОбъект.ЭтапОформления = Перечисления.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе;
	ЭтотОбъект.ДокументОснование = Неопределено;
	ЭтотОбъект.Автор = Неопределено;
	ЭтотОбъект.Номер = Неопределено;
	ЭтотОбъект.СтатусСверкиТоваров = Неопределено;
	ЭтотОбъект.ИсходноеУведомлениеОРасхожденияхИдентификатор = Неопределено;

	ЭтотОбъект.Марки.Очистить();
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.Ошибки.Очистить();
	ЭтотОбъект.РезультатСверки.Очистить();
	ЭтотОбъект.Итоги.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Очистить();
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	РаботаСДокументамиИСМПТКПереопределяемый.ДокументАктВнутреннегоПеремещения_ОбработкаПроверкиЗаполненияИСЦЭДМ(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	РаботаСДокументамиИСМПТКПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
		
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли


