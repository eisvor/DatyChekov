#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	ОбщегоНазначенияРТ.ПроверитьИспользованиеОрганизации(,,Организация);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НачалоДня(Дата) <> НачалоДня(ТекущаяДатаСеанса()) Тогда
		ТекстОшибки = НСтр("ru = 'Не допускается коррекция датой, отличной от текущей'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Дата",, Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КассаККМ)  Тогда
		
		СтруктураСостояниеКассовойСмены = РозничныеПродажиСервер.ПолучитьСостояниеКассовойСмены(КассаККМ);
		
		КассоваяСмена = СтруктураСостояниеКассовойСмены.КассоваяСмена;
		
		ТекстОшибки = НСтр("ru = 'Кассовая смена не открыта'");
		Если НЕ РозничныеПродажиСервер.СменаОткрыта(КассоваяСмена, Дата, ТекстОшибки) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "КассаККМ",, Отказ);
		КонецЕсли;
		
		ТипОборудования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КассаККМ, "ПодключаемоеОборудование.ТипОборудования");
		Если ТипОборудования <> Перечисления.ТипыПодключаемогоОборудования.ККТ Тогда
			ТекстОшибки = НСтр("ru = 'Коррекция допускается только при использовании ККТ'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "КассаККМ",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Оплата.Итог("Сумма") < ПозицииЧека.Итог("СуммаСоСкидками") Тогда
		ТекстОшибки = НСтр("ru = 'Сумма оплат меньше итоговой суммы по позициям чека'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Оплата",, Отказ);
	КонецЕсли;
	
	ПродажиСервер.ПроверитьСистемуНалогообложения(ЭтотОбъект, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПробитЧекПоСсылке = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПробитЧек");
	
	Если ПробитЧекПоСсылке = Истина Тогда
		Текст = НСтр("ru = 'Чек коррекции пробит. Операции над этим документом запрещены'");
		ОбщегоНазначения.СообщитьПользователю(Текст,ЭтотОбъект,,,Отказ);
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(Проведен, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Если чек пробит, тогда в любом случае вносим данные в РС СкорректированныеФискальныеОперации
	// независимо от проведения. Данные после пробития не редактируются.
	Если ПробитЧек И НЕ НеприменениеККТ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СкорректированныеФискальныеОперации.СкорректированныеДокумент КАК СкорректированныеДокумент
		|ИЗ
		|	РегистрСведений.СкорректированныеФискальныеОперации КАК СкорректированныеФискальныеОперации
		|ГДЕ
		|	СкорректированныеФискальныеОперации.СкорректированныеДокумент = &СкорректированныеДокумент";
		
		Запрос.УстановитьПараметр("СкорректированныеДокумент", ДокументОснование);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			
			Менеджер = РегистрыСведений.СкорректированныеФискальныеОперации.СоздатьМенеджерЗаписи();
			
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Магазин", ДокументОснование.Метаданные()) Тогда
				Менеджер.Магазин = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Магазин");
			Иначе
				Менеджер.Магазин = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "КассаККМ.Магазин");
			КонецЕсли;
			
			Менеджер.СкорректированныеДокумент 				= ДокументОснование;
			Менеджер.ПредставлениеКорректирующегоДокумента 	= Строка(ЭтотОбъект);
			
			Менеджер.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	ИспользуетсяКомиссионнаяТорговля = ДополнительныеСвойства.ИспользуетсяКомиссионнаяТорговля;
	ИспользуетсяУчетИмпортныхТоваров = ДополнительныеСвойства.ИспользуетсяУчетИмпортныхТоваров;
	
	Документы.ЧекКоррекции.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ПродажиСервер.ОтразитьПродажи(ДополнительныеСвойства, Движения, Отказ);
	ПродажиСервер.ОтразитьПродажиПоДисконтнымКартам(ДополнительныеСвойства, Движения, Отказ);
	ПродажиСервер.ОтразитьПродажиПоПлатежнымКартам(ДополнительныеСвойства, Движения, Отказ);
	
	ДенежныеСредстваСервер.ОтразитьДенежныеСредстваККМ(ДополнительныеСвойства, Движения, Отказ);
	ДенежныеСредстваСервер.ОтразитьРасчетыСКлиентами(ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля(ДополнительныеСвойства);
	
	// Запись наборов записей
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ОбъектКопирования.НеприменениеККТ Тогда
		
		Текст = НСтр("ru = 'Можно копировать документы только с признаком ""Неприменение ККТ""'");
		
		ВызватьИсключение(Текст);
		
	КонецЕсли;
	
	НомерСмены = 0;
	НомерЧека  = 0;
	ПробитЧек = Ложь;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)

	Ответственный = Пользователи.ТекущийПользователь();
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("КассирНаименование", "ФизическоеЛицо.Наименование");
	СтруктураРеквизитов.Вставить("КассирИНН", "ФизическоеЛицо.ИНН");
	РеквизитыКассира = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ответственный, СтруктураРеквизитов);
	
	Кассир 		= РеквизитыКассира.КассирНаименование;
	КассирИНН 	= РеквизитыКассира.КассирИНН;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("КассаККМ") Тогда
			КассаККМ      = ДанныеЗаполнения.КассаККМ;
			Магазин       = КассаККМ.Магазин;
			Организация   = КассаККМ.Владелец;
		Иначе
			ЗаполнитьЗначенияПоУмолчанию();
		КонецЕсли;
		
	Иначе
		
		ЗаполнитьЗначенияПоУмолчанию();
		
	КонецЕсли;
	
	НеприменениеККТ = Истина;
	ОписаниеКоррекции = НСтр("ru = 'Неприменение ККТ'");
	
КонецПроцедуры

Процедура ЗаполнитьЗначенияПоУмолчанию()
	
	Магазин       = ЗначениеНастроекПовтИсп.МагазинПоУмолчанию(Магазин);
	Организация   = ЗначениеНастроекПовтИсп.ОрганизацияПоУмолчанию(Организация, Ответственный);
	КассаККМ      = ЗначениеНастроекПовтИсп.КассаККМПоУмолчанию(Организация, Магазин, КассаККМ, Ответственный);
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля(ДополнительныеСвойства)

	Массив = Новый Массив;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И НЕ ДополнительныеСвойства.Свойство("ЗагрузкаДанныхИзРабочегоМеста") Тогда
		
	КонецЕсли;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
