#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ВозвратТоваровОтПокупателя - документ для инициализации.
//  СтруктураДополнительныеСвойства - Структура - дополнительные свойства выполнения операции.
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	ТекстЗапроса = ТекстЗапросаТаблицаДенежныеСредстваКПоступлениюНаличные()
		+ ТекстЗапросаТаблицаДенежныеСредстваВКассахККМ();
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваКПоступлениюНаличные", МассивРезультатов[0].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваВКассахККМ",           МассивРезультатов[1].Выгрузить());
	
КонецПроцедуры

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Возвращает пыемки ДС отчета о розничных продажах.
//
// Параметры:
//  ОтчетОРозничныхПродажах - ДокументСсылка.ОтчетОРозничныхПродажах - отчет о розничных продажах.
//
// Возвращаемое значение:
//  Массив - массив выемок ДС.
//
Функция ПолучитьВыемкиДСОтчетаОРозничныхПродажах(ОтчетОРозничныхПродажах) Экспорт
	
	ВыемкиДС = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыемкаДенежныхСредствИзКассыККМ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВыемкаДенежныхСредствИзКассыККМ КАК ВыемкаДенежныхСредствИзКассыККМ
	|ГДЕ
	|	ВыемкаДенежныхСредствИзКассыККМ.Проведен
	|	И НЕ ВыемкаДенежныхСредствИзКассыККМ.ПометкаУдаления
	|	И ВыемкаДенежныхСредствИзКассыККМ.ОтчетОРозничныхПродажах = &ОтчетОРозничныхПродажах";
	
	Запрос.УстановитьПараметр("ОтчетОРозничныхПродажах", ОтчетОРозничныхПродажах);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыемкиДС = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли;
	
	Возврат ВыемкиДС;
	
КонецФункции

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Магазин)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКПоступлениюНаличные()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата                            КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка) КАК Касса,
	|	ДанныеДокумента.Ссылка                          КАК ДокументПередачи,
	|	ДанныеДокумента.СуммаДокумента                  КАК Сумма,
	|	ДанныеДокумента.КассаККМ                        КАК КассаОтправитель
	|	
	|ИЗ
	|	Документ.ВыемкаДенежныхСредствИзКассыККМ КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваВКассахККМ()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата                   	КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 	КАК ВидДвижения,
	|	ДанныеДокумента.Организация            	КАК Организация,
	|	ДанныеДокумента.КассаККМ               	КАК КассаККМ,
	|	ДанныеДокумента.ДоговорКонтрагента	КАК ДоговорКонтрагента,
	|	ДанныеДокумента.СуммаДокумента         	КАК Сумма
	|	
	|ИЗ
	|	Документ.ВыемкаДенежныхСредствИзКассыККМ КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
