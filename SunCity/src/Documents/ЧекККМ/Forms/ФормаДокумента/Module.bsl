
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

&НаКлиенте
Перем ЗначениеПеречислениеВидОперацииПродажа;

&НаКлиенте
Перем ЗначениеПеречислениеВидОперацииВозврат;

&НаКлиенте
Перем ЗначениеСправочникиВидыОплатЧекаККМОплатаПодарочнымСертификатом;

&НаКлиенте
Перем ТекущиеДанныеИдентификатор; // Используется для передачи текущей строки в обработчик ожидания.

&НаКлиенте
Перем ПредыдущиеЗначения; // Используется для отслеживания изменения ключевых реквизитов.

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийПодключаемогоОборудования

&НаКлиенте
Процедура ОповещениеПоискаПоШтрихкоду(Штрихкод, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ ПустаяСтрока(Штрихкод) Тогда
		
		//ИнтеграцияИСМПТК_РозничноеВыбытие
		Если УчетМаркировкаИСМПТК 
			И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
			//Проверяем вводимый в форме штрихкод на маркировку
			Если Не ДополнительныеПараметры = Неопределено Тогда
				Если ДополнительныеПараметры.Свойство("ВызовИзФормыРучногоВводаШК") Тогда
					ЭтоКодМаркировки = РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьШтрихкодПоФорматуКодовМаркировки(Штрихкод);
					Если ЭтоКодМаркировки Тогда
						ТекстСообщения = НСтр("ru = 'Код маркировки необходимо сканировать напрямую в форме документа! Форма поиска по штрихкоду (F7) предназначена для ввода данных с клавиатуры!'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
						Возврат;
					Иначе
						ПоискПоШтрихкодуЗавершениеПроверкаМаркировки(Штрихкод);
					КонецЕсли;
				ИначеЕсли ДополнительныеПараметры.Свойство("РегистрацияНовогоШтрихкода") Тогда
					//Был зарегистрирован новый штрихкод номенклатуры, при этом пользователь сканировал не код маркировки.
					//Нужно оповестить о необходимости указания КМ, если зарегситрированный товар с ОУ по маркировке.
					НайденаяНоменклатура = ПолучитьНоменклатуруПоШтрихкоду(Штрихкод);
					Если Не НайденаяНоменклатура = Неопределено
						И ЗначениеЗаполнено(НайденаяНоменклатура.Номенклатура) Тогда
						ВидПродукцииШК = ПолучитьВидПродукцииПоНоменклатуре(НайденаяНоменклатура.Номенклатура);
						УчетТГНоменклатурыВключен = РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьАктивностьОпцииУчетаПоТГноменклатуры(ВидПродукцииШК);
						Если ЗначениеЗаполнено(ВидПродукцииШК) Тогда 
							Если УчетТГНоменклатурыВключен Тогда
								ТекстСообщения = НСтр("ru = 'Внимание: штрихкод зарегистрирован на номенклатуру с особенностями учета по маркировке! Коды маркировки нужно сразу сканировать в форму документа!'");
								ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
							Иначе
								ТекстОшибки = НСтр("ru = 'Внимание! Штрихкод %1 зарегистрирован на номенклатуру %2 с особенностями учета ""%3""! На текущий момент учет этой товарной группы в базе не включен.'")
											+ НСтр("ru = 'Необходимо изменить вид номенклатуры на ""Товар без особенностей учета"" или включить учет маркировки этой товарной группы!'");
								ТекстОшибки = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстОшибки, Штрихкод, НайденаяНоменклатура.Номенклатура, ВидПродукцииШК);
								ОбщегоНазначенияИСМПТКВызовСервера.СоздатьЗаписьЖурналаРегистрации(НСтр("ru = 'Розничная продажа маркируемого товара'"), "Предупреждение",,, ТекстОшибки);
								ТекстСообщения = НСтр("ru = 'Внимание! Обнаружены проблемы в настройках учета маркируемой продукции! Требуется сообщить Администратору. Подробности в Журнале регистрации.'"); 
								ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
                            КонецЕсли;
			        	КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(
					ЭтотОбъект.Элементы.Товары,
					КэшированныеЗначения,
					ПараметрыУказанияСерий);
		СтруктураПараметровКлиента = ПолученШтрихкодИзСШК(Штрихкод);
		ОбработатьДанныеПоКодуКлиент(СтруктураПараметровКлиента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПоискаПоМагнитномуКоду(ТекКод, ДополнительныеПараметры) Экспорт
	
	Если Не ПустаяСтрока(ТекКод) Тогда
		СтруктураПараметровКлиента = ПолученМагнитныйКод(ТекКод);
		ОбработатьДанныеПоКодуКлиент(СтруктураПараметровКлиента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуВыбораДанныхПоиска(Результат, ДополнительныеПараметры) Экспорт
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если Результат <> Неопределено Тогда
		Если УчетМаркировкаИСМПТК И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
			Номенклатура   = Результат.ЗначенияПоиска[0].Номенклатура;
			Характеристика = Результат.ЗначенияПоиска[0].Характеристика;
			Упаковка = ?(ЗначениеЗаполнено(Результат.ЗначенияПоиска[0].Упаковка), Результат.ЗначенияПоиска[0].Упаковка, РозничноеВыбытиеИСМПТККлиентПереопределяемый.ПолучитьПустуюСсылкуУпаковка());
					
		    ВидПродукцииШК = ПолучитьВидПродукцииПоНоменклатуре(Номенклатура);
			НераспределенныеКм = Объект.КодыМаркировкиИСМПТК.НайтиСтроки(Новый Структура("КлючСвязи", -1));
			УчетТГВключен  = РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьВключениеУчетаТГ(ВидПродукцииШК);
			Если ЗначениеЗаполнено(ВидПродукцииШК) 
				И НераспределенныеКм.Количество() = 0 
				И УчетТГВключен Тогда
				//выбранный товар - маркируемый, при этом не был отсканирован код маркировки
				ТекстСообщения = НСтр("ru = 'Внимание: штрихкод зарегистрирован на номенклатуру с особенностями учета по маркировке! Коды маркировки нужно сразу сканировать в форму документа!'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
		ПродолжитьОбработатьДанныеПоКодуКлиент(Результат);
		ОбработатьДанныеПоКодуКлиент(Результат);
	Иначе
		Если УчетМаркировкаИСМПТК И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
			//Нужно проверить наличие в чеке предзаполненных КМ, т.к. если пользователь отказался от уточнения номенклатуры эти коды зависнут.
			СтрокиБезИндексаСвязи = Объект.КодыМаркировкиИСМПТК.НайтиСтроки(Новый Структура("КлючСвязи", -1));
			Если Не СтрокиБезИндексаСвязи.Количество() = 0 Тогда
				Для Каждого Строка Из СтрокиБезИндексаСвязи Цикл
					Объект.КодыМаркировкиИСМПТК.Удалить(Строка);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолученМагнитныйКод(МагнитныйКод) Экспорт 
	
	СтруктураРезультат = ДанныеПоискаПоМагнитномуКодуСервер(МагнитныйКод);
	Возврат ПодключаемоеОборудованиеРТКлиент.ПолученМагнитныйКод(ЭтотОбъект, СтруктураРезультат);
	
КонецФункции

&НаКлиенте
Функция ПолученШтрихкодИзСШК(Штрихкод) Экспорт
	
	СтруктураРезультат = ДанныеПоискаПоШтрихкодуСервер(Штрихкод);
	Возврат ПодключаемоеОборудованиеРТКлиент.ПолученШтрихкодИзСШК(ЭтотОбъект, СтруктураРезультат);
	
КонецФункции

&НаКлиенте
Процедура ПолучитьВесЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		ПриИзмененииТоварыКоличестваУпаковок();
		ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДанныеПоКодуКлиент(СтруктураПараметровКлиента) Экспорт
	
	ОткрытаБлокирующаяФорма = Ложь;
	
	Если СтруктураПараметровКлиента.Свойство("НайденаДисконтнаяКарта") Тогда
		
		Если ЗначениеЗаполнено(Объект.ДисконтнаяКарта) Тогда
			
			Если СтруктураПараметровКлиента.Свойство("НомерТелефона")
				И ПроверитьВозможностьПечатиБумажногоЧека() Тогда
			
				Объект.АдресЭП		= "";
				Объект.Телефон		= СтруктураПараметровКлиента.НомерТелефона;
				
				ОтказКлиентаОтСохраненияТелефонаEmail = Истина;
				
			ИначеЕсли СтруктураПараметровКлиента.Свойство("АдресЭП")
				И ПроверитьВозможностьПечатиБумажногоЧека() Тогда
			
				Объект.АдресЭП		= СтруктураПараметровКлиента.АдресЭП;
				Объект.Телефон		= "";
		
				ОтказКлиентаОтСохраненияТелефонаEmail = Истина;
				
			КонецЕсли;

			ДисконтнаяКартаПриИзменении(Неопределено);
			
		КонецЕсли;
		
	ИначеЕсли СтруктураПараметровКлиента.Свойство("НеобходимостьВводаАкцизнойМарки")
		И СтруктураПараметровКлиента.Свойство("АктивизироватьСтроку") Тогда
		
		ПерезаполнитьПризнакиМаркировки();
		
		//ИнтеграцияИСМПТК_РозничноеВыбытие
		Если УчетМаркировкаИСМПТК 
			И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда 
			ПерезаполнитьПризнакиМаркировкиИСМПТК();
			ЗаполнитьПризнакКодаМаркировкиВТоварах();
		КонецЕсли;
		//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
	Иначе
		
		ПодключаемоеОборудованиеРТКлиент.ОбработатьДанныеПоКоду(ЭтотОбъект, СтруктураПараметровКлиента, ОткрытаБлокирующаяФорма);
		
		//ИнтеграцияИСМПТК_РозничноеВыбытие
		Если УчетМаркировкаИСМПТК 
			И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
			
			ПерезаполнитьПризнакиМаркировкиИСМПТК();
			ЗаполнитьПризнакКодаМаркировкиВТоварах();
			
		КонецЕсли;
		//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
	КонецЕсли;
	
	Если НЕ ОткрытаБлокирующаяФорма Тогда
		ЗавершитьОбработкуДанныхПоКодуКлиент(СтруктураПараметровКлиента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОбработатьДанныеПослеЗагрузкиИзТСД(СтруктураПараметров, ДополнительныеПараметры) Экспорт
	
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	ПерезаполнитьПризнакиМаркировки();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда 
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецФункции

&НаКлиенте
Функция ДобавитьНайденныеПозицииТоваров(СтруктураПараметров, ДополнительныеПараметры = Неопределено) Экспорт
	
	Возврат ДобавитьНайденныеПозицииТоваровСервер(СтруктураПараметров, ДополнительныеПараметры);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьСозданиеИВыборНовойХарактеристики(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Объект.Товары.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторТекущейСтроки);
	ТекущаяСтрока.Характеристика = Результат;
	ТоварыХарактеристикаПриИзменении(Неопределено);

КонецПроцедуры

&НаСервере
Функция Подключаемый_ОбработатьВводШтрихкода(ДанныеШтрихкода, КэшированныеЗначения, ПараметрыСканирования = Неопределено)
	
	РезультатОбработкиШтрихкода = ШтрихкодированиеОбщегоНазначенияИС.ОбработатьВводШтрихкода(ЭтотОбъект, ДанныеШтрихкода, КэшированныеЗначения, ПараметрыСканирования);
	
	ПослеОбработкиШтрихкодов(
		РезультатОбработкиШтрихкода,
		КэшированныеЗначения);
		
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Неопределено;
	РезультатОбработкиШтрихкода.ДобавленныеСтроки = Неопределено;
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

&НаСервере
Функция Подключаемый_ВыполнитьДействие(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, КэшированныеЗначения)
	
	ПараметрыОбработкиВыбора    = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьПараметрыОбработкиВыбора(РезультатВыбора, РезультатОбработкиШтрихкода, КэшированныеЗначения);
	РезультатОбработкиШтрихкода = ШтрихкодированиеИС.ВыполнитьДействие(ЭтотОбъект, Действие, ПараметрыОбработкиВыбора);
	
	ПослеОбработкиШтрихкодов(
		РезультатОбработкиШтрихкода,
		КэшированныеЗначения);
	
	РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Неопределено;
	РезультатОбработкиШтрихкода.ДобавленныеСтроки = Неопределено;
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

&НаСервере
Функция ДанныеПоискаПоМагнитномуКодуСервер(МагнитныйКод)
	
	Возврат ПодключаемоеОборудованиеРТ.ДанныеПоискаПоМагнитномуКоду(МагнитныйКод, ЭтотОбъект);
	
КонецФункции

&НаСервере
Функция ДанныеПоискаПоШтрихкодуСервер(Штрихкод)
	
	Возврат ПодключаемоеОборудованиеРТ.ДанныеПоискаПоШтрихкоду(Штрихкод, ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура ОповещениеОбработатьДанныеПоКоду(СтруктураРезультат, ДополнительныеПараметры) Экспорт
	
	ПродолжитьОбработатьДанныеПоКодуКлиент(СтруктураРезультат);
	
КонецПроцедуры


&НаКлиенте
Процедура ПродолжитьОбработатьДанныеПоКодуКлиент(СтруктураРезультат) Экспорт
	
	ИдентификаторСтроки = Неопределено;
	СтрокаРезультата = СтруктураРезультат.ЗначенияПоиска[0];
	
	Если НЕ СтруктураРезультат.Свойство("ДействиеОтменено") Тогда
		Если СтрокаРезультата.Свойство("Карта") Тогда
			
			Если СтрокаРезультата.ЭтоРегистрационнаяКарта Тогда
				Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
					СтруктураРезультат.Вставить("ТекстПредупреждения", 
						НСтр("ru = 'Изменение продавца при возврате не предусмотрено.'"));
				Иначе
					Если ТипЗнч(СтрокаРезультата.ВладелецКарты) = Тип("СправочникСсылка.Пользователи") Тогда 
						Объект.Продавец = РозничныеПродажиВызовСервера.ФизЛицоВладельцаКарты(СтрокаРезультата.ВладелецКарты);
					Иначе
						Объект.Продавец = СтрокаРезультата.ВладелецКарты;
					КонецЕсли;
					Модифицированность = Истина;
				КонецЕсли;
			Иначе
				Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
					СтруктураРезультат.Вставить("ТекстПредупреждения", 
						НСтр("ru = 'Изменение дисконтной карты при возврате не предусмотрено.'"));
				Иначе
					Объект.ДисконтнаяКарта = СтрокаРезультата.Карта;
					СтруктураРезультат.Вставить("НайденаДисконтнаяКарта", СтрокаРезультата.Карта);
					Модифицированность = Истина;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли СтрокаРезультата.Свойство("СерийныйНомер") Тогда
			
			ИдентификаторСтроки = ПодключаемоеОборудованиеРТКлиент.ДобавитьНайденныеСерийныеНомера(ЭтотОбъект, СтрокаРезультата);
			
		ИначеЕсли СтрокаРезультата.Свойство("Серия") Тогда
			
			ПодключаемоеОборудованиеРТКлиент.ДобавитьНайденныеВТЧСерииНоменклатуры(ЭтотОбъект, СтрокаРезультата);
			
		ИначеЕсли СтрокаРезультата.Свойство("Номенклатура") Тогда
			
			ИдентификаторСтроки = ДобавитьНайденныеПозицииТоваровСервер(СтрокаРезультата);
			
		ИначеЕсли СтрокаРезультата.Свойство("НомерТелефона") 
			И ПроверитьВозможностьПечатиБумажногоЧека() Тогда
			
			Объект.АдресЭП		= "";
			Объект.Телефон		= СтрокаРезультата.НомерТелефона;
			ДанныеSMSИлиEmail	= СтрокаРезультата.ТелефонСтрокой;
			
			ОтказКлиентаОтСохраненияТелефонаEmail = Не ЗначениеЗаполнено(Объект.ДисконтнаяКарта);
			
			ОбновитьФормуSMSИEmail();
			
		ИначеЕсли СтрокаРезультата.Свойство("АдресЭП")
			И ПроверитьВозможностьПечатиБумажногоЧека() Тогда
			
			Объект.Телефон		= "";
			Объект.АдресЭП		= СтрокаРезультата.АдресЭП;
			ДанныеSMSИлиEmail 	= СтрокаРезультата.АдресЭП;
			
			ОтказКлиентаОтСохраненияТелефонаEmail = Не ЗначениеЗаполнено(Объект.ДисконтнаяКарта);
			
			ОбновитьФормуSMSИEmail();
			
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаРезультата.Свойство("ТекстПредупреждения") Тогда
		СтруктураРезультат.Вставить("ТекстПредупреждения", СтрокаРезультата.ТекстПредупреждения);
	КонецЕсли;
	
	Если СтрокаРезультата.Свойство("НеобходимостьВводаАкцизнойМарки") Тогда
		СтруктураРезультат.Вставить("НеобходимостьВводаАкцизнойМарки", СтрокаРезультата.НеобходимостьВводаАкцизнойМарки);
	КонецЕсли;
	
	Если СтрокаРезультата.Свойство("НеобходимостьВводаКодаМаркировки") Тогда
		СтруктураРезультат.Вставить("НеобходимостьВводаКодаМаркировки", СтрокаРезультата.НеобходимостьВводаКодаМаркировки);
	КонецЕсли;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		СтруктураРезультат.Вставить("АктивизироватьСтроку", ИдентификаторСтроки); 
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция ОбработатьДанныеИзТСДКлиент(СтруктураПараметров, ДополнительныеПараметры) Экспорт
	
	Результат = ПодключаемоеОборудованиеРТКлиент.ОбработатьДанныеПоНоменклатуреИзТСДКлиент(ЭтотОбъект, СтруктураПараметров);
	
	Если Не ДополнительныеПараметры = Неопределено Тогда 
		Если ДополнительныеПараметры.Свойство("ОбработатьПослеЗагрузки") И ДополнительныеПараметры.ОбработатьПослеЗагрузки Тогда 
			ОбработатьДанныеПослеЗагрузкиИзТСД(СтруктураПараметров, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	ПодключаемоеОборудованиеРТКлиент.СообщитьТекстПредупреждения(СтруктураПараметров);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ДобавитьНайденныеПозицииТоваровСервер(СтруктураПараметров, ДополнительныеПараметры = Неопределено)
	
	Если Не ДополнительныеПараметры = Неопределено Тогда
		СтруктураПараметров = ДополнительныеПараметры;
		ИдентификаторСтроки = СтруктураПараметров;
	Иначе
		ИдентификаторСтроки = Неопределено;
	КонецЕсли;
	
	ОбработатьБезМаркировки = Ложь;
	Если СтруктураПараметров.Свойство("ОбработатьБезМаркировки")
		И СтруктураПараметров.ОбработатьБезМаркировки Тогда
		ОбработатьБезМаркировки = Истина;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	Если СтруктураПараметров.Свойство("ТекущаяСтрока") Тогда
		ТекущаяСтрока = СтруктураПараметров.ТекущаяСтрока;
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	Иначе
		ТекущаяСтрока = ИнициализацияСтрокиТоваров(ЭтотОбъект, СтруктураПараметров);
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	КонецЕсли;
	
	ДобавленаСтрока = Ложь;
	Если СтруктураПараметров.Свойство("ДобавленаСтрока") Тогда
		ДобавленаСтрока = СтруктураПараметров.ДобавленаСтрока;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьКлючСвязи(Объект.Товары, ТекущаяСтрока, "КлючСвязи"); 
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		//Выполняем поиск предварительно добавленых КМ в ТЧ КодыМаркировкиИСМПТК:
		//если такие строки нашлись, связываем их с текущей строкой товаров по ключу связи
		СтруктураПоискаМарок = Новый Структура("КлючСвязи", -1);
		НайденныеСтрокиМарок = Объект.КодыМаркировкиИСМПТК.НайтиСтроки(СтруктураПоискаМарок);
		Если Не НайденныеСтрокиМарок.Количество() = 0 Тогда
			Для Каждого СтрокаТЧ Из НайденныеСтрокиМарок Цикл
				Если СтрокаТЧ.Номенклатура = ТекущаяСтрока.Номенклатура
					И СтрокаТЧ.Характеристика = ТекущаяСтрока.Характеристика Тогда
					СтрокаТЧ.КлючСвязи = ТекущаяСтрока.КлючСвязи;
				ИначеЕсли Не ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
					//Строка КМ была добавлена до того, как пользователь уточнил данные номенклатуры, поэтому заполняем эту информацию сейчас
					СтрокаТЧ.Номенклатура = ТекущаяСтрока.Номенклатура;
					СтрокаТЧ.Характеристика = ТекущаяСтрока.Характеристика;
					СтрокаТЧ.КлючСвязи = ТекущаяСтрока.КлючСвязи;
					СтрокаТЧ.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(ТекущаяСтрока.Упаковка), ТекущаяСтрока.Упаковка, ТекущаяСтрока.Номенклатура.ЕдиницаИзмерения);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;					
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
	Если ДобавленаСтрока Тогда
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ДополнитьСтруктуруДействиямиПриИзмененииЭлемента(Объект, СтруктураДействий, "Номенклатура");
		
		СтруктураДействий.Вставить("ПроверитьСерийныеНомераПоВладельцу",
			ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПроверкиСерийныхНомеровПоВладельцу(ТекущаяСтрока, Объект.СерийныеНомера));
		Если ИспользоватьАссортимент Тогда
			СтруктураДействий.Вставить("ПроверитьАссортиментСтроки", АссортиментКлиентСервер.ПараметрыПроверкиАссортимента(Объект, Истина));
		КонецЕсли;
		СтруктураДействий.Вставить("ПроверитьЗапретРозничнойПродажи", СкидкиНаценкиСервер.ПараметрыПроверкиЗапретаРозничнойПродажи(Объект));
		
		СтруктураДействий.Вставить("ПроставитьПродавца", Объект.Продавец);
		СтруктураДействий.Вставить("ЗаполнитьСкладПродажи", ОбработкаТабличнойЧастиТоварыСервер.СтруктураЗаполненияСкладаПродажиВСтрокеТЧ(Объект));
		СтруктураДействий.Вставить("ЗаполнитьВидНалога", ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровСтавкиНДС(Объект));
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.Штрихкод)
			И ИнтеграцияИСМПКлиентСерверПовтИсп.УчитыватьМРЦ() Тогда
			СтруктураДействий.Вставить("ЗаполнитьЦенуМРЦ");
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат И ЗначениеЗаполнено(Объект.ЧекККМПродажа) Тогда
		СтруктураДействий.Вставить("ПересчитатьСуммуВозвратСУчетомОснования",
			Новый Структура("Основание", Объект.ЧекККМПродажа));
	Иначе
		СтруктураДействий.Вставить("ПересчитатьСумму");
		
		Если ДобавленаСтрока Тогда
			СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи",
				ОбработкаТабличнойЧастиТоварыКлиентСервер.ПолучитьСтруктуруЗаполненияЦеныПродажиВСтрокеТЧ(Объект, Истина));
			СтруктураДействий.Вставить("ПересчитатьЦенуСУчетомАгентскогоВознаграждения", Новый Структура("Цена", ТекущаяСтрока.Цена));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.ЗаказПокупателя) Тогда
			СтруктураПересчетаСкидок = Новый Структура;
			СтруктураПересчетаСкидок.Вставить("Очищать", Ложь);
			СтруктураПересчетаСкидок.Вставить("ПересчитыватьСуммуСкидки", Истина);
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки"        , СтруктураПересчетаСкидок);
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", СтруктураПересчетаСкидок);
		Иначе
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки"        , Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
		КонецЕсли;
	КонецЕсли;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	
	ДополнитьСтруктуруДействиямиНДС(СтруктураДействий, ВестиУчетМаркируемойПродукцииИСМП);
	ДополнитьСтруктуруДействиямиГосИС(СтруктураДействий, Объект.ОперацияСДенежнымиСредствами, МаркировкаВключена, ОбработатьБезМаркировки);
	
	ИдентификаторСтроки = ПодключаемоеОборудованиеРТ.ЗавершениеОбработкиСтрокиТоваров(ЭтотОбъект, ТекущаяСтрока, СтруктураДействий);
	
	Если НЕ ИдентификаторСтроки = Неопределено Тогда
		ОбработкаТабличнойЧастиТоварыСервер.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока);
	КонецЕсли;
	
	Если Не ДополнительныеПараметры = Неопределено Тогда
		СтруктураПараметров = ИдентификаторСтроки;
		Возврат СтруктураПараметров;
	Иначе
		Возврат ИдентификаторСтроки;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ОповещениеПоискаПоНаименованию(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат <> Неопределено Тогда
		ПродолжитьОбработатьДанныеПоКодуКлиент(Результат);
		ЗавершитьОбработкуДанныхПоКодуКлиент(Результат);
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается обработкой оповещения после открытия формы вывода сообщений
// либо непосредственно при исполнении процедур расчета скидок,
// если сообщений для вывода нет.
&НаКлиенте
Процедура ОповещениеРасчетСкидокКлиент(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыдатьПодаркиНаВыбор
		И ЗначениеЗаполнено(АдресПодарковНаВыбор) Тогда
		
		// &ЗамерПроизводительности
		ОценкаПроизводительностиРТКлиент.НачатьЗамер(
									Истина, "ОбщаяФорма.ПодаркиНаВыбор.Открытие");
		
		ДополнительныеПараметры = Новый Структура;
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВыдатьПодаркиНаВыбор", ЭтотОбъект, ДополнительныеПараметры);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("АдресВоВременномХранилище", АдресПодарковНаВыбор);
		ОткрытьФорму("ОбщаяФорма.ПодаркиНаВыбор",
			ПараметрыФормы,
			ЭтотОбъект,
			,
			,
			,
			ОбработчикОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОповещениеВыдатьПодаркиНаВыбор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыдатьПодаркиНаВыбор(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ВыдатьПодаркиНаВыборНаСервере(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Подарки);
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Скидки (наценки)'"),
		?(Объект.Ссылка.Пустая(), "", ПолучитьНавигационнуюСсылку(Объект.Ссылка)),
		НСтр("ru = 'Скидки (наценки) рассчитаны'"),
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

// Процедура вызывается обработкой оповещения после ответа на вопрос
// о необходимости отмены проведения документа
// перед оплатой бонусами.
&НаКлиенте
Процедура ОповещениеОплатаБонусамиВопросОбОтменеПроведения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения)) Тогда
			ОткрытьФормуОплатыБонусами(ДополнительныеПараметры);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Не удалось отменить проведение документа'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросУдалениеСертификатовОплаты(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ПогашениеПодарочныхСертификатов.Очистить();
		УдалитьСтрокуОплатыПослеВопроса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросПотеряДанныхОтправленныхВБанк(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьСтрокуОплатыПослеВопроса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросУдалениеОплатыКартой(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ТекущаяСтрока = Объект.Оплата.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторТекущейСтроки);
		Если НЕ ТекущаяСтрока = Неопределено Тогда
			// Отмена передачи
			УдалитьОплатуКартой(ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросУспешностиОплатыНаЭТ(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	РезультатОперации = (РезультатВопроса = КодВозвратаДиалога.Да);
	
	Если НЕ РезультатОперации Тогда
		ТекстСообщения = НСтр("ru = 'Операция отменена'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ЗавершитьОплатуПлатежнойКартойЧерезЭквайринговыйТерминал(РезультатОперации, ДополнительныеПараметры.ПараметрДействия);
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросУспешностиОтменыОперацииНаЭТ(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	РезультатОперации = (РезультатВопроса = КодВозвратаДиалога.Да);
	
	Если НЕ РезультатОперации Тогда
		ТекстСообщения = НСтр("ru = 'Операция отменена'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ЗавершитьОтменуОплатыПлатежнойКартойЧерезЭквайринговыйТерминал(РезультатОперации, ДополнительныеПараметры.ПараметрДействия);
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросУспешностиВозвратаОплатыНаЭТ(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	РезультатОперации = (РезультатВопроса = КодВозвратаДиалога.Да);
	
	Если НЕ РезультатОперации Тогда
		ТекстСообщения = НСтр("ru = 'Операция отменена'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ЗавершитьВозвратОплатыПлатежнойКартойЧерезЭквайринговыйТерминал(РезультатОперации, ДополнительныеПараметры.ПараметрДействия);
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросРассчитатьИОткрытьСкидки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЭтотОбъект.Модифицированность = Истина;
		РассчитатьСкидкиНаценкиКлиент(ДополнительныеПараметры);
		ОткрытьИнформациюОСкидкахЗавершитьОбработкуВопроса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьАвтоматическиеСкидкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		УправляемыеСкидки = Результат.УправляемыеСкидки;
		
		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("ПрименятьКОбъекту",                Истина);
		СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет",      Ложь);
		СтруктураПараметры.Вставить("ВосстанавливатьУправляемыеСкидки", Ложь);
		СтруктураПараметры.Вставить("УправляемыеСкидки",                УправляемыеСкидки);
		СтруктураПараметры.Вставить("ТолькоСообщенияПослеОформления",   Ложь);
		СтруктураПараметры.Вставить("ПеренестиСкидкиПодаркиВТовары");
		СтруктураПараметры.Вставить("КонтролироватьОстаткиТоваров", 	КонтролироватьОстаткиТоваров);
		
		Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО.
			РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
		Иначе
			РабочееМесто = ""
		КонецЕсли;
		
		СтруктураПараметры.Вставить("РабочееМесто", РабочееМесто);
		
		Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
		Для Каждого КодСкидки Из Результат.СписокОдноразовыхКодов Цикл
			НоваяСтрока = Объект.ПредъявленныеКодыОднократныхСкидок.Добавить();
			НоваяСтрока.КодСкидки = КодСкидки.Значение;
		КонецЦикла;
		
		РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры);
		
		Если ВывестиСообщения Тогда
			ДополнительныеПараметры = Новый Структура;
			ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеРасчетСкидокКлиент", ЭтотОбъект, ДополнительныеПараметры);
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("АдресВоВременномХранилище", АдресПримененныхСкидокВоВременномХранилище);
			ОткрытьФорму("ОбщаяФорма.ФормаВыводаСообщений", ПараметрыФормы, ЭтотОбъект, , , , ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ОповещениеРасчетСкидокКлиент();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьРучнуюСкидкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		НазначитьРучнуюСкидкуНаСервере(Результат);
		СкидкиНаценкиКлиент.ОповеститьОбОкончанииНазначенияРучныхСкидокНаценок(Результат, "KZT");
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуВыбораНоменклатуры(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	Если НЕ РезультатОткрытияФормы = Неопределено Тогда
		
		ДобавитьНайденныеПозицииТоваров(РезультатОткрытияФормы);
		
	КонецЕсли;
	
	ЗавершитьОбработкуДанныхПоКодуКлиент(ДополнительныеПараметры.СтруктураПараметровКлиента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуВыбораИнформационнойКарты(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	Если НЕ РезультатОткрытияФормы = Неопределено Тогда
		
		Объект.ДисконтнаяКарта = РезультатОткрытияФормы.ИнформационнаяКарта;
		Если ЗначениеЗаполнено(Объект.ДисконтнаяКарта) Тогда
			ДисконтнаяКартаПриИзменении(Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗавершитьОбработкуДанныхПоКодуКлиент(ДополнительныеПараметры.СтруктураПараметровКлиента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуВыбораОплатыЧекаККМ(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	
	НоваяСтрока = Неопределено;
	
	Если НЕ РезультатОткрытияФормы = Неопределено Тогда
		НоваяСтрока = Объект.Оплата.Добавить();
		НоваяСтрока.ВидОплаты = РезультатОткрытияФормы;
		
		Сумма = Объект.Товары.Итог("СуммаВсего") - Объект.Оплата.Итог("Сумма");
		НоваяСтрока.ТипОплаты = ДополнительныеПараметры.ТипОплаты;
		
		Если НоваяСтрока.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ЗачетАванса") Тогда
			Сумма = Мин(Объект.Товары.Итог("СуммаВсего"), СуммаДокументаРасчета);
		Иначе
			Сумма = Объект.Товары.Итог("СуммаВсего") - Объект.Оплата.Итог("Сумма");
		КонецЕсли;
		
		НоваяСтрока.Сумма = ?(Сумма > 0, Сумма, 0);
		
		Элементы.Оплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		Элементы.Оплата.ТекущийЭлемент = Элементы.ОплатаСумма;
		Элементы.Оплата.ИзменитьСтроку();
		МодифицированыСохраняемыеДанные = Истина;
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		Если ДополнительныеПараметры.Свойство("УстановитьЭквайринговыйТерминалПоУмолчанию") Тогда
			УстановитьЭквайринговыйТерминалПоУмолчанию(НоваяСтрока);
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуВыбораПогашенияПодарочныхСертификатов(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(РезультатОткрытияФормы) Тогда
		ПолучитьПогашениеВХранилище(РезультатОткрытияФормы);
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуАвторизацииЭТЗавершение(РезультатВыполнения, ПараметрДействия) Экспорт 
	
	ЭтотОбъект.Доступность = Истина;
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'При выполнении операции возникла ошибка:
			|""%ОписаниеОшибки%"".
			|Оплата по карте не была произведена.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		ПараметрДействия.НомерКарты          = РезультатВыполнения.НомерКарты;
		ПараметрДействия.НомерСсылкиОперации = РезультатВыполнения.СсылочныйНомер;
		ПараметрДействия.НомерЧекаЭТ         = РезультатВыполнения.НомерЧекаЭТ;
		ПараметрДействия.СтрокаСлипЧека      = РезультатВыполнения.ТекстСлипЧека;
		ЗавершитьОплатуПлатежнойКартойЧерезЭквайринговыйТерминал(Истина, ПараметрДействия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуАвторизацииЭТ(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	
	ПараметрДействия = ДополнительныеПараметры.ПараметрДействия;
	
	Если ТипЗнч(РезультатОткрытияФормы) = Тип("Структура") Тогда
		
		ПараметрДействия.СуммаОперации = РезультатОткрытияФормы.Сумма;
		
		Если НЕ ПараметрДействия.ИспользоватьБезПодключенияОборудованияЭТ Тогда
			
			ПараметрыОперации = МенеджерОборудованияКлиент.ПараметрыВыполненияЭквайринговойОперации();
			ПараметрыОперации.ТипТранзакции  = "AuthorizeSales";
			ПараметрыОперации.СуммаОперации  = РезультатОткрытияФормы.Сумма;
			ПараметрыОперации.НомерЧека      = РезультатОткрытияФормы.НомерЧека;
			ПараметрыОперации.СсылочныйНомер = РезультатОткрытияФормы.СсылочныйНомер;
			
			Оповещение = Новый ОписаниеОповещения("ОповещениеОткрытьФормуАвторизацииЭТЗавершение", ЭтотОбъект, ПараметрДействия);
			МенеджерОборудованияКлиент.НачатьВыполнениеОперацииНаЭквайринговомТерминале(Оповещение, УникальныйИдентификатор, 
				ПараметрДействия.ИдентификаторУстройстваЭТ, ПараметрДействия.ИдентификаторУстройстваФР, ПараметрыОперации);
		
		Иначе
			
			ПараметрДействия.НомерКарты          = РезультатОткрытияФормы.НомерКарты;
			ПараметрДействия.НомерСсылкиОперации = РезультатОткрытияФормы.СсылочныйНомер;
			ПараметрДействия.НомерЧекаЭТ         = РезультатОткрытияФормы.НомерЧека;
			
			ТекстВопроса = НСтр("ru = 'Требуется выполнить операцию оплаты на эквайринговом терминале.'") + Символы.ПС;
			ТекстВопроса = ТекстВопроса + НСтр("ru = 'Сумма операции:'") + " "+ ПараметрДействия.СуммаОперации  + Символы.ПС;
			ТекстВопроса = ТекстВопроса + Символы.ПС;
			ТекстВопроса = ТекстВопроса + НСтр("ru = 'Операция оплаты на эквайринговом терминале прошла успешно?'");
			
			ДополнительныеПараметры = Новый Структура; 
			ДополнительныеПараметры.Вставить("ПараметрДействия", ПараметрДействия);
			ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросУспешностиОплатыНаЭТ", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВводаПодарочныхСертификатов(Результат, ДополнительныеПараметры) Экспорт
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Документ.ЧекККМ.Форма.ФормаТабличнойЧастиПогашениеПодарочныхСертификатов.Открытие");
             
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	АдресВХранилище = ПоместитьПогашениеВХранилище();
	ПараметрыФормы.Вставить("АдресВременногоХранилища_ПогашениеПодарочныхСертификатов", АдресВХранилище);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВыбораПогашенияПодарочныхСертификатов", ЭтотОбъект);
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Документ.ЧекККМ.Форма.ФормаТабличнойЧастиПогашениеПодарочныхСертификатов", ПараметрыФормы, ЭтотОбъект, , , , ОбработчикОповещения, Режим);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОплатыПлатежнойКартой(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	НоваяСтрока = Неопределено;
	ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ПлатежнаяКарта");
	ВидОплатыПлатежнаяКарта = ПолучитьВидОплатыПоТипу(ТипОплаты);
	
	
	Если НЕ ВидОплатыПлатежнаяКарта = Неопределено Тогда
		НоваяСтрока = Объект.Оплата.Добавить();
		НоваяСтрока.ВидОплаты = ВидОплатыПлатежнаяКарта;
		
		Сумма = Объект.Товары.Итог("Сумма") - Объект.Оплата.Итог("Сумма");
		НоваяСтрока.Сумма = ?(Сумма > 0, Сумма, 0);
		
		НоваяСтрока.ТипОплаты = ТипОплаты;
		Элементы.Оплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		Элементы.Оплата.ТекущийЭлемент = Элементы.ОплатаСумма;
		Элементы.Оплата.ИзменитьСтроку();
		МодифицированыСохраняемыеДанные = Истина;
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		УстановитьЭквайринговыйТерминалПоУмолчанию(НоваяСтрока);
	Иначе
		
		Отбор = Новый Структура;
		
		Отбор.Вставить("ИмяПоляОтбораЛевоеЗначение", "ТипОплаты");
		Отбор.Вставить("ПравоеЗначение"            , ТипОплаты);
		Отбор.Вставить("Отрицание"                 , Ложь);
		
		ПараметрыФормы =  Новый Структура("СтруктураПараметрыОтбора", Отбор);
		
		Если ДополнительныеПараметры = Неопределено Тогда
			ДополнительныеПараметры = Новый Структура;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("ТипОплаты", ТипОплаты);
		ДополнительныеПараметры.Вставить("УстановитьЭквайринговыйТерминалПоУмолчанию");
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВыбораОплатыЧекаККМ", ЭтотОбъект, ДополнительныеПараметры);
		Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.ВидыОплатЧекаККМ.ФормаВыбора", ПараметрыФормы,,,,, ОбработчикОповещения, Режим); 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОплатыБанковскимКредитом(Результат, ДополнительныеПараметры) Экспорт
	
	
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.БанковскийКредит");
	ВидОплатыБанковскийКредит = ПолучитьВидОплатыПоТипу(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.БанковскийКредит"));
	
	Если НЕ ВидОплатыБанковскийКредит = Неопределено Тогда
		НоваяСтрока = Объект.Оплата.Добавить();
		НоваяСтрока.ВидОплаты = ВидОплатыБанковскийКредит;
		
		Сумма = Объект.Товары.Итог("Сумма") - Объект.Оплата.Итог("Сумма");
		НоваяСтрока.Сумма = ?(Сумма > 0, Сумма, 0);
		
		НоваяСтрока.ТипОплаты = ТипОплаты;
		Элементы.Оплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		Элементы.Оплата.ТекущийЭлемент = Элементы.ОплатаСумма;
		Элементы.Оплата.ИзменитьСтроку();
		МодифицированыСохраняемыеДанные = Истина;
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		
	Иначе
		
		Отбор = Новый Структура;
		
		Отбор.Вставить("ИмяПоляОтбораЛевоеЗначение", "ТипОплаты");
		Отбор.Вставить("ПравоеЗначение"            , ТипОплаты);
		Отбор.Вставить("Отрицание"                 , Ложь);
		
		ПараметрыФормы =  Новый Структура("СтруктураПараметрыОтбора", Отбор);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ТипОплаты", ТипОплаты);
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВыбораОплатыЧекаККМ", ЭтотОбъект, ДополнительныеПараметры);
		Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.ВидыОплатЧекаККМ.ФормаВыбора", ПараметрыФормы,,,,, ОбработчикОповещения, Режим); 
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОплатыЧерезТерминал(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущаяСтрока = ДополнительныеПараметры.ТекущаяСтрока;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
		
		Если НЕ ТекущаяСтрока.ДанныеПереданыВБанк Тогда
			
			Если Объект.ОперацияСДенежнымиСредствами И Объект.Товары.Количество() = 0 Тогда
				РасчетнаяСумма = ТекущаяСтрока.Сумма;
				МаксимальнаяСуммаОплатыБезналичными = РасчетнаяСумма;
				
				Если РасчетнаяСумма = 0 Тогда
					СтрокаСообщения = НСтр("ru = 'Необходимо ввести сумму платежа'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(
						СтрокаСообщения,
						Объект,
						"Оплата[" + (ТекущаяСтрока.НомерСтроки - 1) + "].Сумма");
					Возврат;
				КонецЕсли;
			Иначе
				МаксимальнаяСуммаОплатыБезналичными = СуммаВсего - (СуммаБезналичнойОплаты() + СуммаОплатыПодарочнымиСертификатами()) + ТекущаяСтрока.Сумма;
				
				Если МаксимальнаяСуммаОплатыБезналичными <= 0 Тогда
					
					СтрокаСообщения = НСтр("ru = 'Безналичная оплата не требуется'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(
						СтрокаСообщения,
						Объект,
						"Оплата[" + (ТекущаяСтрока.НомерСтроки - 1) + "].Сумма");
					Возврат;
				КонецЕсли;
				
				
				Если ТекущаяСтрока.Сумма > МаксимальнаяСуммаОплатыБезналичными Тогда
					
					СтрокаСообщения = НСтр("ru = 'Сумма оплаты картой больше возможной суммы безналичной оплаты'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(
						СтрокаСообщения,
						Объект,
						"Оплата[" + (ТекущаяСтрока.НомерСтроки - 1) + "].Сумма");
					Возврат;
				КонецЕсли;
				
				РасчетнаяСумма = ?(ТекущаяСтрока.Сумма = 0 , МаксимальнаяСуммаОплатыБезналичными, ТекущаяСтрока.Сумма);
			КонецЕсли;
			// Передача данных в банк
			ОплатаПлатежнойКартойЧерезЭквайринговыйТерминал(ТекущаяСтрока, РасчетнаяСумма, МаксимальнаяСуммаОплатыБезналичными)
			
		Иначе
			// Вопрос
			ТекстВопроса = НСтр("ru = 'Данные об оплате уже переданы в банк'") + Символы.ПС; 
			ТекстВопроса = ТекстВопроса + НСтр("ru = 'Хотите отменить эту операцию?'"); 
			
			ИдентификаторТекущейСтроки = ТекущаяСтрока.ПолучитьИдентификатор();
			
			ДополнительныеПараметры = Новый Структура; 
			ДополнительныеПараметры.Вставить("ИдентификаторТекущейСтроки", ИдентификаторТекущейСтроки);
			ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросУдалениеОплатыКартой", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	Иначе
		
		Если НЕ ТекущаяСтрока.ДанныеПереданыВБанк Тогда
			
			//Если ЭтоВозвратДеньВДень() И ЭтоПолныйВозврат() Тогда
			//	// Передача данных в банк
			//	УдалитьОплатуКартой(ТекущаяСтрока);
			//Иначе
				ВернутьОплатуКартой(ТекущаяСтрока);
			//КонецЕсли;
		Иначе
			СтрокаСообщения = НСтр("ru = 'Данные об отмене/возврате оплаты в банк уже переданы.'");
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрокаСообщения,
			Объект,
			"Оплата[" + (ТекущаяСтрока.НомерСтроки - 1) + "]"
			);
			
		КонецЕсли;
			
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеДобавитьОплатуНаличными(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	НоваяСтрока = Объект.Оплата.Добавить();
	НоваяСтрока.ВидОплаты = ПредопределенноеЗначение("Справочник.ВидыОплатЧекаККМ.Наличные");
	НоваяСтрока.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.Наличные");
	
	НоваяСтрока.Сумма = 0;
	
	Элементы.Оплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	Элементы.Оплата.ТекущийЭлемент = Элементы.ОплатаСумма;
	
	Элементы.Оплата.ИзменитьСтроку();
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОплатыБонусами(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = Ложь;
	Если ДополнительныеПараметры <> Неопределено
		И ДополнительныеПараметры.Свойство("УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов") Тогда
		УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = ДополнительныеПараметры.УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов;
	КонецЕсли;
	Если Результат = Неопределено Тогда
		Если (НЕ УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов)
			И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
			ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
		КонецЕсли;
	Иначе
		ОбработатьОплатуБонусами(Результат, УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов);
		Если УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов Тогда
			Отказ = Ложь;
			ПроверитьСкидки(Отказ, ДополнительныеПараметры, Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьУказаниеСерий(ЗначениеВозврата, ПараметрыФормыУказанияСерий) Экспорт
	
	Если ЗначениеВозврата <> Неопределено Тогда
		ПараметрыФормыУказанияСерий.Вставить("ЗначениеВозврата", ЗначениеВозврата);
		ТекущаяСтрокаТоваров = Элементы.Товары.ТекущиеДанные;
		Если ТекущаяСтрокаТоваров = Неопределено Тогда
			ПродажаПодарка = Ложь;
		Иначе
			ПродажаПодарка = ТекущаяСтрокаТоваров.ПродажаПодарка;
		КонецЕсли;
		ОбработатьУказаниеСерийСервер(ПараметрыФормыУказанияСерий, ПродажаПодарка);
		Если ПараметрыФормыУказанияСерий.ИмяТЧТовары = "Товары" И НЕ ПродажаПодарка Тогда
			ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
			ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
		КонецЕсли;
			
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект[ПараметрыФормыУказанияСерий.ИмяТЧТовары]);
			
		Если НЕ ПродажаПодарка Тогда
			Элементы[ПараметрыФормыУказанияСерий.ИмяТЧТовары].ЗакончитьРедактированиеСтроки(Ложь);
			Если ПараметрыФормыУказанияСерий.ИмяТЧТовары = "Товары" Тогда
				// Интерфейс ЕГАИС
				//ПриИзмененииЭлементаГосИС();
				
				//ИнтеграцияИСМПТК_РозничноеВыбытие
				ПриИзмененииЭлементаИСМПТК();
				//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодбораПодарочныхСертификатов(НовыеСерийныеНомера, ДополнительныеПараметры) Экспорт
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПослеВводаНомеровПодарочныхСертификатов(НовыеСерийныеНомера, ДополнительныеПараметры);
	
	ПересчетКоличества = ДополнительныеПараметры.ПересчетКоличества;
	
	Если ПересчетКоличества Тогда
		ПриИзмененииТоварыКоличестваУпаковок();
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ВыдатьПодаркиНаВыборНаСервере(Результат, ДополнительныеПараметры) Экспорт
	
	ПереноситьВПродажи = Ложь;
	ТаблицаПодарков = ПолучитьИзВременногоХранилища(Результат);
	Для каждого СтрокаПодарка Из ТаблицаПодарков Цикл
		НоваяСтрока = Объект.Подарки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПодарка);
		УчитыватьПодарокКакПродажу = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПодарка.СкидкаНаценка, "УчитыватьПодарокКакПродажу");
		Если УчитыватьПодарокКакПродажу Тогда
			ПереноситьВПродажи = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ПереноситьВПродажи Тогда
		КоличествоПеренесенных = 0;
		СкидкиНаценкиСерверПереопределяемый.ПеренестиСкидкиПодаркиВТовары(Объект, Объект.ЦенаВключаетНДС, , КоличествоПеренесенных, , Истина);
		Если КоличествоПеренесенных > 0 Тогда
			ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьСуммуВсегоВТаблице(Объект.Товары, Объект.ЦенаВключаетНДС);
			ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Товары);
			ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Подарки);
			ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
			СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
		КонецЕсли;
	Иначе
		ПараметрыУказанияСерийПодарков = Новый Структура(ПараметрыУказанияСерий);
		ПараметрыУказанияСерийПодарков.Вставить("ИмяТЧТовары", "Подарки");
		ПараметрыУказанияСерийПодарков.Вставить("ИмяТЧСерии", "СерииПодарков");
		ПараметрыУказанияСерийПодарков.Вставить("ИмяПоляКоличество", "КоличествоУпаковок");
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерийПодарков);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыбораНоменклатуры(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда
		
		СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
		СтрокаТабличнойЧасти.Номенклатура = РезультатВыбора;
		
		СвернутьСтрокиГИСМ(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
		ТоварыНоменклатураПриИзменении(СтрокаТабличнойЧасти);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытиеФормыВводаEmail(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатОткрытияФормы = Неопределено Тогда
		
		Если ЗначениеЗаполнено(РезультатОткрытияФормы.Email) Тогда
			Объект.АдресЭП = РезультатОткрытияФормы.Email;
			Объект.Телефон = "";
			ТелефонСтрокой = "";
			ДанныеSMSИлиEmail = Объект.АдресЭП;
			ОтказКлиентаОтСохраненияТелефонаEmail = РезультатОткрытияФормы.ОтказКлиентаОтСохраненияEmail;
		Иначе
			Объект.АдресЭП = "";
			Объект.Телефон = "";
			Телефон = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьФормуSMSИEmail();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытиеФормыВводаТелефона(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатОткрытияФормы = Неопределено Тогда
		
		Если ЗначениеЗаполнено(РезультатОткрытияФормы.ВведенноеЧисло) Тогда
			Телефон = 0;
			ПодключаемоеОборудованиеРТКлиент.ПреобразоватьТелефонКПользовательскомуВиду(РезультатОткрытияФормы.ВведенноеЧисло, Телефон, ТелефонСтрокой);
			Объект.Телефон = Формат(РезультатОткрытияФормы.ВведенноеЧисло, "ЧЦ=10; ЧДЦ=; ЧГ=0");
			Объект.АдресЭП = "";
			ДанныеSMSИлиEmail = ТелефонСтрокой;
			ОтказКлиентаОтСохраненияТелефонаEmail = РезультатОткрытияФормы.ОтказКлиентаОтСохраненияТелефона;
		Иначе
			Объект.Телефон = 0;
			ТелефонСтрокой = "";
			Объект.АдресЭП = "";
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьФормуSMSИEmail();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеЗачетАванса(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ЗачетАванса");
	ВидОплатыЗачетАванса = ПолучитьВидОплатыПоТипу(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ЗачетАванса"));
	
	Если НЕ ВидОплатыЗачетАванса = Неопределено Тогда
		НоваяСтрока = Объект.Оплата.Добавить();
		НоваяСтрока.ВидОплаты = ВидОплатыЗачетАванса;
		
		Сумма = Мин(Объект.Товары.Итог("Сумма"), СуммаДокументаРасчета);
		НоваяСтрока.Сумма = ?(Сумма > 0, Сумма, 0);
		
		НоваяСтрока.ТипОплаты = ТипОплаты;
		Элементы.Оплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		Элементы.Оплата.ТекущийЭлемент = Элементы.ОплатаСумма;
		Элементы.Оплата.ИзменитьСтроку();
		МодифицированыСохраняемыеДанные = Истина;
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		
	Иначе
		
		Отбор = Новый Структура;
		
		Отбор.Вставить("ИмяПоляОтбораЛевоеЗначение", "ТипОплаты");
		Отбор.Вставить("ПравоеЗначение"            , ТипОплаты);
		Отбор.Вставить("Отрицание"                 , Ложь);
		
		ПараметрыФормы =  Новый Структура("СтруктураПараметрыОтбора", Отбор);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ТипОплаты", ТипОплаты);
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВыбораОплатыЧекаККМ", ЭтотОбъект, ДополнительныеПараметры);
		Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.ВидыОплатЧекаККМ.ФормаВыбора", ПараметрыФормы,,,,, ОбработчикОповещения, Режим); 
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОплатыСРассрочку(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ВРассрочку");
	ВидОплатыВРассрочку = ПолучитьВидОплатыПоТипу(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ВРассрочку"));
	
	Если НЕ ВидОплатыВРассрочку = Неопределено Тогда
		НоваяСтрока = Объект.Оплата.Добавить();
		НоваяСтрока.ВидОплаты = ВидОплатыВРассрочку;
		
		Сумма = Объект.Товары.Итог("Сумма") - Объект.Оплата.Итог("Сумма");
		НоваяСтрока.Сумма = ?(Сумма > 0, Сумма, 0);
		
		НоваяСтрока.ТипОплаты = ТипОплаты;
		Элементы.Оплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		Элементы.Оплата.ТекущийЭлемент = Элементы.ОплатаСумма;
		Элементы.Оплата.ИзменитьСтроку();
		МодифицированыСохраняемыеДанные = Истина;
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		
	Иначе
		
		Отбор = Новый Структура;
		
		Отбор.Вставить("ИмяПоляОтбораЛевоеЗначение", "ТипОплаты");
		Отбор.Вставить("ПравоеЗначение"            , ТипОплаты);
		Отбор.Вставить("Отрицание"                 , Ложь);
		
		ПараметрыФормы =  Новый Структура("СтруктураПараметрыОтбора", Отбор);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ТипОплаты", ТипОплаты);
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВыбораОплатыЧекаККМ", ЭтотОбъект, ДополнительныеПараметры);
		Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.ВидыОплатЧекаККМ.ФормаВыбора", ПараметрыФормы,,,,, ОбработчикОповещения, Режим); 
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПодтвержденияВозвратаПродукцииИСМПБезМарок(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатОткрытияФормы = Неопределено Тогда
		
		Если НЕ ВРЕГ(РезультатОткрытияФормы) = "ДА" Тогда
			Возврат;
		КонецЕсли;
		
		ЭтоВозвратПродукцииИСМПБезМарки = Истина;
		
		ПробитьЧекНаКлиентеПродолжение();
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка выборка характеристики, после обработки ШК.
//
// Параметры:
//  Результат - Структура - структура с повторяющимися штрихкодами.
//  Параметры - Структура - структура дополнительных параметров.
//
&НаКлиенте
Процедура ОбработатьСериюНоменклатурыПослеВыбораХарактеристики(Результат, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Параметры.Вставить("Характеристика", Результат);
		ПодключаемоеОборудованиеРТКлиент.ЗаполнениеСерииНоменклатуры(ЭтотОбъект, Параметры);
		ПодключаемоеОборудованиеРТКлиент.ЗаполнениеНоменклатурыИХарактеристики(ЭтотОбъект, Параметры);
		ПодключаемоеОборудованиеРТКлиент.ЗапуститьОбработкуДействийСерий(ЭтотОбъект, Параметры);
	КонецЕсли;
	
КонецПроцедуры

//Обработать изменение строки
&НаКлиенте
Процедура ОбработатьИзменениеСтроки(ИдентификаторСтроки, Действие) Экспорт
	
	ТекущаяСтрока = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если Действие = "ИзмененоКоличество" Тогда
		ПриИзмененииТоварыКоличестваУпаковок(ТекущаяСтрока);
	ИначеЕсли Действие = "ИзмененыНоменклатураХарактеристика" Тогда
		ПриИзмененииТоварыНоменклатура(ТекущаяСтрока);
		ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(ИдентификаторСтроки, КэшированныеЗначения);
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьСтатусСерийИПодарочныхСертификатов(ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОплатыПлатежнойСистемой(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстВопроса = НСтр("ru = 'Для проведения оплаты QR-кодом необходимо записать документ. Продолжить?'");
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОплатаПлатежнойСистемойПослеЗаписиДокумента", ЭтотОбъект),
			ТекстВопроса,
			РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ОплатаПлатежнойСистемойФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПлатежнойСистемойПослеЗаписиДокумента(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Проведен Тогда
		УспешнаяЗапись = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
	Иначе
		УспешнаяЗапись = Записать();
	КонецЕсли;
		
	Если УспешнаяЗапись Тогда
		ОплатаПлатежнойСистемойФрагмент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПлатежнойСистемойФрагмент()
	
	Сумма = Объект.Товары.Итог("Сумма");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИтогПоОрганизации", 	Сумма);
	ПараметрыФормы.Вставить("ВидОперации", 			Объект.ВидОперации);
	ПараметрыФормы.Вставить("Организация", 			Объект.Организация);
	ПараметрыФормы.Вставить("Магазин", 				Объект.Магазин);
	ПараметрыФормы.Вставить("КассаККМ", 			Объект.КассаККМ);
	ПараметрыФормы.Вставить("ЧекККМПродажа", 		Объект.ЧекККМПродажа);
	ПараметрыФормы.Вставить("ДисконтнаяКарта", 		Объект.ДисконтнаяКарта);
	ПараметрыФормы.Вставить("Телефон", 				Объект.Телефон);
	ПараметрыФормы.Вставить("Email", 				Объект.АдресЭП);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
		ПараметрыФормы.Вставить("ДокументОплаты", 		Объект.Ссылка);
	Иначе
		ПараметрыФормы.Вставить("ДокументОплаты", 		Объект.ЧекККМПродажа);
		ПараметрыФормы.Вставить("ДокументВозврата", 	Объект.Ссылка);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Товары", 					 Объект.Товары);
	ПараметрыФормы.Вставить("Кассир", 					 Объект.Продавец);
	ПараметрыФормы.Вставить("НомерДокумента", 			 Объект.Номер);
	ПараметрыФормы.Вставить("Оплачивается", 			 Объект.Оплачивается);
	
	ПараметрыФормы.Вставить("ВозможностьЭлектронногоПлатежа", Ложь); // отключить функцию ввода почты / номера телефона в форме оплаты. вводится в чеке
	
	Если ПараметрыФормы.ИтогПоОрганизации <> 0 Тогда
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОплатаПлатежнойСистемойЗавершение", ЭтотОбъект);
		Режим 				 = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
		
		ОткрытьФорму("Обработка.РМКУправляемыйРежим.Форма.ФормаОплатыПлатежнойСистемой",
					ПараметрыФормы, УникальныйИдентификатор,,,, ОбработчикОповещения, Режим); 
					
	КонецЕсли;
				
КонецПроцедуры
				
&НаКлиенте
Процедура ОплатаПлатежнойСистемойЗавершение(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	
	Если РезультатОткрытияФормы = Неопределено 
		ИЛИ РезультатОткрытияФормы = "ОтложитьЧек" Тогда
		
		Если РезультатОткрытияФормы = "ОтложитьЧек" Тогда
			Объект.Оплачивается = Истина;
			Записать();
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ВозвратПоОснованию = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат")
						И ЗначениеЗаполнено(Объект.ЧекККМПродажа);
	
	Если ВозвратПоОснованию Тогда
		Объект.Оплата.Очистить();	
	КонецЕсли;						
	
	ОплатаПлатежнойСистемойСервер(РезультатОткрытияФормы);
	
	Если Объект.Оплата.Количество() Тогда
	
		МодифицированыСохраняемыеДанные = Истина;
		
		ОбновитьФормуSMSИEmail();
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОплатаПлатежнойСистемойСервер(РезультатОткрытияФормы)
	
	ТаблицаОплаты = ПолучитьИзВременногоХранилища(РезультатОткрытияФормы.АдресТаблицыОплата);
	
	НоваяСтрока   = Неопределено;
	ТипОплаты 	  = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ПлатежнаяСистема");
	
	Для Каждого СтрокаОплаты ИЗ ТаблицаОплаты Цикл
		
		НоваяСтрока = Объект.Оплата.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОплаты);
		
		НоваяСтрока.ТипОплаты = ТипОплаты;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ДополнительныеКолонкиНоменклатуры = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("ДополнительнаяКолонкаПриОтображенииНоменклатуры");
	
	ПодключаемоеОборудованиеРТ.НастроитьПодключаемоеОборудование(ЭтотОбъект);
	ПараметрыСобытийПО = Новый Структура;
	ПараметрыСобытийПО.Вставить("РегистрацияНовойКарты", Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа);
	ПараметрыСобытийПО.Вставить("ПоискАкцизнойМарки", Истина);
	
	ИспользоватьАвтоматическиеСкидкиВПродажах = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиВПродажах");
	ИспользоватьАгентскиеПлатежиИРазделениеВыручки = ПолучитьФункциональнуюОпцию("ИспользоватьАгентскиеПлатежиИРазделениеВыручки");
	ИспользоватьХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	ИспользоватьРасчетыСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетыСКлиентами");
	
	Элементы.СкидкиХарактеристика.Видимость = ИспользоватьХарактеристики;
	Элементы.БонусныеБаллыКНачислениюХарактеристика.Видимость = ИспользоватьХарактеристики;
	
	Элементы.ОплатаЗачетАванса.Видимость = ИспользоватьРасчетыСКлиентами;
	Элементы.ОплатаОплатитьВРассрочку.Видимость = ИспользоватьРасчетыСКлиентами;
	
	ИспользоватьБонусныеПрограммыЛояльности = ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности");
	ИспользоватьКомиссионнуюТорговлю = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссионнуюТорговлю");
	Если ИспользоватьАвтоматическиеСкидкиВПродажах Тогда
		АвтоматическийРасчетСкидок =
			УправлениеПользователямиВызовСервера.ПолучитьБулевоЗначениеПраваПользователя(
				ПланыВидовХарактеристик.ПраваПользователей.АвтоматическийРасчетСкидокПриРедактированииДокументаПродажи,
				Ложь);
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда
		Элементы.СкидкиКоманднаяПанель.Доступность = Ложь;
		Элементы.ГруппаТоварыСкидкиНаценки.Доступность = Ложь;
		ЭтоВозврат = Истина;
		ЭтоВозвратДеньВДень = ЭтоВозвратДеньВДень(); // для условного оформления
		ЭтоПолныйВозврат = ЭтоПолныйВозврат();
		Элементы.ГруппаПечать.Видимость = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияРТ.ЗаполнитьШапкуДокумента(Объект,КартинкаСостоянияДокумента,Элементы.КартинкаСостоянияДокумента.Подсказка,РазрешеноПроведение);
	
	ИспользуетсяСистемаВзаимодействия = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ВидОперации) Тогда // Это новый 
			
			Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа;
			
		КонецЕсли;
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда // Это новый 
			
			Объект.АналитикаХозяйственнойОперации = Справочники.АналитикаХозяйственныхОпераций.РеализацияТоваров;
			
		КонецЕсли;

		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипНоменклатурыВТЧСервер(Объект.Товары);
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьИспользоватьСерийныеНомераВТЧСервер(Объект.Товары);
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипыОплатВТЧСервер(Объект);
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьНДСПоСтрочно(Объект.Товары);
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьВидНалогаВТЧСервер(Объект.Товары, 
				ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
		
		ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(Документы.ЧекККМ.ПараметрыУказанияСерий(Объект));
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Товары);
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Подарки);
		
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Подарки);
		СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
		БонусныеБаллыСервер.ОбновитьОтображениеБонусов(Объект);
		
		ОрганизацияИспользовалаЕНВД();
		ПроверитьВозможностьРазныхНалоговыхРежимовСкладов();
		ЗаполнитьСистемуНалогообложения();
		
		Если Элементы.Найти("ТоварыФискальныеДанные") <> Неопределено Тогда
			Элементы.ТоварыФискальныеДанные.Видимость = Ложь;
		КонецЕсли;
		
	Иначе
		ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(Документы.ЧекККМ.ПараметрыУказанияСерий(Объект));
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.УправлениеЭлементамиАгентскогоВознаграждения(ЭтотОбъект, Объект);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	ТолькоПросмотр = ТолькоПросмотр 
	             ИЛИ Объект.СтатусЧекаККМ = Перечисления.СтатусыЧековККМ.Пробитый
	             ИЛИ Объект.СтатусЧекаККМ = Перечисления.СтатусыЧековККМ.Архивный
	             ИЛИ Объект.СтатусЧекаККМ = Перечисления.СтатусыЧековККМ.Аннулированный;
	
	УстановитьДоступностьКнопокПроведенияСервер();
	
	РабочееМесто = Объект.КассаККМ.РабочееМесто;
	
	НастроитьФормуПоДополнительнымПравам(Ложь);
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипыОплатВТЧСервер(Объект);
	
	Элементы.ОплатаЗаполнитьОплатыПоДокументуПродажи.Доступность = ЗначениеЗаполнено(Объект.ЧекККМПродажа);
	
	ИспользоватьАссортимент = АссортиментСервер.ПолучитьФункциональнуюОпциюКонтроляАссортимента(Объект.Магазин);
	
	КонтролироватьОстаткиТоваров = УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(ПланыВидовХарактеристик.ПраваПользователей.КонтролироватьОстатокПриПроведении, Ложь);
	
	ПродажаПоЗаказу = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
	
	УстановитьКомандыВидовОплаты();
	
	УстановитьДоступностьКомандБуфераОбмена();
	
	ЗаполнитьСтавкиНДС();
	
	УстановитьВидимостьСтатусыСерийИПодарочныхСертификатов();
	
	Элементы.ГруппаБонусныеБаллы.Видимость = ИспользоватьБонусныеПрограммыЛояльности;
	Элементы.ТоварыСуммаСкидкиОплатыБонусом.Видимость = ИспользоватьБонусныеПрограммыЛояльности;
	Элементы.ОплатаГруппаБонусы.Видимость = ИспользоватьБонусныеПрограммыЛояльности;
	
	Элементы.ПросмотрНакоплений.Доступность = ЗначениеЗаполнено(Объект.ДисконтнаяКарта);
	
	РеквизитыКассы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.КассаККМ, "РабочееМесто, ПодключаемоеОборудование, ПодключаемоеОборудование.ТипОборудования, ЭлектронныйЧекEmailПередаютсяПрограммой1С, ЭлектронныйЧекSMSПередаютсяПрограммой1С");
	
	ПараметрыКассыККМ = ЗначениеНастроекВызовСервера.ПараметрыКассыККМ(Объект.КассаККМ);
	
	РабочееМесто = РеквизитыКассы.РабочееМесто;
	ТипОборудованияКассыККМ = РеквизитыКассы.ПодключаемоеОборудованиеТипОборудования;
	
	ВозможностьЭлектронногоПлатежа = ПодключаемоеОборудованиеРТ.ВозможностьЭлектронногоПлатежа(РеквизитыКассы.ПодключаемоеОборудование);
	Если ВозможностьЭлектронногоПлатежа Тогда
		ПередачаEmailЧерезПрограмму = РеквизитыКассы.ЭлектронныйЧекEmailПередаютсяПрограммой1С;
		ПередачаSMSЧерезПрограмму  = РеквизитыКассы.ЭлектронныйЧекSMSПередаютсяПрограммой1С;
	КонецЕсли;
	
	Элементы.КомандаEmail.Видимость = ВозможностьЭлектронногоПлатежа;
	Элементы.КомандаSMS.Видимость   = ВозможностьЭлектронногоПлатежа;
	Элементы.ГруппаДанныеSMSИEmail.Видимость   = ВозможностьЭлектронногоПлатежа;
	
	ЭтоLinuxСервер = ОбщегоНазначенияРТВызовСервера.ЭтоLinuxСервер();
	
	Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
		ЗаполнениеФИО();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ОтчетОРозничныхПродажах) Тогда
		Элементы.ОткрытьОтчетОРозничныхПродажах.Видимость = Истина;
		СтруктураОРП = Новый Структура;
		СтруктураОРП.Вставить("Дата");
		СтруктураОРП.Вставить("Номер");
		РеквизитыОРП = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ОтчетОРозничныхПродажах, СтруктураОРП);
		ЗаголовокОРП = НСтр("ru = 'Отчет о продажах №%1 от %2'");
		ЗаголовокОРП = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ЗаголовокОРП,
							РеквизитыОРП.Номер,
							РеквизитыОРП.Дата);
		Элементы.ОткрытьОтчетОРозничныхПродажах.Заголовок = ЗаголовокОРП;
	Иначе
		Элементы.ОткрытьОтчетОРозничныхПродажах.Видимость = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаКомментарий, Объект.Комментарий);
	
	УстановитьВидимостьДоступностьРасчетыСКлиентами();
	
	Элементы.ЧекККМПродажа.Видимость = Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат;
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Объект.Контрагент = Константы.КонтрагентРозничныйПокупатель.Получить();
	КонецЕсли;
	
	Элементы.ФормаСоздатьДокументЧекККМ.Видимость = ПравоДоступа("Добавление", Метаданные.Документы.ЧекККМ);
	
	ЧекВОчереди = ЧекНаходитсяВОчереди();
	Если Не ЧекВОчереди И Не ЗначениеЗаполнено(Объект.ИдентификаторЧекаВОчереди) Тогда
		Идентификатор = Новый УникальныйИдентификатор;
		Объект.ИдентификаторЧекаВОчереди = XMLСтрока(Идентификатор);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ОбщаяКомандаФискальныеОперации", "Видимость", Объект.Проведен И ТолькоПросмотр);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ОчередьЧеков", "Видимость", ЧекВОчереди);	
		
	УстановитьАвтоОтметкуТЧТовары();
	
	// Интерфейс ГосИС - отключенный функционал
	ВестиУчетАлкогольнойПродукции     = Ложь; // ИнтеграцияЕГАИСРТ.НужноРаботатьСЕГАИС(Объект.Дата);
	ВестиУчетМаркируемойПродукцииИСМП = Ложь; //ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемойПродукцииИСМП");
	ВестиУчетПродукцииГИСМ            = Ложь; //ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ")
		//И НЕ ИнтеграцияГИСМ.ПодсистемаНеИспользуется();
	МаркировкаВключена = Ложь; //ВестиУчетАлкогольнойПродукции Или ВестиУчетМаркируемойПродукцииИСМП;
	// Конец Интерфейс ГосИС
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	УчетМаркировкаИСМПТК = Константы.ВестиУчетМаркируемойПродукцииИСМПТК.Получить(); //Если включена хоть одна ТГ, то эта константа - Истина
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		РозничноеВыбытиеИСМПТК.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
	СобытияФормРТ.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	ПерезаполнитьПризнакиМаркировки();
	
	АвтоматическийРасчетСкидок = АвтоматическийРасчетСкидок И НЕ ПродажаПоЗаказу И НЕ ЭтоВозврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПредыдущиеЗначения = Новый Структура(
		"Магазин,
		|Контрагент,
		|Организация,
		|КассаККМ");
	ЗаполнитьЗначенияСвойств(ПредыдущиеЗначения, Объект);
	
	УправлениеЭлементамиЗаказПокупателя();
	
	ЗначениеПеречислениеВидОперацииПродажа = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа");
	
	ЗначениеПеречислениеВидОперацииВозврат = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат");
	
	ЗначениеСправочникиВидыОплатЧекаККМОплатаПодарочнымСертификатом = ПредопределенноеЗначение("Справочник.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом");
	
	УстановитьДоступностьПоВидуОперацииКлиент();
	УстановитьПараметрыВыбораЧекаККМПродажиКлиент();
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода, СчитывательМагнитныхКарт");
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПредставитьТелефонИEmailКлиент();
	
	Если ЗначениеЗаполнено(Объект.КассаККМ) И НомерДокументаКассыККМ[Объект.КассаККМ] = Неопределено Тогда
		ОбщегоНазначенияРТКлиент.ЗаполнитьНомерДокументаКассыККМ(Объект.КассаККМ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ПоддерживаемыеТипыПодключаемогоОборудования = ПоддерживаемыеТипыПодключаемогоОборудования + ",ККТ,ЭквайринговыйТерминал";

	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборТоваров.Форма.Форма" Тогда
		
		СтрокиИзменены = ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТабличнойЧасти);
						
		Если СтрокиИзменены Тогда
			ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
		КонецЕсли;
		
		ПроверитьСистемуНалогообложения();
		ПерезаполнитьПризнакиМаркировки();
		УстановитьДоступностьВидаОперации();
		
		//ИнтеграцияИСМПТК_РозничноеВыбытие
		Если УчетМаркировкаИСМПТК 
			И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
			ПриИзмененииЭлементаИСМПТК();
		КонецЕсли;
		//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
				
	КонецЕсли;
	
	Если Окно <> Неопределено Тогда
		
		Окно.Активизировать();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КэшированныеЗначения",    КэшированныеЗначения);
	ДополнительныеПараметры.Вставить("СтандартнаяОбработка",    Истина);
	ДополнительныеПараметры.Вставить("ТребуетсяСерверныйВызов", Ложь);
	СобытияФормИСКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник, ДополнительныеПараметры);
	КэшированныеЗначения = ДополнительныеПараметры.КэшированныеЗначения;
	Если ДополнительныеПараметры.ТребуетсяСерверныйВызов Тогда
		ОбработкаОповещенияНаСервере(ИмяСобытия, Параметр, Источник);
		ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
		УстановитьДоступностьВидаОперации();
	КонецЕсли;
	
	Если Источник = "ФормаОпросаВладельцаКарт" Тогда
		ПриИзмененииДисконтнаяКартаСервер();
	КонецЕсли;
	
	Если ИмяСобытия = "КопированиеСтрокВБуферОбмена" Или ИмяСобытия = "ВставкаСтрокИзБуфераОбмена" Тогда
		УстановитьДоступностьКомандБуфераОбмена();
	КонецЕсли;
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		Если ИмяСобытия = "ЗакрытиеФормыПроверкиКМ" 
			И Источник = ЭтаФорма.УникальныйИдентификатор Тогда
			Если ТипЗнч(Параметр) = Тип("Строка") Тогда
				ЗакрытиеФормыПроверкиКМ(Параметр);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияКлиентПереопределяемый.ЕстьНеобработанноеСобытие() Тогда
			
			Если ВозможностьВводаПоШК() Тогда
				
				ДанныеСоСканераСтруктура = МенеджерОборудованияКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр);
				Если ДанныеСоСканераСтруктура = Неопределено Тогда
					Возврат;
				КонецЕсли;
				
				Если УчетМаркировкаИСМПТК 
					И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
					Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
						РозничноеВыбытиеИСМПТККлиент.ОбработкаВнешнегоСобытия(ЭтаФорма, ДанныеСоСканераСтруктура, "ЧекККМ");
					Иначе
						РозничноеВыбытиеИСМПТККлиент.ОбработкаВнешнегоСобытияЧекВозврат(ЭтаФорма, ДанныеСоСканераСтруктура, "ЧекККМ");
					КонецЕсли;
				Иначе
					//Если учет маркировки не ведется, но отсканирован КМ, работать с ним не позволяем 
					Штрихкод = ДанныеСоСканераСтруктура.Штрихкод;
					ИсходныйШтрихкод = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодВBase64(Штрихкод);
					ДанныеМаркировки = РозничноеВыбытиеИСМПТКВызовСервера.РазобратьШтриховойКодТовара(ИсходныйШтрихкод, Истина);
					Если ДанныеМаркировки.Разобран 
						И ДанныеМаркировки.ТипИдентификатораТовара = ПредопределенноеЗначение("Перечисление.ТипыИдентификаторовШКТовараИСМПТК.КодТовараВФорматеDataMatrixGS1") Тогда
						ТекстСообщения = НСтр("ru = 'Был отсканирован код маркировки. Работа с маркировкой в данный момент невозможна: %1!'")
										+ Символы.ПС
										+ НСтр("ru = 'Для добавления товара необходимо отсканировать обычный штрихкод EAN!'");
						Если Не УчетМаркировкаИСМПТК Тогда
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НСтр("ru = 'В базе не включен учет маркируемой продукции'"));
						ИначеЕсли Не РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НСтр("ru = 'У текущего пользователя нет соответствующих прав доступа'"));
						Иначе
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НСтр("ru = 'Обратитесь к Администратору системы'"));
						КонецЕсли;
						ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
						Возврат;
					КонецЕсли;						
					
					ОбработатьКодМаркировки(ДанныеСоСканераСтруктура);
				КонецЕсли;
			КонецЕсли;
			МенеджерОборудованияКлиентПереопределяемый.ОбработатьСобытие();
		КонецЕсли;
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)

	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если ВводДоступен() Тогда
		Если Не (Событие = "Штрихкод" Или Событие = "ПолученШтрихкод") Тогда
			
			ПодключаемоеОборудованиеРТКлиент.ВнешнееСобытиеОборудования(ЭтотОбъект, Источник, Событие, Данные);
			
		КонецЕсли; 
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Подарки);
	
	СтруктураМарки = Новый Структура;
	СтруктураМарки.Вставить("Дата", Объект.Дата);
	СтруктураМарки.Вставить("ОперацияСДенежнымиСредствами", Объект.ОперацияСДенежнымиСредствами);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьНеобходимостьВводаАкцизнойМарки(Объект.Товары, СтруктураМарки);
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипНоменклатурыВТЧСервер(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьИспользоватьСерийныеНомераВТЧСервер(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипыОплатВТЧСервер(Объект);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьВидНалогаВТЧСервер(Объект.Товары, 
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьНДСПоСтрочно(Объект.Товары);
	ОбщегоНазначенияРТКлиентСервер.ОбновитьСостояниеДокумента(Объект, Элементы.КартинкаСостоянияДокумента.Подсказка, КартинкаСостоянияДокумента, РазрешеноПроведение);
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
																"ФормаПробитьЧек",
																"Доступность", 
																Объект.Проведен И НЕ ТолькоПросмотр);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
	БонусныеБаллыСервер.ОбновитьОтображениеБонусов(Объект);
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	ИспользоватьАссортимент = АссортиментСервер.ПолучитьФункциональнуюОпциюКонтроляАссортимента(Объект.Магазин);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Подарки);
	
	Элементы.ПросмотрНакоплений.Доступность = ЗначениеЗаполнено(Объект.ДисконтнаяКарта);
	
	МассивОплатБонусами = Объект.Оплата.НайтиСтроки(Новый Структура("ВидОплаты", Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой));
	Если МассивОплатБонусами.Количество() > 0 Тогда
		ЕстьОплатаБонусами = Истина;
	КонецЕсли;
	
	ВозможностьЭлектронногоПлатежа = ПодключаемоеОборудованиеРТ.ВозможностьЭлектронногоПлатежа(Объект.КассаККМ.ПодключаемоеОборудование);
	Если ВозможностьЭлектронногоПлатежа Тогда
		ПередачаEmailЧерезПрограмму = Объект.КассаККМ.ЭлектронныйЧекEmailПередаютсяПрограммой1С;
		ПередачаSMSЧерезПрограмму  = Объект.КассаККМ.ЭлектронныйЧекSMSПередаютсяПрограммой1С;
	КонецЕсли;
	
	Элементы.КомандаEmail.Видимость = ВозможностьЭлектронногоПлатежа;
	Элементы.КомандаSMS.Видимость   = ВозможностьЭлектронногоПлатежа;
	Элементы.ГруппаДанныеSMSИEmail.Видимость   = ВозможностьЭлектронногоПлатежа;
	
	ОрганизацияИспользовалаЕНВД();
	ПроверитьВозможностьРазныхНалоговыхРежимовСкладов();
	ВидимостьСистемыНалогообложения();
	
	Если Элементы.Найти("ТоварыФискальныеДанные") <> Неопределено Тогда
		Элементы.ТоварыФискальныеДанные.Видимость = Ложь;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// &ЗамерПроизводительности 
	Замер = ОценкаПроизводительностиРТКлиент.НачатьЗамер(Ложь, 
											"Документ.ЧекККМ.ФормаДокумента.Запись",
															Ложь);
	
	ПараметрыЗаписи.Вставить("Замер", Замер);
	// Если документ проводится, рассчитаем скидки.
	
	ПередЗаписьюКлиент(Отказ, ПараметрыЗаписи.РежимЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Подарки);
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипНоменклатурыВТЧСервер(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьИспользоватьСерийныеНомераВТЧСервер(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипыОплатВТЧСервер(Объект);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьНДСПоСтрочно(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьВидНалогаВТЧСервер(Объект.Товары, 
	ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
	
	ОбработкаТабличнойЧастиТоварыСервер.УправлениеЭлементамиАгентскогоВознаграждения(ЭтотОбъект, Объект);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
	БонусныеБаллыСервер.ОбновитьОтображениеБонусов(Объект);
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Подарки);
	
	Если НЕ ОтказКлиентаОтСохраненияТелефонаEmail Тогда
		Если ЗначениеЗаполнено(Объект.Телефон) Тогда
			ПодключаемоеОборудованиеРТ.ЗаписатьТелефонВИнформационнуюКарту(Объект.ДисконтнаяКарта, Объект.Телефон);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.АдресЭП) Тогда
			ПодключаемоеОборудованиеРТ.ЗаписатьEmailВИнформационнуюКарту(Объект.ДисконтнаяКарта, Объект.АдресЭП);
		КонецЕсли;
	КонецЕсли;
	
	СобытияФормИС.ПослеЗаписиНаСервере(ЭтотОбъект);
	ПерезаполнитьПризнакиМаркировки();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда 
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбщегоНазначенияРТКлиентСервер.ОбновитьСостояниеДокумента(Объект, Элементы.КартинкаСостоянияДокумента.Подсказка, КартинкаСостоянияДокумента, РазрешеноПроведение);
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
																"ФормаПробитьЧек",
																"Доступность", 
																Объект.Проведен И НЕ ТолькоПросмотр);
	
	Если ЗначениеЗаполнено(Объект.ЗаказПокупателя) Тогда
		Оповестить("ЗаказПокупателя_Состояния", Объект.ЗаказПокупателя);
	КонецЕсли;
	
	// &ЗамерПроизводительности 
	ОценкаПроизводительностиРТКлиент.ЗакончитьЗамер(ПараметрыЗаписи.Замер);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормРТКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЧекККМПродажаПриИзменении(Элемент)
	
	ПриИзмененииЧекККМПродажаСервер();
	Если АвтоматическийРасчетСкидок Тогда
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
	КонецЕсли;
	
	// Заполняем ТЧ Товары, если чек возврата создается не из формы, а как новый чек с видом операции "Возврат"
	Если ЗначениеЗаполнено(Объект.ЧекККМПродажа) Тогда
		ТекстВопроса = НСтр("ru='Заполнить табличную часть Товары на основании выбранного чека продажи?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнениеТЧТоварыНаОснованииЧекаПродажи", ЭтотОбъект, Объект.ЧекККМПродажа);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КассаККМПриИзменении(Элемент)
	
	УстановитьПараметрыВыбораЧекаККМПродажиКлиент();
	
	ПриИзмененииКассаККМНаСервере(НомерДокументаКассыККМ, ПорядковыйНомерПродажи);
	
	ПроверитьСистемуНалогообложения(, Ложь);
	
	Если НЕ ПродажаПоЗаказу
		 И АвтоматическийРасчетСкидок
		 И НЕ ПропуститьАвтоматическийРасчетСкидок
		 И НЕ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		Если ВывестиСообщения Тогда
			
			ДополнительныеПараметры = Новый Структура;
			ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеРасчетСкидокКлиент", ЭтотОбъект, ДополнительныеПараметры);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("АдресВоВременномХранилище", АдресПримененныхСкидокВоВременномХранилище);
			ОткрытьФорму("ОбщаяФорма.ФормаВыводаСообщений", ПараметрыФормы, ЭтотОбъект, , , , ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе
			ОповещениеРасчетСкидокКлиент();
		КонецЕсли;
		
	КонецЕсли;
	
	ПриИзмененииКлючевыхРеквизитов("КассаККМ", Объект.КассаККМ);

КонецПроцедуры

&НаКлиенте
Процедура ДисконтнаяКартаПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
		
		ИнформационныеКартыКлиент.ПровестиОпросВладельца(Объект.ДисконтнаяКарта, ОбщегоНазначенияКлиент.ДатаСеанса());
		
	КонецЕсли;
	
	ПриИзмененииДисконтнаяКартаСервер();
	Если Элемент <> Неопределено Тогда
		ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	КонецЕсли;
	
	ОбновитьФормуSMSИEmail();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Объект.ЧекККМПродажа = "";
	Объект.ДокументРасчета = "";
	
	ВидОперацииПриИзмененииСервер();
	
	УправлениеЭлементамиЗаказПокупателя();
	
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	
	УстановитьДоступностьПоВидуОперацииКлиент();
	
	НастроитьФормуПоДополнительнымПравам(Истина);
	
	ЭтоВозвратДеньВДень = ЭтоВозвратДеньВДень();
	ЭтоПолныйВозврат = ЭтоПолныйВозврат();

	//Убираем доступность объектов на вкладке Скидки у документа на возврат
	Элементы.Скидки.Доступность = НЕ (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат"));
	Элементы.ГруппаТоварыСкидкиНаценки.Доступность = НЕ (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьКартинкуДляКомментария", 0.5, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииНаСервере();
	
	Если Объект.Товары.Количество() > 0 Тогда 
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", 
									ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровСтавкиНДС(Объект));
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", 
									ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПересчетаСуммыНДСВТЧ(Объект));
									
		ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, , СтруктураДействий, Неопределено);
		ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект);
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	КонецЕсли;
	
	ПроверитьСистемуНалогообложения();
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	ОрганизацияИспользовалаЕНВД();
	ПроверитьВозможностьРазныхНалоговыхРежимовСкладов();
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьВидНалогаВТЧСервер(Объект.Товары, 
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
	
	ЗаполнитьСистемуНалогообложения();
	ЗаполнитьСтавкиНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументРасчетаПриИзменении(Элемент)
	ДокументРасчетаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОперацияСДенежнымиСредствамиПриИзменении(Элемент)
	
	Если Объект.ОперацияСДенежнымиСредствами Тогда
		
		Объект.Серии.Очистить();
		Объект.АкцизныеМарки.Очистить();
		Объект.СерийныеНомера.Очистить();
		
	КонецЕсли;
	
	ВидимостьОплатРасчетыСКлиентами();
	ПерезаполнитьПризнакиМаркировки();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда 
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаХозяйственнойОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
	          Истина, "Справочник.АналитикаХозяйственныхОпераций.Форма.ФормаВыбора.Открытие");
              
КонецПроцедуры

&НаКлиенте
Процедура КассаККМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.КассыККМ.Форма.ФормаВыбора.Открытие");
             
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
								Истина, "Справочник.Контрагенты.Форма.ФормаВыбора.Открытие");
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	ПриИзмененииКлючевыхРеквизитов("Контрагент", Объект.Контрагент);
КонецПроцедуры

&НаКлиенте
Процедура ДисконтнаяКартаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ДисконтнаяКартаОбработкаВыбораНаСервере(ВыбранноеЗначение);
	УправлениеЭлементамиЗаказПокупателя();
КонецПроцедуры

&НаСервере
Процедура ДисконтнаяКартаОбработкаВыбораНаСервере(ВыбранноеЗначение)
	ОбновитьЗаказ = Ложь;
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) ИЛИ НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ОбновитьЗаказ = Истина;
	Иначе
		Если НЕ Объект.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, "ВладелецКарты") Тогда
			ОбновитьЗаказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбновитьЗаказ Тогда
		Объект.ЗаказПокупателя = Неопределено;
	КОнецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФизЛицоПриИзменении(Элемент)
	ЗаполнениеФизЛицаСервер();
	
	Если ЭтоLinuxСервер Тогда
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеУстановкаКомпонентыСклонения", ЭтотОбъект);
		Если ОбщегоНазначенияРТКлиент.КомпонентаСклоненияУстановлена(ОбработчикОповещения) Тогда
			ФИОФизЛица = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 3);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	УстановитьДоступностьВидаОперации();
	Если КэшированныеЗначения = Неопределено Тогда
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(
			Элементы.Товары,
			КэшированныеЗначения,
			ПараметрыУказанияСерий,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТоварыВводДополнительнойИнформации(Элемент) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	Если РедактированиеТЧТовары
		И ТоварыВводДополнительнойИнформации(Элемент) Тогда
		Элементы.Товары.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.КлючСвязиУслугаАгента) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(
				Элемент,КэшированныеЗначения,ПараметрыУказанияСерий);
	
	УдаляемыйКлючСвязи = ТекущиеДанные.КлючСвязи;
	
	ДополнительныеРеквизиты = Новый Структура("ПередУдалением");
	СобытияФормИСКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "Товары", ДополнительныеРеквизиты);
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.УдалитьСтрокиАгентскогоВознаграждения(Объект.Товары, ТекущиеДанные);
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	ОбработатьТаблицуМаркиПриУдаленииТовараИСМПТК(УдаляемыйКлючСвязи);
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если Копирование Тогда
		
		ТекущаяСтрока.КлючСвязиСерийныхНомеров    = 0;
		ТекущаяСтрока.СуммаАвтоматическойСкидки   = 0;
		ТекущаяСтрока.ПроцентАвтоматическойСкидки = 0;
		ТекущаяСтрока.СуммаСкидкиОплатыБонусом    = 0;
		
		ТекущаяСтрока.КлючСвязи                   = 0;
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьКлючСвязи(Объект.Товары, ТекущаяСтрока, "КлючСвязи");
		
		Если ТекущаяСтрока.ИспользоватьСерийныеНомера Тогда
			ТекущаяСтрока.КоличествоУпаковок = 0;
			ТекущаяСтрока.Количество = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекущаяСтрока.Продавец = Объект.Продавец;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(
				Элемент,
				КэшированныеЗначения,
				ПараметрыУказанияСерий,
				Копирование);
	
	РедактированиеТЧТовары = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если КэшированныеЗначения = Неопределено Тогда
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(
			Элементы.Товары,
			КэшированныеЗначения,
			ПараметрыУказанияСерий,
			Ложь);
	КонецЕсли;
	
	Если ОбработкаТабличнойЧастиТоварыКлиент.НеобходимоОбновитьСтатусыСерий(
		Элемент,КэшированныеЗначения,ПараметрыУказанияСерий) Тогда
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		
		ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(ТекущаяСтрокаИдентификатор, КэшированныеЗначения);
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(Элемент,КэшированныеЗначения,ПараметрыУказанияСерий);
		
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьСтатусСерийИПодарочныхСертификатов(ТекущиеДанные);
	
	//ПриИзмененииЭлементаГосИС();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	ПриИзмененииЭлементаИСМПТК();
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
	РедактированиеТЧТовары = Ложь;
	
	Если ПропуститьАвтоматическийРасчетСкидок Тогда
		ПропуститьАвтоматическийРасчетСкидок = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("КлючСвязи", УдаляемыйКлючСвязи);
	
	Если ОбработкаТабличнойЧастиТоварыКлиент.НеобходимоОбновитьСтатусыСерий(
		Элемент,КэшированныеЗначения,ПараметрыУказанияСерий,Истина) Тогда
		
		ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(Неопределено, КэшированныеЗначения);
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(
		Элемент,КэшированныеЗначения,ПараметрыУказанияСерий);
		
	КонецЕсли;
	
	ПроверитьСистемуНалогообложения();
	
	ТоварыПослеУдаленияСервер();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПослеУдаленияСервер()
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("КлючСвязи", УдаляемыйКлючСвязи);
	
	ЕстьУдаляемыеСтроки = УдаляемыйКлючСвязи > 0;
	
	Если ПродажаПоЗаказу Или ДокументРасчетаЗаполнен Тогда
		Если ЕстьУдаляемыеСтроки Тогда
			
			СтрокиСкидкиНаценки = Объект.СкидкиНаценки.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаСкидкиНаценки Из СтрокиСкидкиНаценки Цикл
				Объект.СкидкиНаценки.Удалить(СтрокаСкидкиНаценки);
			КонецЦикла;
			
			УдаляемыйКлючСвязи = 0;
		КонецЕсли;
	КонецЕсли;
	
	УдалитьНесвязанныеСерийныеНомера();
	УдалитьНесвязанныеАкцизы();
	
	СобытияФормИС.ПриИзмененииЭлемента(ЭтотОбъект, "Товары", Неопределено);
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
	
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если АвтоматическийРасчетСкидок Тогда
		ПропуститьАвтоматическийРасчетСкидок = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ПриИзмененииТоварыНоменклатура();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
								Истина, "Справочник.Номенклатура.Форма.ФормаВыбора.Открытие");
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбораНоменклатуры = Новый Структура();
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ПараметрыВыбораНоменклатуры.Вставить("ТекущаяСтрока", ТекущиеДанные.Номенклатура);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВыбораНоменклатуры", ЭтотОбъект);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыВыбораНоменклатуры,,,,, ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
		
	СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураЗаполненияЦеныПродажиВСтрокеТЧ(Объект, Истина));
	СтруктураДействий.Вставить("ПересчитатьЦенуСУчетомАгентскогоВознаграждения", Новый Структура("Цена", ТекущаяСтрока.Цена));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьКлючСвязи(Объект.Товары, ТекущаяСтрока, "КлючСвязи");
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		РозничноеВыбытиеИСМПТККлиентПереопределяемый.ОчиститьМаркиИСМПТК(Объект, ТекущаяСтрока);
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработкаТабличнойЧастиТоварыКлиент.ВыбратьХарактеристикуНоменклатуры(ЭтотОбъект, Элемент, СтандартнаяОбработка, Элементы.Товары.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаСоздание(Элемент, СтандартнаяОбработка)
	
	ОбработкаТабличнойЧастиТоварыКлиент.СоздатьХарактеристикуНоменклатуры(ЭтотОбъект, Элемент, СтандартнаяОбработка, Элементы.Товары.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыУпаковкаПриИзменении(Элемент)
	
	// Отмена операции для табачной продукции
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	Если ТекущаяСтрока.Количество > 0 Тогда
		СтруктураДействий.Вставить("ПересчитатьЦенуЗаУпаковку", ТекущаяСтрока.Количество);
	Иначе
		СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураЗаполненияЦеныПродажиВСтрокеТЧ(Объект, Истина));
		СтруктураДействий.Вставить("ПересчитатьЦенуСУчетомАгентскогоВознаграждения", Новый Структура("Цена", ТекущаяСтрока.Цена));
	КонецЕсли;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	// Интерфейс ЕГАИС
	//ПриИзмененииЭлементаГосИС();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	ПриИзмененииЭлементаИСМПТК();
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыУпаковкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработкаТабличнойЧастиТоварыКлиент.ВыбратьУпаковкуНоменклатуры(ДанныеВыбора, СтандартнаяОбработка, Элементы.Товары.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоУпаковокПриИзменении(Элемент)

	// Отмена операции для табачной продукции
	ПриИзмененииТоварыКоличестваУпаковок();

	// Интерфейс ЕГАИС
	//ПриИзмененииЭлементаГосИС();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	ПриИзмененииЭлементаИСМПТК();
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентРучнойСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуРучнойСкидки");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаРучнойСкидкиПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьПроцентРучнойСкидки");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать, ПересчитыватьСуммуСкидки", Ложь, Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенуСкидкуПоСуммеВПродажах", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаЦеныСкидкиВПродажахВТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкладПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураПараметровСтавкиНДС = Новый Структура;
	СтруктураПараметровСтавкиНДС.Вставить("Дата"       , Объект.Дата);
	СтруктураПараметровСтавкиНДС.Вставить("Организация", Объект.Организация);
	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", СтруктураПараметровСтавкиНДС);
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	
	СтруктураДействий.Вставить("ПроставитьПродавца", Объект.Продавец);
	
	СтруктураДействий.Вставить("ЗаполнитьВидНалога", 
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	ПроверитьСистемуНалогообложения(Элементы.Товары.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОплата

&НаКлиенте
Процедура ОплатаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	Если НЕ Копирование Тогда
		ДобавитьОплатуНаличными();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОплатаСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Оплата.ТекущиеДанные;
	ПересчитатьСуммуКомиссии(ТекущаяСтрока);
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПроцентКомиссииПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Оплата.ТекущиеДанные;
	ПересчитатьСуммуКомиссии(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаВидОплатыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Оплата.ТекущиеДанные;
	
	УстановитьЭквайринговыйТерминалПоУмолчанию(ТекущаяСтрока);
	
	ТекущаяСтрока.ПроцентКомиссии = ЭквайрингВызовСервера.ПолучитьПроцентКомиссииПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
	ПересчитатьСуммуКомиссии(ТекущаяСтрока);
	
	ТекущаяСтрока.ТипОплаты = ТипОплатыПоВиду(ТекущаяСтрока.ВидОплаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаЭквайринговыйТерминалПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Оплата.ТекущиеДанные;
	
	ТекущаяСтрока.ПроцентКомиссии = ЭквайрингВызовСервера.ПолучитьПроцентКомиссииПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
	
	ПересчитатьСуммуКомиссии(ТекущаяСтрока);
	
	ТекущаяСтрока.ТипОплаты = ТипОплатыПоВиду(ТекущаяСтрока.ВидОплаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Оплата.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если НЕ ВопросОбУдаленииСтрокиОплатыУжеЗадан Тогда
			Если ТекущиеДанные.ВидОплаты = ПредопределенноеЗначение("Справочник.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом") 
				  И Объект.ПогашениеПодарочныхСертификатов.Количество() > 0 Тогда
				
				Отказ = Истина;
				ТекстВопроса = НСтр("ru = 'Удаляется оплата подарочным сертификатом, при этом удалятся все сертификаты оплаты. Продолжить?'"); 
				
				ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросУдалениеСертификатовОплаты", ЭтотОбъект);
				ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Нет);
				
			ИначеЕсли ТекущиеДанные.ДанныеПереданыВБанк Тогда
				
				Отказ = Истина;
				
				Если ТекущиеДанные.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ПлатежнаяСистема") Тогда
					ДополнительныеПараметры = Новый Структура();
					ДополнительныеПараметры.Вставить("ТекущаяСтрока", Элементы.Оплата.ТекущиеДанные);
					ТекстВопроса = НСтр("ru = 'Данные об операции отправлены в НСПК и ожидают подтверждения.
					|Хотите удалить текущую операцию?'");
						
					ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросУдалениеДанныхОтправленныхВНСПК",
						ЭтотОбъект,
						ДополнительныеПараметры);
						
					СписокКнопок = Новый СписокЗначений();
					СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Удалить'"));
					СписокКнопок.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отменить'"));
					ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, СписокКнопок,, КодВозвратаДиалога.Нет);
				Иначе
					Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
						СтрокаСообщения = НСтр("ru = 'Данные об оплате отправлены в банк.'") + Символы.ПС;
						СтрокаСообщения = СтрокаСообщения + НСтр("ru = 'Необходимо отменить операцию.'");
						ОбщегоНазначенияКлиент.СообщитьПользователю(
							СтрокаСообщения,
							Объект,
							"ОплатаОплатаЧерезТерминал");
					Иначе
						ТекстВопроса = НСтр("ru = 'Данные об отмене отправлены в банк.'") + Символы.ПС;
						ТекстВопроса = ТекстВопроса + НСтр("ru = 'Хотите удалить строку с потерей данных?'");
						
						ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросПотеряДанныхОтправленныхВБанк", ЭтотОбъект);
						ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Нет);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			Если ТекущиеДанные.ВидОплаты = ПредопределенноеЗначение("Справочник.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой")
				И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
				ОтменитьОплатуБонусами(Ложь);
				Если АвтоматическийРасчетСкидок Тогда
					Если НЕ ПропуститьАвтоматическийРасчетСкидок Тогда
						РассчитатьСкидкиНаценкиКлиент();
					КонецЕсли;
				Иначе
					ОтменитьСкидки();
				КонецЕсли;
				ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Копирование Тогда
		
		ТекущиеДанные = Элементы.Оплата.ТекущиеДанные;
		
		ТекущиеДанные.СсылочныйНомер      = "";
		ТекущиеДанные.НомерЧекаЭТ         = "";
		ТекущиеДанные.НомерПлатежнойКарты = "";
		ТекущиеДанные.ДанныеПереданыВБанк = Ложь;
		
	КонецЕсли;
	
	СуммаПродажи = ?(Объект.ЦенаВключаетНДС, Объект.Товары.Итог("Сумма"), Объект.Товары.Итог("Сумма") + Объект.Товары.Итог("СуммаНДС"));
	СуммаОплат   = Объект.Оплата.Итог("Сумма");
	
	ТекущаяСтрока = Элементы.Оплата.ТекущиеДанные;
	
	Если ТекущаяСтрока.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ЗачетАванса") Тогда
		ОстатокОплаты = Мин(СуммаПродажи, СуммаДокументаРасчета);
	Иначе
		ТекущаяСумма  = ТекущаяСтрока.Сумма;
		
		ОплатаБезТекущейСтроки = СуммаОплат - ТекущаяСумма;
		ОстатокОплаты = СуммаПродажи - ОплатаБезТекущейСтроки;
	КонецЕсли;
	
	Элементы.ОплатаСумма.СписокВыбора.Очистить();
	
	Если ОстатокОплаты > 0 Тогда
		
		Элементы.ОплатаСумма.СписокВыбора.Добавить(ОстатокОплаты, Формат(ОстатокОплаты, "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '; ЧН=; ЧГ=3,0"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПриИзменении(Элемент)
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодарки

&НаКлиенте
Процедура ПодаркиЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Подарки.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Подарки, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиСуммаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Подарки.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенуСкидкуПоСуммеВПродажах");
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Подарки, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ОбработчикиКомандПодключаемогоОборудования

&НаКлиенте
Процедура ПолучитьВес(Команда)
	
	ОповещенияПриПолученииВеса = Новый ОписаниеОповещения("ПолучитьВесЗавершение", ЭтотОбъект);
	ПодключаемоеОборудованиеРТКлиент.ПолучениеВесаСЭлектронныхВесовДляТабличнойЧасти(ОповещенияПриПолученииВеса, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧек(Команда)
	
	//ИнтеграцияИСМПТК
	//Проверим таблицу кодов маркировки на наличие критических ошибок.
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() 
		И Не Объект.КодыМаркировкиИСМПТК.Количество() = 0 Тогда
		
		ЕстьОшибкиМаркировки = Ложь;
		Для Каждого КодМарки Из Объект.КодыМаркировкиИСМПТК Цикл
			НайденныйеСтроки = Объект.Товары.НайтиСтроки(Новый Структура("КлючСвязи", КодМарки.КлючСвязи));
			ЕстьОшибкиМаркировки = НайденныйеСтроки.Количество() = 0;
			Если ЕстьОшибкиМаркировки Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьОшибкиМаркировки Тогда
			ОбработчикОповещения = Новый ОписаниеОповещения("ПробитьЧекПослеОбработкиКМ", ЭтотОбъект);
			ТекстВопроса = НСтр("ru = 'В Чеке обнаружены коды маркировки, не связанные со строками товаров. Пробитие такого чека невозможно!'")
						 + Символы.ПС
						 + НСтр("ru = 'Удалить эти коды маркировки и продолжить пробитие?'");
			ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ПробитьЧекНаКлиенте();
		КонецЕсли;
	Иначе
		ПробитьЧекНаКлиенте();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД(Команда)
	
	ПодключаемоеОборудованиеРТКлиент.ВыгрузитьДокументВТСД(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыбиратьНенайденные", Ложь);
	ДополнительныеПараметры.Вставить("ОбработатьПослеЗагрузки", Истина);
	ПодключаемоеОборудованиеРТКлиент.ПолучитьДанныеИзТСД(ЭтотОбъект, ДополнительныеПараметры);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоМагнитномуКоду(Команда)
	
	ОбработкаТабличнойЧастиТоварыКлиент.ВвестиМагнитныйКод(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ОчиститьСообщения();
	ШтрихкодированиеИСКлиент.ПоказатьВводШтрихкода(ОписаниеОповещенияОбработкиКодаМаркировки());
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеУстановкаКомпонентыСклонения(ДополнительныеПараметры) Экспорт
	ФИОФизЛица = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 3);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоискПоНаименованию(Команда)
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Магазин",Объект.Магазин);
	РаботаСПравиламиИменованияКлиент.ПоискПоНаименованию(ЭтаФорма,ПараметрыПоиска);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПодарочныеСертификатыПогашения(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеВводаПодарочныхСертификатов");
	Отказ = Ложь;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		
	Иначе
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеВводаПодарочныхСертификатов(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьПлатежнойКартой(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеОплатыПлатежнойКартой");
	Отказ = Ложь;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		
	Иначе
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеОплатыПлатежнойКартой(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьМобильный(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеОплатыМобильный");
	
	Отказ = Ложь;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		
	Иначе
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеОплатыМобильный(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьБанковскимКредитом(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеОплатыБанковскимКредитом");
	Отказ = Ложь;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		
	Иначе
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеОплатыБанковскимКредитом(Неопределено, ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОплатыПоДокументуПродажи(Команда)
	
	ЗаполнитьОплатыПоДокументуПродажиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьАвтоматическиеСкидки(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		Истина, "ОбщаяФорма.НазначениеАвтоматическихУправляемыхСкидокНаценок.Открытие");
	
	ДополнительныеПараметры = Новый Структура;
	ОбработчикОповещения = Новый ОписаниеОповещения("НазначитьАвтоматическиеСкидкиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресВоВременномХранилище", ВыполнитьПредварительныйРасчетСкидокНаСервере(ПорядковыйНомерПродажи));
	СписокОдноразовыхКодов = Новый Массив;
	Для Каждого ПредъявленныйКод Из Объект.ПредъявленныеКодыОднократныхСкидок Цикл
		СписокОдноразовыхКодов.Добавить(ПредъявленныйКод.КодСкидки);
	КонецЦикла;
	ПараметрыФормы.Вставить("СписокОдноразовыхКодов", СписокОдноразовыхКодов);
	ПараметрыФормы.Вставить("Магазин", Объект.Магазин);
	ОткрытьФорму("ОбщаяФорма.НазначениеАвтоматическихУправляемыхСкидокНаценок", ПараметрыФормы, ЭтотОбъект, , , , ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьРучнуюСкидку(Команда)
	
	Если Не СкидкиНаценкиКлиент.ПроверитьНеобходимостьНазначенияРучнойСкидкиНаценки(Объект, "Товары", "Товары") Тогда
		Возврат;
	КонецЕсли;
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		Истина, "ОбщаяФорма.НазначениеРучнойСкидкиНаценки.Открытие");
        
	ПараметрыДляРучнойСкидки = ПараметрыДляНазначенияРучнойСкидки();
	ДополнительныеПараметры = Новый Структура;
	ОбработчикОповещения = Новый ОписаниеОповещения("НазначитьРучнуюСкидкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.НазначениеРучнойСкидкиНаценки", ПараметрыДляРучнойСкидки, ЭтотОбъект, , , , ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиОплатуЧерезТерминал(Команда)
	Перем Отказ;
	
	ТекущаяСтрока = Элементы.Оплата.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		СтрокаСообщения = НСтр("ru = 'Необходимо выбрать строку с платежной картой'");
		ПоказатьПредупреждение(, СтрокаСообщения);
		Возврат;
	КонецЕсли;
	
	Если НЕ ТекущаяСтрока.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ПлатежнаяКарта") Тогда
		
		СтрокаСообщения = НСтр("ru = 'Выберите строку с платежной картой'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрокаСообщения,
			Объект,
			"Оплата[" + (ТекущаяСтрока.НомерСтроки - 1) + "].ВидОплаты");
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ЭквайринговыйТерминал) Тогда
		
		СтрокаСообщения = НСтр("ru = 'Не выбран эквайринговый терминал'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрокаСообщения,
			Объект,
			"Оплата[" + (ТекущаяСтрока.НомерСтроки - 1) + "].ЭквайринговыйТерминал");
		
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущаяСтрока", ТекущаяСтрока);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
		
		ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеОплатыЧерезТерминал");
		
		Отказ = Ложь;
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеОплатыЧерезТерминал(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнформациюОСкидках(Команда)
	
	ОткрытьИнформациюОСкидкахКлиент()
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРучныеСкидки(Команда)
	Если Не СкидкиНаценкиКлиент.ПроверитьНеобходимостьОтменыРучныхСкидокНаценок(Объект, "Товары", "Товары") Тогда
		Возврат;
	КонецЕсли;
	
	ОтменитьРучныеСкидкиНаСервере();
	СкидкиНаценкиКлиент.ОповеститьОбОкончанииНазначенияРучныхСкидокНаценок();
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиПодаркиВТовары(Команда);
	
	ПеренестиСкидкиПодаркиВТоварыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиОткрытьПодбор(Команда)
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(Объект.Магазин) Тогда
		ОчиститьСообщения();
		СообщитьОбОшибкахОткрытияПодбора(Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрЗаголовок = НСтр("ru = 'Подбор товаров в %Документ%'");
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", Объект.Ссылка);
	Иначе
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru = 'чек'"));
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Магазин",    Объект.Магазин);
	ПараметрыФормы.Вставить("ИмяТабличнойЧасти", "Подарки");
	ПараметрыФормы.Вставить("РежимПодбораБезСертификатов", Истина);
	ПараметрыФормы.Вставить("Заголовок", ПараметрЗаголовок);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	
	Если НЕ ЕстьПравоИзменятьЦену() Тогда
		ПараметрыФормы.Вставить("РежимПодбораБезСуммовыхПараметров", Истина);
		ПараметрыФормы.Вставить("ЗаголовокКнопкиЗапрашиватьКоличествоИЦену",НСтр("ru = 'Запрашивать количество'"));
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ПодборТоваров.Форма", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрытьИзФормы(Команда)
	Перем Отказ, Объект_Ссылка;
	
	Если Объект.ПометкаУдаления Тогда
		ВызватьИсключение НСтр("ru = 'Помеченный на удаление документ не может быть проведен!'");
	КонецЕсли;
	
	Отказ = Ложь;
	ПередЗаписьюКлиент(Отказ, РежимЗаписиДокумента.Проведение);
	ТекстСообщения = "";
	Если НЕ Отказ Тогда
		ПровестиИзФормыСервер(Отказ, Ложь, Объект_Ссылка,ТекстСообщения);
		Если Отказ 
			И ЗначениеЗаполнено(ТекстСообщения) Тогда
			Возврат;
		КонецЕсли;

		Модифицированность = Отказ;
		Если НЕ Отказ Тогда
			Закрыть();
			ОтобразитьИзменениеДанных(Объект_Ссылка, ВидИзмененияДанных.Изменение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИзФормы(Команда)
	
	Перем Отказ;
	
	Если Объект.ПометкаУдаления Тогда
		ВызватьИсключение НСтр("ru = 'Помеченный на удаление документ не может быть проведен!'");
	КонецЕсли;
	
	Отказ = Ложь;
	ПередЗаписьюКлиент(Отказ, РежимЗаписиДокумента.Проведение);
	ТекстСообщения = "";
	Если НЕ Отказ Тогда
		ПровестиИзФормыСервер(Отказ, Истина,,ТекстСообщения);
		Если Отказ
			И ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ОбщегоНазначенияРТКлиентСервер.ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение);
		ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
																	"ФормаПробитьЧек",
																	"Доступность", 
																	Объект.Проведен И НЕ ТолькоПросмотр);
		
		ОбновитьОтображениеДанных();
		Модифицированность = Отказ;
		
		ОтобразитьИзменениеДанных(ЭтотОбъект.Объект.Ссылка, ВидИзмененияДанных.Изменение);
	КонецЕсли;
	
	ОбменССайтомРТВызовСервера.ЗаписатьСостояниеОплатыЗаказаПокупателя(Объект.ЗаказПокупателя, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСкидкиНаценки(Команда)
	
	РассчитатьСкидкиНаценкиКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОткрытьПодбор(Команда)
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(Объект.Магазин) Тогда
		ОчиститьСообщения();
		СообщитьОбОшибкахОткрытияПодбора(Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрЗаголовок = НСтр("ru = 'Подбор товаров в %Документ%'");
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", Объект.Ссылка);
	Иначе
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru = 'чек'"));
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Магазин",    Объект.Магазин);
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		ПараметрыФормы.Вставить("РежимПодбораБезСертификатов", Истина);
		ПараметрыФормы.Вставить("РежимПодбораБезУслуг", Ложь);
	КонецЕсли;
	ПараметрыФормы.Вставить("РежимПодбораСУчетомМинимальныхЦен", Истина);
	ПараметрыФормы.Вставить("Заголовок", ПараметрЗаголовок);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	
	Если НЕ ЕстьПравоИзменятьЦену() Тогда
		ПараметрыФормы.Вставить("РежимПодбораБезСуммовыхПараметров", Истина);
		ПараметрыФормы.Вставить("ЗаголовокКнопкиЗапрашиватьКоличествоИЦену",НСтр("ru = 'Запрашивать количество'"));
	КонецЕсли;
	
	Если ИспользоватьАссортимент Тогда
		ПараметрыФормы.Вставить("МагазинАссортимента", Объект.Магазин);
		ПараметрыФормы.Вставить("РежимПодбораСУчетомАссортимента", Истина);
		ПараметрыФормы.Вставить("УсловиеАссортимента", "РазрешеныПродажи");
	КонецЕсли;
	
	Если СкидкиНаценкиВызовСервера.ФункциональнаяОпцияИспользованияЗапретаРозничнойПродажиАлкоголя() Тогда
		ПараметрыФормы.Вставить("РежимПодбораСУчетомЗапрещенныхКПродаже", Истина);
		ПараметрыФормы.Вставить("ПродажиОптовые", Ложь);
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ПодборТоваров.Форма", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьСвернутьТЧ(Команда)
	РазвернутьСвернутьТЧНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПросмотрНакоплений(Команда)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Обработка.РМКУправляемыйРежим.Форма.ФормаПросмотраНакоплений.Открытие");
        
	ПараметрыОткрытия = Новый Структура;
	РеквизитыДокумента = Новый Структура;
	РеквизитыДокумента.Вставить("Ссылка", Объект.Ссылка);
	РеквизитыДокумента.Вставить("Дата",  Объект.Дата);
	РеквизитыДокумента.Вставить("Проведен", Объект.Проведен);
	РеквизитыДокумента.Вставить("ВидОперации", Объект.ВидОперации);
	РеквизитыДокумента.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
	РеквизитыДокумента.Вставить("СтатусЧекаККМ", ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.ПустаяСсылка"));
	РеквизитыДокумента.Вставить("ОтчетОРозничныхПродажах", ПредопределенноеЗначение("Документ.ОтчетОРозничныхПродажах.ПустаяСсылка"));
	
	ПараметрыОткрытия.Вставить("РеквизитыДокумента", РеквизитыДокумента);
	ПараметрыОткрытия.Вставить("ВыборИнформационнойКартыТолькоПоКоду", Ложь);
	ПараметрыОткрытия.Вставить("ВводКартДоступен", Ложь);
	ОткрытьФорму("Обработка.РМКУправляемыйРежим.Форма.ФормаПросмотраНакоплений", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьБонусами(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ВозможнаОплата = ВозможнаОплатаБонусами(ДополнительныеПараметры);
	Если ВозможнаОплата Тогда
		Если Объект.Проведен Тогда
			ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОплатаБонусамиВопросОбОтменеПроведения", ЭтотОбъект, ДополнительныеПараметры);
			ТекстВопроса = НСтр("ru = 'Оплата бонусами возможна только в не проведенном документе, отменить проведение?'");
			ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ОткрытьФормуОплатыБонусами(ДополнительныеПараметры);
		КонецЕсли;
		
	Иначе
		Если ДополнительныеПараметры.Свойство("ТекстПредупреждения") Тогда
			ПоказатьПредупреждение(,ДополнительныеПараметры.ТекстПредупреждения);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчетОРозничныхПродажах(Команда)
	
	ПоказатьЗначение(, Объект.ОтчетОРозничныхПродажах);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоЗаказу(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru='Список ""Товары"" будет перезаполнен. Продолжить?'");
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ПоказатьВопросЗаполнитьПоЗаказуЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПоЗаказуПокупателя();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиВОтчетеОРозничныхПродажах(Команда)
	
	Если ЗначениеЗаполнено(Объект.ОтчетОРозничныхПродажах) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", Объект.ОтчетОРозничныхПродажах);
		
		ТекущиеДанныеТовары = Элементы.Товары.ТекущиеДанные;
		Если ТекущиеДанныеТовары = Неопределено Тогда
			Если Объект.Товары.Количество() > 0  Тогда
				ТекущиеДанныеТовары = Объект.Товары[0];
			КонецЕсли;
		КонецЕсли;
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
			ПараметрыФормы.Вставить("ТекущийЭлемент", "Товары");
			
			Если НЕ ТекущиеДанныеТовары = Неопределено Тогда
				ПараметрыФормы.Вставить("ТекущийРеквизитОбъекта", "Товары");
				ПараметрыТекущейСтроки = Новый Структура;
				ПараметрыТекущейСтроки.Вставить("Склад"          , ТекущиеДанныеТовары.Склад);
				ПараметрыТекущейСтроки.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
				ПараметрыТекущейСтроки.Вставить("Номенклатура"   , ТекущиеДанныеТовары.Номенклатура);
				ПараметрыТекущейСтроки.Вставить("Характеристика" , ТекущиеДанныеТовары.Характеристика);
				ПараметрыТекущейСтроки.Вставить("СтавкаНДС"      , ТекущиеДанныеТовары.СтавкаНДС);
				ПараметрыТекущейСтроки.Вставить("Продавец"       , ТекущиеДанныеТовары.Продавец);
				ПараметрыТекущейСтроки.Вставить("Упаковка"       , ТекущиеДанныеТовары.Упаковка);
				ПараметрыТекущейСтроки.Вставить("Цена"           , ТекущиеДанныеТовары.Цена);
				ПараметрыТекущейСтроки.Вставить("КодСтроки"      , ТекущиеДанныеТовары.КодСтроки);
				ПараметрыТекущейСтроки.Вставить("ЗаказПокупателя", ТекущиеДанныеТовары.ЗаказПокупателя);
				ПараметрыТекущейСтроки.Вставить("Резервировать"  , ТекущиеДанныеТовары.Резервировать);
				
				ПараметрыФормы.Вставить("ТекущаяСтрока", ПараметрыТекущейСтроки);
				
			КонецЕсли;
			
		Иначе
			ПараметрыФормы.Вставить("ТекущийЭлемент", "ВозвращенныеТовары");
			Если НЕ ТекущиеДанныеТовары = Неопределено Тогда
				ПараметрыФормы.Вставить("ТекущийРеквизитОбъекта", "ВозвращенныеТовары");
				ПараметрыТекущейСтроки = Новый Структура;
				ПараметрыТекущейСтроки.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
				ПараметрыТекущейСтроки.Вставить("Склад"          , ТекущиеДанныеТовары.Склад);
				ПараметрыТекущейСтроки.Вставить("Номенклатура"   , ТекущиеДанныеТовары.Номенклатура);
				ПараметрыТекущейСтроки.Вставить("Характеристика" , ТекущиеДанныеТовары.Характеристика);
				ПараметрыТекущейСтроки.Вставить("Продавец"       , ТекущиеДанныеТовары.Продавец);
				ПараметрыТекущейСтроки.Вставить("СтавкаНДС"      , ТекущиеДанныеТовары.СтавкаНДС);
				ПараметрыТекущейСтроки.Вставить("Упаковка"       , ТекущиеДанныеТовары.Упаковка);
				ПараметрыТекущейСтроки.Вставить("Цена"           , ТекущиеДанныеТовары.Цена);
				
				ПараметрыТекущейСтроки.Вставить("АналитикаХозяйственнойОперации", Объект.АналитикаХозяйственнойОперации);
				
				ПараметрыФормы.Вставить("ТекущаяСтрока", ПараметрыТекущейСтроки);
				
			КонецЕсли;
		КонецЕсли;
		
		ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаEmail(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытиеФормыВводаEmail", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Email", Объект.АдресЭП);
	ПараметрыФормы.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Введите адрес электронной почты (Email)'"));
	ПараметрыФормы.Вставить("ОтказКлиентаОтСохраненияEmail", ОтказКлиентаОтСохраненияТелефонаEmail);
	
	ПодключаемоеОборудованиеРТКлиент.ОткрытьФормуВводаEmailОтправкиЧерезОФД(ОбработчикОповещения, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаSMS(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытиеФормыВводаТелефона", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(Объект.Телефон) Тогда
		Телефон = Число(Объект.Телефон);
	Иначе
		Телефон = 0;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок" , НСтр("ru = 'Введите номер телефона'"));
	ПараметрыФормы.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
	ПараметрыФормы.Вставить("ЧислоВвода", Телефон);
	ПараметрыФормы.Вставить("ОтказКлиентаОтСохраненияТелефона", ОтказКлиентаОтСохраненияТелефонаEmail);
	
	ПодключаемоеОборудованиеРТКлиент.ОткрытьФормуВводаТелефонаДляОтправкиЧерезОФД(ОбработчикОповещения, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗачетАванса(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		
	Иначе
		
		ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеЗачетАванса");
		
		Отказ = Ложь;
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеЗачетАванса(Неопределено, ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Процедура ОплатитьВРассрочку(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		
	Иначе
		
		ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеОплатыСРассрочку");
		
		Отказ = Ложь;
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеОплатыСРассрочку(Неопределено, ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументЧекККМ(Команда)
	
	ЧекККМСсылка = Объект.Ссылка;
	
	Если ЗначениеЗаполнено(ЧекККМСсылка) Тогда
		
		СтруктураОснования = Новый Структура; 
		СтруктураОснования.Вставить("ДанныеЗаполнения", ЧекККМСсылка);
		СтруктураОснования.Вставить("СоздатьЧекПродажи");
		
		ОткрытьФорму("Документ.ЧекККМ.Форма.ФормаДокумента", Новый Структура("Основание", СтруктураОснования));
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	КоличествоТоваровДоВставки = Объект.Товары.Количество();
	ПолучитьСтрокиИзБуфераОбмена();
	КоличествоВставленных = Объект.Товары.Количество()-КоличествоТоваровДоВставки;
	КопированиеСтрокКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоВставленных);
	ПерезаполнитьПризнакиМаркировки();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда 
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	Если КопированиеСтрокКлиент.ВозможноКопированиеСтрок(Элементы.Товары.ТекущаяСтрока) Тогда
		СкопироватьСтрокиНаСервере();
		КопированиеСтрокКлиент.ОповеститьПользователяОКопированииСтрок(Элементы.Товары.ВыделенныеСтроки.Количество());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФискальныеДанные(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, 
											   ЭтотОбъект.Команды["ПодменюПечатьОбычное_ФискальнаяОперация"], 
											   Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормИСКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОплатуВПлатежнойСистеме(Команда)
	ОтменитьОплатуВПлатежнойСистемеКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗаказПокупателя(Команда)
	ПоказатьЗначение(, Объект.ЗаказПокупателя);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗаказПокупателя(Команда)
	
	Если НЕ ПроверитьЗаполнениеКлючевыхРеквизитов() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Товары.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru='Список ""Товары"" будет перезаполнен остатками по заказу. Продолжить?'");
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ПоказатьВопросВыбратьЗаказПокупателяЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ОткрытьФормуВыбораЗаказаПокупателя();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗаказПокупателя(Команда)
	
	Объект.ЗаказПокупателя = Неопределено;
	УправлениеЭлементамиЗаказПокупателя();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчередьЧеков(Команда)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ДокументОснование", Объект.Ссылка);
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Отбор", СтруктураОтбора);
	ОткрытьФорму("РегистрСведений.ОчередьЧековККТ.ФормаСписка", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьQRКодом(Команда)
	
	ВозвратПоОснованию = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат")
						И ЗначениеЗаполнено(Объект.ЧекККМПродажа);
	
	Если Не ВозвратПоОснованию
		И Объект.Оплата.Количество() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Совместное использование с другими видами оплат не поддерживается!'"),, "Объект.Оплата[0].ВидОплаты");
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Неопределено;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		ОповещениеОплатыПлатежнойСистемой(Неопределено, ДополнительныеПараметры);
		
	Иначе
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеОплатыПлатежнойСистемой");
		
		Отказ = Ложь;
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
		ОповещениеОплатыПлатежнойСистемой(Неопределено, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

	#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПроверитьИспользованиеПодключаемогоОборудования(ИспользоватьПодключаемоеОборудование)

	ТипыПодключенногоОборудования = МенеджерОборудованияВызовСервера.ТипыИспользуемогоОборудованияТекущегоРабочегоМеста();
	ИспользоватьПодключаемоеОборудование = ОбщегоНазначенияРТ.ИспользоватьПодключаемоеОборудование(ТипыПодключенногоОборудования);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидНалога()
	
	Объект.СистемаНалогообложения = ПодключаемоеОборудованиеРТ.ПолучитьСистемуНалогообложения(
		Объект.Дата,
		Объект.Организация,
		Объект.Магазин,
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Магазин, "СкладПродажи"));
	
КонецПроцедуры

&НаСервере
Функция ЭтоВозвратДеньВДень()
	
	Если ЗначениеЗаполнено(Объект.ЧекККМПродажа) Тогда
		
		Возврат НЕ ЗначениеЗаполнено(Объект.ЧекККМПродажа.ОтчетОРозничныхПродажах);
	Иначе
		
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЭтоПолныйВозврат()
	
	Если ЗначениеЗаполнено(Объект.ЧекККМПродажа) Тогда
		
		НеобходимаяСуммаВозврата = Объект.ЧекККМПродажа.Товары.Итог("Сумма");
		ПолнаяСуммаОплаты = Объект.Оплата.Итог("Сумма");
		
		Возврат НеобходимаяСуммаВозврата = ПолнаяСуммаОплаты;
	Иначе
		
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ДокументРасчетаПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ДокументРасчета)  Тогда
		СуммаДокументаРасчета = Объект.ДокументРасчета.СуммаДокумента;
	Иначе
		СуммаДокументаРасчета = 0;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьРасчетыСКлиентами();
	
КонецПроцедуры

&НаКлиенте
Функция ВозможностьВводаПоШК()
	
	Результат = Истина;
	
	Если ТолькоПросмотр Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Если НЕ Результат Тогда
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Форма заблокирована. Ввод невозможен.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура РазвернутьСвернутьТЧНаСервере()
	
	РазвернутаТЧ = НЕ РазвернутаТЧ;
	
	ВидимостьЭлементов = НЕ РазвернутаТЧ;
	
	ЭтотОбъект.ПоложениеКоманднойПанели             = ?(ВидимостьЭлементов, ПоложениеКоманднойПанелиФормы.Авто, ПоложениеКоманднойПанелиФормы.Нет);
	Элементы.ГруппаОснование.Видимость            = ВидимостьЭлементов;
	Элементы.Шапка.Видимость                      = ВидимостьЭлементов;
	Элементы.ИтогоОплата.Видимость                = ВидимостьЭлементов;
	
	Элементы.РазвернутьСвернутьТЧ.Картинка = ?(ВидимостьЭлементов, БиблиотекаКартинок.РазвернутьТабличнуюЧасть, БиблиотекаКартинок.СвернутьТабличнуюЧасть);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтатусыСерийИПодарочныхСертификатов()
	
	Если НЕ ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры И НЕ Справочники.Номенклатура.ИспользуютсяПодарочныеСертификаты() Тогда
		Элементы.ТоварыСтатусыСерийИПодарочныхСертификатов.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьСерии(ТекущееПоле, ИмяТЧТовары, ИмяТЧСерии)
	
	ВводСерийРазрешен = Истина;
	ОбработкаТабличнойЧастиТоварыКлиент.ПроверитьВозможностьУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий, ВводСерийРазрешен, ИмяТЧТовары);
	Если НЕ ВводСерийРазрешен Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеИдентификатор = Элементы[ИмяТЧТовары].ТекущиеДанные.ПолучитьИдентификатор();
	Если ИмяТЧТовары = "Товары" Тогда
		ПараметрыФормыУказанияСерий = ПараметрыФормыУказанияСерий(ТекущиеДанныеИдентификатор);
	Иначе
		ПараметрыУказанияСерийПодарков = Новый Структура(ПараметрыУказанияСерий);
		ПараметрыУказанияСерийПодарков.Вставить("ИмяТЧТовары", "Подарки");
		ПараметрыУказанияСерийПодарков.Вставить("ИмяТЧСерии", "СерииПодарков");
		ПараметрыУказанияСерийПодарков.Вставить("ИмяКолонкиКоличество", "КоличествоУпаковок");
		ПараметрыФормыУказанияСерий = ПараметрыФормыУказанияСерий(ТекущиеДанныеИдентификатор, ПараметрыУказанияСерийПодарков);
	КонецЕсли;
	ПараметрыФормыУказанияСерий.Вставить("ТекущееПоле", ТекущееПоле);
	ПараметрыФормыУказанияСерий.Вставить("КоличествоЕдиницДоПересчета");
	ПараметрыФормыУказанияСерий.Вставить("ИмяТЧТовары", ИмяТЧТовары);
	ПараметрыФормыУказанияСерий.Вставить("ИмяТЧСерии", ИмяТЧСерии);
	
	ОбработчикУказанияСерий = Новый ОписаниеОповещения("ОбработатьУказаниеСерий", ЭтотОбъект, ПараметрыФормыУказанияСерий);
	РежимБлокировки = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму(ПараметрыФормыУказанияСерий.ИмяФормы, ПараметрыФормыУказанияСерий, ЭтотОбъект,,,, ОбработчикУказанияСерий, РежимБлокировки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПодарочныеСертификаты()
	
	ПересчетКоличества = Ложь;
	ОбработкаТабличнойЧастиТоварыКлиент.ВвестиСерийныеНомераНоменклатурыВТЧ(ЭтотОбъект,
		Объект.СерийныеНомера,
		Элементы.Товары.ТекущиеДанные);
		
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Процедура ВидОперацииПриИзмененииСервер()

	ПриИзмененииЧекККМПродажаСервер();
	ОбновитьСтатусыУказанияСерий();
	
	Объект.АналитикаХозяйственнойОперации = Справочники.АналитикаХозяйственныхОпераций.ПустаяСсылка();
	ЭтоВозврат = Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат;
	
	Если ЭтоВозврат Тогда
		Объект.АналитикаХозяйственнойОперации = ЗначениеНастроекПовтИсп.АналитикаХозяйственнойОперацииПоУмолчанию(Объект.АналитикаХозяйственнойОперации, Перечисления.ХозяйственныеОперации.ВозвратОтПокупателя);
	Иначе
		Объект.АналитикаХозяйственнойОперации = Справочники.АналитикаХозяйственныхОпераций.РеализацияТоваров;//ПредопределенноеЗначение("Справочник.АналитикаХозяйственныхОпераций.РеализацияТоваров");
	КонецЕсли;
	
	УстановитьВидимостьДоступностьРасчетыСКлиентами();
	
	Элементы.ЧекККМПродажа.Видимость = ЭтоВозврат;
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьВидНалогаВТЧСервер(Объект.Товары, 
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
	
	ЗаполнитьСистемуНалогообложения();
	
	ЗаполнитьСтавкиНДС();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДисконтнаяКартаСервер()

	Если НЕ Объект.ВладелецДисконтнойКарты = Объект.ДисконтнаяКарта.ВладелецКарты Тогда
	
		Объект.ВладелецДисконтнойКарты = Объект.ДисконтнаяКарта.ВладелецКарты
	
	КонецЕсли;
	
	Элементы.ПросмотрНакоплений.Доступность = ЗначениеЗаполнено(Объект.ДисконтнаяКарта);
	
	ПродажиСервер.ЗаполнитьКонтрагентаПоДисконтнойКарте(Объект, Объект.ДисконтнаяКарта);
	
	Если ПроверитьВозможностьПечатиБумажногоЧека() Тогда
		ПодключаемоеОборудованиеРТ.ЗаполнитьДанныеЭлектронногоЧека(Объект, Объект.ДисконтнаяКарта, ДанныеSMSИлиEmail);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКассаККМСервер()
	
	Если НЕ ЗначениеЗаполнено(Объект.КассаККМ) Тогда
		ПараметрыКассыККМ = Неопределено;
		Возврат;
	КонецЕсли;
	
	НДСПересчитан 			= Ложь;
	ПерезаполнитьСНО 		= Ложь;
	ИзменилсяМагазин		= Ложь;
	ИзмениласьОрганизация	= Ложь;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Магазин", 									"Магазин");
	СтруктураРеквизитов.Вставить("ПравилоЦенообразования", 						"Магазин.ПравилоЦенообразования");
	СтруктураРеквизитов.Вставить("Владелец", 									"Владелец");
	СтруктураРеквизитов.Вставить("РабочееМесто", 								"РабочееМесто");
	СтруктураРеквизитов.Вставить("ПодключаемоеОборудование", 					"ПодключаемоеОборудование");
	СтруктураРеквизитов.Вставить("ТипОборудования", 							"ПодключаемоеОборудование.ТипОборудования");
	СтруктураРеквизитов.Вставить("ЭлектронныйЧекEmailПередаютсяПрограммой1С", 	"ЭлектронныйЧекEmailПередаютсяПрограммой1С");
	СтруктураРеквизитов.Вставить("ЭлектронныйЧекSMSПередаютсяПрограммой1С", 	"ЭлектронныйЧекSMSПередаютсяПрограммой1С");
	
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.КассаККМ, СтруктураРеквизитов);
	
	ПараметрыКассыККМ = ЗначениеНастроекВызовСервера.ПараметрыКассыККМ(Объект.КассаККМ);
	
	Если НЕ Объект.Магазин = РеквизитыКассыККМ.Магазин Тогда
		Объект.Магазин = РеквизитыКассыККМ.Магазин;
		ИзменилсяМагазин = Истина;
	КонецЕсли;
	
	Если НЕ Объект.Организация = РеквизитыКассыККМ.Владелец Тогда
		Объект.Организация = РеквизитыКассыККМ.Владелец;
		ИзмениласьОрганизация = Истина;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	
	Если ИзменилсяМагазин ИЛИ ИзмениласьОрганизация Тогда
		
		СтруктураДействий.Вставить("ЗаполнитьВидНалога", 
			ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
			
		ПерезаполнитьСНО = Истина;
		
	КонецЕсли;
	
	Если ИзменилсяМагазин Тогда
		
		Если НЕ Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда
			
			Объект.ЦенаВключаетНДС = ОбщегоНазначенияРТ.ЗначениеРеквизитаВПривилегированномРежиме(
				РеквизитыКассыККМ.ПравилоЦенообразования, "ЦенаВключаетНДС");
				
			СтруктураПараметровСтавкиНДС = Новый Структура;
			СтруктураПараметровСтавкиНДС.Вставить("Дата"       , Объект.Дата);
			СтруктураПараметровСтавкиНДС.Вставить("Организация", Объект.Организация);
			СтруктураПараметровСтавкиНДС.Вставить("НеобходимоОбработатьВсюТЧ", Истина);
				
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", СтруктураПараметровСтавкиНДС);
				
			СтруктураПараметровПересчетаНДС = ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект);
			СтруктураПараметровПересчетаНДС.Вставить("НеобходимоОбработатьВсюТЧ", Истина);
				
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПараметровПересчетаНДС);
				
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
			
			СтруктураПараметровДействия = Новый Структура;
			СтруктураПараметровДействия.Вставить("Магазин",      РеквизитыКассыККМ.Магазин);
			СтруктураПараметровДействия.Вставить("РабочееМесто", РеквизитыКассыККМ.РабочееМесто);
			СтруктураПараметровДействия.Вставить("Организация",  РеквизитыКассыККМ.Владелец);
			СтруктураДействий.Вставить("ЗаполнитьСкладПродажи",  СтруктураПараметровДействия);
			
			НДСПересчитан = Истина;
				
		КонецЕсли;
		
		ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(Документы.ЧекККМ.ПараметрыУказанияСерий(Объект));
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Подарки);
		
	КонецЕсли;
	
	Если ИзмениласьОрганизация Тогда
		
		Если НЕ НДСПересчитан Тогда
			
			СтруктураПараметровСтавкиНДС = Новый Структура;
			СтруктураПараметровСтавкиНДС.Вставить("Дата"       , Объект.Дата);
			СтруктураПараметровСтавкиНДС.Вставить("Организация", Объект.Организация);
			СтруктураПараметровСтавкиНДС.Вставить("НеобходимоОбработатьВсюТЧ", Истина);
			
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", СтруктураПараметровСтавкиНДС);
			
			СтруктураПараметровПересчетаНДС = ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект);
			СтруктураПараметровПересчетаНДС.Вставить("НеобходимоОбработатьВсюТЧ", Истина);
			
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПараметровПересчетаНДС);
			Если НЕ ПродажаПоЗаказу Тогда
				СтруктураДействий.Вставить("ПересчитатьСумму");
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	СтруктураТЧ = Новый Структура;
	СтруктураТЧ.Вставить("СтрокиТЧ" , Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ПриИзмененииРеквизитовВТЧСервер(СтруктураТЧ, СтруктураДействий, Неопределено);
	ОбработкаТабличнойЧастиТоварыСервер.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	РабочееМесто 			= РеквизитыКассыККМ.РабочееМесто;
	ТипОборудованияКассыККМ = РеквизитыКассыККМ.ТипОборудования;
	
	ИспользоватьАссортимент = АссортиментСервер.ПолучитьФункциональнуюОпциюКонтроляАссортимента(Объект.Магазин);
	
	ВозможностьЭлектронногоПлатежа = ПодключаемоеОборудованиеРТ.ВозможностьЭлектронногоПлатежа(РеквизитыКассыККМ.ПодключаемоеОборудование);
	Если ВозможностьЭлектронногоПлатежа Тогда
		ПередачаEmailЧерезПрограмму = РеквизитыКассыККМ.ЭлектронныйЧекEmailПередаютсяПрограммой1С;
		ПередачаSMSЧерезПрограмму   = РеквизитыКассыККМ.ЭлектронныйЧекSMSПередаютсяПрограммой1С;
	КонецЕсли;
	
	Элементы.КомандаEmail.Видимость = ВозможностьЭлектронногоПлатежа;
	Элементы.КомандаSMS.Видимость   = ВозможностьЭлектронногоПлатежа;
	Элементы.ГруппаДанныеSMSИEmail.Видимость = ВозможностьЭлектронногоПлатежа;
	
	Если НЕ ВозможностьЭлектронногоПлатежа Тогда
		
		Объект.Телефон = "";
		Объект.АдресЭП = "";
		
	КонецЕсли;
	
	Если ПерезаполнитьСНО Тогда
		
		ОрганизацияИспользовалаЕНВД();
		ПроверитьВозможностьРазныхНалоговыхРежимовСкладов();
		ЗаполнитьСистемуНалогообложения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТоварыКоличестваУпаковок(ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий, Объект, ПродажаПоЗаказу ИЛИ ДокументРасчетаЗаполнен);
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	Если ПродажаПоЗаказу 
		ИЛИ ДокументРасчетаЗаполнен Тогда
		ОбработкаТабличнойЧастиТоварыКлиентСервер.РаспределитьСуммуИзмененийСтрокиТоваровНаТабличнуюЧастьСкидки(
			Объект.СкидкиНаценки, 
			ТекущаяСтрока.КлючСвязи, 
			ТекущаяСтрока.СуммаАвтоматическойСкидки)
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииЧекККМПродажаСервер()
	
	Если НЕ Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда
		ЦенаВключаетНДС = ОбщегоНазначенияРТ.ЗначениеРеквизитаВПривилегированномРежиме(Объект.Магазин.ПравилоЦенообразования, "ЦенаВключаетНДС");
		Объект.ЦенаВключаетНДС = ЦенаВключаетНДС;
		Элементы.ОплатаЗаполнитьОплатыПоДокументуПродажи.Доступность = Ложь;
	Иначе
		Если ЗначениеЗаполнено(Объект.ЧекККМПродажа) Тогда
			Объект.ЦенаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЧекККМПродажа, "ЦенаВключаетНДС");
			Элементы.ОплатаЗаполнитьОплатыПоДокументуПродажи.Доступность = Истина;
		Иначе
			Элементы.ОплатаЗаполнитьОплатыПоДокументуПродажи.Доступность = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ДисконтнаяКарта) Тогда
			Объект.ДисконтнаяКарта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЧекККМПродажа, "ДисконтнаяКарта");
			Если ЗначениеЗаполнено(Объект.ДисконтнаяКарта) Тогда
				ПриИзмененииДисконтнаяКартаСервер();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьВидНалогаВТЧСервер(Объект.Товары, 
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
	
	ЗаполнитьСистемуНалогообложения();
	
	ЗаполнитьСтавкиНДС();
	
	Объект.СерийныеНомера.Очистить();
	Объект.ПогашениеПодарочныхСертификатов.Очистить();
	ЗапасыСервер.УдалитьПодарочныеСертификаты(Объект.Товары);
	
	РаботаСПодарочнымиСертификатами.ДополнитьТоварыОстаткамиПодарочнхСертификатов(Объект, Объект.ЧекККМПродажа);
	
	УстановитьКомандыВидовОплаты();
	
КонецПроцедуры

&НаСервере
Функция ЕстьПравоИзменятьЦену()
	
	Возврат УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(ПланыВидовХарактеристик.ПраваПользователей.ИзменятьЦену);
	
КонецФункции

&НаСервере
Процедура УстановитьАвтоОтметкуТЧТовары()
	
	ЕстьВозможностьПустойТЧТовары = Документы.ЧекККМ.ВозможностьПустойТЧТовары(Объект);
	Элементы.Товары.АвтоОтметкаНезаполненного = НЕ ЕстьВозможностьПустойТЧТовары;
	Элементы.Товары.ОтметкаНезаполненного     = НЕ ЕстьВозможностьПустойТЧТовары;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриИзмененииТоварыНоменклатура(ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	КонецЕсли;
	
	РозничныеПродажиКлиент.ОчиститьМарки(Объект, ТекущаяСтрока);
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		РозничноеВыбытиеИСМПТККлиентПереопределяемый.ОчиститьМаркиИСМПТК(Объект, ТекущаяСтрока);
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
	СтруктураДействий = Новый Структура;
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ДополнитьСтруктуруДействиямиПриИзмененииЭлемента(Объект, СтруктураДействий, "Номенклатура");
	
	СтруктураДействий.Вставить("ПроверитьСерийныеНомераПоВладельцу",
		ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПроверкиСерийныхНомеровПоВладельцу(ТекущаяСтрока, Объект.СерийныеНомера));
	Если ИспользоватьАссортимент Тогда
		СтруктураДействий.Вставить("ПроверитьАссортиментСтроки", АссортиментКлиентСервер.ПараметрыПроверкиАссортимента(Объект, Истина));
	КонецЕсли;
	СтруктураДействий.Вставить("ПроверитьЗапретРозничнойПродажи", СкидкиНаценкиКлиент.ПараметрыПроверкиЗапретаРозничнойПродажи(Объект));
	
	СтруктураДействий.Вставить("ОчиститьДанныеПоЗаказу");
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	СтруктураПараметровДействия = Новый Структура;
	СтруктураПараметровДействия.Вставить("Магазин", Объект.Магазин);
	СтруктураПараметровДействия.Вставить("РабочееМесто", РабочееМесто);
	СтруктураПараметровДействия.Вставить("Организация", Объект.Организация);
	
	СтруктураДействий.Вставить("ЗаполнитьСкладПродажи", СтруктураПараметровДействия);
	
	СтруктураДействий.Вставить("ЗаполнитьВидНалога", 
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
	
	СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", ОбработкаТабличнойЧастиТоварыКлиент.СтруктураЗаполненияЦеныПродажиВСтрокеТЧ(Объект, Истина));
	СтруктураДействий.Вставить("ПересчитатьЦенуСУчетомАгентскогоВознаграждения", Новый Структура("Цена", ТекущаяСтрока.Цена));
	
	СтруктураПараметровСтавкиНДС = Новый Структура;
	СтруктураПараметровСтавкиНДС.Вставить("Дата"       , Объект.Дата);
	СтруктураПараметровСтавкиНДС.Вставить("Организация", Объект.Организация);
	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", СтруктураПараметровСтавкиНДС);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС"           , ОбработкаТабличнойЧастиТоварыКлиент.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки"        , Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	
	СтруктураДействий.Вставить("ПроставитьПродавца", Объект.Продавец);
	
	ДополнитьСтруктуруДействиямиНДС(СтруктураДействий, ВестиУчетМаркируемойПродукцииИСМП);
	ДополнитьСтруктуруДействиямиГосИС(СтруктураДействий, Объект.ОперацияСДенежнымиСредствами, МаркировкаВключена);
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьКлючСвязи(Объект.Товары, ТекущаяСтрока, "КлючСвязи");
	
	ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
	Если ОбработкаТабличнойЧастиТоварыКлиент.НеобходимоОбновитьСтатусыСерий(
		Элементы.Товары, КэшированныеЗначения, ПараметрыУказанияСерий) Тогда
		
		ТекущаяСтрокаИдентификатор = ТекущаяСтрока.ПолучитьИдентификатор();
		
		ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(ТекущаяСтрокаИдентификатор, КэшированныеЗначения);
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(Элементы.Товары, КэшированныеЗначения,ПараметрыУказанияСерий);
		
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьСтатусСерийИПодарочныхСертификатов(ТекущаяСтрока);
	
	Если ПропуститьАвтоматическийРасчетСкидок Тогда
		ПропуститьАвтоматическийРасчетСкидок = Ложь;
	КонецЕсли;
	
	ПроверитьСистемуНалогообложения(Элементы.Товары.ТекущаяСтрока);
	//ПриИзмененииЭлементаГосИС();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	ПриИзмененииЭлементаИСМПТК();
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
	УстановитьДоступностьВидаОперации();
    
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Серии

&НаСервере
Процедура ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(ТекущаяСтрокаИдентификатор, КэшированныеЗначения)
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(Объект, 
				ПараметрыУказанияСерий, ТекущаяСтрокаИдентификатор, КэшированныеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусыУказанияСерийСервер()
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусыУказанияСерий()

	ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(Документы.ЧекККМ.ПараметрыУказанияСерий(Объект));
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);

КонецПроцедуры

&НаСервере
Процедура ОбработатьУказаниеСерийСервер(ПараметрыФормыУказанияСерий, Знач ПродажаПодарка = Ложь)
	
	СтруктураДействий = Новый Структура();
	
	Если ПараметрыФормыУказанияСерий.ИмяТЧТовары = "Товары" Тогда
		Если НЕ ПродажаПодарка Тогда
			СтруктураДействий.Вставить("ПересчитатьЦенуЗаУпаковку");
			СтруктураДействий.Вставить("ОбновлятьКоличествоТоваровПриРегистрацииСерий", Истина);
			ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(
				СтруктураДействий,
				Объект,
				ПродажаПоЗаказу ИЛИ ДокументРасчетаЗаполнен);
		КонецЕсли;
		
		ТекущиеПараметрыУказанияСерий = ПараметрыУказанияСерий;
	Иначе
		Если НЕ ПродажаПодарка Тогда
			СтруктураДействий.Вставить("ОбновлятьКоличествоТоваровПриРегистрацииСерий", Истина);
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		КонецЕсли;
		
		ТекущиеПараметрыУказанияСерий = Новый Структура(ПараметрыУказанияСерий);
		ТекущиеПараметрыУказанияСерий.Вставить("ИмяТЧТовары", "Подарки");
		ТекущиеПараметрыУказанияСерий.Вставить("ИмяТЧСерии", "СерииПодарков");
		ТекущиеПараметрыУказанияСерий.Вставить("ИмяПоляКоличество", "КоличествоУпаковок");
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.ОбработатьУказаниеСерий(
		Объект,
		ТекущиеПараметрыУказанияСерий,
		ПараметрыФормыУказанияСерий,
		СтруктураДействий);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыФормыУказанияСерий(ТекущиеДанныеИдентификатор, ПараметрыУказанияСерийВходящие = Неопределено)
	
	Если ПараметрыУказанияСерийВходящие = Неопределено Тогда
		Результат = ОбработкаТабличнойЧастиТоварыСервер.ПоместитьСерииВХранилище(Объект, ПараметрыУказанияСерий, ТекущиеДанныеИдентификатор, ЭтотОбъект);
	Иначе
		Результат = ОбработкаТабличнойЧастиТоварыСервер.ПоместитьСерииВХранилище(Объект, ПараметрыУказанияСерийВходящие, ТекущиеДанныеИдентификатор, ЭтотОбъект);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Запрашивает у пользователя скидку и применяет ее к табличной части Товары.
//
&НаСервере
Процедура НазначитьРучнуюСкидкуНаСервере(СуммаСкидкиНаценки)
	
	Если СуммаСкидкиНаценки <> 0 Тогда
		СкидкиНаценкиСервер.НазначитьРучнуюСкидку(Объект, "Товары", СуммаСкидкиНаценки, Истина, Истина);
	Иначе
		СкидкиНаценкиСервер.ОтменитьРучныеСкидки(Объект, "Товары", Истина, Истина);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект);
	
КонецПроцедуры

// Очищает ручные скидки в табличной части Товары.
//
&НаСервере
Процедура ОтменитьРучныеСкидкиНаСервере()
	
	СкидкиНаценкиСервер.ОтменитьРучныеСкидки(Объект, "Товары", Истина, Истина);
	
	ОбработкаТабличнойЧастиТоварыСервер.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиСкидкиПодаркиВТоварыСервер()
	
	ЕстьОшибки = Ложь;
	СкидкиНаценкиСерверПереопределяемый.СообщитьОбОстающихсяПодарках(Объект, ЕстьОшибки);
	Если НЕ ЕстьОшибки Тогда
		СкидкиНаценкиСерверПереопределяемый.ПеренестиСкидкиПодаркиВТовары(Объект, Объект.ЦенаВключаетНДС,,,, Истина);
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьСуммуВсегоВТаблице(Объект.Товары, Объект.ЦенаВключаетНДС);
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Товары);
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Подарки);
		
		ПерезаполнитьПризнакиМаркировки();
		
		//ИнтеграцияИСМПТК_РозничноеВыбытие
		Если УчетМаркировкаИСМПТК 
			И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда 
			ПерезаполнитьПризнакиМаркировкиИСМПТК();
			ЗаполнитьПризнакКодаМаркировкиВТоварах();
		КонецЕсли;
		//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
		СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Подарки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСкидки(Отказ, ДополнительныеПараметры = Неопределено, ПроверятьРасчет = Истина)
	
	Если ИспользоватьАвтоматическиеСкидкиВПродажах
		И ((НЕ Объект.СкидкиРассчитаны) ИЛИ (НЕ ПроверятьРасчет))
		И НЕ ПродажаПоЗаказу
		И НЕ ДокументРасчетаЗаполнен
		И НЕ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат")
		И Объект.Товары.Количество() > 0 Тогда
		
		РассчитатьСкидкиНаценкиКлиент(ДополнительныеПараметры);
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
			РасчитатьСкидкуПриВозврате();
				
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСообщенияПослеОформленияЧека()

	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ПрименятьКОбъекту",                Ложь);
	СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет",      Ложь);
	СтруктураПараметры.Вставить("ВосстанавливатьУправляемыеСкидки", Ложь);
	СтруктураПараметры.Вставить("УправляемыеСкидки",                УправляемыеСкидки);
	СтруктураПараметры.Вставить("ТолькоСообщенияПослеОформления",   Истина);
	СтруктураПараметры.Вставить("ПеренестиСкидкиПодаркиВТовары");
	СтруктураПараметры.Вставить("КонтролироватьОстаткиТоваров", 	КонтролироватьОстаткиТоваров);
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО.
		РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	Иначе
		РабочееМесто = ""
	КонецЕсли;
	
	СтруктураПараметры.Вставить("РабочееМесто", РабочееМесто);
	
	РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры);
	
	Если ВывестиСообщения Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("АдресВоВременномХранилище", АдресПримененныхСкидокВоВременномХранилище);
		ОткрытьФорму("ОбщаяФорма.ФормаВыводаСообщений", ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСкидкиНаценкиКлиент(ДополнительныеПараметры = Неопределено)
	
	Если Объект.Товары.Количество() = 0 Тогда
		ОтменитьСкидки();
		ОтменитьОплатуБонусами();
		Возврат;
	КонецЕсли;
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ПрименятьКОбъекту",                Истина);
	СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет",      Ложь);
	СтруктураПараметры.Вставить("ВосстанавливатьУправляемыеСкидки", Истина);
	СтруктураПараметры.Вставить("УправляемыеСкидки",                УправляемыеСкидки);
	СтруктураПараметры.Вставить("ТолькоСообщенияПослеОформления",   Ложь);
	СтруктураПараметры.Вставить("ПорядковыйНомерПродажи",           ПорядковыйНомерПродажи);
	СтруктураПараметры.Вставить("ПеренестиСкидкиПодаркиВТовары");
	СтруктураПараметры.Вставить("КонтролироватьОстаткиТоваров", 	КонтролироватьОстаткиТоваров);
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО.
		РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	Иначе
		РабочееМесто = ""
	КонецЕсли;
	
	СтруктураПараметры.Вставить("РабочееМесто", РабочееМесто);
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Если ДополнительныеПараметры.Свойство("БудетОплатаБонусами") Тогда
			СтруктураПараметры.Вставить("БудетОплатаБонусами", ДополнительныеПараметры.БудетОплатаБонусами);
		КонецЕсли;
	КонецЕсли;
	
	Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры);
	
	Если ВывестиСообщения Тогда
		Если ДополнительныеПараметры = Неопределено Тогда
			ДополнительныеПараметры = Новый Структура;
			ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеРасчетСкидокКлиент", ЭтотОбъект, ДополнительныеПараметры);
		Иначе
			ОбработчикОповещения = Новый ОписаниеОповещения(ДополнительныеПараметры.ИмяОбработкиОповещения, ЭтотОбъект, ДополнительныеПараметры);
			ДополнительныеПараметры.Вставить("ВыведеныСообщения", Истина);
		КонецЕсли;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("АдресВоВременномХранилище", АдресПримененныхСкидокВоВременномХранилище);
		ОткрытьФорму("ОбщаяФорма.ФормаВыводаСообщений", ПараметрыФормы, ЭтотОбъект, , , , ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды "РассчитатьСкидкиНаценки".
//
&НаСервере
Процедура РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры)
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ПеренестиДоговорКотрагентаВТЧ(ЭтотОбъект);
	
	ПримененныеСкидки = СкидкиНаценкиСерверПереопределяемый.Рассчитать(Объект, СтруктураПараметры);
	
	ВывестиСообщения = Ложь;
	
	Если ЗначениеЗаполнено(АдресПримененныхСкидокВоВременномХранилище) Тогда
		УдалитьИзВременногоХранилища(АдресПримененныхСкидокВоВременномХранилище);
	КонецЕсли;
	АдресПримененныхСкидокВоВременномХранилище = ПоместитьВоВременноеХранилище(ПримененныеСкидки, УникальныйИдентификатор);
	
	Если ПримененныеСкидки.Свойство("ТаблицаПодарковНаВыбор") Тогда
		АдресПодарковНаВыбор = ПоместитьВоВременноеХранилище(ПримененныеСкидки.ТаблицаПодарковНаВыбор, УникальныйИдентификатор);
		ВыдатьПодаркиНаВыбор = Истина;
	Иначе
		АдресПодарковНаВыбор = "";
		ВыдатьПодаркиНаВыбор = Ложь;
	КонецЕсли;
	
	Если ПримененныеСкидки.ТаблицаСообщений.Количество() > 0 Тогда
		ВывестиСообщения = Истина;
	КонецЕсли;
	
	Модифицированность = НЕ СтруктураПараметры.ТолькоСообщенияПослеОформления;
	
	Если Объект.ВидОперации <> Перечисления.ВидыОперацийЧекККМ.Возврат
		И НЕ СтруктураПараметры.ТолькоСообщенияПослеОформления Тогда
		СкидкиНаценкиСервер.ОкруглитьЧекВПользуПокупателя(Объект, "Товары", "СкидкиНаценки");
	КонецЕсли;
	
	СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
	БонусныеБаллыСервер.ОбновитьОтображениеБонусов(Объект);	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Подарки);
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	ПараметрыУказанияСерийПодарков = Новый Структура(ПараметрыУказанияСерий);
	ПараметрыУказанияСерийПодарков.Вставить("ИмяТЧТовары", "Подарки");
	ПараметрыУказанияСерийПодарков.Вставить("ИмяТЧСерии", "СерииПодарков");
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерийПодарков);
	
	ОбработкаТабличнойЧастиТоварыСервер.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект);
	
	Если НЕ Объект.СкидкиРассчитаны Тогда
	
		Объект.СкидкиРассчитаны = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Проведение

&НаСервере
Процедура ПровестиИзФормыСервер(Отказ, ЗапускатьОбработкуПослеЗаписи = Ложь, Объект_Ссылка = Неопределено, Текст)
	
	ЗаписываемыйОбъект = РеквизитФормыВЗначение("Объект");
	
	СтрокаОшибки = "";
	Если ЗаписываемыйОбъект.ПроверитьЗаполнение() Тогда
		Попытка
			Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаСеанса()) Тогда
				ЗаписываемыйОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
			Иначе
				ЗаписываемыйОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			КонецЕсли;
		Исключение
			Отказ = Истина;
		КонецПопытки;
		
		Если ЗаписываемыйОбъект.ДополнительныеСвойства.Свойство("Отказ") Тогда
			Если ЗаписываемыйОбъект.ДополнительныеСвойства.Отказ Тогда
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗаписываемыйОбъект.ДополнительныеСвойства.Свойство("ТаблицыДляДвижений")
			И ЗаписываемыйОбъект.ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПопыткиПродажПревышающихОстаток") Тогда
			
			ПродажиСервер.ОтразитьПопыткиПродаж(ЗаписываемыйОбъект.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПопыткиПродажПревышающихОстаток, ЗаписываемыйОбъект.Дата);
			
		КонецЕсли;
		
		Объект_Ссылка = ЗаписываемыйОбъект.Ссылка;
		
		Если Отказ Тогда
		
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось провести ""%1"". '"),
					Строка(ЗаписываемыйОбъект)
					);
		ИначеЕсли ЗапускатьОбработкуПослеЗаписи Тогда
			
			ЗначениеВРеквизитФормы(ЗаписываемыйОбъект, "Объект");
			
			ПослеЗаписиНаСервере(Объект, РежимЗаписиДокумента.Проведение);
			
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий, Объект, ПересчитыватьТолькоСуммуСкидки)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	Если ПересчитыватьТолькоСуммуСкидки Тогда
		СтруктураПересчетаСкидок = Новый Структура;
		СтруктураПересчетаСкидок.Вставить("Очищать", Ложь);
		СтруктураПересчетаСкидок.Вставить("ПересчитыватьСуммуСкидки", Истина);
		
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки"        , СтруктураПересчетаСкидок);
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", СтруктураПересчетаСкидок);
	Иначе
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки"        , Новый Структура("Очищать", Ложь));
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОплатыПоДокументуПродажиСервер()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
	КонецЕсли;
	
	ТаблицаОплатыПродажи = Объект.ЧекККМПродажа.Оплата.Выгрузить();
	ТаблицаОплатыПродажи.ЗаполнитьЗначения(Ложь, "ДанныеПереданыВБанк");
	МассивСтрокБонусов = ТаблицаОплатыПродажи.НайтиСтроки(Новый Структура("ВидОплаты", Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой));
	Для Каждого СтрокаБонуса Из МассивСтрокБонусов Цикл
		ТаблицаОплатыПродажи.Удалить(СтрокаБонуса);
	КонецЦикла;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ДанныеПереданыВБанк", Истина);
	
	СтрокиПереданныеВБанк = Объект.Оплата.НайтиСтроки(СтруктураПоиска);
	
	СтруктураПоиска = Новый Структура("ВидОплаты, ЭквайринговыйТерминал, Сумма, ПроцентКомиссии, СуммаКомиссии, СсылочныйНомер, НомерЧекаЭТ, НомерПлатежнойКарты");
	
	Индекс = 0 ;
	Для каждого СтрокаПереданнаяВБанк Из СтрокиПереданныеВБанк Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаПереданнаяВБанк);
		
		СтрокиТаблицыОплатыПродажи = ТаблицаОплатыПродажи.НайтиСтроки(СтруктураПоиска);
		Если СтрокиТаблицыОплатыПродажи.Количество() > 0 Тогда
			Для каждого СтрокаТаблицыОплатыПродажи Из СтрокиТаблицыОплатыПродажи Цикл
				СтрокаТаблицыОплатыПродажи.ДанныеПереданыВБанк = Истина;
			КонецЦикла;
		Иначе
			
			СтрокаТаблицыОплатыПродажи = ТаблицаОплатыПродажи.Вставить(Индекс);
			Индекс = Индекс + 1;
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыОплатыПродажи, СтруктураПоиска);
			СтрокаТаблицыОплатыПродажи.ДанныеПереданыВБанк = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Объект.Оплата.Загрузить(ТаблицаОплатыПродажи);
	
	Для каждого ТекущаяСтрока Из Объект.Оплата Цикл
		ТекущаяСтрока.ПроцентКомиссии = ЭквайрингВызовСервера.ПолучитьПроцентКомиссииПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
		ТекущаяСтрока.СуммаКомиссии   = ТекущаяСтрока.Сумма * ТекущаяСтрока.ПроцентКомиссии / 100;
	КонецЦикла;
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьТипыОплатВТЧСервер(Объект);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоДополнительнымПравам(ТолькоЦенообразование)
	
	Элементы.ТоварыСуммаРучнойСкидки.ТолькоПросмотр = ЭтоВозврат;
	Элементы.ТоварыПроцентРучнойСкидки.ТолькоПросмотр = ЭтоВозврат;
	Элементы.ТоварыНазначитьРучнуюСкидку.Доступность = НЕ ЭтоВозврат;
	Элементы.ТоварыОтменитьРучныеСкидки.Доступность = НЕ ЭтоВозврат;
	Элементы.ТоварыЦена.ТолькоПросмотр = ЭтоВозврат;
	Элементы.ТоварыСумма.ТолькоПросмотр = ЭтоВозврат;
	Элементы.ПодаркиЦена.ТолькоПросмотр = ЭтоВозврат;
	Элементы.ПодаркиСумма.ТолькоПросмотр = ЭтоВозврат;
	
	УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
		Элементы.ТоварыСуммаРучнойСкидки.ТолькоПросмотр,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьРучнуюСкидку);
		
	УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
		Элементы.ТоварыПроцентРучнойСкидки.ТолькоПросмотр,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьРучнуюСкидку);
		
	УправлениеПользователями.УстановитьДоступностьДляРеквизитовТабличнойЧасти(
		Элементы.ТоварыНазначитьРучнуюСкидку.Доступность,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьРучнуюСкидку);
	
	УправлениеПользователями.УстановитьДоступностьДляРеквизитовТабличнойЧасти(
		Элементы.ТоварыОтменитьРучныеСкидки.Доступность,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьРучнуюСкидку);
	
	УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
		Элементы.ТоварыЦена.ТолькоПросмотр,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьЦену);
		
	УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
		Элементы.ТоварыСумма.ТолькоПросмотр,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьЦену);
		
	УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
		Элементы.ПодаркиЦена.ТолькоПросмотр,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьЦену);
		
	УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
		Элементы.ПодаркиСумма.ТолькоПросмотр,
		ПланыВидовХарактеристик.ПраваПользователей.ИзменятьЦену);
	
	Если НЕ ТолькоЦенообразование Тогда
		УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
			Элементы.Продавец.ТолькоПросмотр,
			ПланыВидовХарактеристик.ПраваПользователей.ИзменятьПродавца);
			
		УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
			Элементы.ТоварыПродавец.ТолькоПросмотр,
			ПланыВидовХарактеристик.ПраваПользователей.ИзменятьПродавца);
			
		УправлениеПользователями.УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(
			Элементы.Дата.ТолькоПросмотр,
			ПланыВидовХарактеристик.ПраваПользователей.ИзменятьДату);
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста

Процедура ОбновитьИтоговыеПоказателиКлиентСервер(Форма, РассчитатьСдачу = Истина)
	
	Объект = Форма.Объект;
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСуммыПодвала(Объект.Товары, Объект.ЦенаВключаетНДС, Форма.СуммаВсего);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьСуммуВсегоВТаблице(Объект.Товары, Объект.ЦенаВключаетНДС);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСуммыПодвала(Объект.Оплата, Истина, Форма.СуммаОплачено);
	
	СуммаРучнойСкидки = Объект.Товары.Итог("СуммаРучнойСкидки");
	СуммаАвтоматическойСкидки = Объект.Товары.Итог("СуммаАвтоматическойСкидки");
	СуммаСкидкиОплатыБонусом = Объект.Товары.Итог("СуммаСкидкиОплатыБонусом");
	СкидкиНаценкиИтогСумма = СуммаАвтоматическойСкидки + СуммаРучнойСкидки + СуммаСкидкиОплатыБонусом;
	
	Форма.СкидкиНаценкиИтогСумма = СкидкиНаценкиИтогСумма;
	Форма.СуммаБезСкидки = Форма.СуммаВсего + Форма.СкидкиНаценкиИтогСумма;
	Форма.СуммаКОплате = Форма.СуммаВсего - Форма.СуммаОплачено;
	
	Если РассчитатьСдачу Тогда
		РассчитатьСдачуКлиентСервер(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПлатежнойКартойЧерезЭквайринговыйТерминал(ВыбраннаяСтрока, СуммаОперации, ПределСуммы)
	
	ИдентификаторУстройстваЭТ = Неопределено;
	ИдентификаторУстройстваФР = Неопределено;
	ОписаниеОшибки            = "";
	
	НомерКарты          = "";
	НомерСсылкиОперации = "";
	НомерЧекаЭТ         = "";
	СтрокаСлипЧека      = "";
	ВидКарты            = "";
	НомерЧека           = Объект.Номер;
	
	Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	ИндексСтроки = Объект.Оплата.Индекс(ВыбраннаяСтрока);
	
	Если Объект.Проведен Тогда
		РезультатПроведения = Записать(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный));
	Иначе
		РезультатПроведения = Записать(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Оперативный));
	КонецЕсли;
	
	Если НЕ РезультатПроведения Тогда
		Возврат;
	КонецЕсли;
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "Справочник.ПодключаемоеОборудование.Форма.ФормаАвторизацииЭТ.Открытие");
             
	ВыбраннаяСтрока = Объект.Оплата[ИндексСтроки];
		
	СтруктураЭквайринговыйТерминал = СтруктураДанныхЭТ(ВыбраннаяСтрока.ЭквайринговыйТерминал);
	ИдентификаторУстройстваЭТ = СтруктураЭквайринговыйТерминал.ПодключаемоеОборудование;
	ИдентификаторУстройстваФР = ПараметрыКассыККМ.ИдентификаторУстройства;
	
	Если НЕ ИспользоватьПодключаемоеОборудование Тогда
		ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования = Истина;
		СтруктураЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования = Истина;
	КонецЕсли;
	
	ИспользоватьКассуККМБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	ИспользоватьБезПодключенияОборудованияЭТ = СтруктураЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования;
	
	// Предварительно авторизуем операцию.
	ПараметрыФормы = Новый Структура();
	
	ПараметрыФормы.Вставить("Сумма"      , СуммаОперации);
	ПараметрыФормы.Вставить("ПределСуммы", ПределСуммы);
	ПараметрыФормы.Вставить("ЗапретРедактированияСуммы", Истина);
	
	Если ИспользоватьБезПодключенияОборудованияЭТ Тогда
		ПараметрыФормы.Вставить("ПоказыватьНомерКарты", Истина);   
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ПараметрДействия = Новый Структура;
	ПараметрДействия.Вставить("ИспользоватьКассуККМБезПодключенияОборудования", ИспользоватьКассуККМБезПодключенияОборудования);
	ПараметрДействия.Вставить("ИдентификаторУстройстваФР", ИдентификаторУстройстваФР);
	ПараметрДействия.Вставить("ИспользоватьБезПодключенияОборудованияЭТ", ИспользоватьБезПодключенияОборудованияЭТ);
	ПараметрДействия.Вставить("ИдентификаторУстройстваЭТ", ИдентификаторУстройстваЭТ);
	
	ПараметрДействия.Вставить("ТекстОписаниеОшибки", "");
	ПараметрДействия.Вставить("СтрокаСлипЧека", СтрокаСлипЧека);
	ПараметрДействия.Вставить("ВыбраннаяСтрокаИдентификатор", ВыбраннаяСтрока.ПолучитьИдентификатор());
	ПараметрДействия.Вставить("СуммаОперации", СуммаОперации);
	ПараметрДействия.Вставить("НомерКарты", НомерКарты);
	ПараметрДействия.Вставить("НомерСсылкиОперации", НомерСсылкиОперации);
	ПараметрДействия.Вставить("НомерЧекаЭТ", НомерЧекаЭТ);
	ПараметрДействия.Вставить("НомерЧека", НомерЧека);
	ДополнительныеПараметры.Вставить("ПараметрДействия", ПараметрДействия);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуАвторизацииЭТ", ЭтотОбъект, ДополнительныеПараметры);
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.ФормаАвторизацииЭТ", ПараметрыФормы,,,,, ОбработчикОповещения, Режим); 
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюКлиент(Отказ, РежимЗаписи)
	
	// Если документ проводится, рассчитаем скидки.
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ПроверитьСкидки(Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПересчитатьСуммуКомиссии(ТекущаяСтрока)
	
	ТекущаяСтрока.СуммаКомиссии = ТекущаяСтрока.Сумма * ТекущаяСтрока.ПроцентКомиссии / 100;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПогашениеВХранилище(АдресВременногоХранилища)
	
	СуммаДокумента = ?(Объект.ЦенаВключаетНДС, Объект.Товары.Итог("Сумма"), Объект.Товары.Итог("Сумма") + Объект.Товары.Итог("СуммаНДС"));
	СуммаДругихОплат = 0;
	Для Каждого СтрокаОплаты Из Объект.Оплата Цикл
		Если Не СтрокаОплаты.ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом Тогда
			СуммаДругихОплат = СуммаДругихОплат + СтрокаОплаты.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ИтогоПогашения = 0;
	СуммаКПогашению = СуммаДокумента - СуммаДругихОплат;
	Если СуммаКПогашению = 0 Тогда
		Возврат ;
	КонецЕсли;
	Таблица_ПогашениеПодарочныхСертификатов = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Для Каждого СтрокаПогашения Из Таблица_ПогашениеПодарочныхСертификатов Цикл
		НоваяСтрокаПогашения = Объект.ПогашениеПодарочныхСертификатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПогашения, СтрокаПогашения);
		Если СтрокаПогашения.ЧастичноеПогашение Или СтрокаПогашения.ПроизвольныйНоминал Тогда
			СуммаПоСертификату = СтрокаПогашения.Остаток;
		Иначе
			СуммаПоСертификату = СтрокаПогашения.Номинал;
		КонецЕсли;
		СуммаПогашенияСертификата = Мин(СуммаКПогашению, СуммаПоСертификату);
		НоваяСтрокаПогашения.СуммаПогашенияСертификата = СуммаПогашенияСертификата;
		ИтогоПогашения = ИтогоПогашения + СуммаПогашенияСертификата;
		
		СуммаКПогашению = СуммаКПогашению - СуммаПогашенияСертификата;
		Если СуммаКПогашению = 0 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Таблица_ПогашениеПодарочныхСертификатов.Количество() = 0 Тогда
	
		СтрокиКУдалению = Объект.Оплата.НайтиСтроки(Новый Структура("ВидОплаты", Справочники.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом));
		Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
			Объект.Оплата.Удалить(Объект.Оплата.Индекс(СтрокаКУдалению));
		
		КонецЦикла;
	Иначе
		СтрокиТаблицы = Объект.Оплата.НайтиСтроки(Новый Структура("ВидОплаты", Справочники.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом));
		Если СтрокиТаблицы.Количество() = 0  Тогда
			СтрокаТаблицы = Объект.Оплата.Добавить();
			СтрокаТаблицы.ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом;
		Иначе
			СтрокаТаблицы = СтрокиТаблицы[0];
		КонецЕсли;
		
		СтрокаТаблицы.Сумма = Мин(СуммаДокумента, ИтогоПогашения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураДанныхЭТ(Терминал)
	
	Возврат РозничныеПродажиСервер.СтруктураДанныхЭТ(Терминал);
	
КонецФункции

// Функция считает сумму, оплаченную безналичными.
//
// Возвращаемое значение:
//  Число - сумма, оплаченная безналичными.
//
&НаСервере
Функция СуммаБезналичнойОплаты() 

	СуммаБезналичнойОплаты = 0;
	Для Каждого ТекОплата Из Объект.Оплата Цикл
		Если ЗначениеЗаполнено(ТекОплата.ВидОплаты) И ТекОплата.ВидОплаты.ТипОплаты <> Перечисления.ТипыОплатЧекаККМ.Наличные 
		И НЕ ТекОплата.ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом Тогда
			СуммаБезналичнойОплаты = СуммаБезналичнойОплаты + ТекОплата.Сумма;
		КонецЕсли;
	КонецЦикла;

	Возврат СуммаБезналичнойОплаты;

КонецФункции

&НаСервере
// Функция считает сумму, оплаченную подарочными сертификатами.
//
// Возвращаемое значение:
//  Число - сумма, оплаченная подарочными сертификатами.
//
Функция СуммаОплатыПодарочнымиСертификатами() 

	СуммаОплаты = 0;
	Для Каждого ТекОплата Из Объект.Оплата Цикл
		Если ТекОплата.ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаПодарочнымСертификатом Тогда
			СуммаОплаты = СуммаОплаты + ТекОплата.Сумма;
		КонецЕсли;
	КонецЦикла;

	Возврат СуммаОплаты;

КонецФункции

&НаСервереБезКонтекста
Функция ТипОплатыПоВиду(ВидОплаты)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидОплаты, "ТипОплаты");

КонецФункции

&НаСервере
Функция ПоместитьПогашениеВХранилище()

	Возврат ОбщегоНазначенияРТ.ПоместитьВоВременноеХранилищеТабличнуюЧастьОбъекта(Объект, "ПогашениеПодарочныхСертификатов", УникальныйИдентификатор);

КонецФункции

&НаСервере
Функция ПоместитьСкидкиВХранилище()

	СтруктураТаблиц = Новый Структура;
	СтруктураТаблиц.Вставить("СкидкиНаценки", Объект.СкидкиНаценки.Выгрузить());
	СтруктураТаблиц.Вставить("Подарки", Объект.Подарки.Выгрузить());
	СтруктураТаблиц.Вставить("БонусныеБаллыКНачислению", Объект.БонусныеБаллыКНачислению.Выгрузить());
	
	Адрес = ПоместитьВоВременноеХранилище(СтруктураТаблиц, УникальныйИдентификатор);
	
	Возврат Адрес;

КонецФункции

#Область ПробитиеЧека

&НаКлиенте
Процедура ПробитьЧекВыполнить(ОповещениеПриЗавершении)
	
	ИдентификаторУстройстваФР = ПараметрыКассыККМ.ИдентификаторУстройства;
	
	ИспользоватьКассуККМБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	Если НЕ ИспользоватьПодключаемоеОборудование ИЛИ ИспользоватьКассуККМБезПодключенияОборудования Тогда
		
		Объект.НомерСменыККМ = 0;
		
		Если НомерДокументаКассыККМ[Объект.КассаККМ] <> Неопределено Тогда
			Объект.НомерЧекаККМ  = НомерДокументаКассыККМ[Объект.КассаККМ];
		Иначе
			Объект.НомерЧекаККМ  = ПорядковыйНомерПродажи;
		КонецЕсли;
		
		Объект.СтатусЧекаККМ = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробитый");
		Объект.Дата          = ОбщегоНазначенияКлиент.ДатаСеанса();
		
		Модифицированность = Истина;
		РезультатЗаписи = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
		Если РезультатЗаписи = Истина Тогда
			ПогаситьОднократныеСкидки();
			РезультатОбработкиНакоплений = СкидкиНаценкиВызовСервера.РезультатОбработкиНакопленийВДисконтномСервере(Объект.Ссылка);
			Если РезультатОбработкиНакоплений.Свойство("СообщениеПользователю") Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОбработкиНакоплений.СообщениеПользователю);
			КонецЕсли;
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, РезультатЗаписи = Истина);
		Возврат;
	КонецЕсли;
	
	Если ИдентификаторУстройстваФР <> Неопределено Тогда
		
		ЭтотОбъект.Доступность = Ложь;
		
		НомерЧека = НомерДокументаКассыККМ[Объект.КассаККМ] + 1;;
		ТекстСообщенияЕГАИС = НСтр("ru = ''");
		
		ОбщиеПараметры = ПодготовитьДанныеДляПробитияЧека(НомерЧека);
		
		ОбщиеПараметры.Вставить("ТекстСообщенияЕГАИС", ТекстСообщенияЕГАИС);
		ОбщиеПараметры.Вставить("ПечатьИзФормыЧека", Истина);
		
		Контекст = Новый Структура();
		Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Контекст.Вставить("ЕстьАлкогольнаяПродукцияЕГАИС", ЕстьАлкогольнаяПродукцияЕГАИС);
		
		Оповещение = Новый ОписаниеОповещения("ПробитьЧекВыполнитьЗавершение", ЭтотОбъект, Контекст);
		ПослеОткрытияЧека     = Новый ОписаниеОповещения("ПечатьЧека_ПослеОткрытияЧека", ЭтотОбъект, Контекст);
		ПослеОшибкиПечатиЧека = Новый ОписаниеОповещения("ПечатьЧека_ПослеОшибкиПечатиЧека", ЭтотОбъект, Контекст);
		
		МенеджерОборудованияКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(Оповещение, 
										УникальныйИдентификатор, 
										ОбщиеПараметры, 
										ИдентификаторУстройстваФР,
										,
										ПослеОткрытияЧека,
										ПослеОшибкиПечатиЧека);
		
	Иначе
		ТекстСообщения = НСтр("ru = 'Устройство для печати чеков не выбрано.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Ложь);
	КонецЕсли;
	
	ОбменССайтомРТВызовСервера.ЗаписатьСостояниеОплатыЗаказаПокупателя(Объект.ЗаказПокупателя, Объект.Ссылка);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПробитьЧекВыполнитьЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтотОбъект.Доступность = Истина;
	
	Если РезультатВыполнения.Результат Тогда
		
		// Установить полученное значение номера чека реквизиту документа.
		Объект.НомерСменыККМ = РезультатВыполнения.ВыходныеПараметры[0];
		Если ЗначениеЗаполнено(РезультатВыполнения.ВыходныеПараметры[1]) Тогда
			Объект.НомерЧекаККМ = РезультатВыполнения.ВыходныеПараметры[1];
		Иначе
			Объект.НомерЧекаККМ = НомерДокументаКассыККМ[Объект.КассаККМ];
		КонецЕсли;
		
		Объект.СтатусЧекаККМ = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробитый");
		Объект.Дата          = ОбщегоНазначенияКлиент.ДатаСеанса();
		Если Не ЗначениеЗаполнено(Объект.НомерЧекаККМ) Тогда
			Объект.НомерЧекаККМ = ПорядковыйНомерПродажи;
		КонецЕсли;
		
		Модифицированность = Истина;
		
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
		РезультатЗаписи = Записать(ПараметрыЗаписи);
		
		Если РезультатЗаписи Тогда
			ПогаситьОднократныеСкидки();
			РезультатОбработкиНакоплений = СкидкиНаценкиВызовСервера.РезультатОбработкиНакопленийВДисконтномСервере(Объект.Ссылка);
			Если РезультатОбработкиНакоплений.Свойство("СообщениеПользователю") Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОбработкиНакоплений.СообщениеПользователю);
			КонецЕсли;
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриЗавершении, РезультатЗаписи);
		
	Иначе
		ТекстСообщения = НСтр("ru = 'При печати чека произошла ошибка.
		                            |Дополнительное описание: %ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриЗавершении, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧека_ПослеОткрытияЧека(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ШапкаЧека = ПараметрыВыполнения.ВходныеПараметры;
	
	НомерСмены = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ПараметрыВыполнения.НомерСмены);
	
	Если НомерСмены <> 0 Тогда
		ШапкаЧека.НомерСмены = НомерСмены;
	КонецЕсли;
	
	НомерЧека = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ПараметрыВыполнения.НомерЧека);
	
	Если НомерЧека <> 0 Тогда
		ШапкаЧека.НомерЧека = НомерЧека;
	КонецЕсли;
	
	Если ПараметрыВыполнения.Свойство("ЗаводскойНомерФН") И ЗначениеЗаполнено(ПараметрыВыполнения.ЗаводскойНомерФН) Тогда
		ШапкаЧека.СерийныйНомер = ПараметрыВыполнения.ЗаводскойНомерФН;
	КонецЕсли;
	
	//Отключенный функционал
	Если Ложь И ДополнительныеПараметры.ЕстьАлкогольнаяПродукцияЕГАИС Тогда
		
		
		ПараметрыОперации = Новый Структура;
		ПараметрыОперации.Вставить("НомерСмены", ШапкаЧека.НомерСмены);
		ПараметрыОперации.Вставить("НомерЧека", ШапкаЧека.НомерЧека);
		ПараметрыОперации.Вставить("СерийныйНомер", ШапкаЧека.СерийныйНомер);
		
		//ИнтеграцияЕГАИСКлиент.ПередатьНемедленно(
		//	Объект.Ссылка,
		//	ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные"),
		//	ПараметрыОперации,
		//	Новый ОписаниеОповещения("ПослеПередачиЧекаЕГАИС", ЭтотОбъект, ПараметрыВыполнения));
		
	//Конец Отключенный функционал
	Иначе
		ВыполнитьОбработкуОповещения(ПараметрыВыполнения.ОповещениеПродолжения, ПараметрыВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧека_ПослеОшибкиПечатиЧека(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	Если НЕ Объект.ОперацияСДенежнымиСредствами Тогда
		ДеактивироватьСкидкиПоИдентификаторуЧека();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПробитьЧекОчередиВыполнитьЗавершение(РезультатВыполнения) Экспорт
	
	ЭтотОбъект.Доступность = Истина;
	
	Если РезультатВыполнения.ЧекПробит Тогда
		
		Модифицированность = Истина;
		
		ПогаситьОднократныеСкидки();
		РезультатОбработкиНакоплений = СкидкиНаценкиВызовСервера.РезультатОбработкиНакопленийВДисконтномСервере(Объект.Ссылка);
		Если РезультатОбработкиНакоплений.Свойство("СообщениеПользователю") Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОбработкиНакоплений.СообщениеПользователю);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается при нажатии кнопки "НапечататьЧек" командной панели.
//
&НаКлиенте
Процедура ПробитьЧекНаКлиенте()
	
	ОчиститьСообщения();
	
	// Выполним проверки перед пробитием чека.
	ЕстьОшибки = Ложь;
	Ошибки = Неопределено;
	
	ПроверитьИспользованиеПодключаемогоОборудования(ИспользоватьПодключаемоеОборудование);
	ИспользоватьКассуККМБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	Если Не ИспользоватьПодключаемоеОборудование И Не ИспользоватьКассуККМБезПодключенияОборудования Тогда
		ТекстОшибки = НСтр("ru = 'При пробитии чека произошла ошибка. Чек не пробит. Необходимо проверить оборудование.'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки);
	КонецЕсли;
	
	Если Объект.ПометкаУдаления Тогда
		ТекстОшибки = НСтр("ru='Документ помечен на удаление'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки);
	ИначеЕсли НЕ Объект.Проведен ИЛИ ЭтотОбъект.Модифицированность Тогда
		ТекстОшибки = НСтр("ru = 'Необходимо провести документ перед пробитием'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки);
	КонецЕсли;
	
	Если Объект.НомерЧекаККМ <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Чек уже пробит на фискальном регистраторе'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, ЕстьОшибки);
	
	ПроверитьСкидки(ЕстьОшибки);
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	СписокНоменклатуры = Новый СписокЗначений;
	Если РозничныеПродажиКлиент.ЭтоВозвратПродукцииИСМПБезУказанияМарок(Объект, СписокНоменклатуры) Тогда
		ПодтвердитьВозвратПродукцииИСМПБезМарок(СписокНоменклатуры);
	Иначе
		ПробитьЧекНаКлиентеПродолжение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекНаКлиентеПродолжение()
	
	Если ПроверитьЗаполнение() Тогда
		// &ЗамерПроизводительности 
		Замер = ОценкаПроизводительностиРТКлиент.НачатьЗамер(Ложь, 
										 "Документ.ЧекККМ.Форма.ФормаДокумента.Команда.ПробитьЧек",
																 Ложь);
		ЭтотОбъект.Доступность = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ПробитьЧекНаКлиентеЗавершение", ЭтотОбъект);
		ПробитьЧекВыполнить(Оповещение);
	КонецЕсли;
	
	ЭтоВозвратПродукцииИСМПБезМарки = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекНаКлиентеЗавершение(ЧекПробит, ПараметрыОперации) Экспорт
	
	ЭтотОбъект.Доступность = Истина;
	
	Если ЧекПробит Тогда
		ТолькоПросмотр = Истина;
		УстановитьДоступностьКнопокПроведенияКлиент();
		ПроверитьСообщенияПослеОформленияЧека();
		НомерДокументаКассыККМ[Объект.КассаККМ] = Объект.НомерЧекаККМ +	1;
		ПорядковыйНомерПродажи = ПорядковыйНомерПродажи + 1;
	
		// &ЗамерПроизводительности 
		ОценкаПроизводительностиРТКлиент.ЗакончитьЗамер(Замер);
		
	КонецЕсли;
	
	ШтрихкодированиеИСМПВызовСервера.ОчиститьРезультатыПроверкиСредствамиККТ(Объект.Ссылка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ОбщаяКомандаФискальныеОперации", "Видимость", ЧекПробит);
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСдачуКлиентСервер(Форма)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ВидОплаты", ПредопределенноеЗначение("Справочник.ВидыОплатЧекаККМ.Наличные"));
	
	СуммаНаличных  = 0;
	СтрокиНаличных = Форма.Объект.Оплата.НайтиСтроки(СтруктураПоиска);
	
	Для каждого СтрокаНаличных Из СтрокиНаличных Цикл
		СуммаНаличных = СуммаНаличных + СтрокаНаличных.Сумма;
	КонецЦикла;
	
	Если Форма.Объект.Товары.Количество() = 0 Тогда
		Форма.Сдача = 0;
	Иначе
		Форма.Сдача = Макс(Форма.Объект.Оплата.Итог("Сумма") - Форма.СуммаВсего, 0);
		Форма.Сдача = Мин(Форма.Сдача, СуммаНаличных);
	КонецЕсли;
	
КонецПроцедуры

// Процедура сообщает о необходимости заполнения реквизитов документа при вызове подбора.
// Параметры:
//  Отказ - Булево
&НаКлиенте
Процедура СообщитьОбОшибкахОткрытияПодбора(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.Магазин) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Магазин"" не заполнено'"), Объект, "Объект.Магазин",,Отказ);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОплатуКартойЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтотОбъект.Доступность = Истина;
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'При выполнении операции возникла ошибка:
			|""%ОписаниеОшибки%"".
			|Оплата по карте не была произведена.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		ЗавершитьОтменуОплатыПлатежнойКартойЧерезЭквайринговыйТерминал(Истина, Параметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьОплатуКартойЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтотОбъект.Доступность = Истина;
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'При выполнении операции возникла ошибка:
			|""%ОписаниеОшибки%"".
			|Возврат оплаты по карте не был произведен.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		
		ЗавершитьВозвратОплатыПлатежнойКартойЧерезЭквайринговыйТерминал(Истина, Параметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОплатуКартой(ТекущиеДанные)
	
	ИдентификаторУстройстваЭТ = Неопределено;
	ИдентификаторУстройстваФР = Неопределено;
	
	СуммаОперации       = ТекущиеДанные.Сумма;
	НомерКарты          = ТекущиеДанные.НомерПлатежнойКарты;
	НомерСсылкиОперации = ТекущиеДанные.СсылочныйНомер;
	НомерЧекаЭТ         = ТекущиеДанные.НомерЧекаЭТ;
	СтрокаСлипЧека      = "";
		
	СтруктураЭквайринговыйТерминал = СтруктураДанныхЭТ(ТекущиеДанные.ЭквайринговыйТерминал);
	ИдентификаторУстройстваЭТ = СтруктураЭквайринговыйТерминал.ПодключаемоеОборудование;
	
	ИдентификаторУстройстваФР  = ПараметрыКассыККМ.ИдентификаторУстройства;
	Если НЕ ИспользоватьПодключаемоеОборудование Тогда
		ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования = Истина;
		СтруктураЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования = Истина;
	КонецЕсли;
	
	ИспользоватьКассуККМБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	ИспользоватьБезПодключенияОборудованияЭТ = СтруктураЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования;
	
	ПараметрДействия = Новый Структура;
	ПараметрДействия.Вставить("ИспользоватьКассуККМБезПодключенияОборудования", ИспользоватьКассуККМБезПодключенияОборудования);
	ПараметрДействия.Вставить("ИдентификаторУстройстваФР", ИдентификаторУстройстваФР);
	ПараметрДействия.Вставить("ИспользоватьБезПодключенияОборудованияЭТ", ИспользоватьБезПодключенияОборудованияЭТ);
	ПараметрДействия.Вставить("ИдентификаторУстройстваЭТ", ИдентификаторУстройстваЭТ);
	ПараметрДействия.Вставить("ТекстОписаниеОшибки", "");
	ПараметрДействия.Вставить("СтрокаСлипЧека", СтрокаСлипЧека);
	ПараметрДействия.Вставить("ВыбраннаяСтрокаИдентификатор", ТекущиеДанные.ПолучитьИдентификатор());
	ПараметрДействия.Вставить("СуммаОперации", СуммаОперации);
	ПараметрДействия.Вставить("НомерСсылкиОперации", НомерСсылкиОперации);
	ПараметрДействия.Вставить("НомерЧекаЭТ", НомерЧекаЭТ);
	
	Если НЕ ИспользоватьБезПодключенияОборудованияЭТ Тогда
		
		ЭтотОбъект.Доступность = Ложь;
			
		ПараметрыОперации = МенеджерОборудованияКлиент.ПараметрыВыполненияЭквайринговойОперации();
		ПараметрыОперации.ТипТранзакции  = "AuthorizeVoid";
		ПараметрыОперации.СуммаОперации  = СуммаОперации;
		ПараметрыОперации.НомерКарты     = НомерКарты;
		ПараметрыОперации.НомерЧекаЭТ    = НомерЧекаЭТ;
		ПараметрыОперации.СсылочныйНомер = НомерСсылкиОперации;
		
		Оповещение = Новый ОписаниеОповещения("УдалитьОплатуКартойЗавершение", ЭтотОбъект, ПараметрДействия);
		МенеджерОборудованияКлиент.НачатьВыполнениеОперацииНаЭквайринговомТерминале(Оповещение, УникальныйИдентификатор, 
			ПараметрДействия.ИдентификаторУстройстваЭТ, ПараметрДействия.ИдентификаторУстройстваФР, ПараметрыОперации);
	
	Иначе
		
		ТекстВопроса = НСтр("ru = 'Требуется выполнить операцию отмены оплаты на эквайринговом терминале.'") + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Сумма операции:'")        + " "+ СуммаОперации         + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Номер карты:'")           + " "+ НомерКарты            + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Ссылочный номер:'")       + " "+ НомерСсылкиОперации   + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Номер чека:'")            + " "+ НомерЧекаЭТ           + Символы.ПС;
		ТекстВопроса = ТекстВопроса + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Операция отмены оплаты на эквайринговом терминале прошла успешно?'");
		
		ДополнительныеПараметры = Новый Структура; 
		ДополнительныеПараметры.Вставить("ПараметрДействия", ПараметрДействия);
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросУспешностиОтменыОперацииНаЭТ", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьОплатуКартой(ТекущиеДанные)
	
	ИдентификаторУстройстваЭТ = Неопределено;
	ИдентификаторУстройстваФР = Неопределено;
	
	СуммаОперации       = ТекущиеДанные.Сумма;
	НомерКарты          = ТекущиеДанные.НомерПлатежнойКарты;
	НомерСсылкиОперации = ТекущиеДанные.СсылочныйНомер;
	НомерЧекаЭТ         = ТекущиеДанные.НомерЧекаЭТ;
	СтрокаСлипЧека      = "";
		
	СтруктураЭквайринговыйТерминал = СтруктураДанныхЭТ(ТекущиеДанные.ЭквайринговыйТерминал);
	ИдентификаторУстройстваЭТ = СтруктураЭквайринговыйТерминал.ПодключаемоеОборудование;
	
	ИдентификаторУстройстваФР  = ПараметрыКассыККМ.ИдентификаторУстройства;
	Если НЕ ИспользоватьПодключаемоеОборудование Тогда
		ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования = Истина;
		СтруктураЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования = Истина;
	КонецЕсли;
	
	ИспользоватьКассуККМБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	ИспользоватьБезПодключенияОборудованияЭТ = СтруктураЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования;
	
	ПараметрДействия = Новый Структура;
	ПараметрДействия.Вставить("ИспользоватьКассуККМБезПодключенияОборудования", ИспользоватьКассуККМБезПодключенияОборудования);
	ПараметрДействия.Вставить("ИдентификаторУстройстваФР", ИдентификаторУстройстваФР);
	ПараметрДействия.Вставить("ИспользоватьБезПодключенияОборудованияЭТ", ИспользоватьБезПодключенияОборудованияЭТ);
	ПараметрДействия.Вставить("ИдентификаторУстройстваЭТ", ИдентификаторУстройстваЭТ);
	ПараметрДействия.Вставить("ТекстОписаниеОшибки", "");
	ПараметрДействия.Вставить("СтрокаСлипЧека", СтрокаСлипЧека);
	ПараметрДействия.Вставить("ВыбраннаяСтрокаИдентификатор", ТекущиеДанные.ПолучитьИдентификатор());
	ПараметрДействия.Вставить("СуммаОперации", СуммаОперации);
	ПараметрДействия.Вставить("НомерСсылкиОперации", НомерСсылкиОперации);
	ПараметрДействия.Вставить("НомерЧекаЭТ", НомерЧекаЭТ);
	
	Если НЕ ИспользоватьБезПодключенияОборудованияЭТ Тогда
		
		ЭтотОбъект.Доступность = Ложь;
			
		ПараметрыОперации = МенеджерОборудованияКлиент.ПараметрыВыполненияЭквайринговойОперации();
		ПараметрыОперации.ТипТранзакции  = "AuthorizeRefund";
		ПараметрыОперации.СуммаОперации  = СуммаОперации;
		ПараметрыОперации.НомерКарты     = НомерКарты;
		ПараметрыОперации.НомерЧекаЭТ    = НомерЧекаЭТ;
		ПараметрыОперации.СсылочныйНомер = НомерСсылкиОперации;
		
		Оповещение = Новый ОписаниеОповещения("ВернутьОплатуКартойЗавершение", ЭтотОбъект, ПараметрДействия);
		МенеджерОборудованияКлиент.НачатьВыполнениеОперацииНаЭквайринговомТерминале(Оповещение, УникальныйИдентификатор, 
			ПараметрДействия.ИдентификаторУстройстваЭТ, ПараметрДействия.ИдентификаторУстройстваФР, ПараметрыОперации);
	
	Иначе
		
		ТекстВопроса = НСтр("ru = 'Требуется выполнить операцию возврата оплаты на эквайринговом терминале.'") + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Сумма операции:'")        + " "+ СуммаОперации         + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Номер карты:'")           + " "+ НомерКарты            + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Ссылочный номер:'")       + " "+ НомерСсылкиОперации   + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Номер чека:'")            + " "+ НомерЧекаЭТ           + Символы.ПС;
		ТекстВопроса = ТекстВопроса + Символы.ПС;
		ТекстВопроса = ТекстВопроса + НСтр("ru = 'Операция возврата оплаты на эквайринговом терминале прошла успешно?'");
		
		ДополнительныеПараметры = Новый Структура; 
		ДополнительныеПараметры.Вставить("ПараметрДействия", ПараметрДействия);
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросУспешностиВозвратаОплатыНаЭТ", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопокПроведенияКлиент()
	
	Если Элементы.Найти("ФормаПровестиИЗакрытьИзФормы") <> Неопределено Тогда
		Элементы.ФормаПровестиИЗакрытьИзФормы.Доступность = НЕ ТолькоПросмотр;
	КонецЕсли;
	
	Если Элементы.Найти("ФормаПровестиИзФормы") <> Неопределено Тогда
		Элементы.ФормаПровестиИзФормы.Доступность         = НЕ ТолькоПросмотр;
	КонецЕсли;
	
	Если Элементы.Найти("ТоварыФискальныеДанные") <> Неопределено Тогда
		Элементы.ТоварыФискальныеДанные.Видимость = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
																"ФормаПробитьЧек",
																"Доступность", 
																Объект.Проведен И НЕ ТолькоПросмотр);
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКнопокПроведенияСервер()
	
	Если Элементы.Найти("ФормаПровестиИЗакрытьИзФормы") <> Неопределено Тогда
		Элементы.ФормаПровестиИЗакрытьИзФормы.Доступность = НЕ ТолькоПросмотр;
	КонецЕсли;
	
	Если Элементы.Найти("ФормаПровестиИзФормы") <> Неопределено Тогда
		Элементы.ФормаПровестиИзФормы.Доступность         = НЕ ТолькоПросмотр;
	КонецЕсли;
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
																"ФормаПробитьЧек",
																"Доступность", 
																Объект.Проведен И НЕ ТолькоПросмотр);
КонецПроцедуры

// Устанавливает доступность элементов по виду операции.
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура УстановитьДоступностьПоВидуОперацииКлиент()
	
	ЭтоВозврат = Объект.ВидОперации = ЗначениеПеречислениеВидОперацииВозврат;
	
	Элементы.АналитикаХозяйственнойОперации.ТолькоПросмотр = НЕ ЭтоВозврат;
	Элементы.ЧекККМПродажа.ТолькоПросмотр                  = НЕ ЭтоВозврат;
	Элементы.ДисконтнаяКарта.ТолькоПросмотр                = ЭтоВозврат;
	
КонецПроцедуры

// Устанавливает значение выбора чека возврата.
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура УстановитьПараметрыВыбораЧекаККМПродажиКлиент()

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.КассаККМ"   , Объект.КассаККМ));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВидОперации",ЗначениеПеречислениеВидОперацииПродажа));
	
	МассивСтатусовЧекаККМ = Новый Массив;
	
	МассивСтатусовЧекаККМ.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.ПустаяСсылка"));
	МассивСтатусовЧекаККМ.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробитый"));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.СтатусЧекаККМ",Новый ФиксированныйМассив(МассивСтатусовЧекаККМ)));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Проведен", Истина));
	
	Элементы.ЧекККМПродажа.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);

КонецПроцедуры

&НаСервере
Функция ПараметрыДляНазначенияРучнойСкидки()
	
	Возврат СкидкиНаценкиСервер.ПараметрыДляНазначенияРучнойСкидки(Объект);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьОплатуНаличными()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеДобавитьОплатуНаличными");
	Отказ = Ложь;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
		
	Иначе
		
		ПроверитьСкидки(Отказ, ДополнительныеПараметры);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОповещениеДобавитьОплатуНаличными(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКомандыВидовОплаты()

	ПлатежнаяКарта = ЗначениеНастроекПовтИсп.ВидОплатыПоТипу(Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта);
	
	Если ПлатежнаяКарта = Неопределено Тогда
		Элементы.ОплатаПлатежнаяКарта.Заголовок = Элементы.ОплатаПлатежнаяКарта.Заголовок + "...";
	КонецЕсли;
	
	БанковскийКредит = ЗначениеНастроекПовтИсп.ВидОплатыПоТипу(Перечисления.ТипыОплатЧекаККМ.БанковскийКредит);
	
	Если БанковскийКредит = Неопределено Тогда
		Элементы.ОплатаБанковскийКредит.Заголовок = Элементы.ОплатаБанковскийКредит.Заголовок + "...";
	КонецЕсли;

	ВидОплаты		  = Объект.Оплата.Выгрузить().ВыгрузитьКолонку("ВидОплаты");
	Интеграции 		  = ИнтеграцияСПлатежнымиСистемамиРТСервер.НастройкиИнтеграции(Объект.Организация, Объект.Магазин);
	
	ОтменаОплаты	  = Ложь;
	ОплатаQR		  = Ложь;
	
	Если Интеграции <> Неопределено
		И Интеграции.Количество() Тогда
		
		ОплатаQR = Истина;
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
			
			Для Каждого НастройкаИнтеграции ИЗ Интеграции Цикл
				
				Если ВидОплаты.Найти(НастройкаИнтеграции.ВидОплаты) <> Неопределено Тогда
				
					НастройкиОпераций = ИнтеграцияСПлатежнымиСистемамиРТВызовСервера.НастройкиТорговойТочки(Интеграции[0].Интеграция);
					ОтменаОплаты	  = НастройкиОпераций.ОтменаОплаты;
					
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ФормаОтменитьОплатуВПлатежнойСистеме.Видимость = ОтменаОплаты;
	Элементы.ОплатаQRКодом.Видимость						= ОплатаQR;
	
	Элементы.ОплатаОплатитьЭСНСПК.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВидОплатыПоТипу(ТипыОплаты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ВидыОплатЧекаККМ.Ссылка
	|ИЗ
	|	Справочник.ВидыОплатЧекаККМ КАК ВидыОплатЧекаККМ
	|ГДЕ
	|	НЕ ВидыОплатЧекаККМ.ПометкаУдаления
	|	И ВидыОплатЧекаККМ.ТипОплаты = &ТипыОплаты";
	Запрос.УстановитьПараметр("ТипыОплаты", ТипыОплаты);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьЭквайринговыйТерминалПоУмолчанию(ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда
		
		ЭквайринговыйТерминалПоУмолчанию = ПолучитьЭквайринговыйТерминалПоУмолчанию(Неопределено);
		Для Каждого ТекущаяСтрока Из Объект.Оплата Цикл 
			
			ТекущаяСтрока.ЭквайринговыйТерминал = ЭквайринговыйТерминалПоУмолчанию;
			ТекущаяСтрока.ПроцентКомиссии = ЭквайрингВызовСервера.ПолучитьПроцентКомиссииПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
			ПересчитатьСуммуКомиссии(ТекущаяСтрока);
			ТекущаяСтрока.ТипОплаты = ТипОплатыПоВиду(ТекущаяСтрока.ВидОплаты);
			
		КонецЦикла;
		
	Иначе
		
		ЭквайринговыйТерминалПоУмолчанию = ПолучитьЭквайринговыйТерминалПоУмолчанию(ТекущаяСтрока.ВидОплаты);
		ТекущаяСтрока.ЭквайринговыйТерминал = ЭквайринговыйТерминалПоУмолчанию;
		ТекущаяСтрока.ПроцентКомиссии = ЭквайрингВызовСервера.ПолучитьПроцентКомиссииПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
		ПересчитатьСуммуКомиссии(ТекущаяСтрока);
		ТекущаяСтрока.ТипОплаты = ТипОплатыПоВиду(ТекущаяСтрока.ВидОплаты);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЭквайринговыйТерминалПоУмолчанию(ВидОплаты)
	
	Результат = Справочники.ЭквайринговыеТерминалы.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Объект.КассаККМ) Тогда
		Результат = Справочники.ЭквайринговыеТерминалы.ЭквайринговыйТерминалПоУмолчанию(Объект.КассаККМ, Объект.Организация, Объект.Магазин, ВидОплаты);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ВидимостьОплатРасчетыСКлиентамиСервер()
	
	ЭтоВозврат = Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат;
	
	Элементы.ОплатаОплатитьВРассрочку.Видимость   = НЕ Объект.ОперацияСДенежнымиСредствами;
	Элементы.ОплатаБанковскийКредит.Видимость     = НЕ Объект.ОперацияСДенежнымиСредствами;
	Элементы.ОплатаПодарочныйСертификат.Видимость = НЕ Объект.ОперацияСДенежнымиСредствами И НЕ ЭтоВозврат;
	Элементы.ОплатаОплатитьБонусами.Видимость     = НЕ Объект.ОперацияСДенежнымиСредствами И НЕ ЭтоВозврат;
	Элементы.ОплатаЗачетАванса.Видимость          = НЕ ЭтоВозврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьОплатРасчетыСКлиентами()
	
	ЭтоВозврат = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат");
	
	Элементы.ОплатаБанковскийКредит.Видимость     = НЕ Объект.ОперацияСДенежнымиСредствами;
	Элементы.ОплатаПодарочныйСертификат.Видимость = НЕ Объект.ОперацияСДенежнымиСредствами;
	Элементы.ОплатаОплатитьБонусами.Видимость     = НЕ Объект.ОперацияСДенежнымиСредствами;
	Элементы.ОплатаЗачетАванса.Видимость          = НЕ ЭтоВозврат;
	Элементы.ОплатаОплатитьВРассрочку.Видимость   = НЕ ЭтоВозврат;
	
	Если Объект.ОперацияСДенежнымиСредствами Тогда
		
		УдалитьЛишнееСтрокиОплат(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.БанковскийКредит"));
		УдалитьЛишнееСтрокиОплат(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.Бонусы"));
		УдалитьЛишнееСтрокиОплат(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ПодарочныйСертификат"));
		УдалитьЛишнееСтрокиОплат(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ЗачетАванса"));
		
		Если НЕ ЭтоВозврат Тогда
			УдалитьЛишнееСтрокиОплат(ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.ВРассрочку"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЛишнееСтрокиОплат(ТипОплатЧекаККМ)
	
	СтруктуруПоиска = Новый Структура;
	СтруктуруПоиска.Вставить("ТипОплаты", ТипОплатЧекаККМ);
	Строки = Объект.Оплата.НайтиСтроки(СтруктуруПоиска);
	
	Для каждого СтрокаОплаты Из Строки Цикл
		Объект.Оплата.Удалить(СтрокаОплаты);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция АдресТабличнойЧастиТовары()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ОбработатьОплатуБонусами(Параметр, УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов)
	
	ТаблицаРаспределения = ПолучитьИзВременногоХранилища(Параметр.АдресВоВременномХранилище);
	КоличествоБонусовОплачено = ТаблицаРаспределения.Итог("КоличествоБонусовОплачено");
	СуммаОплачено = ТаблицаРаспределения.Итог("СуммаОплачено");
	
	НоваяСтрока = Объект.Оплата.Добавить();
	НоваяСтрока.БонуснаяПрограммаЛояльности = Параметр.БонуснаяПрограмма;
	НоваяСтрока.ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой;
	
	Если НоваяСтрока.ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой Тогда
		НоваяСтрока.КоличествоБонусовВСкидках = КоличествоБонусовОплачено;
		НоваяСтрока.СуммаБонусовВСкидках = СуммаОплачено;
		
		//
		ОчиститьОплатуБонусамиВТоварах();
		Для Каждого СтрокаРаспределения Из ТаблицаРаспределения Цикл
			СтрокаТоваров = Объект.Товары[СтрокаРаспределения.НомерСтроки - 1];
			СтрокаТоваров.СуммаСкидкиОплатыБонусом = СтрокаРаспределения.СуммаОплачено;
			СтрокаТоваров.Сумма = СтрокаТоваров.Сумма - СтрокаТоваров.СуммаСкидкиОплатыБонусом;
			СтрокаТоваров.СуммаНДС = ОбработкаТабличнойЧастиТоварыКлиентСервер.СуммаНДС(СтрокаТоваров.Сумма, СтрокаТоваров.СтавкаНДС, Объект.ЦенаВключаетНДС);
		КонецЦикла;
	Иначе
		НоваяСтрока.КоличествоБонусов = КоличествоБонусовОплачено;
		НоваяСтрока.Сумма = СуммаОплачено;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьСуммуВсегоВТаблице(Объект.Товары, Объект.ЦенаВключаетНДС);
	
	Если УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов Тогда
		Объект.СкидкиРассчитаны = Ложь;
	Иначе
		Если Объект.ВидОперации <> Перечисления.ВидыОперацийЧекККМ.Возврат
			И НЕ ПродажаПоЗаказу
			И НЕ ДокументРасчетаЗаполнен Тогда
			СкидкиНаценкиСервер.ОкруглитьЧекВПользуПокупателя(Объект, "Товары", "СкидкиНаценки");
		КонецЕсли;
	КонецЕсли;
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	ЕстьОплатаБонусами = Истина;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция БонуснаяПрограммаКарты(ДисконтнаяКарта)
	
	БонуснаяПрограмма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДисконтнаяКарта, "БонуснаяПрограммаЛояльности");
	
	Возврат БонуснаяПрограмма;
	
КонецФункции

&НаСервере
Функция ВозможнаОплатаБонусами(ДополнительныеПараметры)
	Результат = Ложь;
	ТекстПредупреждения = "";
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
		
		Если ЗначениеЗаполнено(Объект.ДисконтнаяКарта) Тогда
			БонуснаяПрограммаЛояльности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДисконтнаяКарта, "БонуснаяПрограммаЛояльности");
			Если ЗначениеЗаполнено(БонуснаяПрограммаЛояльности) Тогда
				УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					БонуснаяПрограммаЛояльности,
					"УменьшатьСуммуЧекаДляСкидок");
					Если УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = Null
						ИЛИ УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = Неопределено Тогда
					УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = Ложь;
				КонецЕсли;
			Иначе
				УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = Ложь;
			КонецЕсли;
		Иначе
			УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = Ложь;
		КонецЕсли;
		ДополнительныеПараметры.Вставить("УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов", УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов);
		
		ЕстьОплатаБонусами = Ложь;
		ЕстьДругиеОплаты = Ложь;
		Для Каждого СтрокаОплаты Из Объект.Оплата Цикл
			Если СтрокаОплаты.ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой Тогда
				ТекстПредупреждения = НСтр("ru = 'Оплата бонусами уже выбрана.'")
					   + Символы.ПС + НСтр("ru = 'Повторная оплата бонусами невозможна.'");
				
				ЕстьОплатаБонусами = Истина;
			Иначе
				ЕстьДругиеОплаты = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЕстьОплатаБонусами Тогда
			Если ЗначениеЗаполнено(Объект.ДокументРасчета) Тогда
				ТекстПредупреждения = НСтр("ru = 'Цена зафиксирована документом расчета. Применение бонусов как распределение скидки невозможно.'")
			Иначе
				Если УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов И ЕстьДругиеОплаты Тогда
					ТекстПредупреждения = НСтр("ru = 'Оплата бонусами возможна только до указания других видов оплат и расчета скидок.'");
				Иначе
					Если ЗначениеЗаполнено(Объект.ДисконтнаяКарта) Тогда
						БонуснаяПрограммаЛояльности = БонуснаяПрограммаКарты(Объект.ДисконтнаяКарта);
						Если ЗначениеЗаполнено(БонуснаяПрограммаЛояльности) Тогда
							ДополнительныеПараметры.Вставить("БонуснаяПрограммаЛояльности", БонуснаяПрограммаЛояльности);
							Результат = Истина;
						Иначе
							ТекстПредупреждения = НСтр("ru = 'Дисконтная карта не участвует в бонусных программах.'")
								   + Символы.ПС + НСтр("ru = 'Оплата бонусами невозможна.'");
						КонецЕсли;
					Иначе
						ТекстПредупреждения = НСтр("ru = 'Не выбрана дисконтная карта.'")
							   + Символы.ПС + НСтр("ru = 'Оплата бонусами невозможна.'");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ТекстПредупреждения = НСтр("ru = 'При возврате бонусные баллы сторнируются автоматически.'");
	КонецЕсли;
	ДополнительныеПараметры.Вставить("ТекстПредупреждения", ТекстПредупреждения);
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ОчиститьОплатуБонусамиВТоварах()
	
	БонусныеБаллыСервер.ОчиститьОплатуБонусамиВТоварах(Объект);
	ЕстьОплатаБонусами = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьСкидкиИОплатуБонусамиКСторнированию()
	
	СкидкиНаценкиСерверПереопределяемый.ПодготовитьСкидкиИОплатуБонусамиКСторнированиюПриВозврате(Объект);
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьСкидки()
	
	Если НЕ ПродажаПоЗаказу 
		И НЕ ДокументРасчетаЗаполнен Тогда
		Если Объект.СкидкиРассчитаны Тогда
			СкидкиНаценкиСервер.ОтменитьСкидки(Объект, "Товары", "СуммаСкидкиОплатыБонусом");
			ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьСуммуВсегоВТаблице(Объект.Товары, Объект.ЦенаВключаетНДС);
			Объект.Подарки.Очистить();
			Объект.БонусныеБаллыКНачислению.Очистить();
			Объект.СкидкиРассчитаны = Ложь;
			СкидкиНаценкиСерверПереопределяемый.ОчиститьТоварыОтПодарков(Объект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьОплатуБонусами(УдалятьСтрокиИзОплаты = Истина)
	
	Если ЕстьОплатаБонусами Тогда
		БонусныеБаллыСервер.ОчиститьОплатуБонусамиВТоварах(Объект);
		Если УдалятьСтрокиИзОплаты Тогда
			МассивСтрокОплаты = Объект.Оплата.НайтиСтроки(Новый Структура("ВидОплаты", Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой));
			Для Каждого УдаляемаяСтрока Из МассивСтрокОплаты Цикл
				Объект.Оплата.Удалить(УдаляемаяСтрока);
			КонецЦикла;
		КонецЕсли;
		ЕстьОплатаБонусами = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнформациюОСкидкахЗавершитьОбработкуВопроса()
	
	АдресСкидок = ПоместитьСкидкиВХранилище();
	СкидкиНаценкиКлиент.ОткрытьФормуПримененныеСкидки(Элементы.Товары.ТекущиеДанные, Объект, ЭтотОбъект, АдресСкидок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнформациюОСкидкахКлиент()
	
	Если Объект.СкидкиРассчитаны Тогда
		ОткрытьИнформациюОСкидкахЗавершитьОбработкуВопроса();
	Иначе
		ДополнительныеПараметры = Новый Структура; 
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросРассчитатьИОткрытьСкидки", ЭтотОбъект, ДополнительныеПараметры);
		ТекстВопроса = НСтр("ru = 'Скидки (наценки) не рассчитаны, рассчитать?'");
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОплатыБонусами(ДополнительныеПараметры)
	
	Отказ = Ложь;
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов") Тогда
		УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов = ДополнительныеПараметры.УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов;
	КонецЕсли;
	ДополнительныеПараметры.Вставить("БудетОплатаБонусами", Истина);
	ДополнительныеПараметры.Вставить("ИмяОбработкиОповещения", "ОповещениеРасчетСкидокКлиент");
	Если УменьшатьСуммуЧекаДляСкидокНаСуммуБонусов Тогда
		ОтменитьСкидки();
	Иначе
		ПроверитьСкидки(Отказ, ДополнительныеПараметры, Ложь);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Документ.ЧекККМ.Форма.ФормаОплатыБонуснымиБаллами.Открытие");
             
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Магазин", Объект.Магазин);
	ПараметрыОткрытия.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
	ПараметрыОткрытия.Вставить("АдресТабличнойЧастиТовары", АдресТабличнойЧастиТовары());
	ПараметрыОткрытия.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыОткрытия.Вставить("БонуснаяПрограммаЛояльности", ДополнительныеПараметры.БонуснаяПрограммаЛояльности);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОплатыБонусами", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Документ.ЧекККМ.Форма.ФормаОплатыБонуснымиБаллами",
					ПараметрыОткрытия,
					ЭтаФорма,
					,
					,
					,
					ОбработчикОповещения,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуОплатыПослеВопроса()
	
	ВопросОбУдаленииСтрокиОплатыУжеЗадан = Истина;
	
	ТекущиеДанные = Элементы.Оплата.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЭтотОбъект.Модифицированность = Истина;
		Объект.Оплата.Удалить(ТекущиеДанные);
		Если ТекущиеДанные.ВидОплаты = ПредопределенноеЗначение("Справочник.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой")
			И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
			ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
		КонецЕсли;
	КонецЕсли;
	
	ВопросОбУдаленииСтрокиОплатыУжеЗадан = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОплатуПлатежнойКартойЧерезЭквайринговыйТерминал(РезультатОперации, ПараметрДействия)
	
	Если РезультатОперации Тогда
		
		// Сохранить в таблице данные оплаты картой.
		ВыбраннаяСтрока = Объект.Оплата.НайтиПоИдентификатору(ПараметрДействия.ВыбраннаяСтрокаИдентификатор);
		
		ВыбраннаяСтрока.НомерПлатежнойКарты = ПараметрДействия.НомерКарты;
		ВыбраннаяСтрока.Сумма               = ПараметрДействия.СуммаОперации;
		ВыбраннаяСтрока.СсылочныйНомер      = ПараметрДействия.НомерСсылкиОперации;
		ВыбраннаяСтрока.НомерЧекаЭТ         = ПараметрДействия.НомерЧекаЭТ;
		ВыбраннаяСтрока.ДанныеПереданыВБанк = Истина;
		
		Записать(); // Обязательно необходимо записать документ, для предотвращения потери информации.
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОтменуОплатыПлатежнойКартойЧерезЭквайринговыйТерминал(РезультатОперации, ПараметрДействия)
	
	Если РезультатОперации Тогда
		
		ВыбраннаяСтрока = Объект.Оплата.НайтиПоИдентификатору(ПараметрДействия.ВыбраннаяСтрокаИдентификатор);
		
		// Сохранить в таблице данные оплаты картой.
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
			ВыбраннаяСтрока.СсылочныйНомер = "";
			ВыбраннаяСтрока.НомерЧекаЭТ    = "";
			ВыбраннаяСтрока.НомерПлатежнойКарты = "";
			ВыбраннаяСтрока.ДанныеПереданыВБанк = Ложь;
		Иначе
			ВыбраннаяСтрока.ДанныеПереданыВБанк = Истина;
		КонецЕсли;
		
		Записать(); // Обязательно необходимо записать документ, для предотвращения потери информации.
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВозвратОплатыПлатежнойКартойЧерезЭквайринговыйТерминал(РезультатОперации, ПараметрДействия)
	
	Если РезультатОперации Тогда
		
		ВыбраннаяСтрока = Объект.Оплата.НайтиПоИдентификатору(ПараметрДействия.ВыбраннаяСтрокаИдентификатор);
		
		ВыбраннаяСтрока.ДанныеПереданыВБанк = Истина;
		
		Записать(); // Обязательно необходимо записать документ, для предотвращения потери информации.
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОбработкуДанныхПоКодуКлиент(СтруктураПараметровКлиента)
	
	ИдентификаторСтроки = ПодключаемоеОборудованиеРТКлиент.ЗавершитьОбработкуДанныхПоКодуКлиент(ЭтотОбъект, СтруктураПараметровКлиента);
	
	ПроверитьСистемуНалогообложения(ИдентификаторСтроки);
	
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		//После завершения обработки товаров удаляем из таблицы марок несвязанные строки
		СтрокиБезИндексаСвязи = Объект.КодыМаркировкиИСМПТК.НайтиСтроки(Новый Структура("КлючСвязи", -1));
		Если Не СтрокиБезИндексаСвязи.Количество() = 0 Тогда
			Для Каждого Строка Из СтрокиБезИндексаСвязи Цикл
				Объект.КодыМаркировкиИСМПТК.Удалить(Строка);
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
КонецПроцедуры

&НаСервере
Процедура УдалитьНесвязанныеСерийныеНомера()
	
	ОбработкаТабличнойЧастиТоварыСервер.УдалитьНесвязанныеСерийныеНомера(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНесвязанныеАкцизы()
	
	ОбработкаТабличнойЧастиТоварыСервер.УдалитьНесвязанныеАкцизы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьКартинкуДляКомментария()
	ОбщегоНазначенияРТКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаКомментарий, Объект.Комментарий);
КонецПроцедуры

// Процедура заполняет товары из подбора.
// Параметры: 
//  ВыбранноеЗначение - Структура,
//  ИмяТабличнойЧасти  - Строка
&НаСервере
Функция ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИмяТабличнойЧасти  = "")
	
	СтрокиИзменены = Ложь;
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресТоваровВХранилище);
	ИмяТабличнойЧасти = ?(ПустаяСтрока(ИмяТабличнойЧасти), "Товары", ИмяТабличнойЧасти);
	ТаблицаСерийныхНомеров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресСерийныхНомеровВХранилище);
	ЕстьСерийныеНомера = (ИмяТабличнойЧасти = "Товары") И (ТаблицаСерийныхНомеров.Количество() > 0);
	НужноЗаполнятьЦену = НЕ ЕстьПравоИзменятьЦену();
	СписокСвойствДляЗаполнения = "Номенклатура, Характеристика, Упаковка, Цена, КоличествоУпаковок, ТипНоменклатуры,
		|ХарактеристикиИспользуются, ИспользоватьСерийныеНомера";
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		//ИнтеграцияИСМПТК_РозничноеВыбытие
		Если УчетМаркировкаИСМПТК 
			И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
			
			БылоСообщениеОбОшибках = Ложь;
			ВидПродукцииШК = ПолучитьВидПродукцииПоНоменклатуре(СтрокаТовара.Номенклатура);
			УчетТГВключен  = РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьВключениеУчетаТГ(ВидПродукцииШК);
			
			Если ЗначениеЗаполнено(ВидПродукцииШК) Тогда //Номенклатура с ОУ по маркировке
				Если Не УчетТГВключен Тогда 
					ТекстОшибки = НСтр("ru = 'Внимание! Номенклатура %1 имеет особенность учета ""%2""! На текущий момент учет этой товарной группы в базе не включен.'")
								+ НСтр("ru = 'Необходимо изменить вид номенклатуры на ""Товар без особенностей учета"" или включить учет маркировки этой товарной группы!'");
					ТекстОшибки = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстОшибки, СтрокаТовара.Номенклатура, ВидПродукцииШК);
					ОбщегоНазначенияИСМПТКВызовСервера.СоздатьЗаписьЖурналаРегистрации(НСтр("ru = 'Розничная продажа маркируемого товара'"), "Предупреждение",,, ТекстОшибки);
					Если Не БылоСообщениеОбОшибках Тогда
						ТекстСообщения = НСтр("ru = 'Внимание! Обнаружены проблемы в настройках учета маркируемой продукции! Требуется сообщить Администратору. Подробности в Журнале регистрации.'"); 
						ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
						БылоСообщениеОбОшибках = Истина;
					КонецЕсли;					
				Иначе
					ТекстСообщения = НСтр("ru = 'Внимание: среди выбранных товаров есть маркируемые (%1%2), нужно заполнить коды маркировки в отдельной таблице!'");
					ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТовара.Номенклатура, ?(ЗначениеЗаполнено(СтрокаТовара.Характеристика), " (" + СтрокаТовара.Характеристика + ")", ""));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		//Конец ИнтеграцияИСМПТК_РозничноеВыбытие

		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", СтрокаТовара.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", СтрокаТовара.Характеристика);
		
		МассивСтрок = Объект.Товары.НайтиСтроки(СтруктураПоиска);
		КоличествоНайденныхСтрок = МассивСтрок.Количество();
		Если КоличествоНайденныхСтрок > 0 
			И МассивСтрок[0].ОсобенностиУчетаНоменклатуры = Перечисления.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха Тогда
			
			ТекущаяСтрока = МассивСтрок[0];
			ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.КоличествоУпаковок + СтрокаТовара.КоличествоУпаковок;
			
		Иначе
			
			ТекущаяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока,СтрокаТовара, СписокСвойствДляЗаполнения);
			
		КонецЕсли;
		
		Если ЕстьСерийныеНомера Тогда
			МассивСерийныхНомеров = ОбработкаТабличнойЧастиТоварыКлиентСервер.МассивСерийныхНомеровДляСтрокиТоваров(СтрокаТовара.КлючСвязиСерийныхНомеров , ТаблицаСерийныхНомеров);
			ТекущаяСтрока.КлючСвязиСерийныхНомеров = ОбработкаТабличнойЧастиТоварыСервер.ДобавитьСерийныеНомераВТабличнуюЧасть(Объект.СерийныеНомера, МассивСерийныхНомеров, 0);
		КонецЕсли;
		Если СтрокаТовара.ИспользоватьСерийныеНомера Тогда
			Если ЕстьСерийныеНомера Тогда
				ТекущаяСтрока.КоличествоУпаковок = МассивСерийныхНомеров.Количество();
			Иначе
				ТекущаяСтрока.КоличествоУпаковок = 0;
			КонецЕсли;
		КонецЕсли;
		СтруктураДействий = Новый Структура;
		СтруктураПараметровДействия = Новый Структура;
		СтруктураПараметровДействия.Вставить("Магазин", Объект.Магазин);
		СтруктураПараметровДействия.Вставить("РабочееМесто", Объект.КассаККМ.РабочееМесто);
		СтруктураПараметровДействия.Вставить("Организация", Объект.Организация);
		
		СтруктураДействий.Вставить("ЗаполнитьСкладПродажи", СтруктураПараметровДействия);
		СтруктураДействий.Вставить("ЗаполнитьВидНалога", 
			ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
		
		Если ИмяТабличнойЧасти = "Товары" Тогда
			Если НужноЗаполнятьЦену Тогда
				СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи" ,
					ОбработкаТабличнойЧастиТоварыКлиентСервер.ПолучитьСтруктуруЗаполненияЦеныПродажиВСтрокеТЧ(Объект, Истина));
				СтруктураДействий.Вставить("ПересчитатьЦенуСУчетомАгентскогоВознаграждения", Новый Структура("Цена", ТекущаяСтрока.Цена));
			КонецЕсли;
			ТекущаяСтрока.Продавец = Объект.Продавец;
			СтруктураПараметровСтавкиНДС = Новый Структура;
			СтруктураПараметровСтавкиНДС.Вставить("Дата"       , Объект.Дата);
			СтруктураПараметровСтавкиНДС.Вставить("Организация", Объект.Организация);
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", СтруктураПараметровСтавкиНДС);
			СтруктураДействий.Вставить("ЗаполнитьТипНоменклатуры", СтруктураПараметровСтавкиНДС);
			СтруктураДействий.Вставить("ЗаполнитьДоговорПлатежногоАгента", Новый Структура("Дата", Объект.Дата));
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
			ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьКлючСвязи(Объект.Товары, ТекущаяСтрока, "КлючСвязи");
			
		КонецЕсли;
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		
		ДополнитьСтруктуруДействиямиНДС(СтруктураДействий, ВестиУчетМаркируемойПродукцииИСМП);
		ДополнитьСтруктуруДействиямиГосИС(СтруктураДействий, Объект.ОперацияСДенежнымиСредствами, МаркировкаВключена);
		
		КэшированныеЗначения = ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураКэшируемыхЗначений();
		ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
		Если ИмяТабличнойЧасти = "Товары" Тогда
			ОбработкаТабличнойЧастиТоварыСервер.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
		КонецЕсли;
		
		СтрокиИзменены = Истина;
		
	КонецЦикла;
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	КонецЕсли;
	ЗаполнитьСтатусыУказанияСерийСервер();
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
	
	Возврат СтрокиИзменены;
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьИлиОтменитьСкидкиИОплатуБонусами(УдалятьСтрокиИзОплаты = Истина)
	
	Если АвтоматическийРасчетСкидок Тогда
		Если НЕ ПропуститьАвтоматическийРасчетСкидок Тогда
			Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат") Тогда
				ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
			Иначе
				РассчитатьСкидкиНаценкиКлиент();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда
			ОтменитьСкидки();
			ОтменитьОплатуБонусами(УдалятьСтрокиИзОплаты);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПредварительныйРасчетСкидокНаСервере(ПорядковыйНомерПродажи)
	
	СтруктураПараметры = РозничныеПродажиКлиентСервер.СтруктураДляПредварительногоРасчетСкидок();
	СтруктураПараметры.ПрименятьКОбъекту                = Ложь;
	СтруктураПараметры.ТолькоПредварительныйРасчет      = Истина;
	СтруктураПараметры.ВосстанавливатьУправляемыеСкидки = Истина;
	СтруктураПараметры.УправляемыеСкидки                = УправляемыеСкидки;
	СтруктураПараметры.ТолькоСообщенияПослеОформления   = Ложь;
	СтруктураПараметры.ПорядковыйНомерПродажи           = ПорядковыйНомерПродажи;
	СтруктураПараметры.РабочееМесто                     = РабочееМесто;
	СтруктураПараметры.КонтролироватьОстаткиТоваров     = КонтролироватьОстаткиТоваров;
	
	Возврат РозничныеПродажиСервер.ВыполнитьПредварительныйРасчетСкидокНаСервере(
		СтруктураПараметры, 
		Объект, 
		УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Функция ТоварыВводДополнительнойИнформации(Элемент)
	
	ВводДополнительнойИнформации = Ложь;
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ЭтотОбъект.ТолькоПросмотр ИЛИ ТекущиеДанные = Неопределено Тогда
		Возврат ВводДополнительнойИнформации;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.ТоварыКоличествоУпаковок
		ИЛИ Элемент.ТекущийЭлемент = Элементы.ТоварыУпаковка
		ИЛИ Элемент.ТекущийЭлемент = Элементы.ТоварыНоменклатураЕдиницаИзмерения Тогда
		
		Если ТекущиеДанные.ИспользоватьСерийныеНомера Тогда
			ВводДополнительнойИнформации = Истина;
			ВвестиПодарочныеСертификаты();
		ИначеЕсли ТекущиеДанные.СтатусУказанияСерий <> 0 Тогда
			ВводДополнительнойИнформации = Истина;
			Если Элемент.ТекущийЭлемент = Элементы.ТоварыКоличествоУпаковок Тогда
				УказатьСерии("КоличествоУпаковок", "Товары", "Серии");
			Иначе
				УказатьСерии("Упаковка", "Товары", "Серии");
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Элемент.ТекущийЭлемент = Элементы.ТоварыЦена
		ИЛИ Элемент.ТекущийЭлемент = Элементы.ТоварыСумма
		ИЛИ Элемент.ТекущийЭлемент = Элементы.ТоварыЦенаСАгентскимВознаграждением Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ДанныеАгентскогоДоговора) Тогда
			ВводДополнительнойИнформации = Истина;
			ВводПараметровАгентскогоПлатежа(ТекущиеДанные, Элемент.ТекущийЭлемент);
		КонецЕсли;
		
	ИначеЕсли Элемент.ТекущийЭлемент.Имя = "ТоварыСтатусПроверкиГосИС" Тогда
		
		ВводДополнительнойИнформации = Истина;
		
		ДанныеЧастичногоВыбытия = Новый Структура("ВидПродукцииИС, Номенклатура, Характеристика, Количество, GTIN");
		ЗаполнитьЗначенияСвойств(ДанныеЧастичногоВыбытия, ТекущиеДанные);
		Если ТекущиеДанные.СтатусПроверкиГосИС = 2
			И ЗначениеЗаполнено(ДанныеЧастичногоВыбытия.ВидПродукцииИС)
			И ЗначениеЗаполнено(ДанныеЧастичногоВыбытия.Номенклатура)
			И ОбщегоНазначенияИСКлиентСерверПовтИсп.ПоддерживаетсяЧастичноеВыбытие(ДанныеЧастичногоВыбытия.ВидПродукцииИС)
			И Не ОбщегоНазначенияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки().ПодбиратьКодыМаркировкиВскрытыхПотребительскихУпаковокПоFIFO
			И ТребуетсяУточнениеПоКодуGTIN(ДанныеЧастичногоВыбытия)
			И ЗначениеЗаполнено(ДанныеЧастичногоВыбытия.GTIN) Тогда
			ОбработатьКодМаркировки(Новый Структура("Количество, Штрихкод", 1, ДанныеЧастичногоВыбытия.GTIN));
		Иначе
			СобытияФормИСКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Новый Структура("Имя", ""), ДанныеЧастичногоВыбытия);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВводДополнительнойИнформации;
	
КонецФункции

&НаКлиенте
Процедура ВводПараметровАгентскогоПлатежа(ТекущиеДанные, Элемент)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("РедактируемаяСтрока", ТекущиеДанные);
	
	ПараметрыФормы = Новый Структура;
	Если Элемент = Элементы.ТоварыСумма Тогда
		ПараметрыФормы.Вставить("Платеж", ТекущиеДанные.Сумма);
		ДополнительныеПараметры.Вставить("ПересчиталиСумму", Истина);
	Иначе
		ПараметрыФормы.Вставить("Платеж", ТекущиеДанные.Цена);
	КонецЕсли;
	ПараметрыФормы.Вставить("ДанныеАгентскогоДоговора",         ТекущиеДанные.ДанныеАгентскогоДоговора);
	ПараметрыФормы.Вставить("ВознаграждениеВключеноВСтоимость", Элемент = Элементы.ТоварыЦенаСАгентскимВознаграждением);
	
	ОбработчикОповещения = Новый ОписаниеОповещения(
									"ОповещениеВводПараметровАгентскогоПлатежа" ,
									ЭтотОбъект,
									ДополнительныеПараметры);
									
	ОткрытьФорму("ОбщаяФорма.ВводПараметровАгентскогоПлатежа", ПараметрыФормы, ЭтотОбъект,,,,ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВводПараметровАгентскогоПлатежа(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	РедактируемаяСтрока = ДополнительныеПараметры.РедактируемаяСтрока;
	Если ДополнительныеПараметры.Свойство("ПересчиталиСумму") И ДополнительныеПараметры.ПересчиталиСумму Тогда
		РедактируемаяСтрока.Сумма = Результат.Платеж;
		ТоварыСуммаПриИзменении(Неопределено);
	Иначе
		РедактируемаяСтрока.Цена = Результат.Платеж;
		ТоварыЦенаПриИзменении(Неопределено);
	КонецЕсли;
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиПередНачаломИзменения(Элемент, Отказ)
	
	ПодаркиВводДополнительнойИнформации(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиВводДополнительнойИнформации(Элемент, Отказ)
	
	Если Элемент.ТекущийЭлемент = Элементы.ПодаркиКоличествоУпаковок
		ИЛИ Элемент.ТекущийЭлемент = Элементы.ПодаркиУпаковка 
		ИЛИ Элемент.ТекущийЭлемент = Элементы.ПодаркиНоменклатураЕдиницаИзмерения Тогда
		ТекущиеДанные = Элементы.Подарки.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.СтатусУказанияСерий = 0 Тогда
				Отказ = Истина;
			Иначе
				Элементы.Подарки.ЗакончитьРедактированиеСтроки(Ложь);
				Если Элемент.ТекущийЭлемент = Элементы.ПодаркиКоличествоУпаковок Тогда
					УказатьСерии("КоличествоУпаковок", "Подарки", "СерииПодарков");
				Иначе
					УказатьСерии("Упаковка", "Подарки", "СерииПодарков");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСистемуНалогообложения(ИдентификаторСтроки = Неопределено, ЗаполнятьВидНалога = Истина)
	
	Если Объект.Товары.Количество() > 0 Тогда
		Если Не Объект.СистемаНалогообложения = Объект.Товары[0].ВидНалога Тогда
			Объект.СистемаНалогообложения = Объект.Товары[0].ВидНалога;
		КонецЕсли;
	Иначе
		Если ЗаполнятьВидНалога Тогда
			ЗаполнитьВидНалога();
		КонецЕсли;
	КонецЕсли;
	
	Если ВозможныРазныеНалоговыеРежимыСкладов И
		ТипОборудованияКассыККМ = ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККТ") Тогда
		
		Если Объект.Товары.Количество() = 1 Тогда
			
			ТекстСообщения = НСтр("ru = 'Далее в этот чек следует добавлять товары с системой налогообложения ""%1"".'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Объект.Товары[0].ВидНалога);
			
			ЗаголовокСообщения = НСтр("ru = 'Система налогообложения чека'");
			
			ПоказатьОповещениеПользователя(ЗаголовокСообщения,, ТекстСообщения);
			
		ИначеЕсли Объект.Товары.Количество() > 1 Тогда
		
			ЕстьРазличныеЗначения = Ложь;
			
			ВидНалогаПервойСтроки = Объект.Товары[0].ВидНалога;
			
			Если ИдентификаторСтроки = Неопределено Тогда
				Для Инд = 1 По Объект.Товары.Количество() - 1 Цикл
					Если Не ВидНалогаПервойСтроки = Объект.Товары[Инд].ВидНалога Тогда
						ЕстьРазличныеЗначения = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ЭлементКоллекции = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
				Инд = Объект.Товары.Индекс(ЭлементКоллекции);
				Если Не ВидНалогаПервойСтроки = Объект.Товары[Инд].ВидНалога Тогда
					ЕстьРазличныеЗначения = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ЕстьРазличныеЗначения Тогда
				ТекстСообщения = НСтр("ru = 'В одном чеке, пробиваемом в ККТ с передачей данных, могут пробиваться товары с одной системой налогообложения. Для строки 1 определена: ""%1"". Для строки %2 определена: ""%3"".'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Объект.Товары[0].ВидНалога, Инд + 1, Объект.Товары[Инд].ВидНалога);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Объект.Товары[" + Инд + "].Номенклатура");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьРазныхНалоговыхРежимовСкладов()
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрименениеСистемНалогообложенияСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
	|	ПрименениеСистемНалогообложенияСрезПоследних.Организация КАК Организация,
	|	ПрименениеСистемНалогообложенияСрезПоследних.Период КАК Период,
	|	ПрименениеСистемНалогообложенияСрезПоследних.Склад КАК Склад,
	|	ПрименениеСистемНалогообложенияСрезПоследних.ТоварнаяГруппа КАК ТоварнаяГруппа
	|ПОМЕСТИТЬ ВТСрезПоследних
	|ИЗ
	|	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних(
	|			&Дата,
	|			Магазин = &Магазин
	|				И ВЫБОР
	|					КОГДА СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.ТипыСистемНалогообложенияККТ.Патент)
	|						ТОГДА &Дата >= ДатаНачалаПатента
	|									И &Дата <= ДатаОкончанияПатента
	|								ИЛИ ДатаНачалаПатента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|									И ДатаОкончанияПатента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ) КАК ПрименениеСистемНалогообложенияСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСрезПоследних.Организация КАК Организация,
	|	ВТСрезПоследних.Склад КАК Склад,
	|	ВТСрезПоследних.ТоварнаяГруппа КАК ТоварнаяГруппа,
	|	МАКСИМУМ(ВТСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ ВТПериод
	|ИЗ
	|	ВТСрезПоследних КАК ВТСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСрезПоследних.Организация,
	|	ВТСрезПоследних.Склад,
	|	ВТСрезПоследних.ТоварнаяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения
	|ИЗ
	|	ВТСрезПоследних КАК ВТСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериод КАК ВТПериод
	|		ПО ВТСрезПоследних.Организация = ВТПериод.Организация
	|			И ВТСрезПоследних.Склад = ВТПериод.Склад
	|			И ВТСрезПоследних.ТоварнаяГруппа = ВТПериод.ТоварнаяГруппа";
	Запрос.УстановитьПараметр("Дата", НачалоДня(Объект.Дата));
	Запрос.УстановитьПараметр("Магазин", Объект.Магазин);
	Результат = Запрос.Выполнить().Выбрать();
	
	ВозможныРазныеНалоговыеРежимыСкладов = Результат.Количество() > 1;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияИспользовалаЕНВД()
	
	ИспользовалсяЕНВД =
		РегистрыСведений.ПрименениеСистемНалогообложения.ОрганизацияИспользовалаЕНВД(
			?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()),
			Объект.Организация);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСистемуНалогообложения()
	
	ВидимостьСистемыНалогообложения();
	
	Если Объект.Товары.Количество() > 0 Тогда
		Если НЕ Объект.СистемаНалогообложения = Объект.Товары[0].ВидНалога Тогда
			Объект.СистемаНалогообложения = Объект.Товары[0].ВидНалога;
		КонецЕсли;
	Иначе
		ЗаполнитьВидНалога();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидимостьСистемыНалогообложения()
	
	Элементы.СтраницаСНОРедактировать.Видимость = ИспользовалсяЕНВД;
	Элементы.СтраницаСНОНеРедактировать.Видимость = ВозможныРазныеНалоговыеРежимыСкладов И НЕ ИспользовалсяЕНВД;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуSMSИEmail()
	
	Если ЗначениеЗаполнено(ДанныеSMSИлиEmail)
		И ЗначениеЗаполнено(Объект.ДисконтнаяКарта) Тогда
		Если ОтказКлиентаОтСохраненияТелефонаEmail Тогда
			Элементы.ДекорацияОтказКлиентаОтСохраненияТелефонаEmail.Картинка = БиблиотекаКартинок.НеСохранять16;
		Иначе
			Элементы.ДекорацияОтказКлиентаОтСохраненияТелефонаEmail.Картинка = БиблиотекаКартинок.Сохранить16;
		КонецЕсли;
	Иначе
		Элементы.ДекорацияОтказКлиентаОтСохраненияТелефонаEmail.Картинка = БиблиотекаКартинок.Пустая;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВозможностьПечатиБумажногоЧека()
	
	Возврат ПодключаемоеОборудованиеРТ.ПроверитьВозможностьПечатиБумажногоЧека(Объект.КассаККМ);
	
КонецФункции

#Область ЕГАИС

&НаСервере
Функция ПодготовитьДанныеДляПробитияЧека(НомерЧека)
	
	СтруктураДанных = РозничныеПродажиКлиентСервер.СтруктураДанныхДляПробитияЧека();
	СтруктураДанных.ЧекККМСсылка = Объект.Ссылка;
	СтруктураДанных.НомерЧека = НомерЧека;
	СтруктураДанных.Объект = Объект;
	СтруктураДанных.ЭтоВозвратПродукцииИСМПБезМарки = ЭтоВозвратПродукцииИСМПБезМарки;
	СтруктураДанных.НеПечататьБумажныйЧек = ПараметрыКассыККМ.НеПечататьБумажныйЧек;
	
	Возврат РозничныеПродажиСервер.ПодготовитьДанныеДляПробитияЧека(СтруктураДанных);
	
КонецФункции

&НаКлиенте
Процедура ПредставитьТелефонИEmailКлиент()
	
	Если ЗначениеЗаполнено(Объект.Телефон) И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Объект.Телефон) Тогда
		СохраненныйТелефон = Число(Объект.Телефон);
		Телефон = 0;
		ПодключаемоеОборудованиеРТКлиент.ПреобразоватьТелефонКПользовательскомуВиду(СохраненныйТелефон, Телефон, ТелефонСтрокой);
		ДанныеSMSИлиEmail = ТелефонСтрокой;
	ИначеЕсли ЗначениеЗаполнено(Объект.АдресЭП) Тогда
		ДанныеSMSИлиEmail = Объект.АдресЭП;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПослеПередачиЧекаЕГАИС(Изменения, ПараметрыВыполнения) Экспорт
	
	РозничныеПродажиКлиент.ПослеПередачиЧекаЕГАИС(Изменения, ПараметрыВыполнения, "ЧекККМ", Объект);
	
	ВыполнитьОбработкуОповещения(ПараметрыВыполнения.ОповещениеПродолжения, ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ДеактивироватьСкидкиПоИдентификаторуЧека()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
		СкидкиНаценкиСерверПереопределяемый.ДеактивироватьСкидкиПоИдентификаторуЧека(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПогаситьОднократныеСкидки()
	
	СкидкиНаценкиСерверПереопределяемый.ПогаситьОднократныеСкидки(Объект);
	
КонецПроцедуры

#Область РаботаСБуферомОбмена

&НаСервере
Процедура ПолучитьСтрокиИзБуфераОбмена()
	
	ТаблицаТоваров = КопированиеСтрокСервер.СтрокиИзБуфераОбмена();
	
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ЗаполнитьДоговорПлатежногоАгента");
		
		СтруктураДействий.Вставить("ОчиститьДанныеПоЗаказу");
		СтруктураДействий.Вставить("ПроверитьСерийныеНомераПоВладельцу",
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПроверкиСерийныхНомеровПоВладельцу(ТекущаяСтрока, Объект.СерийныеНомера));
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу"   , ТекущаяСтрока.Характеристика);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		
		СтруктураПараметровДействия = Новый Структура;
		СтруктураПараметровДействия.Вставить("Магазин", Объект.Магазин);
		СтруктураПараметровДействия.Вставить("РабочееМесто", РабочееМесто);
		СтруктураПараметровДействия.Вставить("Организация", Объект.Организация);
		
		СтруктураДействий.Вставить("ЗаполнитьСкладПродажи", СтруктураПараметровДействия);
		
		СтруктураДействий.Вставить("ЗаполнитьВидНалога", 
			ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПараметровЗаполненияВидаНалога(Объект));
		
		СтруктураПараметровСтавкиНДС = Новый Структура;
		СтруктураПараметровСтавкиНДС.Вставить("Дата"       , Объект.Дата);
		СтруктураПараметровСтавкиНДС.Вставить("Организация", Объект.Организация);
		
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДССкладВСтроке", СтруктураПараметровСтавкиНДС);
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураПересчетаСуммыНДСВСтрокеТЧ(Объект));
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки"        , Новый Структура("Очищать", Истина));
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
		
		СтруктураДействий.Вставить("ПроставитьПродавца", Объект.Продавец);
		
		Если ИспользоватьАссортимент Тогда
			СтруктураДействий.Вставить("ПроверитьАссортиментСтроки", АссортиментКлиентСервер.ПараметрыПроверкиАссортимента(Объект, Истина));
		КонецЕсли;
		
		СтруктураДействий.Вставить("ПроверитьЗапретРозничнойПродажи", СкидкиНаценкиСервер.ПараметрыПроверкиЗапретаРозничнойПродажи(Объект));
		
		ДополнитьСтруктуруДействиямиНДС(СтруктураДействий, ВестиУчетМаркируемойПродукцииИСМП);
		ДополнитьСтруктуруДействиямиГосИС(СтруктураДействий, Объект.ОперацияСДенежнымиСредствами, МаркировкаВключена);
		
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ЗаполнитьКлючСвязи(Объект.Товары, ТекущаяСтрока, "КлючСвязи");
		
		КэшированныеЗначения = ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураКэшируемыхЗначений();
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьКэшированныеЗначенияДляУчетаСерий(ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий, Ложь);
		ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
		ОбработкаТабличнойЧастиТоварыСервер.ВыделитьАгентскоеВознаграждение(Объект, ЭтотОбъект, СтруктураДействий, ТекущаяСтрока, КэшированныеЗначения);
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		
		ТекущаяСтрокаИдентификатор = ТекущаяСтрока.ПолучитьИдентификатор();
		
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(Объект, 
			ПараметрыУказанияСерий, ТекущаяСтрокаИдентификатор, КэшированныеЗначения);
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьКэшированныеЗначенияДляУчетаСерий(ТекущаяСтрока,
			КэшированныеЗначения,ПараметрыУказанияСерий,Ложь);
		
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатовВСтроке(ТекущаяСтрока);
		
		Если ПропуститьАвтоматическийРасчетСкидок Тогда
			ПропуститьАвтоматическийРасчетСкидок = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	КопированиеСтрокСервер.ОчиститьБуферОбмена();
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьСтрокиНаСервере()
	
	КопированиеСтрокСервер.ПоместитьВыделенныеСтрокиВБуферОбмена(Элементы.Товары.ВыделенныеСтроки, Объект.Товары);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКомандБуфераОбмена()
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ТоварыВставитьСтроки");
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Доступность", 
		НЕ ОбщегоНазначения.ПустойБуферОбмена("Строки"));
		
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьСтавкиНДС()
	
	ТекущаяДата = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	ДатаПереходногоПериода = УчетНДС.ДатаПереходногоПериода();
	ЭтоВозврат  = Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат;
	
	Если ЗначениеЗаполнено(Объект.ЧекККМПродажа) И Объект.ЧекККМПродажа.Дата < ДатаПереходногоПериода  //Возврат чека за 2018 год.
		И ТекущаяДата > ДатаПереходногоПериода И ЭтоВозврат Тогда
		
		УчетНДС.ЗаполнитьСписокВыбораСтавокНДС(Элементы, ДатаПереходногоПериода - 86400);
		
	ИначеЕсли ЗначениеЗаполнено(Объект.ДокументРасчета) И Объект.ДокументРасчета.Дата < ДатаПереходногоПериода //Возврат чека на Аванс за 2018 год.
		И ТекущаяДата > ДатаПереходногоПериода И ЭтоВозврат Тогда
		
		УчетНДС.ЗаполнитьСписокВыбораСтавокНДС(Элементы, ДатаПереходногоПериода - 86400);
		
	Иначе 
		
		УчетНДС.ЗаполнитьСписокВыбораСтавокНДС(Элементы, ТекущаяДата);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКассаККМНаСервере(НомерДокументаКассыККМ, ПорядковыйНомерПродажи)
	
	ПриИзмененииКассаККМСервер();
	
	Если ЗначениеЗаполнено(Объект.КассаККМ) И НомерДокументаКассыККМ[Объект.КассаККМ] = Неопределено Тогда
		ЗаполнитьНомерДокументаКассыККМ(Объект.КассаККМ, НомерДокументаКассыККМ);
	КонецЕсли;
	
	Если НЕ ПродажаПоЗаказу Тогда
		ПересчитатьИлиОтменитьСкидкиИОплатуБонусамиНаСервере(ПорядковыйНомерПродажи);
	КонецЕсли;
	
	УстановитьЭквайринговыйТерминалПоУмолчаниюСервер();
	УстановитьКомандыВидовОплаты();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНомерДокументаКассыККМ(КассаККМ, НомерДокументаКассыККМ)
	
	Если ЗначениеЗаполнено(КассаККМ) И НомерДокументаКассыККМ[КассаККМ] = Неопределено Тогда
		НомерЧекаККМ = ОбщегоНазначенияРТВызовСервера.ПоследнийНомерДокументаКассыККМ(КассаККМ);
	КонецЕсли; 
	Если Не (НомерЧекаККМ <> Неопределено И НомерЧекаККМ <> 0) Тогда
		НомерЧекаККМ = 1;
	КонецЕсли; 
	НомерДокументаКассыККМ.Вставить(КассаККМ, НомерЧекаККМ);
		
КонецПроцедуры

&НаСервере
Процедура РассчитатьСкидкиНаценкиСервер(ПорядковыйНомерПродажи, ДополнительныеПараметры = Неопределено)
	
	Если Объект.Товары.Количество() = 0 Тогда
		ОтменитьСкидки();
		ОтменитьОплатуБонусами();
		Возврат;
	КонецЕсли;
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ПрименятьКОбъекту",                Истина);
	СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет",      Ложь);
	СтруктураПараметры.Вставить("ВосстанавливатьУправляемыеСкидки", Истина);
	СтруктураПараметры.Вставить("УправляемыеСкидки",                УправляемыеСкидки);
	СтруктураПараметры.Вставить("ТолькоСообщенияПослеОформления",   Ложь);
	СтруктураПараметры.Вставить("ПорядковыйНомерПродажи",           ПорядковыйНомерПродажи);
	СтруктураПараметры.Вставить("ПеренестиСкидкиПодаркиВТовары");
	СтруктураПараметры.Вставить("КонтролироватьОстаткиТоваров", 	КонтролироватьОстаткиТоваров);
	СтруктураПараметры.Вставить("РабочееМесто", РабочееМесто);
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Если ДополнительныеПараметры.Свойство("БудетОплатаБонусами") Тогда
			СтруктураПараметры.Вставить("БудетОплатаБонусами", ДополнительныеПараметры.БудетОплатаБонусами);
		КонецЕсли;
	КонецЕсли;
	
	Объект.Дата = ОбщегоНазначения.РабочаяДатаПользователя();
	
	РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЭквайринговыйТерминалПоУмолчаниюСервер(ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда
		
		ЭквайринговыйТерминалПоУмолчанию = ПолучитьЭквайринговыйТерминалПоУмолчанию(Неопределено);
		Для Каждого ТекущаяСтрока Из Объект.Оплата Цикл 
			
			ТекущаяСтрока.ЭквайринговыйТерминал = ЭквайринговыйТерминалПоУмолчанию;
			ТекущаяСтрока.ПроцентКомиссии = ЭквайрингВызовСервера.ПолучитьПроцентКомиссииПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
			ПересчитатьСуммуКомиссии(ТекущаяСтрока);
			ТекущаяСтрока.ТипОплаты = ТипОплатыПоВиду(ТекущаяСтрока.ВидОплаты);
			
		КонецЦикла;
		
	Иначе
		
		ЭквайринговыйТерминалПоУмолчанию = ПолучитьЭквайринговыйТерминалПоУмолчанию(ТекущаяСтрока.ВидОплаты);
		ТекущаяСтрока.ЭквайринговыйТерминал = ЭквайринговыйТерминалПоУмолчанию;
		ТекущаяСтрока.ПроцентКомиссии = ЭквайрингВызовСервера.ПолучитьПроцентКомиссииПоТерминалу(ТекущаяСтрока.ЭквайринговыйТерминал, ТекущаяСтрока.ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
		ПересчитатьСуммуКомиссии(ТекущаяСтрока);
		ТекущаяСтрока.ТипОплаты = ТипОплатыПоВиду(ТекущаяСтрока.ВидОплаты);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИлиОтменитьСкидкиИОплатуБонусамиНаСервере(ПорядковыйНомерПродажи, УдалятьСтрокиИзОплаты = Истина)
	
	Если АвтоматическийРасчетСкидок Тогда
		Если НЕ ПропуститьАвтоматическийРасчетСкидок Тогда
			Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда
				ПодготовитьСкидкиИОплатуБонусамиКСторнированию();
			Иначе
				РассчитатьСкидкиНаценкиСервер(ПорядковыйНомерПродажи);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
			ОтменитьСкидки();
			ОтменитьОплатуБонусами(УдалятьСтрокиИзОплаты);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ИнициализацияСтрокиТоваров(Форма, СтруктураПараметров) Экспорт
	
	ОбработатьБезМаркировки = Ложь;
	Если СтруктураПараметров.Свойство("ОбработатьБезМаркировки")
		И СтруктураПараметров.ОбработатьБезМаркировки Тогда
		ОбработатьБезМаркировки = Истина;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Количество") Тогда
		КоличествоУпаковок = СтруктураПараметров.Количество;
	Иначе
		КоличествоУпаковок = 1;
	КонецЕсли;
	
	ОбновитьКоличество = Ложь;
	Если СтруктураПараметров.Свойство("ОбновитьКоличество") Тогда
		ОбновитьКоличество = Истина;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("УчетУпаковок") Тогда
		УчетУпаковок = СтруктураПараметров.УчетУпаковок;
	Иначе
		УчетУпаковок = Истина;
	КонецЕсли; 
	
	Если СтруктураПараметров.Свойство("СворачиватьПоСтрокеПоиска") Тогда
		СворачиватьПоСтрокеПоиска = СтруктураПараметров.СворачиватьПоСтрокеПоиска;
	Иначе
		СворачиватьПоСтрокеПоиска = Истина;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура;
	
	МассивСтрок = Новый Массив;
	Если СворачиватьПоСтрокеПоиска И Форма.Объект.Товары.Количество() > 0 Тогда
		СтруктураПоиска.Вставить("Номенклатура", СтруктураПараметров.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", СтруктураПараметров.Характеристика);
		Если УчетУпаковок Тогда
			СтруктураПоиска.Вставить("Упаковка", СтруктураПараметров.Упаковка);
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ОбработатьБезМаркировки")
			И СтруктураПараметров.ОбработатьБезМаркировки Тогда 
			
			СтруктураПоиска.Вставить("МаркируемаяПродукция", Ложь);
		КонецЕсли;
		
		МассивСтрок = Форма.Объект.Товары.НайтиСтроки(СтруктураПоиска);
		
	КонецЕсли;
	
	Если МассивСтрок.Количество() > 0 Тогда 
		
		ТекущаяСтрока = МассивСтрок[0];
		
		Если СтруктураПараметров.Свойство("СтруктураОтбораРедактируемыхСтрок") Тогда
			РедактируемыеСтроки = Форма.Объект.Товары.НайтиСтроки(СтруктураПараметров.СтруктураОтбораРедактируемыхСтрок);
			Если РедактируемыеСтроки.Найти(ТекущаяСтрока) = Неопределено Тогда
				Возврат ТекущаяСтрока;
			КонецЕсли;
		КонецЕсли;
		
		Если ТекущаяСтрока.Свойство("ИспользоватьСерийныеНомера") Тогда
			УстановитьКоличество = ОбновитьКоличество И НЕ ТекущаяСтрока.ИспользоватьСерийныеНомера;
		Иначе
			УстановитьКоличество = ОбновитьКоличество;
		КонецЕсли;
		
		Если УстановитьКоличество Тогда
			
			Если УчетУпаковок Тогда
				ТекущаяСтрока.КоличествоУпаковок = КоличествоУпаковок;
			Иначе
				ТекущаяСтрока.Количество = КоличествоУпаковок;
			КонецЕсли;
			
		ИначеЕсли УчетУпаковок Тогда
			
			Если СтруктураПараметров.Упаковка = ТекущаяСтрока.Упаковка Тогда
				КоэффициентПересчета = 1;
			Иначе
				КоэффициентУпаковкиПоиска = 1;
				Если ЗначениеЗаполнено(СтруктураПараметров.Упаковка) Тогда
					КоэффициентУпаковкиПоиска = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураПараметров.Упаковка, "Коэффициент");
					Если КоэффициентУпаковкиПоиска <= 0 Тогда
						КоэффициентУпаковкиПоиска = 1;
					КонецЕсли;
				КонецЕсли;
				
				КоэффициентУпаковкиСтроки = 1;
				Если ЗначениеЗаполнено(ТекущаяСтрока.Упаковка) Тогда
					КоэффициентУпаковкиПоиска = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Упаковка, "Коэффициент");
					Если КоэффициентУпаковкиСтроки <= 0 Тогда
						КоэффициентУпаковкиСтроки = 1;
					КонецЕсли;
				КонецЕсли;
			
				КоэффициентПересчета = КоэффициентУпаковкиПоиска / КоэффициентУпаковкиСтроки;
			КонецЕсли;
			
			ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.КоличествоУпаковок + КоличествоУпаковок * КоэффициентПересчета;
		Иначе
			ТекущаяСтрока.Количество = ТекущаяСтрока.Количество + КоличествоУпаковок;
		КонецЕсли;
		
	Иначе
		
		ТекущаяСтрока = Форма.Объект.Товары.Добавить();
		ТекущаяСтрока.Номенклатура = СтруктураПараметров.Номенклатура;
		ТекущаяСтрока.Характеристика = СтруктураПараметров.Характеристика;
		Если УчетУпаковок Тогда
			ТекущаяСтрока.Упаковка = СтруктураПараметров.Упаковка;
		КонецЕсли;
		Если ТекущаяСтрока.Свойство("ИспользоватьСерийныеНомера") Тогда
			ТекущаяСтрока.ИспользоватьСерийныеНомера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураПараметров.Номенклатура, "ИспользоватьСерийныеНомера");
		КонецЕсли;
		Если ТекущаяСтрока.Свойство("Штрихкод")
			И СтруктураПараметров.Свойство("Штрихкод") Тогда
			Если СтруктураПараметров.Свойство("ШтрихкодУпаковки") Тогда
				Если НЕ ЗначениеЗаполнено(СтруктураПараметров.ШтрихкодУпаковки) Тогда
					ТекущаяСтрока.Штрихкод = СтруктураПараметров.Штрихкод;
				КонецЕсли;
			Иначе
				ТекущаяСтрока.Штрихкод = СтруктураПараметров.Штрихкод;
			КонецЕсли;
		КонецЕсли;
		Если УчетУпаковок Тогда
			ТекущаяСтрока.КоличествоУпаковок = КоличествоУпаковок;
		Иначе
			ТекущаяСтрока.Количество = КоличествоУпаковок
		КонецЕсли;
		
		СтруктураПараметров.Вставить("ДобавленаСтрока", Истина);
		
	КонецЕсли;
	
	Если ТекущаяСтрока.Свойство("ИспользоватьСерийныеНомера")
		И ТекущаяСтрока.ИспользоватьСерийныеНомера
		И НЕ СтруктураПараметров.Свойство("ДобавитьКоличество") Тогда
		МассивСерийныхНомеров = ОбработкаТабличнойЧастиТоварыКлиентСервер.МассивСерийныхНомеровДляСтрокиТоваров(
									ТекущаяСтрока.КлючСвязиСерийныхНомеров,
									Форма.Объект.СерийныеНомера);
		Если УчетУпаковок Тогда
			ТекущаяСтрока.КоличествоУпаковок = МассивСерийныхНомеров.Количество();
		Иначе
			ТекущаяСтрока.Количество = МассивСерийныхНомеров.Количество();
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущаяСтрока;
	
КонецФункции

&НаСервере
Процедура СвернутьСтрокиГИСМ(Номенклатура, Характеристика)
	
	ПерезаполнитьПризнакиМаркировки();
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", Номенклатура);
	СтруктураПоиска.Вставить("Характеристика", Характеристика);
	
	МассивСтрок = Объект.Товары.НайтиСтроки(СтруктураПоиска);
	КоличествоНайденныхСтрок = МассивСтрок.Количество();
	Если КоличествоНайденныхСтрок > 1 
		И МассивСтрок[0].ОсобенностиУчетаНоменклатуры = Перечисления.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха Тогда
		
		НоваяТекущаяСтрока = МассивСтрок[КоличествоНайденныхСтрок - 2];
		Элементы.Товары.ТекущаяСтрока = НоваяТекущаяСтрока.ПолучитьИдентификатор();
		Объект.Товары.Удалить(Объект.Товары.Количество() - 1);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьВозвратПродукцииИСМПБезМарок(СписокНоменклатуры)
	
	ЗаголовокИнформации = НСтр("ru = 'Возврат маркируемой продукции'");
	
	ТекстВопроса = НСтр("ru = 'Не указаны коды маркировки для следующих товаров:'") + Символы.ПС;
	Для Каждого ЭлементСписка Из СписокНоменклатуры Цикл 
		ТекстВопроса = ТекстВопроса + "%1" + Символы.ПС;
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, ЭлементСписка);
	КонецЦикла;
	ТекстВопроса = ТекстВопроса + Символы.ПС + НСтр("ru = 'Продолжить пробитие?'");
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеПодтвержденияВозвратаПродукцииИСМПБезМарок", ЭтотОбъект);
	ОбщегоНазначенияРТКлиент.ВывестиВопросДляРМКУправляемой(ЗаголовокИнформации, ТекстВопроса, ,ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьВидаОперации()
	
	МассивМаркируемыхТоваров = Объект.Товары.НайтиСтроки(Новый Структура("НеобходимостьВводаАкцизнойМарки", Истина));
	Если МассивМаркируемыхТоваров.Количество() > 0 Тогда 
		Элементы.ВидОперации.Доступность = Ложь;
	Иначе 
		Элементы.ВидОперации.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Область Штрихкодирование

&НаКлиенте
Процедура Подключаемый_ПослеОбработкиШтрихкодов()
	
	ШтрихкодированиеИСКлиентПереопределяемый.ПослеОбработкиШтрихкодов(
		ЭтотОбъект,
		ДанныеДляОбработки,
		КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКодМаркировки(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ИсходныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		Если ДополнительныеПараметры.Свойство("ВызовИзФормыРучногоВводаШК") Тогда 
			ЭтоКодМаркировки = РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьШтрихкодПоФорматуКодовМаркировки(ИсходныеДанные.Штрихкод);
			Если ЭтоКодМаркировки Тогда
				ТекстСообщения = НСтр("ru = 'Код маркировки необходимо сканировать напрямую в форме документа! Форма поиска по штрихкоду (F7) предназначена для ввода данных с клавиатуры!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Возврат;
			Иначе
				ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(ИсходныеДанные.Штрихкод);
				Если Не ДанныеНоменклатуры = Неопределено 
					И ЗначениеЗаполнено(ДанныеНоменклатуры.Номенклатура) Тогда
					ВидПродукцииШК = ПолучитьВидПродукцииПоНоменклатуре(ДанныеНоменклатуры.Номенклатура);
					УчетТГВключен = РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьВключениеУчетаТГ(ВидПродукцииШК);
					Если ЗначениеЗаполнено(ВидПродукцииШК) Тогда
						Если Не УчетТГВключен Тогда 
							ТекстОшибки = НСтр("ru = 'Внимание! Штрихкод %1 зарегистрирован на номенклатуру %2 с особенностями учета ""%3""! На текущий момент учет этой товарной группы в базе не включен.'")
							+ НСтр("ru = 'Необходимо изменить вид номенклатуры на ""Товар без особенностей учета"" или включить учет маркировки этой товарной группы!'");
							ТекстОшибки = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстОшибки, ИсходныеДанные.Штрихкод, ДанныеНоменклатуры.Номенклатура, ВидПродукцииШК);
							ОбщегоНазначенияИСМПТКВызовСервера.СоздатьЗаписьЖурналаРегистрации(НСтр("ru = 'Розничная продажа маркируемого товара'"), "Предупреждение",,, ТекстОшибки);
							ТекстСообщения = НСтр("ru = 'Внимание! Обнаружены проблемы в настройках учета маркируемой продукции! Требуется сообщить Администратору. Подробности в Журнале регистрации.'"); 
							ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
						Иначе
							ТекстСообщения = НСтр("ru = 'Внимание: среди выбранных товаров есть маркируемые, нужно заполнить коды маркировки в отдельной таблице!'");
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
	ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования(ЭтотОбъект);
	
	ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ЗакодироватьШтрихкодДанныхBase64(ИсходныеДанные);
	
	РезультатОбработки = ОбработатьВводШтрихкода(ИсходныеДанные, Неопределено, ПараметрыСканирования, ДополнительныеПараметры);

	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если Ложь И ДополнительныеПараметры.Свойство("ОбработатьКодМаркировкиПовторно") Тогда
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		ОбработатьКодМаркировки(ИсходныеДанные, ДополнительныеПараметры);
	Иначе
		ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияВводаШтрихкода(ИсходныеДанные, РезультатОбработки, ПараметрыСканирования);
		ШтрихкодированиеОбщегоНазначенияИСКлиент.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьВводШтрихкода(ДанныеШтрихкода, КэшированныеЗначения, ПараметрыСканирования = Неопределено, ДополнительныеПараметры)
	
	РезультатОбработкиШтрихкода =
		ШтрихкодированиеОбщегоНазначенияИС.ОбработатьВводШтрихкода(ЭтотОбъект, ДанныеШтрихкода, КэшированныеЗначения, ПараметрыСканирования);

	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если Ложь Тогда
	#Область ОтключенныйФункционал
	
	Если ДополнительныеПараметры.Свойство("ОбработатьКодМаркировкиПовторно") Тогда
		// Исключаем множественный вызов.
		ДополнительныеПараметры.Удалить("ОбработатьКодМаркировкиПовторно");
	ИначеЕсли РезультатОбработкиШтрихкода.ТребуетсяУточнениеДанных
		И НЕ РезультатОбработкиШтрихкода.ВидыПродукции.Найти(Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха) = Неопределено
		И ИнтеграцияГИСМ.ПодсистемаНеИспользуется() Тогда
		// Создание ШтрихкодаУпаковки для продукции из натурального меха по Серии.
		ДанныеПоискаПоШтрихкоду = ПодключаемоеОборудованиеРТ.ДанныеПоискаПоШтрихкоду(РезультатОбработкиШтрихкода.Штрихкод, ЭтотОбъект);
		Для Каждого ЭлементПоиска Из ДанныеПоискаПоШтрихкоду.ЗначенияПоиска Цикл
			Если ТипЗнч(ЭлементПоиска) = Тип("Структура") И ЭлементПоиска.Свойство("Серия")
				И ЗначениеЗаполнено(ЭлементПоиска.Серия) Тогда
				Если ИнтеграцияИСРТ.ЗарегистрироватьШтрихкодУпаковкиПоСерии(ЭлементПоиска.Серия) Тогда
					ДополнительныеПараметры.Вставить("ОбработатьКодМаркировкиПовторно");
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ДополнительныеПараметры.Свойство("ОбработатьКодМаркировкиПовторно") Тогда
		ПослеОбработкиШтрихкодов(РезультатОбработкиШтрихкода, КэшированныеЗначения);
	КонецЕсли;
	#КонецОбласти
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

&НаСервере
Процедура ПослеОбработкиШтрихкодов(РезультатОбработкиШтрихкода, КэшированныеЗначения)
	
	Если Не РезультатОбработкиШтрихкода.ТребуетсяОбработкаШтрихкода Тогда
		
		ОбработатьСтрокиТЧ(
			РезультатОбработкиШтрихкода.ДобавленныеСтроки,
			РезультатОбработкиШтрихкода.ИзмененныеСтроки,
			КэшированныеЗначения,
			РезультатОбработкиШтрихкода.ОбработкаШтрихкодаБезМарки);
			
		РезультатОбработкиШтрихкода.ИзмененныеСтроки = Неопределено;
		РезультатОбработкиШтрихкода.ДобавленныеСтроки = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЗавершенияВводаШтрихкода(ИсходныеДанные, РезультатОбработки, ПараметрыСканирования)
	
	ПараметрыЗавершенияВводаШтрихкода = ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыЗавершенияОбработкиШтрихкода(,,"ОповещениеЗавершениеОбработкиКодаМаркировки");
	ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода = РезультатОбработки;
	ПараметрыЗавершенияВводаШтрихкода.Форма                       = ЭтотОбъект;
	ПараметрыЗавершенияВводаШтрихкода.ПараметрыСканирования       = ПараметрыСканирования;
	ПараметрыЗавершенияВводаШтрихкода.ДанныеШтрихкода             = ИсходныеДанные;
	
	Возврат ПараметрыЗавершенияВводаШтрихкода;
	
КонецФункции

&НаКлиенте
Процедура ОповещениеЗавершениеОбработкиКодаМаркировки(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.ТребуетсяОбработкаШтрихкода Тогда
		Штрихкод = Неопределено;
		Если ЗначениеЗаполнено(Результат.Штрихкод) Тогда
			Штрихкод = Результат.Штрихкод;
		ИначеЕсли ТипЗнч(Результат.ИсходныеДанные) = Тип("Структура") И Результат.ИсходныеДанные.Свойство("Штрихкод") Тогда
			Штрихкод = Результат.ИсходныеДанные.Штрихкод;
		ИначеЕсли ТипЗнч(Результат.ДанныеШтрихкода) = Тип("Структура") И Результат.ДанныеШтрихкода.Свойство("Штрихкод") Тогда
			Штрихкод = Результат.ДанныеШтрихкода.Штрихкод;
		ИначеЕсли ТипЗнч(Результат.ДанныеШтрихкода) = Тип("Строка") Тогда
			Штрихкод = Результат.ДанныеШтрихкода;
		КонецЕсли;
		
		// Обработаем штрихкод средствами РТ.
		Если ЗначениеЗаполнено(Штрихкод) Тогда
			ОповещениеПоискаПоШтрихкоду(Штрихкод);
		КонецЕсли;
		
	КонецЕсли;
	
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	УстановитьДоступностьВидаОперации();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокиТЧ(ДобавленныеСтроки, ИзмененныеСтроки, КэшированныеЗначения = Неопределено, ОбработатьБезМаркировки = Ложь) Экспорт
	
	Для Каждого Строка Из ДобавленныеСтроки Цикл
		СтруктураПараметров = ШтрихкодированиеИСРТ.ДанныеШтрихкодаДляОбработки();
		СтруктураПараметров.Вставить("ТекущаяСтрока", Строка);
		СтруктураПараметров.Вставить("ДобавленаСтрока", Истина);
		СтруктураПараметров.ОбработатьБезМаркировки = ОбработатьБезМаркировки;
		
		ДобавитьНайденныеПозицииТоваровСервер(СтруктураПараметров);
	КонецЦикла;
	
	Для Каждого Строка Из ИзмененныеСтроки Цикл
		СтруктураПараметров = ШтрихкодированиеИСРТ.ДанныеШтрихкодаДляОбработки();
		СтруктураПараметров.Вставить("ТекущаяСтрока", Строка);
		СтруктураПараметров.ОбработатьБезМаркировки = ОбработатьБезМаркировки;
		
		ДобавитьНайденныеПозицииТоваровСервер(СтруктураПараметров);
	КонецЦикла;
	
	Если ДобавленныеСтроки.Количество()>0 Или ИзмененныеСтроки.Количество()>0 Тогда
		СобытияФормИС.ПриИзмененииЭлемента(ЭтотОбъект, "Товары", Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеОповещенияОбработкиКодаМаркировки()

	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если ОбщегоНазначенияИСМПТКВызовСервера.ПроверитьЗначениеОбщейФОМаркировки() 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		ДопПараметры = Новый Структура("ВызовИзФормыРучногоВводаШК", Истина);
		//Передача доп.параметра необходима для определения места вызова в ОповещениеПоискаПоШтрихкоду: если вызов пришел из формы Ф7, 
		//нужно будет выводить сообщение по маркировке. Место вызова определяем по наличию доп.параметра
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьКодМаркировки", ЭтотОбъект, ДопПараметры);	
	Иначе	
	    ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьКодМаркировки", ЭтотОбъект);
	КонецЕсли;
	
	Возврат ОписаниеОповещения;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
КонецФункции

&НаКлиенте
Функция ОбработкаКодаМаркировкиВыполнитьДействие(ДанныеДляВыполненияДействия, ДополнительныеПараметры) Экспорт
	
	РезультатВыбора             = ДанныеДляВыполненияДействия.РезультатВыбора;
	РезультатОбработкиШтрихкода = ДанныеДляВыполненияДействия.РезультатОбработкиШтрихкода;
	КэшированныеЗначения        = ДанныеДляВыполненияДействия.КэшированныеЗначения;
	ПараметрыСканирования       = ДанныеДляВыполненияДействия.ПараметрыСканирования;
	
	Действие = ДанныеДляВыполненияДействия.Действие;
	РезультатОбработкиШтрихкода = ОбработкаКодаМаркировкиВыполнитьДействиеСервер(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения);
	
	ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияВводаШтрихкода(
		ДанныеДляВыполненияДействия.ИсходныеДанные, РезультатОбработкиШтрихкода, ДанныеДляВыполненияДействия.ПараметрыСканирования);
	ШтрихкодированиеОбщегоНазначенияИСКлиент.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
	
КонецФункции

&НаСервере
Функция ОбработкаКодаМаркировкиВыполнитьДействиеСервер(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения)
	
	ПараметрыОбработкиВыбора    = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьПараметрыОбработкиВыбора(РезультатВыбора, РезультатОбработкиШтрихкода, КэшированныеЗначения);
	РезультатОбработкиШтрихкода = ШтрихкодированиеИС.ВыполнитьДействие(ЭтотОбъект, Действие, ПараметрыОбработкиВыбора);
	
	ПослеОбработкиШтрихкодов(РезультатОбработкиШтрихкода, КэшированныеЗначения);
	
	РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Неопределено;
	РезультатОбработкиШтрихкода.ДобавленныеСтроки = Неопределено;
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОткрытьФормуУточненияДанных()
	
	СобытияФормРТКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "Подключаемый_ОткрытьФормуУточненияДанных");
	
КонецПроцедуры

#КонецОбласти

#Область Маркировка

&НаКлиентеНаСервереБезКонтекста
Процедура ДополнитьСтруктуруДействиямиНДС(СтруктураДействий, ВестиУчетМаркируемойПродукцииИСМП)
	
	ИнтеграцияИСРТКлиентСервер.ДополнитьСтруктуруДействиямиНДС(СтруктураДействий, ВестиУчетМаркируемойПродукцииИСМП);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДополнитьСтруктуруДействиямиГосИС(СтруктураДействий, ЭтоОперацияСДС, МаркировкаВключена, БезМаркировки = Ложь)
	
	ИнтеграцияИСРТКлиентСервер.ДополнитьСтруктуруДействиямиГосИС(СтруктураДействий, ЭтоОперацияСДС, МаркировкаВключена, БезМаркировки);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияНаСервере(ИмяСобытия, Параметр, Источник)

	ДополнительныеПараметры = Новый Структура("ИмяСобытия, Параметр, Источник", ИмяСобытия, Параметр, Источник);
	СобытияФормИС.ПриИзмененииЭлемента(ЭтотОбъект, "Событие", ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЭлементаГосИС()
	
	ДополнительныеПараметры = Новый Структура;
	СобытияФормИСКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "Товары", ДополнительныеПараметры);
	Если ДополнительныеПараметры.Свойство("ТребуетсяСерверныйВызов") Тогда
		ТоварыПриОкончанииРедактированияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПриОкончанииРедактированияНаСервере()
	
	СобытияФормИС.ПриИзмененииЭлемента(ЭтотОбъект, "Товары", Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПризнакиМаркировки()
	
	//Отключенный функционал
	Если Ложь И МаркировкаВключена Тогда 
		ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьДанныеМаркировки(ЭтотОбъект);
		
		Настройки = ПроверкаИПодборПродукцииИСМПРТ.НастройкиИсточникаКешаЧека();
		ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(ЭтотОбъект, Настройки);
		ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтотОбъект, Настройки);
		ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ТребуетсяУточнениеПоКодуGTIN(ДанныеЧастичногоВыбытия)
	
	//Отключенный функционал
	Возврат Ложь И ИнтеграцияИСРТ.ТребуетсяУточнениеПоКодуGTIN(ДанныеЧастичногоВыбытия,
		ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ЭтотОбъект));
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ОтменитьОплатуВПлатежнойСистемеКлиент()
	
	ПараметрыПроцедуры = Новый Структура();
	ПараметрыПроцедуры.Вставить("Организация", 		Объект.Организация);
	ПараметрыПроцедуры.Вставить("Магазин", 			Объект.Магазин);
	ПараметрыПроцедуры.Вставить("КассаККМ", 		Объект.КассаККМ);
	ПараметрыПроцедуры.Вставить("ДокументОплаты", 	Объект.Ссылка);
	
	РезультатОтмены = ОтменитьОплатуВПлатежнойСистемеСервер(ПараметрыПроцедуры);
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОтмены.СообщениеОбОшибке);
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервере
Функция ОтменитьОплатуВПлатежнойСистемеСервер(ПараметрыПроцедуры)
	
	ВидОплаты	= объект.Оплата.Выгрузить().ВыгрузитьКолонку("ВидОплаты");
	ВидОплаты	= ?(ВидОплаты.Количество(), ВидОплаты[0], Неопределено);
	
	Интеграции 	= ИнтеграцияСПлатежнымиСистемамиРТСервер.НастройкиИнтеграции(ПараметрыПроцедуры.Организация, ПараметрыПроцедуры.Магазин, ВидОплаты);
	
	Если Интеграции.Количество() Тогда
		
		ПараметрыПроцедуры.Вставить("Интеграция", Интеграции[0].Интеграция);
		
		Возврат ИнтеграцияСПлатежнымиСистемамиРТСервер.ОтменитьОплату(ПараметрыПроцедуры);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьРасчетыСКлиентами()
	
	ДокументРасчетаЗаполнен = ЗначениеЗаполнено(Объект.ДокументРасчета);
	
	Элементы.ДокументРасчета.Видимость = НЕ Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат;
	Элементы.ОперацияСДенежнымиСредствами.Доступность = НЕ ДокументРасчетаЗаполнен;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
		Элементы.ОперацияСДенежнымиСредствами.Заголовок = НСтр("ru = 'Прием денежных средств без передачи товаров'");
	Иначе
		Элементы.ОперацияСДенежнымиСредствами.Заголовок = НСтр("ru = 'Возврат денежных средств без передачи товаров'");
	КонецЕсли;
	
	Элементы.Контрагент.Доступность    = НЕ ДокументРасчетаЗаполнен;
	Элементы.ВидОперации.Доступность   = НЕ ДокументРасчетаЗаполнен;
	Элементы.ЧекККМПродажа.Доступность = НЕ ДокументРасчетаЗаполнен;
	
	Элементы.Контрагент.ПодсказкаВвода = Константы.КонтрагентРозничныйПокупатель.Получить().Наименование;
	
	ВидимостьОплатРасчетыСКлиентамиСервер()
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеКлючевыхРеквизитов()
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Магазин) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Магазин'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Магазин", "Объект", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Организация", "Объект", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Контрагент'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Контрагент", "Объект", Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииКлючевыхРеквизитов(Источник, Значение)
	
	Если ПредыдущиеЗначения[Источник] = Значение Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник = "КассаККМ" Тогда
		ПриИзмененииКлючевыхРеквизитов("Магазин", Объект.Магазин);
		ПриИзмененииКлючевыхРеквизитов("Организация", Объект.Организация);
	ИначеЕсли ЗначениеЗаполнено(Объект.ЗаказПокупателя) Тогда
		Объект.ЗаказПокупателя = Неопределено;
		УправлениеЭлементамиЗаказПокупателя();
	КонецЕсли;
	
	ПредыдущиеЗначения.Вставить(Источник, Значение);
	
КонецПроцедуры

#Область ЗаказПокупателя

&НаКлиенте
Процедура ПоказатьВопросВыбратьЗаказПокупателяЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОткрытьФормуВыбораЗаказаПокупателя();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораЗаказаПокупателя()
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", Объект.Организация);
	ПараметрыОтбора.Вставить("Контрагент",  Объект.Контрагент);
	ПараметрыОтбора.Вставить("Магазин",     Объект.Магазин);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("Документ.ЗаказПокупателя.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ВыборЗаказаПокупателяЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗаказаПокупателяЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ЗаказПокупателя = Результат;
	УправлениеЭлементамиЗаказПокупателя();
	ЗаполнитьПоЗаказуПокупателя();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросЗаполнитьПоЗаказуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоЗаказуПокупателя();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоЗаказуПокупателя()
	
	Объект.Товары.Очистить();
	Объект.СкидкиНаценки.Очистить();
	Объект.СерийныеНомера.Очистить();
	Объект.Серии.Очистить();
	
	Объект.ПогашениеПодарочныхСертификатов.Очистить();
	Объект.Подарки.Очистить();
	Объект.УправляемыеСкидки.Очистить();
	
	ЗаполнитьПоЗаказуПокупателяНаСервере();
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	
	ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоЗаказуПокупателяНаСервере()
	
	РозничныеПродажиСервер.ЗаполнитьПоОстаткамЗаказа(Объект, Объект.ЗаказПокупателя);
	
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьПризнакИспользованияХарактеристик(Объект.Подарки);
	ПерезаполнитьПризнакиМаркировки(); 
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда 
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Товары);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.ОбновитьСтатусыСерийИПодарочныхСертификатов(Объект.Подарки);
	СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
	БонусныеБаллыСервер.ОбновитьОтображениеБонусов(Объект);
	
	// Проверим соответствие документа расчетов заказу.
	Если ЗначениеЗаполнено(Объект.ДокументРасчета) Тогда
		Если ТипЗнч(Объект.ДокументРасчета) = Тип("ДокументСсылка.РегистрацияБезналичнойОплаты") Тогда
			ЗаказДокументаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументРасчета, "ДокументОснование");
		Иначе
			ЗаказДокументаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументРасчета, "ЗаказПокупателя");
		КонецЕсли;
		
		Если НЕ Объект.ЗаказПокупателя = ЗаказДокументаРасчетов Тогда
			Объект.ДокументРасчета = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.УправлениеЭлементамиАгентскогоВознаграждения(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭлементамиЗаказПокупателя()
	
	ЭтоВозврат = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат");
	ПродажаПоЗаказу = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
	
	Элементы.ОткрытьЗаказПокупателя.Заголовок = Строка(Объект.ЗаказПокупателя);
	
	// Установим параметры выбора документа расчетов.
	ПараметрыВыбораДокументРасчетов = Новый Массив;
	Если ПродажаПоЗаказу Тогда
		ПараметрыВыбораДокументРасчетов.Добавить(Новый ПараметрВыбора("Отбор.ЗаказПокупателя", Объект.ЗаказПокупателя));
	КонецЕсли;
	Элементы.ДокументРасчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораДокументРасчетов);
	
	// Установим видимость.
	Элементы.ГруппаЗаказПокупателя.Видимость   = НЕ ЭтоВозврат;
	Элементы.ВыбратьЗаказПокупателя.Видимость  = НЕ ПродажаПоЗаказу;
	Элементы.ОткрытьЗаказПокупателя.Видимость  = ПродажаПоЗаказу;
	Элементы.ОчиститьЗаказПокупателя.Видимость = ПродажаПоЗаказу;
	Элементы.ТоварыРезервировать.Видимость     = ПродажаПоЗаказу;
	
	// Установим доступность зависимых элементов управления.
	Элементы.ТоварыЗаполнитьПоЗаказу.Доступность = ПродажаПоЗаказу И НЕ ЭтоВозврат;
	
	ЭлементыДоступность = Новый Массив;
	ЭлементыДоступность.Добавить("ТоварыРассчитатьСкидкиНаценки");
	ЭлементыДоступность.Добавить("ТоварыНазначитьАвтоматическиеСкидки");
	ЭлементыДоступность.Добавить("ТоварыПеренестиПодаркиВТовары");
	ЭлементыДоступность.Добавить("ТоварыНазначитьРучнуюСкидку");
	ЭлементыДоступность.Добавить("ТоварыОтменитьРучныеСкидки");
	ЭлементыДоступность.Добавить("ПодаркиПеренестиПодаркиВТовары");
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, ЭлементыДоступность, "Доступность", НЕ ПродажаПоЗаказу И НЕ ЭтоВозврат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеФизЛицаСервер()
	
	Если Не ТипЗнч(Объект.ФизЛицо) = Тип("Строка") И ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
		
		УдостоверениеЛичности = РегистрыСведений.ДокументыФизическихЛиц.ДокументУдостоверяющийЛичностьФизлица(Объект.Физлицо);
		Объект.ПоДокументу = УдостоверениеЛичности;
		Объект.ДатаРождения = Объект.ФизЛицо.ДатаРождения;
		
	КонецЕсли;
	
	ЗаполнениеФИО();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеФИО()
	
	Если Не ТипЗнч(Объект.ФизЛицо) = Тип("Строка") И ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
		ФизЛицоФИО = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(, Новый Структура("ФизЛицо", Объект.ФизЛицо)); 
		ФИОФизЛицаИП = ФизЛицоФИО.Фамилия + " " + ФизЛицоФИО.Имя + " " + ФизЛицоФИО.Отчество;

		Если СокрЛП(ФИОФизЛицаИП) = "" Тогда
			ФИОФизЛицаИП = Объект.ФизЛицо.Наименование;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект.ФизЛицо) = Тип("Строка") Тогда
		ФИОФизЛицаИП = Объект.ФизЛицо;
	Иначе
		ФИОФизЛицаИП = ""
	КонецЕсли;
	Если Не ЭтоLinuxСервер Тогда
		ФИОФизЛица = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 3);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ЧекНаходитсяВОчереди()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Ложь;
	Если Параметры.Ключ.Пустая() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОчередьЧековККТ.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	РегистрСведений.ОчередьЧековККТ КАК ОчередьЧековККТ
	|ГДЕ
	|	ОчередьЧековККТ.ДокументОснование = &Ссылка
	|	И ОчередьЧековККТ.СтатусЧека <> &СтатусЧека";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("СтатусЧека", Перечисления.СтатусЧекаККТВОчереди.Фискализирован);
	Результат = Не Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОповещениеОплатыМобильный(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ДополнительныеПараметры = Неопределено
		 И ДополнительныеПараметры.Свойство("ВыведеныСообщения") Тогда
		ОповещениеРасчетСкидокКлиент();
	КонецЕсли;
	
	НоваяСтрока = Неопределено;
	ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.МобильныйПлатеж");
	ВидОплатыМобильныйПлатеж = ПолучитьВидОплатыПоТипу(ТипОплаты);
	
	
	Если НЕ ВидОплатыМобильныйПлатеж = Неопределено Тогда
		НоваяСтрока = Объект.Оплата.Добавить();
		НоваяСтрока.ВидОплаты = ВидОплатыМобильныйПлатеж;
		
		Сумма = Объект.Товары.Итог("Сумма") - Объект.Оплата.Итог("Сумма");
		НоваяСтрока.Сумма = ?(Сумма > 0, Сумма, 0);
		
		НоваяСтрока.ТипОплаты = ТипОплаты;
		Элементы.Оплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		Элементы.Оплата.ТекущийЭлемент = Элементы.ОплатаСумма;
		Элементы.Оплата.ИзменитьСтроку();
		МодифицированыСохраняемыеДанные = Истина;
		
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
		УстановитьЭквайринговыйТерминалПоУмолчанию(НоваяСтрока);
	Иначе
		
		Отбор = Новый Структура;
		
		Отбор.Вставить("ИмяПоляОтбораЛевоеЗначение", "ТипОплаты");
		Отбор.Вставить("ПравоеЗначение"            , ТипОплаты);
		Отбор.Вставить("Отрицание"                 , Ложь);
		
		ПараметрыФормы =  Новый Структура("СтруктураПараметрыОтбора", Отбор);
		
		Если ДополнительныеПараметры = Неопределено Тогда
			ДополнительныеПараметры = Новый Структура;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("ТипОплаты", ТипОплаты);
		ДополнительныеПараметры.Вставить("УстановитьЭквайринговыйТерминалПоУмолчанию");
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВыбораОплатыЧекаККМ", ЭтотОбъект, ДополнительныеПараметры);
		Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.ВидыОплатЧекаККМ.ФормаВыбора", ПараметрыФормы,,,,, ОбработчикОповещения, Режим); 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыборGTIN(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда
		
		СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
		СтрокаТабличнойЧасти.GTIN = РезультатВыбора;
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьТипКонтрагента()
	
	Возврат ?(Объект.Контрагент.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"), Истина, Ложь);
	
КонецФункции

&НаКлиенте
Процедура ЗаполнениеТЧТоварыНаОснованииЧекаПродажи(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение ТЧ Товары через обработку заполнения документа ЧекККМ
	ЗаполнениеТЧТоварыНаОснованииЧекаПродажиСервер(Параметры);
	
	ПересчитатьИлиОтменитьСкидкиИОплатуБонусами();
	Если КэшированныеЗначения = Неопределено Тогда
		ОбработкаТабличнойЧастиТоварыКлиент.ОбновитьКэшированныеЗначенияДляУчетаСерий(
		Элементы.Товары,
		КэшированныеЗначения,
		ПараметрыУказанияСерий,
		Ложь);
	КонецЕсли;
	 
КонецПроцедуры	

&НаСервере
Процедура ЗаполнениеТЧТоварыНаОснованииЧекаПродажиСервер(Параметры)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.Заполнить(Объект.ЧекККМПродажа);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		
КонецПроцедуры

&НаСервере
Функция  ПроверитьСуммуТЧТовары()
	
	ИтоговаяСуммаБезОкругления = 0;
	
	Для Каждого СтрокаТЧ Из Объект.Товары Цикл
		
		ИтоговаяСуммаБезОкругления = ИтоговаяСуммаБезОкругления + СтрокаТЧ.СуммаВсего;
		
	КонецЦикла;
	
	Возврат ?(ИтоговаяСуммаБезОкругления = Цел(ИтоговаяСуммаБезОкругления), Ложь, Истина);                                
	
КонецФункции

&НаСервере
Процедура РасчитатьСкидкуПриВозврате()
	
	НеобходимоОкругление = ПроверитьСуммуТЧТовары();
		
	Если НеобходимоОкругление Тогда
		Если ЗначениеЗаполнено(Объект.СкидкиНаценки) Тогда
			Объект.СкидкиРассчитаны = Истина;
		   	ОтменитьСкидки();
		КонецЕсли;
		СкидкиНаценкиСервер.ОкруглитьЧекВПользуПокупателя(Объект, "Товары", "СкидкиНаценки");
		СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект);
		ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

 //ИнтеграцияИСМПТК_РозничноеВыбытие
 #Область МаркировкаИСМПТК
 
 #Область НастройкиФормы

&НаСервере
Процедура ПерезаполнитьПризнакиМаркировкиИСМПТК()
	
	Если УчетМаркировкаИСМПТК Тогда
		РозничноеВыбытиеИСМПТКПереопределяемый.ЗаполнитьДанныеМаркировки(ЭтотОбъект);
		Настройки = РозничноеВыбытиеИСМПТКПереопределяемый.НастройкиИсточникаКешаШтрихкодовУпаковок();
		Настройки.Штрихкоды = "КодыМаркировкиИСМПТК";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЭлементаИСМПТК()
	
	Если УчетМаркировкаИСМПТК 
		И РозничноеВыбытиеИСМПТКВызовСервера.ЕстьПравоЧтенияОбработкаРозничноеВыбытиеИСМПТК() Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакКодаМаркировкиВТоварах()
	
	РозничноеВыбытиеИСМПТКПереопределяемый.ЗаполнитьПризнакКодаМаркировкиВТоварах(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкиШтрихкода

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершениеПроверкаМаркировки(Штрихкод)
	
	//Проверяем, если это ЕАН, относится ли он к маркировке
	Если ЗначениеЗаполнено(Штрихкод) Тогда 
		
		ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(Штрихкод);
		Если Не ДанныеНоменклатуры = Неопределено Тогда
			Номенклатура = ДанныеНоменклатуры.Номенклатура; 
			ВидПродукцииШКПоМаркировке = ПолучитьВидПродукцииПоНоменклатуре(Номенклатура);
			Если ЗначениеЗаполнено(ВидПродукцииШКПоМаркировке) 
				И РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьВключениеУчетаТГ(ВидПродукцииШКПоМаркировке) Тогда
				ТекстСообщения = НСтр("ru = 'Внимание: среди выбранных товаров есть маркируемые, нужно заполнить коды маркировки в отдельной таблице!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КомандаКодыМаркировки

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКомандуИСМПТК(Команда)
	
	РозничноеВыбытиеИСМПТККлиентПереопределяемый.ВыполнитьПереопределяемуюКомандуЧекККМ(ЭтотОбъект, Команда, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыПроверкиКМ(ТаблицаПроверенныхКМ) Экспорт

	Если Не ТаблицаПроверенныхКМ = Неопределено Тогда //Форма проверки и подбора КМ вернула какой-то результат
		
		КодыКДобавлению = Неопределено;
		ЧекИзменен = Ложь;
		ЗакрытиеФормыПроверкиКМНаСервере(ЧекИзменен, ТаблицаПроверенныхКМ, КодыКДобавлению);
		Модифицированность = ЧекИзменен;
				
		Если ЧекИзменен Тогда
			
			//Добавляем коды
			Если Не КодыКДобавлению = Неопределено Тогда
				Для Каждого КодКДобавлению Из КодыКДобавлению Цикл
					
					ДанныеКодаДляДобавления = Новый Структура("Количество, Штрихкод", 1, КодКДобавлению.КодМаркировки);
					ОбработатьКодМаркировки(ДанныеКодаДляДобавления);
					
				КонецЦикла;
			КонецЕсли;
			
			Для Каждого СтрокаТовар Из Объект.Товары Цикл
				ПриИзмененииТоварыНоменклатура(СтрокаТовар);
				//ОбработкаТабличнойЧастиТоварыКлиент.ПриИзмененииРеквизитовВТЧКлиент(Объект.Товары, СтрокаТовар, СтруктураДействий, КэшированныеЗначения);
			КонецЦикла;
			
			ОбновитьИтоговыеПоказателиКлиентСервер(ЭтотОбъект);
			ЗаполнитьСтатусыУказанияСерий();
			
		КонецЕсли;
		
		//Устанавливаем иконку марки в строках товаров
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусыУказанияСерий()

	ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(Документы.ЧекККМ.ПараметрыУказанияСерий(Объект));
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
		
КонецПроцедуры

&НаСервере
Процедура ЗакрытиеФормыПроверкиКМНаСервере(ЧекИзменен, ТаблицаПроверенныхКМ, КодыКДобавлению) Экспорт

	ДанныеТаблиц = Новый Структура();
	ДанныеТаблиц.Вставить("ТаблицаКодов", 	Объект.КодыМаркировкиИСМПТК.Выгрузить());
	ДанныеТаблиц.Вставить("ТаблицаТоваров", Объект.Товары.Выгрузить());
	
	ДополнительныеПараметрыОбработкиРезультата = Новый Структура();
	ДополнительныеПараметрыОбработкиРезультата.Вставить("НеОбработанныеКодыКДобавлению", 	   Неопределено); //Для Чека ККМ значение этого параметра всегда будет возвращаться Неопределено.
	ДополнительныеПараметрыОбработкиРезультата.Вставить("ОбъединятьПозицииСОдинаковымТоваром", Истина); //В чеке всегда по умолчанию объединяем строки.
	
	РозничноеВыбытиеИСМПТКПереопределяемый.ЗакрытиеФормыПроверкиКМНаСервере(ТаблицаПроверенныхКМ, ЧекИзменен, ДанныеТаблиц, ДополнительныеПараметрыОбработкиРезультата);
	//Проверяем результат обработки данных формы Проверки КМ.
	Если ЧекИзменен Тогда
		//Значит удалялись или добавлялись КМ, а также могли измениться строки товаров. 
		//Загружаем скорректированные таблицы в документ.
		ТоварыПослеИзменения = ДанныеТаблиц.ТаблицаТоваров;
		КодыПослеИзменения	 = ДанныеТаблиц.ТаблицаКодов;
		Объект.Товары.Загрузить(ТоварыПослеИзменения);
		Объект.КодыМаркировкиИСМПТК.Загрузить(КодыПослеИзменения);
		
		Если Не ДополнительныеПараметрыОбработкиРезультата.НеОбработанныеКодыКДобавлению = Неопределено Тогда 
			//Есть коды, которые нужно добавить вместе с товарами
			КодыКДобавлению = ДополнительныеПараметрыОбработкиРезультата.НеОбработанныеКодыКДобавлению;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаДействий_ФормаВводаКодаМаркировки

&НаКлиенте
Процедура ДобавлениеМПБезКодаМаркировкиПредупреждение(ВидПродукции, ДанныеШтрихкода, ДанныеНоменклатуры, ОткрытиеПослеДобавления = Ложь) Экспорт
	
	//Для табачной продукции на стороне ИС МПТ реализован сервис автоматического выбытия.
	//Поэтому в розницу могут выбывать пачки и блоки без сканирования кодов. Для того, чтобы система
	//каждый раз не запрашивала код, реализована опция, позволяющая отключить проверку.
	Если (РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьОпциюНеЗапрашиватьКМТабакаВРознице() 
		И ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Табачная"))
		Или (РозничноеВыбытиеИСМПТКВызовСервера.ПроверитьОпциюНеЗапрашиватьКМФармыВРознице() 
		И ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты")) Тогда
		ИсходныеДанные = ДополнитьДанныеШтрихкодаТабачнойПродукцииПриУказанииБезКМ(ДанныеШтрихкода, ДанныеНоменклатуры, ВидПродукции);
		ИсходныеДанные.Вставить("ОткрытиеПослеДобавления", Истина);
		ДобавлениеКМКакНемаркированногоТовараИСМПТК(ИсходныеДанные, Неопределено);
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("ВидПродукции",     ВидПродукции);
	ПараметрыОткрытияФормы.Вставить("Номенклатура",     ДанныеНоменклатуры.Номенклатура);
	ПараметрыОткрытияФормы.Вставить("Характеристика",   ДанныеНоменклатуры.Характеристика);
	ПараметрыОткрытияФормы.Вставить("ЕдиницаИзмерения", ДанныеНоменклатуры.ЕдиницаИзмерения);
	ПараметрыОткрытияФормы.Вставить("ДанныеШтрихкода",  ДанныеШтрихкода);
	ПараметрыОткрытияФормы.Вставить("ОткрытиеПослеДобавления", ОткрытиеПослеДобавления);
	Если ОткрытиеПослеДобавления Тогда
		ПараметрыОткрытияФормы.Вставить("КлючСвязиСтроки", 0);
	КонецЕсли;
	
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеЗакрытиеФормыВводаКМ", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.РозничноеВыбытиеМаркированнойПродукцииИСМПТК.Форма.ФормаВводаКодаМаркировки", ПараметрыОткрытияФормы, ЭтотОбъект,,,,ОписаниеОповещения, РежимОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеЗакрытиеФормыВводаКМ(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ИмяСобытия = Результат.ОбработчикОповещения;
		РезультатОбработкиШтрихкода = Результат.ДопДанные;
						
		ОписаниеОповещения = Новый ОписаниеОповещения(ИмяСобытия, ЭтотОбъект);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатОбработкиШтрихкода);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ДополнитьДанныеШтрихкодаТабачнойПродукцииПриУказанииБезКМ(ДанныеШтрихкода, ДанныеНоменклатуры, ВидПродукции)
	
	ДанныеШтрихкодаБезМаркировки = РозничноеВыбытиеИСМПТК.ИнициализироватьДанныеШтрихкода();
	ДанныеШтрихкодаБезМаркировки.Номенклатура            = ДанныеНоменклатуры.Номенклатура;
	ДанныеШтрихкодаБезМаркировки.Характеристика          = ДанныеНоменклатуры.Характеристика;
	ДанныеШтрихкодаБезМаркировки.Упаковка                = ДанныеНоменклатуры.ЕдиницаИзмерения;
	ДанныеШтрихкодаБезМаркировки.Количество		         = 1;
	ДанныеШтрихкодаБезМаркировки.ТипУпаковки             = ПредопределенноеЗначение("Перечисление.ТипыУпаковокИСМПТК.МаркированныйТовар");
	ДанныеШтрихкодаБезМаркировки.ОбработатьБезМаркировки = Истина;
	ДанныеШтрихкодаБезМаркировки.ВидыПродукции.Добавить(ВидПродукции);
	ДанныеШтрихкодаБезМаркировки.Штрихкод             = ДанныеШтрихкода.EAN;
	ДанныеШтрихкодаБезМаркировки.МаркируемаяПродукция = Ложь;
	
	ИсходныеДанные = Новый Структура();
	ИсходныеДанные.Вставить("ИсходныеДанные", ДанныеШтрихкодаБезМаркировки);
	ИсходныеДанные.Вставить("ТребуетсяОбработкаШтрихкода", Истина);
	
	Возврат ИсходныеДанные;
	
КонецФункции

&НаКлиенте
Процедура ВводКодаМаркировкиПослеПредупрежденияИСМПТК(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ИсходныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;		
			
	//Обрабатываем штрихкод как маркируемую продукцию, т.к. пользователь указал Код маркировки после предупреждения
	ДанныеКодаМаркировки = ИсходныеДанные.ИсходныеДанные;
	
	Если ИсходныеДанные.Свойство("КлючСвязиСтроки") 
		И ИсходныеДанные.КлючСвязиСтроки Тогда 
		КлючСтрокиТоваровВременныйИСМПТК = ИсходныеДанные.КлючСвязиСтроки;
	КонецЕсли;

	//Сначала проверяем КМ на дубль в таблице марок
	ШтрихкодДляПроверки = ДанныеКодаМаркировки.Штрихкод;
	МожноПродолжитьОбработкуКМ = ПроверитьТЧКодыМаркировкиНаНаличиеКМ(ШтрихкодДляПроверки);
	
	Если Не МожноПродолжитьОбработкуКМ Тогда
		ВывестиСообщениеОбОшибке(НСтр("ru = 'Код маркировки уже указан в документе!'"));	
		Возврат;
	КонецЕсли;
	
	ДанныеМаркировки   = РозничноеВыбытиеИСМПТКВызовСервера.РазобратьШтриховойКодТовара(ШтрихкодДляПроверки, Истина);
	ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(ДанныеМаркировки.EAN);
	
	ДополнительныеПараметры = Новый Структура();
	Если ИсходныеДанные.Свойство("ОткрытиеПослеДобавления")
		И ИсходныеДанные.ОткрытиеПослеДобавления Тогда
		
		ДанныеНоменклатурыИзСтрокиТЧ = Новый Структура("Номенклатура,Характеристика,ИспользуютсяУпаковки,Упаковка,БазоваяЕдиницаИзмерения");
		//В этом случае товар в документ уже добавлен и нужно выполнить только дозаполнение кода маркировки.
		СтрокаТЧ  = Объект.Товары.НайтиСтроки(Новый Структура("КлючСвязи", КлючСтрокиТоваровВременныйИСМПТК))[0];
		БазоваяЕИ = РозничноеВыбытиеИСМПТКПереопределяемый.ПолучитьЗначениеБазовойЕИНоменклатуры(СтрокаТЧ.Номенклатура); 
		ДанныеНоменклатурыИзСтрокиТЧ.Вставить("Номенклатура", 	СтрокаТЧ.Номенклатура);
		ДанныеНоменклатурыИзСтрокиТЧ.Вставить("Характеристика", СтрокаТЧ.Характеристика);		
		ДанныеНоменклатурыИзСтрокиТЧ.Вставить("ИспользуютсяУпаковки", ЗначениеЗаполнено(СтрокаТЧ.Упаковка));
		ДанныеНоменклатурыИзСтрокиТЧ.Вставить("Упаковка", ?(ЗначениеЗаполнено(СтрокаТЧ.Упаковка), СтрокаТЧ.Упаковка, БазоваяЕИ));
		ДанныеНоменклатурыИзСтрокиТЧ.Вставить("БазоваяЕдиницаИзмерения", БазоваяЕИ);
		ДополнительныеПараметры.Вставить("ОткрытиеПослеДобавления", ИсходныеДанные.ОткрытиеПослеДобавления);
		ДополнительныеПараметры.Вставить("ДанныеНоменклатуры", 		ДанныеНоменклатурыИзСтрокиТЧ);
		
	Иначе
		ДополнительныеПараметры.Вставить("ДанныеНоменклатуры", ДанныеНоменклатуры);
	КонецЕсли;
	ДополнительныеПараметры.Вставить("ДанныеМаркировки", ДанныеМаркировки);
	
	ОбработатьКодМаркировкиИСМПТКПродолжение(ДанныеКодаМаркировки, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавлениеКМКакНемаркированногоТовараИСМПТК(ИсходныеДанные, КэшированныеЗначения) Экспорт
	
	//Обрабатываем штрихкод как немаркируемую продукцию, т.к. пользователь отказался от указания КМ
	//Т.е. используем стандартный механизм добавления ШК
	Если ИсходныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИсходныеДанные.Свойство("ОткрытиеПослеДобавления") Тогда
		Если ИсходныеДанные.ОткрытиеПослеДобавления Тогда
			Возврат; //Т.к. товар уже добавлен в документ
		КонецЕсли;
	КонецЕсли;
	
	ИсходныеДанные = ИсходныеДанные.ИсходныеДанные;
	ОбработатьКодМаркировки(ИсходныеДанные);
	
	//Актуализируем иконки КМ в строке
	ЗаполнитьПризнакКодаМаркировкиВТоварах();
			
КонецПроцедуры

#КонецОбласти

&НаСервере
функция ПолучитьВидПродукцииПоНоменклатуре(Номенклатура) Экспорт 
	
	Возврат ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьВидПродукцииПоНоменклатуре(Номенклатура);
		
КонецФункции

&НаСервере
функция ПолучитьНоменклатуруПоШтрихкоду(Штрихкод) Экспорт
	
	Возврат РозничноеВыбытиеИСМПТКПереопределяемый.ПолучитьНоменклатуруПоШтрихкодуВРозничномКонтуре(Штрихкод);
		
КонецФункции

&НаКлиенте
Функция ПроверитьТЧКодыМаркировкиНаНаличиеКМ(КодМарикровки) Экспорт
	
	НайденныеСтроки = Объект.КодыМаркировкиИСМПТК.НайтиСтроки(Новый Структура("КодМаркировки", КодМарикровки));
	
	Возврат ?(НайденныеСтроки.Количество() > 0, Ложь, Истина);	
	
КонецФункции

&НаКлиенте
Процедура ОбработатьКодМаркировкиИСМПТК(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ИсходныеДанные = Неопределено 
		Или (ТипЗнч(ИсходныеДанные) = Тип("Массив") И ИсходныеДанные.Количество() = 0) Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(ИсходныеДанные) = Тип("Массив") Тогда
		ИсхДанныеДляОбработки = ИсходныеДанные[0];
	Иначе
		ИсхДанныеДляОбработки = ИсходныеДанные;
	КонецЕсли;
	
	//Если пришли в процедуру из формы регистрации нового штрихкода, нужно проверить, какая номенклатура была зарегистрирована:
	//если с особенностями учета, но при этом был отсканирован не код марикровки, а ЕАН, необходимо вывести предупреждение.
	Если ИсхДанныеДляОбработки.Свойство("ФормаПоискаНоменклатуры_Успешно") Тогда
		
		ДанныеСоСканераСтруктура = ДополнительныеПараметры.ДанныеСоСканераСтруктура;
		
		ЭтоКодМаркировки = ДополнительныеПараметры.ЭтоКодМаркировки;
		Если ЭтоКодМаркировки Тогда
			EAN = ИсхДанныеДляОбработки.СтруктураКодовМаркировки.EAN;
		Иначе
			EAN = ИсхДанныеДляОбработки.ШтрихкодыНоменклатуры[0].Штрихкод;
		КонецЕсли;
		
		//Получаем данные по номенклатуре
		ДанныеНоменклатуры  	 = ПолучитьНоменклатуруПоШтрихкоду(EAN);
		Если Не ДанныеНоменклатуры = Неопределено Тогда
			Номенклатура     	 = ДанныеНоменклатуры.Номенклатура; 
			Характеристика   	 = ДанныеНоменклатуры.Характеристика;
			Упаковка		 	 = ДанныеНоменклатуры.Упаковка;
			БазоваяЕИ		 	 = ДанныеНоменклатуры.БазоваяЕдиницаИзмерения;
			ИспользуютсяУпаковки = ДанныеНоменклатуры.ИспользуютсяУпаковки;
			ВидПродукцииШК   	 = ПолучитьВидПродукцииПоНоменклатуре(Номенклатура);
			
			Если Не ЗначениеЗаполнено(ВидПродукцииШК)
				И ЭтоКодМаркировки Тогда
			    ТекстОшибки = НСтр("ru = 'Внимание! В Чеке был отсканирован код маркировки ""%1"", связанный с номенклатурой ""%2"".'")
							+ НСтр("ru = 'Данная номенклатура не имеет особенностей учета по маркировке! Необходимо изменить вид номенклатуры на ""Товар с особенностями учета"" соответствующей товарной группы!'");
				ТекстОшибки = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстОшибки, ИсхДанныеДляОбработки.СтруктураКодовМаркировки.КодИдентификации, Номенклатура);
				ОбщегоНазначенияИСМПТКВызовСервера.СоздатьЗаписьЖурналаРегистрации(НСтр("ru = 'Розничная продажа маркируемого товара'"), "Предупреждение",,, ТекстОшибки);
				
				ТекстСообщения = НСтр("ru = 'Внимание! Обнаружены проблемы в настройках учета маркируемой продукции! Требуется сообщить Администратору. Подробности в Журнале регистрации.'"); 
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
			
			Если ИсхДанныеДляОбработки.Свойство("СтруктураКодовМаркировки") Тогда
				СведенияОКодеМаркировки = Новый Структура();
				СведенияОКодеМаркировки.Вставить("ДанныеМаркировки",   ИсхДанныеДляОбработки.СтруктураКодовМаркировки);
				СведенияОКодеМаркировки.Вставить("ДанныеНоменклатуры", ДанныеНоменклатуры);
				ЗаполнитьКодМарикровкиПредварительные(СведенияОКодеМаркировки);
			КонецЕсли;
			ОбработатьКодМаркировки(ДанныеСоСканераСтруктура);
		КонецЕсли;			
	Иначе
		//Если вызов процедуры произошел при сканировании штрихкода и он уже был зарегистрирован в базе:
		//в этом случае точно уже известно, что отканирован код маркировки и у ном-ры есть особенность учета МП.
		//Необходимо заполнить таблицу для передачи данных в ОФД.
		//В этом случае ИсходныеДанные это ДанныеСоСканераСтруктура, ДополнительныеПараметры содержат сведения о разборе КМ (структура)
		ОбработатьКодМаркировкиИСМПТКПродолжение(ИсхДанныеДляОбработки, ДополнительныеПараметры);
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКодМаркировкиИСМПТКПродолжение(ИсходныеДанные, ДополнительныеПараметры)
	
	Если ИсходныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеПараметры.ДанныеМаркировки.ТипИдентификатораТовара = ПредопределенноеЗначение("Перечисление.ТипыИдентификаторовШКТовараИСМПТК.КодТовараВФорматеDataMatrixGS1") Тогда
		ЗаполнитьКодМарикровкиПредварительные(ДополнительныеПараметры);
	КонецЕсли;
	
	//После того как предварительно заполнили КМ, нужно выполнить стандартную обработку и заполнение ТЧ Товары.
	//Для этого вызываем общий механизм, но предварительно подменяем данные КМ на ЕАН в ИсходныхДанных;
	Если Не ДополнительныеПараметры = Неопределено
		И ДополнительныеПараметры.Свойство("ДанныеМаркировки")
		И ДополнительныеПараметры.ДанныеМаркировки.Свойство("EAN") Тогда
		ИсходныеДанные.Штрихкод = ДополнительныеПараметры.ДанныеМаркировки.EAN;
	КонецЕсли;
	
	ОбработатьКодМаркировки(ИсходныеДанные);
	
КонецПроцедуры

//Выполняется предварительное добавление кода марикровки: в этот момент еще не известна строка номенклатуры, с которой он будет связан, т.к. она добавится позже.
//Поэтому ключ связи у строки КМ оставляем неопределенным и возвращаемся к нему позже.
//
&НаКлиенте
Процедура ЗаполнитьКодМарикровкиПредварительные(ДополнительныеПараметры) Экспорт
	
	Если Не ДополнительныеПараметры = Неопределено
		И ДополнительныеПараметры.Свойство("ДанныеМаркировки") Тогда
				
		ДанныеМаркировки    = ДополнительныеПараметры.ДанныеМаркировки;
		ДанныеНоменклатуры  = ДополнительныеПараметры.ДанныеНоменклатуры;
		
		//В зависимости от того, из какой формы было иниициировано заполнение,имя параметра будет различаться
		КодМаркировкиBase64 = ?(ДанныеМаркировки.Свойство("ШтрихкодBase64"), ДанныеМаркировки.ШтрихкодBase64, ДанныеМаркировки.КодМаркировки);
		//Для заполнения таблицы код маркировки необходимо привести к нормализзованному виду
		НормализованныйКМ   = РозничноеВыбытиеИСМПТКВызовСервера.НормализованныйШтрихкод(КодМаркировкиBase64);
		
		Номенклатура 	     = ДанныеНоменклатуры.Номенклатура;
		Характеристика	     = ДанныеНоменклатуры.Характеристика;
		ИспользуютсяУпаковки = ДанныеНоменклатуры.ИспользуютсяУпаковки;
		Упаковка		     = ДанныеНоменклатуры.Упаковка;
		БазоваяЕИ		     = ДанныеНоменклатуры.БазоваяЕдиницаИзмерения;
		
		Если Не Объект.ОперацияСДенежнымиСредствами Тогда
			
			НоваяСтрокаМарки = Объект.КодыМаркировкиИСМПТК.Добавить();
			НоваяСтрокаМарки.КодМаркировки    = НормализованныйКМ;
			НоваяСтрокаМарки.Номенклатура     = Номенклатура;
			НоваяСтрокаМарки.Характеристика   = Характеристика;
			НоваяСтрокаМарки.ЕдиницаИзмерения = ?(ИспользуютсяУпаковки, Упаковка, БазоваяЕИ); //В ТЧ кодов маркировки храним значение или Упаковки, или базовой ЕИ хранения номенклатуры
			НоваяСтрокаМарки.КлючСвязи = -1;
						
		КонецЕсли;
		
		ПерезаполнитьПризнакиМаркировкиИСМПТК();
		ЗаполнитьПризнакКодаМаркировкиВТоварах();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьТаблицуМаркиПриУдаленииТовараИСМПТК(УдаляемыйКлючСвязи)
	
	СтруктураПоискаМарок = Новый Структура("КлючСвязи", УдаляемыйКлючСвязи);
	НайденныеМарки = Объект.КодыМаркировкиИСМПТК.НайтиСтроки(СтруктураПоискаМарок);
	Если Не НайденныеМарки.Количество() = 0 Тогда
		Для Каждого СтрокаМарка Из НайденныеМарки Цикл
			Объект.КодыМаркировкиИСМПТК.Удалить(СтрокаМарка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодбораНоменклатурыИРегистрацииШК(ИсходныеДанные, ЭтоКодМарки) Экспорт
	
	РозничноеВыбытиеИСМПТККлиентПереопределяемый.ОткрытьФормуПодбораНоменклатурыИРегистрацииШК_Общая(ИсходныеДанные, ЭтоКодМарки, ЭтотОбъект); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСообщениеОбОшибке(ТекстСообщения) Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура("ТекстОшибки", ТекстСообщения);
	ОткрытьФорму("Обработка.РозничноеВыбытиеМаркированнойПродукцииИСМПТК.Форма.ИнформацияОНевозможностиДобавленияОтсканированного", ПараметрыОткрытияФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекПослеОбработкиКМ(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ПроверкаИУдалениеОшибочныхКМ();
		
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
		РезультатЗаписи = Записать(ПараметрыЗаписи);
		ПробитьЧекНаКлиенте();
	Иначе
		ТекстСообщения = НСтр("ru = 'Процесс отменен.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПроверкаИУдалениеОшибочныхКМ()
	
	ТаблицаМарки = Объект.КодыМаркировкиИСМПТК.Выгрузить();
	НоваяТаблица = ТаблицаМарки.СкопироватьКолонки();
	
	Для Каждого СтрокаТЧ Из ТаблицаМарки Цикл
		НайденныеТовары = Объект.Товары.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаТЧ.КлючСвязи));
		Если Не НайденныеТовары.Количество() = 0 Тогда
			НоваяСтрокаТЧ = НоваяТаблица.Добавить(); 
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаТЧ);
		КонецЕсли;
	КонецЦикла;	
	Объект.КодыМаркировкиИСМПТК.Загрузить(НоваяТаблица);
	
КонецПроцедуры

#КонецОбласти
//Конец ИнтеграцияИСМПТК_РозничноеВыбытие