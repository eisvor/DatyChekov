#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает документ для интерфейсной работы шаблона чека ККМ.
//
Функция ДокументДляШаблонаЧека() Экспорт
	Перем ДокументДляПечатиШаблона;
	
	ДокументДляПечатиШаблона = Документы.ЧекККМ.ПустаяСсылка();
	
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЧекККМ.Ссылка
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДокументДляПечатиШаблона = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ДокументДляПечатиШаблона;
	
КонецФункции

// Получает макет фискального чека.
//
Функция МакетФискальногоЧека(Ссылка, ШаблонЧека, ШиринаЧека, Параметры) Экспорт
	
	РезультатТовары       = Новый ТаблицаЗначений;
	РезультатПодарки      = Новый ТаблицаЗначений;
	
	МассивСтрокНабора = ШаблонЧека.Строки.НайтиСтроки(Новый Структура("ИмяМакета", "ПоляШаблонаТЧ_Подарки"));
	
	ВШаблонеЕстьПодарки = Ложь;
	
	Для каждого СтрокаПодарки Из МассивСтрокНабора Цикл
		Если СтрокаПодарки.Строки.Количество() > 0 Тогда
			ВШаблонеЕстьПодарки = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ВШаблонеЕстьПодарки Тогда
		МассивСтрокШапкиИПодвала = ШаблонЧека.Строки.НайтиСтроки(Новый Структура("ИмяМакета", "ПоляШаблонаШапкаПодарки"));
		
		Для каждого СтрокаПодарки Из МассивСтрокШапкиИПодвала Цикл
			Если СтрокаПодарки.Строки.Количество() > 0 Тогда
				ВШаблонеЕстьПодарки = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИмяМакета = "";
	Если ВШаблонеЕстьПодарки Тогда
		ИмяМакета = "ПоляШаблонаПодарки";
	КонецЕсли;
	
	ИмяМакетаБыло = "";
	СменилиМакет = Ложь;
	Если Параметры.Свойство("ИмяМакета",ИмяМакетаБыло) Тогда
		Если Не ИмяМакетаБыло = ИмяМакета Тогда
			Параметры.КомпоновщикМакета = Неопределено;
			СменилиМакет                = Истина;
			Параметры.ИмяМакета         = ИмяМакета;
		КонецЕсли;
	КонецЕсли;
	
	Значение = Неопределено;
	Если НЕ СменилиМакет И Параметры.Свойство("КэшТаблицыРезультатаКомпоновки", Значение) Тогда
		ТаблицыРезультатаКомпоновки        = Значение;
	Иначе
		// Значение по умолчанию
		ТаблицыРезультатаКомпоновки = УправлениеШаблонами.ВыполнитьКомпоновкуДанныхДляИерархическогоШаблона(ШаблонЧека, Ссылка, Параметры.СхемаКомпоновкиДанных, Параметры.КомпоновщикМакета,,ИмяМакета);
		Параметры.Вставить("КэшТаблицыРезультатаКомпоновки", ТаблицыРезультатаКомпоновки);
	КонецЕсли;
	
	Для каждого Таблицы Из ТаблицыРезультатаКомпоновки.Строки Цикл
		Если Таблицы.ЧастьЗапроса = "Товары" Тогда
			РезультатТовары = Таблицы.Строки;
		КонецЕсли;
		Если Таблицы.ЧастьЗапроса = "Подарки" Тогда
			РезультатПодарки = Таблицы.Строки;
		КонецЕсли;
	КонецЦикла;
	
	Структура = УправлениеШаблонами.ИдентификаторыПользовательскихПолей(ШаблонЧека);
	
	// Механизм кэширования макетов.
	Если Параметры.КэшМакетов = Неопределено Тогда
		Структура.Вставить("КэшМакетов", Новый Соответствие);
	Иначе
		Структура.Вставить("КэшМакетов", Параметры.КэшМакетов);
	КонецЕсли;
	
	СтруктураМассивы = Новый Структура;
	
	// Подготовка Шапки и Подвала
	СтрокаШапка = ШаблонЧека.Строки.Найти("Шапка",,Ложь);
	Если СтрокаШапка <> Неопределено Тогда
		
		Если РезультатТовары.Количество() > 0 Тогда
			УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(РезультатТовары.Родитель, Структура, СтрокаШапка);
		КонецЕсли;
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(СтрокаШапка, Структура);
		
		МассивШапка = Новый Массив;
		Для каждого Строка2Уровня Из СтрокаШапка.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивШапка.Добавить(Соответствие);
		КонецЦикла;
		СтруктураМассивы.Вставить("Шапка",МассивШапка);
		
	КонецЕсли;
	
	МассивПодвал = Новый Массив;
	Если РезультатПодарки.Количество()>0 Тогда
		СтрокаПодаркиЗаголовок = ШаблонЧека.Строки.Найти("Табличная часть ""Подарки"" (Заголовок)",,Ложь);
		
		Если СтрокаПодаркиЗаголовок <> Неопределено Тогда
			
			УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(РезультатПодарки.Родитель, Структура, СтрокаПодаркиЗаголовок);
			УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(СтрокаПодаркиЗаголовок, Структура);
			
			Для каждого Строка2Уровня Из СтрокаПодаркиЗаголовок.Строки Цикл
				Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
				Соответствие = Новый Соответствие;
				Соответствие.Вставить(Строка2Уровня, Значение);
				МассивПодвал.Добавить(Соответствие);
			КонецЦикла;
		КонецЕсли;
		
		СтрокаПодарки = ШаблонЧека.Строки.Найти("Табличная часть ""Подарки""",,Ложь);
		Если СтрокаПодарки <> Неопределено Тогда
			// Итоговая строка идет последней, ее не включаем в ТЧ.
			КоличествоСтрокРезультата = РезультатПодарки.Количество() - 1;
			Для Счетчик = 0 По КоличествоСтрокРезультата Цикл
				УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(РезультатПодарки[Счетчик], Структура, СтрокаПодарки);
				УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(СтрокаПодарки, Структура);
				
				Для каждого Строка2Уровня Из СтрокаПодарки.Строки Цикл
					Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
					Соответствие = Новый Соответствие;
					Соответствие.Вставить(Строка2Уровня, Значение);
					МассивПодвал.Добавить(Соответствие);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		СтрокаПодаркиПодвал = ШаблонЧека.Строки.Найти("Табличная часть ""Подарки"" (Подвал)",,Ложь);
		
		Если СтрокаПодаркиЗаголовок <> Неопределено Тогда
			
			УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(РезультатПодарки.Родитель, Структура, СтрокаПодаркиПодвал);
			УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(СтрокаПодаркиПодвал, Структура);
			
			Для каждого Строка2Уровня Из СтрокаПодаркиПодвал.Строки Цикл
				Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
				Соответствие = Новый Соответствие;
				Соответствие.Вставить(Строка2Уровня, Значение);
				МассивПодвал.Добавить(Соответствие);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаПодвал = ШаблонЧека.Строки.Найти("Подвал",,Ложь);
	Если СтрокаШапка <> Неопределено Тогда
		
		Если РезультатТовары.Количество() > 0 Тогда
			УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(РезультатТовары.Родитель, Структура, СтрокаПодвал);
		КонецЕсли;
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(СтрокаПодвал, Структура);
		
		Для каждого Строка2Уровня Из СтрокаПодвал.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивПодвал.Добавить(Соответствие);
		КонецЦикла;
		
		СтруктураМассивы.Вставить("Подвал",МассивПодвал);
		
	КонецЕсли;
	
	КоличествоСтрокРезультата = РезультатТовары.Количество() - 1; // Итоговая строка идет последней, ее не включаем в ТЧ.
	Для Счетчик = 0 По КоличествоСтрокРезультата Цикл
		
		ТелоШапка = ШаблонЧека.Строки.Найти("Табличная часть ""Товары"" (Шапка)",,Ложь);
		УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(РезультатТовары[Счетчик], Структура, ТелоШапка);
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(ТелоШапка, Структура);
		
		МассивТелоШапка = Новый Массив;
		Для каждого Строка2Уровня Из ТелоШапка.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивТелоШапка.Добавить(Соответствие);
		КонецЦикла;
		
		ТелоПодвал = ШаблонЧека.Строки.Найти("Табличная часть ""Товары"" (Подвал)",,Ложь);
		УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(РезультатТовары[Счетчик], Структура, ТелоПодвал);
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(ТелоПодвал, Структура);
		
		МассивТелоПодвал = Новый Массив;
		Для каждого Строка2Уровня Из ТелоПодвал.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивТелоПодвал.Добавить(Соответствие);
		КонецЦикла;
		
		СтруктураМассивы.Вставить("ТелоШапка_"+Счетчик,МассивТелоШапка);
		СтруктураМассивы.Вставить("ТелоПодвал_"+Счетчик,МассивТелоПодвал);
		
	КонецЦикла;
	
	СтруктураМассивы.Вставить("РезультатКомпоновкиДанных", РезультатТовары);
	СтруктураМассивы.Вставить("КоличествоСтрокТабличнойЧасти", Счетчик);
	СтруктураМассивы.Вставить("ШиринаЧека", ШиринаЧека);
	
	// Механизм кэширования макетов.
	Кэш = Структура.КэшМакетов;
	
	СкидкиНаценкиСерверПереопределяемый.ОбработатьСтруктуруШаблонаЧекаПриПредварительномПросмотре(СтруктураМассивы, Ссылка);
	
	Возврат СтруктураМассивы;

КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//	Объект - ДокументОбъект или ДанныеФормыСтруктура - документ, для которого нужно сформировать параметры проверки.
//
// Возвращаемое значение:
//	Структура - Состав полей определяется требованиями функции
//	            ОбработкаТабличнойЧастиСервер.ЗаполнитьСтатусыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект)Экспорт
	
	ПоляСвязи = Новый Массив;
	
	ПараметрыУказанияСерий = Новый Структура;
	ИспользованиеСерийСклад = Ложь;
	
	ИспользованиеСерийСклад = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	ПараметрыУказанияСерий.Вставить("ИспользоватьСерииНоменклатуры", ИспользованиеСерийСклад);
	ПараметрыУказанияСерий.Вставить("ПоляСвязи",ПоляСвязи);
	ПараметрыУказанияСерий.Вставить("ЭтоОрдер", Истина);
	
	СкладскиеОперации = Новый Массив;
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
		СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ОтгрузкаВРозницу);
	Иначе
		СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаПоВозвратуОтКлиента);
	КонецЕсли;
	
	ПараметрыУказанияСерий.Вставить("СкладскиеОперации", СкладскиеОперации);
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Определяет отбор на хозяйственную операцию, устанавливаемый на список выбора документа Чек ККМ в форме элемента
// справочника хранилище шаблонов.
//
Функция СвязьПараметровВыбораНаХозяйственнуюОперацию() Экспорт
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет области шаблона.
//
Функция СформироватьПервичнуюСтруктуруШаблона(ИерархическийШаблон, ИмяОбъекта, ПервичнаяСтруктура, Загружать) Экспорт
	// Загрузим ИерархическийШаблон из макета.
	Если Загружать Тогда
		ТекстовыйДокумент = Документы.ЧекККМ.ПолучитьМакет("ШаблонФискальногоЧекаПоУмолчанию");
		Попытка
			Шаблон = ЗначениеИзСтрокиВнутр(ТекстовыйДокумент.ПолучитьТекст());
			Если ТипЗнч(Шаблон) <> Тип("ДеревоЗначений") Тогда
				Загружать = Ложь;
			КонецЕсли;
		Исключение
			Загружать = Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Если Загружать Тогда
		ИерархическийШаблон = Шаблон.Скопировать();
	Иначе
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = "Шапка";
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаШапка";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Табличная часть ""Товары"" (Шапка)'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаТЧ_Товары";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Табличная часть ""Товары"" (Подвал)'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаТЧ_Товары";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Табличная часть ""Подарки"" (Заголовок)'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаШапкаПодарки";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Табличная часть ""Подарки""'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаТЧ_Подарки";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Табличная часть ""Подарки"" (Подвал)'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаШапкаПодарки";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = "Подвал";
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаШапка";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
	КонецЕсли;
	
	Возврат Документы.ЧекККМ.ПустаяСсылка();
КонецФункции

// Функция выполняет добавление фискальных строк в дерево шаблона перед формированием
//  по нему представления чека ККМ.
//
// Параметры:
//  КопияШаблона          - КоллекцияСтрокДереваЗначений
//                          Коллекция строк в которую необходимо добавить фискальные строки.
//  ШиринаЧека            - Число
//                          Ширина чека в символах.
//  ОднаФискальнаяСтрока  - Булево
//                          Определяется наличие режима одна фискальная строка.
//
// Возвращаемое значение:
//   КоллекцияСтрокДереваЗначений   - Коллекция строк дерева значений.
//
Функция СформироватьФискальныеСтроки(КопияШаблона, ШиринаЧека, ОднаФискальнаяСтрока) Экспорт
	
	// Шапка
	
	Если ШиринаЧека < 18 Тогда
		Возврат КопияШаблона;
	КонецЕсли;
	
	ФР_Строка_0_0 = КопияШаблона.Строки.Найти("Шапка",,Ложь);
	Если ФР_Строка_0_0 <> Неопределено Тогда 
		
		// Первая фискальная строка
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР1");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР1");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 0, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , НСтр("ru = 'Касса № 01'"));
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР2");
		ПараметрыФункции.Вставить("Ширина"          , 10);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		Если ШиринаЧека >= 25 Тогда
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.КассаККМ.Владелец.ИНН%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаКассаККМОрганизацияИНН");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР3");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека - 10);
			ПараметрыФункции.Вставить("РазмещениеТекста", 1); // Обрезать
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Префикс"         , "ИНН: ");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		КонецЕсли;
		
		// Вторая фискальная строка
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР4");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР4");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 1, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.Date%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаDate");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР5");
		ПараметрыФункции.Вставить("Ширина"          , 16);
		ПараметрыФункции.Вставить("РазмещениеТекста", 2); // Забивать
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПараметрыФункции.Вставить("Префикс"         , "Дата  ");
		ПараметрыФункции.Вставить("Формат"         , "ДФ=dd.MM.yyyy");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		// Третья фискальная строка
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР7");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР7");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 2, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.Date%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаDate");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР8");
		ПараметрыФункции.Вставить("Ширина"          , 11);
		ПараметрыФункции.Вставить("РазмещениеТекста", 2); // Забивать
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПараметрыФункции.Вставить("Префикс"         , "Время ");
		ПараметрыФункции.Вставить("Формат"          , "ДФ=чч:мм");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		Если ШиринаЧека >= 23 Тогда
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.НомерЧекаККМ%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаНомерЧекаККМ");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР9");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека-11);
			ПараметрыФункции.Вставить("РазмещениеТекста", 2); // Забивать
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Префикс"         , "ЧЕК.№ ");
			ПараметрыФункции.Вставить("Формат"          , "ЧЦ=6; ЧВН=; ЧГ=0");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		КонецЕсли;
		
	КонецЕсли;
	
	// Подвал
	
	ФР_Строка_0_0 = КопияШаблона.Строки.Найти("Подвал",,Ложь);
	Если ФР_Строка_0_0 <> Неопределено И ОднаФискальнаяСтрока Тогда
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР11");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР11");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "Всего:");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР12");
		ПараметрыФункции.Вставить("Ширина"          , 6);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.СуммаДокумента%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаСуммаДокумента");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР13");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека - 6);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
	КонецЕсли;
	
	Если ФР_Строка_0_0 <> Неопределено Тогда
		
		Пробелы50 = "**************************************************";
		Пробелы = "";
		МинимальноеКоличествоСтрок = Цел(ШиринаЧека/50);
		Для Счетчик = 0 По МинимальноеКоличествоСтрок Цикл
			Пробелы = Пробелы + Пробелы50;
		КонецЦикла;
		Пробелы = Лев(Пробелы, ШиринаЧека);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , Пробелы);
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР14");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР15");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР15");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ИТОГ");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР16");
		ПараметрыФункции.Вставить("Ширина"          , 4);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.СуммаДокумента%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаСуммаДокумента");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР17");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека - 4);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , Пробелы);
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР18");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
	КонецЕсли;

	// Фискальные строки табличной части.
	
	Если НЕ ОднаФискальнаяСтрока Тогда
		
		ФР_Строка_0_0 = КопияШаблона.Строки.Найти("Табличная часть ""Товары"" (Подвал)",,Ложь);
		Если ФР_Строка_0_0 <> Неопределено Тогда
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР19");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СоставнаяСтрока");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР19");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
			ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 0, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "?(Структура%%Товары.Количество%% = 1,"""",Структура%%Товары.Количество%%)");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР20");
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=3");
			ПараметрыФункции.Вставить("Вычислять"       , Истина);
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "?(Структура%%Товары.Количество%% = 1,"""","" Х "")");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР21");
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Вычислять"       , Истина);
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "?(Структура%%Товары.Количество%% = 1,"""",Структура%%Товары.ЦенаБезСкидки%%)");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР22");
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Вычислять"       , Истина);
			ПараметрыФункции.Вставить("Формат"          ,"ЧДЦ=2");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Товары.Сумма%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "ТоварыСумма");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР23");
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
			ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2");
			ПараметрыФункции.Вставить("Префикс"         , "=");
			ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 1, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР24");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР24");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
			ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 2, ПараметрыФункции);
			
			// Строка со скидкой
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР25");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СоставнаяСтрока");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР25");
			ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
			ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
			ФР_Строка_2_0  = ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , НСтр("ru = '?(Структура%%Скидки.ПроцентСкидки%% <> 0,""СКИДКА"", """")'"));
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР26");
			ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
			ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
			ПараметрыФункции.Вставить("Вычислять"       , Истина);
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_2_0.Строки, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Скидки.ПроцентСкидки%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "СкидкиПроцентСкидки");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР27");
			ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
			ПараметрыФункции.Вставить("Ширина"          , 6); // Переносить
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Скидки.СуммаСкидки%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "СкидкиСуммаСкидки");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР28");
			ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека - 12); // Переносить
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КопияШаблона;
	
КонецФункции

// Инициализирует таблицы значений, содержащие данные документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	Перем СуммаБезналичных;
	
	ИспользоватьКомиссионнуюТорговлю = ДополнительныеСвойства.ИспользуетсяКомиссионнаяТорговля;
	ИспользоватьУчетИмпортныхТоваров = ДополнительныеСвойства.ИспользуетсяУчетИмпортныхТоваров;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЧекККМ.Дата КАК Период,
	|	ЧекККМ.ВидОперации КАК ВидОперации,
	|	ЧекККМ.ВладелецДисконтнойКарты КАК ВладелецДисконтнойКарты,
	|	ЧекККМ.ДисконтнаяКарта КАК ДисконтнаяКарта,
	|	ЧекККМ.КассаККМ КАК КассаККМ,
	|	ЧекККМ.Магазин КАК Магазин,
	|	ЧекККМ.ЧекККМПродажа КАК ЧекККМПродажа,
	|	ЧекККМ.Продавец КАК Продавец,
	|	ЧекККМ.СтатусЧекаККМ КАК СтатусЧекаККМ,
	|	ЧекККМ.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЧекККМ.АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации,
	|	ЧекККМ.СуммаДокумента КАК СуммаДокумента,
	|	ЧекККМ.Контрагент КАК Контрагент,
	|	ЧекККМ.Ссылка КАК Ссылка,
	|	ЧекККМ.Магазин.СкладПродажи КАК СкладПродажи,
	|	ЧекККМ.Организация КАК Организация,
	|	ЧекККМ.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ЧекККМ.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПродажаПоЗаказу,
	|	ЧекККМ.ДокументРасчета КАК ДокументРасчета,
	|	ЧекККМ.ВыручкаНаличными КАК ВыручкаНаличными,
	|	ЧекККМ.ОперацияСДенежнымиСредствами КАК ОперацияСДенежнымиСредствами,
	|	ВЫБОР
	|		КОГДА ЧекККМ.ДокументРасчета = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЛОЖЬ
	|		КОГДА ЧекККМ.ДокументРасчета ССЫЛКА Документ.ЧекККМ
	|			ТОГДА НЕ ЧекККМ.ДокументРасчета.ОперацияСДенежнымиСредствами
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДокументРасчетаЭтоДокументОтгрузки,
	|	ЧекККМ.ЧекККМПродажа.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|ГДЕ
	|	ЧекККМ.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий();
	
	ОбщегоНазначенияРТ.ПеренестиСтрокуВыборкиВПараметрыЗапроса(РезультатЗапроса, Реквизиты, Запрос);
	
	ОперацияСДенежнымиСредствами = Реквизиты.ОперацияСДенежнымиСредствами;
	
	ВозвратПослеЗакрытияСмены = Ложь;
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
		Запрос.УстановитьПараметр("АналитикаХозяйственнойОперации", Справочники.АналитикаХозяйственныхОпераций.РеализацияТоваров);
		ЭтоВозврат = Ложь;
		Запрос.УстановитьПараметр("ЭтоВозврат", ЭтоВозврат);
		Запрос.УстановитьПараметр("СкладскаяОперация", Перечисления.СкладскиеОперации.ОтгрузкаВРозницу);
		Запрос.УстановитьПараметр("СкладскаяОперацияСписание", Перечисления.СкладскиеОперации.ОтгрузкаНаВнутренниеНужды);
	Иначе
		Запрос.УстановитьПараметр("АналитикаХозяйственнойОперации", Реквизиты.АналитикаХозяйственнойОперации);
		ЭтоВозврат = Истина;
		Запрос.УстановитьПараметр("ЭтоВозврат", ЭтоВозврат);
		Запрос.УстановитьПараметр("СкладскаяОперация", Перечисления.СкладскиеОперации.ПриемкаПоВозвратуОтКлиента);
		Запрос.УстановитьПараметр("СкладскаяОперацияСписание", Перечисления.СкладскиеОперации.ПриемкаПоВозвратуОтКлиента);
		
		Если ЗначениеЗаполнено(Реквизиты.ОтчетОРозничныхПродажах) Тогда
			ВозвратПослеЗакрытияСмены = Истина
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("АналитикаХозяйственнойОперацииСкидкиПодарки" , Справочники.АналитикаХозяйственныхОпераций.СкидкиПодарки);
	Запрос.УстановитьПараметр("ФормироватьДвижения", НЕ ОбменДаннымиРТ.ЭтоПодчиненныйУзелПоРабочемуМесту());
	
	СтруктураКарты = Новый Структура;
	СтруктураКарты.Вставить("БонуснаяПрограммаЛояльности", "БонуснаяПрограммаЛояльности");
	СтруктураКарты.Вставить("КартаДляНакоплений", "КартаДляНакоплений");
	РеквизитыКарты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.ДисконтнаяКарта, СтруктураКарты);
	
	Запрос.УстановитьПараметр("БонуснаяПрограммаЛояльности", РеквизитыКарты.БонуснаяПрограммаЛояльности);
	Запрос.УстановитьПараметр("Приход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("Расход", ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("ПустаяДата", Дата("00010101"));
	ПериодБонусов = НачалоДня(Реквизиты.Период);
	Запрос.УстановитьПараметр("ПериодБонусов", ПериодБонусов);
	Запрос.УстановитьПараметр("ЗачетАванса", Перечисления.ТипыОплатЧекаККМ.ЗачетАванса);
	Запрос.УстановитьПараметр("ВРассрочку" , Перечисления.ТипыОплатЧекаККМ.ВРассрочку);
	Запрос.УстановитьПараметр("ВидОплатыНаличные", Справочники.ВидыОплатЧекаККМ.Наличные);
	
	ВыручкаНаличными = ?(Реквизиты.ВыручкаНаличными = 0, ПроведениеСервер.ВыручкаНаличными(ДокументСсылка), Реквизиты.ВыручкаНаличными);
	Запрос.УстановитьПараметр("ВыручкаНаличными", ВыручкаНаличными);
	
	ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
						Реквизиты.Организация,
						Реквизиты.Магазин);
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);						
	
	ИспользоватьРасчетыСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетыСКлиентами");
	Запрос.УстановитьПараметр("ИспользоватьРасчетыСКлиентами", ИспользоватьРасчетыСКлиентами);
	
	Если ЗначениеЗаполнено(РеквизитыКарты.КартаДляНакоплений) Тогда
		КартаБонусов = РеквизитыКарты.КартаДляНакоплений;
	Иначе
		КартаБонусов = Реквизиты.ДисконтнаяКарта;
	КонецЕсли;
	Запрос.УстановитьПараметр("КартаБонусов", КартаБонусов);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЧекККМТовары.Ссылка,
	|	ЧекККМТовары.НомерСтроки,
	|	ЧекККМТовары.Номенклатура,
	|	ЧекККМТовары.Характеристика,
	|	ЧекККМТовары.Количество,
	|	ЧекККМТовары.Цена,
	|	ЧекККМТовары.Сумма,
	|	ЧекККМТовары.СтавкаНДС,
	|	ЧекККМТовары.СуммаНДС,
	|	ЧекККМТовары.РегистрацияПродажи,
	|	ЧекККМТовары.Склад,
	|	ЧекККМТовары.КлючСвязиСерийныхНомеров,
	|	ЧекККМТовары.Продавец,
	|	ЧекККМТовары.ПродажаПодарка,
	|	ЧекККМТовары.Упаковка,
	|	ЧекККМТовары.КоличествоУпаковок,
	|	ЧекККМТовары.КодСтроки,
	|	ЧекККМТовары.ЗаказПокупателя,
	|	ЧекККМТовары.Резервировать
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ЧекККМТовары
	|ГДЕ
	|	ЧекККМТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНДС.Ссылка КАК СтавкаНДС,
	|	СтавкиНДС.Ссылка КАК ПроцентСтавки
	|ПОМЕСТИТЬ СтавкиНДС
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|;
	|
//		ДенежныеСредстваККМ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&КассаККМ КАК КассаККМ,
	|	ВложенныйЗапрос.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Сумма(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЧекККМОплата.Сумма КАК Сумма,
	|		ЧекККМОплата.ДоговорКонтрагента КАК ДоговорКонтрагента
	|	ИЗ
	|		Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|	ГДЕ
	|		ЧекККМОплата.ВидОплаты.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.Наличные)
	|		И ЧекККМОплата.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	|ГДЕ
	|	&ВыручкаНаличными <> 0
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ДоговорКонтрагента
	|;
	|
//		Продажи
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Магазин КАК Магазин,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.ЗаказПокупателя КАК ЗаказПокупателя,
	|	&Ссылка КАК ДокументПродажи,
	|	ТаблицаТовары.Продавец КАК Продавец,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ТаблицаТовары.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ТаблицаТовары.Сумма КАК Стоимость,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ТаблицаТовары.Количество * ТаблицаТовары.Цена / ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|				ИЛИ ТаблицаТовары.Упаковка.Коэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ ТаблицаТовары.Упаковка.Коэффициент
	|	КОНЕЦ КАК СтоимостьБезСкидок,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ТаблицаТовары.СуммаНДС КАК НДС,
	|	&АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНДС КАК СтавкиНДС
	|		ПО ТаблицаТовары.СтавкаНДС = СтавкиНДС.СтавкаНДС
	|ГДЕ
	|	&ФормироватьДвижения И НЕ &ОперацияСДенежнымиСредствами
	|;
	|
//		ПродажиПоДисконтнымКартам
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&ДисконтнаяКарта КАК ДисконтнаяКарта,
	|	&ВладелецДисконтнойКарты КАК ВладелецДисконтнойКарты,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ТаблицаТовары.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ТаблицаТовары.Количество КАК Количество
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	НЕ &ДисконтнаяКарта = ЗНАЧЕНИЕ(Справочник.ИнформационныеКарты.ПустаяСсылка) И НЕ &ОперацияСДенежнымиСредствами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧекККМОплата.Ссылка,
	|	ЧекККМОплата.НомерСтроки,
	|	ЧекККМОплата.ВидОплаты,
	|	ЧекККМОплата.Сумма,
	|	ЧекККМОплата.ПроцентКомиссии,
	|	ЧекККМОплата.СуммаКомиссии,
	|	ЧекККМОплата.ЭквайринговыйТерминал
	|ПОМЕСТИТЬ ТаблицаОплата
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	ЧекККМОплата.Ссылка = &Ссылка
	|;
	|
//		ПродажиПоПлатежнымКартам
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Магазин КАК Магазин,
	|	&Организация КАК Организация,
	|	ТаблицаОплата.ВидОплаты КАК ВидОплаты,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОплата.Сумма
	|		КОНЕЦ) КАК СуммаОперацийПродажи,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА ТаблицаОплата.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаОперацийВозврата,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаОплата.СуммаКомиссии > 0
	|							ТОГДА ТаблицаОплата.СуммаКомиссии
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ ТаблицаОплата.СуммаКомиссии
	|		КОНЕЦ) КАК НачисленнаяСуммаКомиссии,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаОплата.СуммаКомиссии < 0
	|							ТОГДА -ТаблицаОплата.СуммаКомиссии
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОтмененнаяСуммаКомиссии,
	|	ТаблицаОплата.ЭквайринговыйТерминал
	|ИЗ
	|	ТаблицаОплата КАК ТаблицаОплата
	|ГДЕ
	|	ТаблицаОплата.ВидОплаты.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.ПлатежнаяКарта)
	|		ИЛИ ТаблицаОплата.ВидОплаты.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.МобильныйПлатеж)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОплата.ВидОплаты,
	|	ТаблицаОплата.ЭквайринговыйТерминал
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Магазин КАК Магазин,
	|	&Организация КАК Организация,
	|	ТаблицаОплата.ВидОплаты КАК ВидОплаты,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОплата.Сумма
	|		КОНЕЦ) КАК СуммаОперацийПродажи,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА ТаблицаОплата.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаОперацийВозврата,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаОплата.СуммаКомиссии > 0
	|							ТОГДА ТаблицаОплата.СуммаКомиссии
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ ТаблицаОплата.СуммаКомиссии
	|		КОНЕЦ) КАК НачисленнаяСуммаКомиссии,
	|	СУММА(ВЫБОР
	|			КОГДА &ЭтоВозврат
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаОплата.СуммаКомиссии < 0
	|							ТОГДА -ТаблицаОплата.СуммаКомиссии
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОтмененнаяСуммаКомиссии,
	|	ТаблицаОплата.ЭквайринговыйТерминал
	|ИЗ
	|	ТаблицаОплата КАК ТаблицаОплата
	|ГДЕ
	|	ТаблицаОплата.ВидОплаты.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.ПлатежнаяСистема)
	|		ИЛИ ТаблицаОплата.ВидОплаты.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.МобильныйПлатеж)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОплата.ВидОплаты,
	|	ТаблицаОплата.ЭквайринговыйТерминал
	|;
	|
//		ТоварыНаСкладах
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Резервировать И НЕ &ЭтоВозврат
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Резерв,
	|	&АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ТаблицаТовары.КодСтроки КАК КодСтроки
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|	И НЕ (ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат) И &ЭтоВозврат)
	|	И &ФормироватьДвижения И НЕ &ОперацияСДенежнымиСредствами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЧекККМПодарки.Склад,
	|	ЧекККМПодарки.Номенклатура,
	|	ЧекККМПодарки.Характеристика,
	|	ЧекККМПодарки.Количество,
	|	0,
	|	&АналитикаХозяйственнойОперацииСкидкиПодарки,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	ЧекККМПодарки.НомерСтроки,
	|	&Период,
	|	0
	|ИЗ
	|	Документ.ЧекККМ.Подарки КАК ЧекККМПодарки
	|ГДЕ
	|	ЧекККМПодарки.Ссылка = &Ссылка
	|	И &ФормироватьДвижения И НЕ &ОперацияСДенежнымиСредствами
	|;
	|
//		ДвиженияСерийТоваров
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	ТаблицаСерии.Количество КАК Количество,
	|	&Магазин КАК Магазин,
	|	&СкладскаяОперация КАК СкладскаяОперация,
	|	&Ссылка КАК Документ,
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ЧекККМ.Серии КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И &ФормироватьДвижения И НЕ &ОперацияСДенежнымиСредствами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	ТаблицаСерии.Количество КАК Количество,
	|	&Магазин КАК Магазин,
	|	&СкладскаяОперацияСписание КАК СкладскаяОперация,
	|	&Ссылка КАК Документ,
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ЧекККМ.СерииПодарков КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И &ФормироватьДвижения И НЕ &ОперацияСДенежнымиСредствами
	|;
	|";
	
	ТекстЗапроса = ТекстЗапроса + ТекстЗапросаТаблицаСерийныхНомеров();
	ТекстЗапроса = ТекстЗапроса + ТекстЗапросаТаблицаПодарочныхСертификатов();
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.Цена,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС,
	|	ТаблицаТовары.РегистрацияПродажи,
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.КлючСвязиСерийныхНомеров,
	|	ТаблицаТовары.Продавец,
	|	ТаблицаТовары.ПродажаПодарка,
	|	ТаблицаТовары.Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок,
	|	ТаблицаТовары.КодСтроки
	|ПОМЕСТИТЬ ТаблицаТоварыПродажаПоЗаказу
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.КодСтроки > 0 И НЕ &ОперацияСДенежнымиСредствами
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&ЗаказПокупателя КАК Заказ,
	|	&Магазин КАК Магазин,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаТоварыПродажаПоЗаказу.Номенклатура,
	|	ТаблицаТоварыПродажаПоЗаказу.Характеристика,
	|	ТаблицаТоварыПродажаПоЗаказу.КодСтроки,
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА - ТаблицаТоварыПродажаПоЗаказу.Количество
	|		ИНАЧЕ ТаблицаТоварыПродажаПоЗаказу.Количество
	|	КОНЕЦ КАК Заказано
	|ИЗ
	|	ТаблицаТоварыПродажаПоЗаказу КАК ТаблицаТоварыПродажаПоЗаказу
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = ТекстЗапроса + ТекстЗапросаБонусы();
	ТекстЗапроса = ТекстЗапроса + ТекстЗапросаТаблицаРасчетыСКлиентами();
	ТекстЗапроса = ТекстЗапроса + ТекстЗапросаАкцизныеМарки();
	
	Если ИспользоватьКомиссионнуюТорговлю 
		Или ИспользоватьУчетИмпортныхТоваров Тогда
		
			//27 ТаблицаТоварыОрганизаций
			ТекстЗапроса = ТекстЗапроса + "
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
			|ВЫБОР
			|		КОГДА &ЭтоВозврат
			|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|	КОНЕЦ                                  КАК ВидДвижения,
			|	&Период                                КАК Период,
			|	&Организация                           КАК Организация,
			|	&Организация                           КАК ОрганизацияОтгрузки,
			|	ТаблицаТовары.Склад                    КАК Склад,
			|	&Поставщик                             КАК Поставщик,
			|	&Договор                               КАК Договор,
			|	&НомерГТД                              КАК НомерГТД,
			|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
			|	ТаблицаТовары.Характеристика           КАК Характеристика,
			|	ТаблицаТовары.Сумма                    КАК СуммаПродажи,
			|	ТаблицаТовары.Количество               КАК Количество,
			|	0 КАК КоличествоПоРНПТ
			|ИЗ
			|	%ТаблицаТоваровВЗапросе%               КАК ТаблицаТовары
			|
			|ГДЕ
			|	&ФормироватьДвижения
			|	И (НЕ &ОперацияСДенежнымиСредствами)
			|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
			|	И НЕ (ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат) И &ЭтоВозврат)
			|	И ТаблицаТовары.Ссылка = &Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ТаблицаТоваровВЗапросе%", "ТаблицаТовары");
			
			Запрос.УстановитьПараметр("Поставщик", Справочники.Контрагенты.ПустаяСсылка());
			Запрос.УстановитьПараметр("НомерГТД" , Справочники.НомераГТД.ПустаяСсылка());
			Запрос.УстановитьПараметр("Договор"  , Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
			
	КонецЕсли;
		
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаДенежныеСредстваККМ = Результат[2].Выгрузить();
	
	СуммаПолученныхКупюр = ТаблицаДенежныеСредстваККМ.Итог("Сумма");
	Сдача = СуммаПолученныхКупюр - ВыручкаНаличными;
	
	Если Сдача > 0 Тогда
		СтруктураПоискаСобственныхСредств = Новый Структура;
		СтруктураПоискаСобственныхСредств.Вставить("ДоговорКонтрагента", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
		
		МассивПоиска = ТаблицаДенежныеСредстваККМ.НайтиСтроки(СтруктураПоискаСобственныхСредств);
		
		Если МассивПоиска.Количество() = 1 Тогда
			СтрокаСобственные = МассивПоиска[0];
			Если Сдача >= СтрокаСобственные.Сумма Тогда
				ТаблицаДенежныеСредстваККМ.Удалить(СтрокаСобственные);
			Иначе 
				СтрокаСобственные.Сумма = СтрокаСобственные.Сумма - Сдача;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваККМ"      	, ТаблицаДенежныеСредстваККМ);
	ТаблицыДляДвижений.Вставить("ТаблицаПродажи"                  	, Результат[3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПродажиПоДисконтнымКартам"	, Результат[4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПродажиПоПлатежнымКартам" 	, Результат[6].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаСкладах"          	, Результат[7].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерийТоваров"     	, Результат[8].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСерийныхНомеров"          	, Результат[16].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПодарочныхСертификатов"   	, Результат[19].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателей"        	, Результат[21].Выгрузить());
	
	ТаблицаОплатыБонусами = Результат[22].Выгрузить();
	ТаблицаБонусныеБаллы = Результат[24].Выгрузить();
	ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСКлиентами"        	, Результат[26].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаОстаткиАлкогольнойПродукцииЕГАИС" , Результат[27].Выгрузить());
	
	Если ИспользоватьКомиссионнуюТорговлю 
		Или ИспользоватьУчетИмпортныхТоваров Тогда
			ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизаций" , Результат[28].Выгрузить());
	КонецЕсли;
	
	
	Если ИспользоватьРасчетыСКлиентами И ВыручкаНаличными <> 0  Тогда
		
		СтрокаТаблицы = ТаблицыДляДвижений.ТаблицаРасчетыСКлиентами.Добавить();
		Если ЭтоВозврат Тогда
			СтрокаТаблицы.ВидДвижения     = ВидДвиженияНакопления.Расход;
		Иначе
			СтрокаТаблицы.ВидДвижения     = ВидДвиженияНакопления.Приход;
		КонецЕсли;
		СтрокаТаблицы.ДокументРасчета = Реквизиты.ДокументРасчета;
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументРасчета) Тогда
			СтрокаТаблицы.ДокументРасчета     = Реквизиты.Ссылка;
		КонецЕсли;
		СтрокаТаблицы.ЗаказПокупателя = Реквизиты.ЗаказПокупателя;
		СтрокаТаблицы.Контрагент      = Реквизиты.Контрагент;
		СтрокаТаблицы.Магазин         = Реквизиты.Магазин;
		СтрокаТаблицы.Организация     = Реквизиты.Организация;
		СтрокаТаблицы.Период          = Реквизиты.Период;
		СтрокаТаблицы.Регистратор     = Реквизиты.Ссылка;
		СтрокаТаблицы.Сумма           = ВыручкаНаличными;
		
		Если НЕ Реквизиты.ОперацияСДенежнымиСредствами Тогда
			СтрокаТаблицы = ТаблицыДляДвижений.ТаблицаРасчетыСКлиентами.Добавить();
			Если ЭтоВозврат Тогда
				СтрокаТаблицы.ВидДвижения     = ВидДвиженияНакопления.Приход;
			Иначе
				СтрокаТаблицы.ВидДвижения     = ВидДвиженияНакопления.Расход;
			КонецЕсли;
			СтрокаТаблицы.ДокументРасчета = Реквизиты.ДокументРасчета;
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументРасчета) Тогда
				СтрокаТаблицы.ДокументРасчета     = Реквизиты.Ссылка;
			КонецЕсли;
			СтрокаТаблицы.ЗаказПокупателя = Реквизиты.ЗаказПокупателя;
			СтрокаТаблицы.Контрагент      = Реквизиты.Контрагент;
			СтрокаТаблицы.Магазин         = Реквизиты.Магазин;
			СтрокаТаблицы.Организация     = Реквизиты.Организация;
			СтрокаТаблицы.Период          = Реквизиты.Период;
			СтрокаТаблицы.Регистратор     = Реквизиты.Ссылка;
			СтрокаТаблицы.Сумма           = ВыручкаНаличными;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТаблицаОплатыБонусами.Количество() > 0 Тогда
		Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
			
			Запрос.УстановитьПараметр("МассивКарт", МаркетинговыеАкцииСервер.ИсторияДисконтнойКарты(Реквизиты.Период, КартаБонусов, Истина));
			Запрос.УстановитьПараметр("ПустаяКарта", Справочники.ИнформационныеКарты.ПустаяСсылка());
			
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА Карты.КартаДляНакоплений = &ПустаяКарта
			|			ТОГДА Карты.Ссылка
			|		ИНАЧЕ Карты.КартаДляНакоплений
			|	КОНЕЦ КАК ДисконтнаяКарта,
			|	&БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
			|	&ДисконтнаяКарта КАК ОсновнаяКарта
			|ПОМЕСТИТЬ ЗаменыКарт
			|ИЗ
			|	Справочник.ИнформационныеКарты КАК Карты
			|ГДЕ
			|	Карты.Ссылка В(&МассивКарт)
			|	И Карты.БонуснаяПрограммаЛояльности = &БонуснаяПрограммаЛояльности
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	БонусныеБаллы.ДисконтнаяКарта КАК ДисконтнаяКарта,
			|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
			|	СУММА(БонусныеБаллы.ОстатокБаллов) КАК ОстатокБаллов
			|ИЗ
			|	(ВЫБРАТЬ
			|		БонусныеБаллы.ДисконтнаяКарта КАК ДисконтнаяКарта,
			|		БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
			|		БонусныеБаллы.НачисленоОстаток - БонусныеБаллы.КСписаниюОстаток КАК ОстатокБаллов
			|	ИЗ
			|		РегистрНакопления.БонусныеБаллы.Остатки(
			|				&ПериодБонусов,
			|				(ДисконтнаяКарта, БонуснаяПрограммаЛояльности) В
			|					(ВЫБРАТЬ
			|						Т.ДисконтнаяКарта,
			|						Т.БонуснаяПрограммаЛояльности
			|					ИЗ
			|						ЗаменыКарт КАК Т)) КАК БонусныеБаллы
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		БонусныеБаллы.ДисконтнаяКарта,
			|		БонусныеБаллы.БонуснаяПрограммаЛояльности,
			|		ВЫБОР
			|			КОГДА БонусныеБаллы.ВидДвижения = &Приход
			|				ТОГДА БонусныеБаллы.Начислено - БонусныеБаллы.КСписанию
			|			ИНАЧЕ БонусныеБаллы.КСписанию - БонусныеБаллы.Начислено
			|		КОНЕЦ
			|	ИЗ
			|		РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
			|	ГДЕ
			|		БонусныеБаллы.Период = &ПериодБонусов
			|		И БонусныеБаллы.Регистратор <> &Ссылка
			|		И (БонусныеБаллы.ДисконтнаяКарта, БонусныеБаллы.БонуснаяПрограммаЛояльности) В
			|				(ВЫБРАТЬ
			|					Т.ДисконтнаяКарта,
			|					Т.БонуснаяПрограммаЛояльности
			|				ИЗ
			|					ЗаменыКарт КАК Т)) КАК БонусныеБаллы
			|
			|СГРУППИРОВАТЬ ПО
			|	БонусныеБаллы.ДисконтнаяКарта,
			|	БонусныеБаллы.БонуснаяПрограммаЛояльности
			|
			|ИМЕЮЩИЕ
			|	СУММА(БонусныеБаллы.ОстатокБаллов) > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	БонусныеБаллы.Период,
			|	БонусныеБаллы.ДисконтнаяКарта,
			|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
			|	БонусныеБаллы.ДатаПервоначальногоНачисления,
			|	СУММА(ВЫБОР
			|			КОГДА БонусныеБаллы.ВидДвижения = &Приход
			|				ТОГДА БонусныеБаллы.КСписанию
			|			ИНАЧЕ -БонусныеБаллы.КСписанию
			|		КОНЕЦ) КАК КСписанию
			|ИЗ
			|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаменыКарт КАК ЗаменыКарт
			|		ПО БонусныеБаллы.ДисконтнаяКарта = ЗаменыКарт.ДисконтнаяКарта
			|			И БонусныеБаллы.БонуснаяПрограммаЛояльности = ЗаменыКарт.БонуснаяПрограммаЛояльности
			|			И (БонусныеБаллы.ДатаПервоначальногоНачисления <= &ПериодБонусов)
			|			И (БонусныеБаллы.ДатаПервоначальногоНачисления <> &ПустаяДата)
			|			И (БонусныеБаллы.Период > &ПериодБонусов)
			|			И (БонусныеБаллы.КСписанию <> 0)
			|			И (БонусныеБаллы.Регистратор <> &Ссылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	БонусныеБаллы.Период,
			|	БонусныеБаллы.ДисконтнаяКарта,
			|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
			|	БонусныеБаллы.ДатаПервоначальногоНачисления
			|
			|ИМЕЮЩИЕ
			|	СУММА(БонусныеБаллы.КСписанию) > 0";
			
			Результат = Запрос.ВыполнитьПакет();
			БонусныеБаллыСервер.ОбъединитьТаблицыБонусовПриОплате(
				ТаблицаБонусныеБаллы,
				ТаблицаОплатыБонусами,
				Результат,
				ПериодБонусов);
			
		Иначе
			Для Каждого СтрокаТаблицы Из ТаблицаОплатыБонусами Цикл
				НоваяСтрока = ТаблицаБонусныеБаллы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицыДляДвижений.Вставить("ТаблицаБонусныеБаллы", ТаблицаБонусныеБаллы);
	
	Если ИспользоватьКомиссионнуюТорговлю 
		ИЛИ ИспользоватьУчетИмпортныхТоваров Тогда
		Если ЭтоВозврат Тогда
			Если ВозвратПослеЗакрытияСмены Тогда
				ПроведениеСервер.ЗаполнитьТаблицуДвиженийПриходаРегистраТоварыОрганизаций(
													Реквизиты.ОтчетОРозничныхПродажах,
													ТаблицыДляДвижений.ТаблицаТоварыОрганизаций,
													Реквизиты.Период);
			ИначеЕсли ЗначениеЗаполнено(Реквизиты.ЧекККМПродажа) Тогда
				ПроведениеСервер.ЗаполнитьТаблицуДвиженийПриходаРегистраТоварыОрганизаций(
													Реквизиты.ЧекККМПродажа,
													ТаблицыДляДвижений.ТаблицаТоварыОрганизаций,
													Реквизиты.Период);
				Иначе
				 // Остается возврат без основания - ничего делать не надо.
			КонецЕсли;
		Иначе
			ПроведениеСервер.ПерезаполнитьТаблицуДвиженийТоварыОрганизаций(
													ТаблицыДляДвижений.ТаблицаТоварыОрганизаций,
													Реквизиты.Ссылка,
													Реквизиты.Период);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Товарный чек
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварныйЧек";
	КомандаПечати.Представление = НСтр("ru = 'Товарный чек'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	
	// Опись номенклатуры
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОписьНоменклатуры";
	КомандаПечати.Представление = НСтр("ru = 'Опись номенклатуры'");
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 52;
	
	// Гарантийный талон
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ГарантийныйТалон";
	КомандаПечати.Представление = НСтр("ru = 'Гарантийный талон'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	КомандаПечати.Порядок = 55;
	
	// Акт о возврате денежных сумм (КМ3).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "КМ3";
	КомандаПечати.Представление = НСтр("ru = 'Акт о возврате денежных сумм (КМ3)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюРТКлиент.ОбработкаКомандыПечатиКМ3";
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	КомандаПечати.Порядок = 60;
	КомандаПечати.ФункциональныеОпции = "ОтключенныйФункционал";
	
	// Товарный чек для принтера документов.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварныйЧекДляПД";
	КомандаПечати.Представление = НСтр("ru = 'Товарный чек для принтера документов'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	КомандаПечати.Порядок = 65;
	
	// Товарный чек для фискального регистратора.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварныйЧекДляФР";
	КомандаПечати.Представление = НСтр("ru = 'Товарный чек на ККМ, принтере чеков'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюРТКлиент.ОбработкаКомандыПечатиТоварногоЧекаДляФР";
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	КомандаПечати.Порядок = 80;
	
	ЭтоLinuxСервер = ОбщегоНазначенияРТВызовСервера.ЭтоLinuxСервер();
	
	Если ЭтоLinuxСервер И НЕ ОбщегоНазначения.ЭтоLinuxКлиент() Тогда
		
		// Заявление на возврат
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ЗаявлениеНаВозврат";
		КомандаПечати.Представление = НСтр("ru = 'Заявление на возврат'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Обработчик = "УправлениеПечатьюРТКлиент.ОбработкаКомандыПечатиЗаявленияПриВозврате";
		КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
		КомандаПечати.Порядок = 75;

	Иначе
		
		// Заявление на возврат
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ЗаявлениеНаВозврат";
		КомандаПечати.Представление = НСтр("ru = 'Заявление на возврат'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 75;

	КонецЕсли;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТоварныйЧек") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТоварныйЧек",
				ПараметрыПечати.Представление,
				ПечатьЧека(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОписьНоменклатуры") Тогда
	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОписьНоменклатуры",
				ПараметрыПечати.Представление,
				ПечатьОписиНоменклатуры(МассивОбъектов, ОбъектыПечати));
	
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТоварныйЧекДляПД") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТоварныйЧекДляПД",
				ПараметрыПечати.Представление,
				ПечатьТоварногоЧекаДляПД(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КМ3") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "КМ3",
				"КМ3",
				ПечатьКМ3(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
			
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ГарантийныйТалон") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ГарантийныйТалон",
				ПараметрыПечати.Представление,
				ПечатьГарантийныйТалон(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФискальнаяОперация") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФискальнаяОперация",
				ПараметрыПечати.Представление,
				ПродажиСервер.ПечатьФискальныхОпераций(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	СоответствиеПолучателей = Новый Соответствие;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеНаВозврат") Тогда
		
		ПараметрыПечати = СоответствиеДанныхОбОрганизациях(МассивОбъектов, СоответствиеПолучателей, ПараметрыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЗаявлениеНаВозврат",
			НСтр("ru = 'Заявление на возврат'"),
			ПечатьЗаявленияНаВозврат(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВозвратТоваровОтПокупателя.ПФ_MXL_ЗаявлениеНаВозврат");
			
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеОбОшибочноПробитомЧеке") Тогда
		
		ПараметрыПечати = СоответствиеДанныхОбОрганизациях(МассивОбъектов, СоответствиеПолучателей, ПараметрыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЗаявлениеОбОшибочноПробитомЧеке",
			НСтр("ru = 'Заявление об ошибочно пробитом чеке'"),
			ПечатьЗаявленияОбОшибочноПробитомЧеке(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВозвратТоваровОтПокупателя.ПФ_MXL_ЗаявлениеОбОшибочноПробитомЧеке");
			
			
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеНаУтерянныйЧек") Тогда
		
		ПараметрыПечати = СоответствиеДанныхОбОрганизациях(МассивОбъектов, СоответствиеПолучателей, ПараметрыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЗаявлениеНаУтерянныйЧек",
			НСтр("ru = 'Заявление на утерянный чек'"),
			ПечатьЗаявленияНаУтерянныйЧек(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВозвратТоваровОтПокупателя.ПФ_MXL_ЗаявлениеНаУтерянныйЧек");
		
	КонецЕсли;
	
	
КонецПроцедуры

// Функция формирует табличный документ с печатной формой.
//
// Возвращаемое значение:
//  ТабличныйДокумент - печатная форма.
//
Функция ПечатьЧека(МассивОбъектов, ОбъектыПечати) Экспорт

	КолонкаКодов       = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	ВыводитьКоды       = ЗначениеЗаполнено(КолонкаКодов);
	ВыводитьУпаковки   = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Чек ККМ'");
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЧекаККМ_ТоварныйЧек";
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.Магазин КАК Магазин,
	|	Документ.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозврат,
	|	ВЫБОР КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|		ТОГДА ПРЕДСТАВЛЕНИЕ(Документ.Организация)
	|		ИНАЧЕ &ТекстИП +
	|		    ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Фамилия, """") = """"
	|				ТОГДА """"
	|			ИНАЧЕ
	|				ФИОФизЛицСрезПоследнихИП.Фамилия + &ТекстПробел +
	|				ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Имя, """") = """"
	|						ТОГДА """"
	|						ИНАЧЕ ФИОФизЛицСрезПоследнихИП.Имя + &ТекстПробел
	|				КОНЕЦ
	|				+
	|				ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Отчество, """") = """"
	|						ТОГДА """"
	|						ИНАЧЕ ФИОФизЛицСрезПоследнихИП.Отчество
	|				КОНЕЦ
	|			КОНЕЦ
	|			+ """"
	|	КОНЕЦ КАК НаименованиеОрганизации,
	|	ВЫБОР КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрганизацияЮридическоеЛицо,
	|	Документ.Организация КАК Организация,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Магазин) КАК МагазинПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Организация) КАК ОрганизацияПредставление,
	|	Документ.Ответственный.ФизическоеЛицо КАК Ответственный
	|ИЗ
	|	Документ.ЧекККМ КАК Документ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних() КАК ФИОФизЛицСрезПоследнихИП
	|ПО
	|	Документ.Организация = ФИОФизЛицСрезПоследнихИП.ФизЛицо
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.Проведен
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка, Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОплата.ВидОплаты.ТипОплаты КАК ТипОплаты,
	|	ТаблицаОплата.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаОплата.ВидОплаты.ТипОплаты) КАК ПредставлениеТипОплаты,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|			И ТаблицаОплата.ВидОплаты.ТипОплаты <> Значение(Перечисление.ТипыОплатЧекаККМ.Наличные)
	|			ТОГДА ТаблицаОплата.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаБезналичныхОплат,
	|	СУММА(ТаблицаОплата.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ТаблицаОплата
	|ГДЕ
	|	ТаблицаОплата.Ссылка В(&МассивОбъектов)
	|	И ТаблицаОплата.Ссылка.Проведен
	|	И НЕ (ТаблицаОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат) И ТаблицаОплата.ВидОплаты.ТипОплаты = Значение(Перечисление.ТипыОплатЧекаККМ.Наличные))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОплата.Ссылка,
	|	ТаблицаОплата.ВидОплаты.ТипОплаты
	|
	|	ИМЕЮЩИЕ
	|		(НЕ СУММА(ТаблицаОплата.Сумма) = 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.Представление КАК Товар,
	|	" + ?(ВыводитьКоды, "ТаблицаТовары.Номенклатура." + КолонкаКодов +" КАК КолонкаКодов,", "") + "
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Упаковка.ЕдиницаИзмерения)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|	И ТаблицаТовары.Ссылка.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	УстановитьТекстовыеПараметрыЗапроса(Запрос);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЧекККМ.ПФ_MXL_Накладная");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
	ОбластьИНН       = Макет.ПолучитьОбласть("ИНН");
	ОбластьМагазин   = Макет.ПолучитьОбласть("Магазин");
	
	ОбластьШапкаНомера = Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");
	ОбластьШапкаКодов  = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
	ОбластьШапкаДанных = Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");
	ОбластьШапкаСкидок = Макет.ПолучитьОбласть("ШапкаТаблицы|Скидка");
	ОбластьШапкаСуммы  = Макет.ПолучитьОбласть("ШапкаТаблицы|Сумма");
	
	ОбластьКолонкаТовар = Макет.Область("Товар");
	Если Не ВыводитьКоды Тогда
		ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки
		                                  + Макет.Область("КолонкаКодов").ШиринаКолонки;
	КонецЕсли;
	ОбластьСтрокаНомера = Макет.ПолучитьОбласть("Строка|НомерСтроки");
	ОбластьСтрокаКодов  = Макет.ПолучитьОбласть("Строка|КолонкаКодов");
	ОбластьСтрокаДанных = Макет.ПолучитьОбласть("Строка|Данные");
	ОбластьСтрокаСкидок = Макет.ПолучитьОбласть("Строка|Скидка");
	ОбластьСтрокаСуммы  = Макет.ПолучитьОбласть("Строка|Сумма");
	
	// Вывести Итого.
	ОбластьИтогоНомера = Макет.ПолучитьОбласть("Итого|НомерСтроки");
	ОбластьИтогоКодов  = Макет.ПолучитьОбласть("Итого|КолонкаКодов");
	ОбластьИтогоДанных = Макет.ПолучитьОбласть("Итого|Данные");
	ОбластьИтогоСкидок = Макет.ПолучитьОбласть("Итого|Скидка");
	ОбластьИтогоСуммы  = Макет.ПолучитьОбласть("Итого|Сумма");
	
	ОбластьСуммаПрописью = Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписей      = Макет.ПолучитьОбласть("Подписи");
	
	ОбластьОплата = Макет.ПолучитьОбласть("Оплата");
	
	ВыборкаПоДокументам = Результаты[0].Выбрать();
	ВыборкаПоТабличнойЧастиОплата = Результаты[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоТабличнымЧастям = Результаты[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ВыборкаПоДокументам.ЭтоВозврат Тогда
			СинонимДокумента   = НСтр("ru='Товарный чек (возврат)'");
		Иначе
			СинонимДокумента   = НСтр("ru='Товарный чек'");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ВыборкаПоДокументам);
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, СинонимДокумента);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		СведенияОПоставщике = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ВыборкаПоДокументам.Организация, ВыборкаПоДокументам.Дата);
		
		Если ВыборкаПоДокументам.ОрганизацияЮридическоеЛицо Тогда
			ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ПолноеНаименование");
		Иначе
			ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ВыборкаПоДокументам.НаименованиеОрганизации;
		КонецЕсли;
		ОбластьПоставщик.Параметры.Поставщик = ВыборкаПоДокументам.Организация;
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		

		ОбластьИНН.Параметры.ИНН = СведенияОПоставщике.ИНН;
		ТабличныйДокумент.Вывести(ОбластьИНН);

		ОбластьМагазин.Параметры.ПредставлениеМагазина = ВыборкаПоДокументам.МагазинПредставление;
		ОбластьМагазин.Параметры.Магазин = ВыборкаПоДокументам.Магазин;
		ТабличныйДокумент.Вывести(ОбластьМагазин);
		
		// Шапка
		
		ТабличныйДокумент.Вывести(ОбластьШапкаНомера);
		Если ВыводитьКоды Тогда
			ОбластьШапкаКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			ТабличныйДокумент.Присоединить(ОбластьШапкаКодов);
		КонецЕсли;
		ТабличныйДокумент.Присоединить(ОбластьШапкаДанных);
		ТабличныйДокумент.Присоединить(ОбластьШапкаСкидок);
		ТабличныйДокумент.Присоединить(ОбластьШапкаСуммы);
		
		
		ВсегоНаименований = 0;
		Сумма             = 0;
		ВсегоСкидок       = 0;
		ВсегоБезСкидок    = 0;
		
		// СТРОКИ ТЧ
		Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамТЧ.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;

			ОбластьСтрокаНомера.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ТабличныйДокумент.Вывести(ОбластьСтрокаНомера);

			Если ВыводитьКоды Тогда
				
				ОбластьСтрокаКодов.Параметры.Артикул = ВыборкаПоСтрокамТЧ["КолонкаКодов"];
				ТабличныйДокумент.Присоединить(ОбластьСтрокаКодов);
				
			КонецЕсли;

			ОбластьСтрокаДанных.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ОбластьСтрокаДанных.Параметры.Товар = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(ВыборкаПоСтрокамТЧ.НоменклатураПредставление,ВыборкаПоСтрокамТЧ.ХарактеристикаПредставление);
			ТабличныйДокумент.Присоединить(ОбластьСтрокаДанных);
			
			Скидка = Окр(ВыборкаПоСтрокамТЧ.Цена * ВыборкаПоСтрокамТЧ.Количество, 2) - ВыборкаПоСтрокамТЧ.Сумма;
			ОбластьСтрокаСкидок.Параметры.Скидка         = Скидка;
			ОбластьСтрокаСкидок.Параметры.СуммаБезСкидки = ВыборкаПоСтрокамТЧ.Сумма + Скидка;
			ТабличныйДокумент.Присоединить(ОбластьСтрокаСкидок);

			ОбластьСтрокаСуммы.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ТабличныйДокумент.Присоединить(ОбластьСтрокаСуммы);
			
			ВсегоНаименований = ВсегоНаименований + 1;
			
			Сумма          = Сумма + ВыборкаПоСтрокамТЧ.Сумма;
			ВсегоСкидок    = ВсегоСкидок + Скидка;
			ВсегоБезСкидок = Сумма + ВсегоСкидок;
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьИтогоНомера);
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьИтогоКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьИтогоДанных);
		ОбластьИтогоСкидок.Параметры.ВсегоСкидок    = ВсегоСкидок;
		ОбластьИтогоСкидок.Параметры.ВсегоБезСкидок = ВсегоБезСкидок;
		ТабличныйДокумент.Присоединить(ОбластьИтогоСкидок);
		ОбластьИтогоСуммы.Параметры.Всего = Сумма;
		ТабличныйДокумент.Присоединить(ОбластьИтогоСуммы);
		
		// Вывести Сумму прописью.
		
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований%, на сумму %Итого%'");
		
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", ВсегоНаименований);
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%Итого%", ФормированиеПечатныхФормСервер.ФорматСумм(Сумма));
		
		ОбластьСуммаПрописью.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ОбластьСуммаПрописью.Параметры.СуммаПрописью  = ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(Сумма);
		
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		// Оплата
		Если ВыборкаПоТабличнойЧастиОплата.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			ОплатаДобавлена = Ложь;
			СтрокаОплаты = "";
			СуммаБезналичныхОплат = 0;
			СуммаОплат = 0;
			
			ВыборкаПоОплата = ВыборкаПоТабличнойЧастиОплата.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоОплата.Следующий() Цикл
				Если ОплатаДобавлена Тогда
					СтрокаОплаты = СтрокаОплаты + Символы.ПС;
				КонецЕсли;
				ОплатаДобавлена = Истина;
				СтрокаОплаты = СтрокаОплаты + ВыборкаПоОплата.ПредставлениеТипОплаты + ": "  + Формат(ВыборкаПоОплата.Сумма, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				СуммаБезналичныхОплат = СуммаБезналичныхОплат + ВыборкаПоОплата.СуммаБезналичныхОплат;
				СуммаОплат = СуммаОплат + ВыборкаПоОплата.Сумма;
			КонецЦикла;
			
			Если ВыборкаПоДокументам.ЭтоВозврат Тогда
				СуммаНаличных = ВыборкаПоДокументам.СуммаДокумента - СуммаБезналичныхОплат;
				Если СуммаНаличных > 0  Тогда
					Если ОплатаДобавлена Тогда
						СтрокаОплаты = СтрокаОплаты + Символы.ПС;
					КонецЕсли;
					СтрокаОплаты = СтрокаОплаты + НСтр("ru = 'Наличные:'") + " " + Формат(СуммаНаличных, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				КонецЕсли;
				ОбластьОплата.Параметры.Сдача = "0.00";
			Иначе
				ОбластьОплата.Параметры.Сдача = Формат(СуммаОплат - ВыборкаПоДокументам.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
			КонецЕсли;
			
			ОбластьОплата.Параметры.Оплата = СтрокаОплаты;
			ТабличныйДокумент.Вывести(ОбластьОплата);
		КонецЕсли;
		
		
		// ПОДПИСИ
		ОбластьПодписей.Параметры.Заполнить(ВыборкаПоДокументам);
		ОбластьПодписей.Параметры.ФИО = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(ВыборкаПоДокументам.Ответственный);
		ТабличныйДокумент.Вывести(ОбластьПодписей);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Возвращает текст печати на ФР
//
Функция ТекстПечатиЧекаНаФР(МассивОбъектов, ШиринаЛенты) Экспорт
	
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.Магазин КАК Магазин,
	|	Документ.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозврат,
	|	ВЫБОР КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|		ТОГДА ПРЕДСТАВЛЕНИЕ(Документ.Организация)
	|		ИНАЧЕ &ТекстИП +
	|		    ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Фамилия, """") = """"
	|				ТОГДА """"
	|			ИНАЧЕ
	|				ФИОФизЛицСрезПоследнихИП.Фамилия + &ТекстПробел +
	|				ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Имя, """") = """"
	|						ТОГДА """"
	|						ИНАЧЕ ФИОФизЛицСрезПоследнихИП.Имя + &ТекстПробел
	|				КОНЕЦ
	|				+
	|				ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Отчество, """") = """"
	|						ТОГДА """"
	|						ИНАЧЕ ФИОФизЛицСрезПоследнихИП.Отчество
	|				КОНЕЦ
	|			КОНЕЦ
	|			+ """"
	|	КОНЕЦ КАК НаименованиеОрганизации,
	|	ВЫБОР КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрганизацияЮридическоеЛицо,
	|	Документ.Организация КАК Организация,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Магазин) КАК МагазинПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Организация) КАК ОрганизацияПредставление,
	|	Документ.Ответственный.ФизическоеЛицо КАК Ответственный
	|ИЗ
	|	Документ.ЧекККМ КАК Документ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних() КАК ФИОФизЛицСрезПоследнихИП
	|ПО
	|	Документ.Организация = ФИОФизЛицСрезПоследнихИП.ФизЛицо
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.Проведен
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка, Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОплата.ВидОплаты.ТипОплаты КАК ТипОплаты,
	|	ТаблицаОплата.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаОплата.ВидОплаты.ТипОплаты) КАК ПредставлениеТипОплаты,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|			И ТаблицаОплата.ВидОплаты.ТипОплаты <> Значение(Перечисление.ТипыОплатЧекаККМ.Наличные)
	|			ТОГДА ТаблицаОплата.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаБезналичныхОплат,
	|	СУММА(ТаблицаОплата.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ТаблицаОплата
	|ГДЕ
	|	ТаблицаОплата.Ссылка В(&МассивОбъектов)
	|	И ТаблицаОплата.Ссылка.Проведен
	|	И НЕ (ТаблицаОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат) И ТаблицаОплата.ВидОплаты.ТипОплаты = Значение(Перечисление.ТипыОплатЧекаККМ.Наличные))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОплата.Ссылка,
	|	ТаблицаОплата.ВидОплаты.ТипОплаты
	|
	|	ИМЕЮЩИЕ
	|		(НЕ СУММА(ТаблицаОплата.Сумма) = 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.Представление КАК Товар,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Упаковка.ЕдиницаИзмерения)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|	И ТаблицаТовары.Ссылка.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	УстановитьТекстовыеПараметрыЗапроса(Запрос);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ДокументЧекККМ  = Новый ТекстовыйДокумент();

	// Получение макета
	МакетЧекаККМ      = Документы.ЧекККМ.ПолучитьМакет("ЧекПокупателя");
	
	// макет существует только для ленты шириной: 24 32 36 40 48 64
	Если ШиринаЛенты = Неопределено Или ШиринаЛенты = 0 Тогда
		ШиринаЛенты = 32;
	ИначеЕсли ШиринаЛенты <= 24 Тогда
		ШиринаЛенты = 24;
	ИначеЕсли ШиринаЛенты <= 32 Тогда
		ШиринаЛенты = 32;
	ИначеЕсли ШиринаЛенты <= 36 Тогда
		ШиринаЛенты = 36;
	ИначеЕсли ШиринаЛенты <= 40 Тогда
		ШиринаЛенты = 40;
	ИначеЕсли ШиринаЛенты <= 48 Тогда
		ШиринаЛенты = 48;
	Иначе
		ШиринаЛенты = 64;
	КонецЕсли;

	ОбластьШапкаЧека  = МакетЧекаККМ.ПолучитьОбласть("ШапкаЧека"  + ШиринаЛенты);
	ОбластьТелоЧека   = МакетЧекаККМ.ПолучитьОбласть("ТелоЧека"   + ШиринаЛенты);
	ОбластьПодвалЧека = МакетЧекаККМ.ПолучитьОбласть("ПодвалЧека" + ШиринаЛенты);
	ОбластьРазрывЧека = МакетЧекаККМ.ПолучитьОбласть("РазрывЧека" + ШиринаЛенты);
	
	ВыборкаПоДокументам = Результаты[0].Выбрать();
	ВыборкаПоТабличнойЧастиОплата = Результаты[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоТабличнымЧастям = Результаты[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если НЕ ПервыйДокумент Тогда
			
			ДокументЧекККМ.Вывести(ОбластьРазрывЧека);
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		Если ВыборкаПоДокументам.ЭтоВозврат Тогда
			СинонимДокумента   = НСтр("ru='Товарный чек (возврат)'");
		Иначе
			СинонимДокумента   = НСтр("ru='Товарный чек'");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ВыборкаПоДокументам);
		ОбластьШапкаЧека.Параметры.Заголовок = ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, СинонимДокумента);
		СведенияОПоставщике = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ВыборкаПоДокументам.Организация, ВыборкаПоДокументам.Дата);
		ОбластьШапкаЧека.Параметры.ИНН = СведенияОПоставщике.ИНН;
		ОбластьШапкаЧека.Параметры.Организация = ВыборкаПоДокументам.НаименованиеОрганизации;
		
		ДокументЧекККМ.Вывести(ОбластьШапкаЧека);
		
		// СТРОКИ ТЧ
		Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамТЧ.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;

			ОбластьТелоЧека.Параметры.НаименованиеТовара = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(ВыборкаПоСтрокамТЧ.НоменклатураПредставление,ВыборкаПоСтрокамТЧ.ХарактеристикаПредставление);
			ОбластьТелоЧека.Параметры.КоличествоЦена     = Формат(ВыборкаПоСтрокамТЧ.Количество, "ЧЦ=17; ЧДЦ=3; ЧРД=.; ЧН=; ЧГ=0")
			                                             + " х "
			                                             + Формат(ВыборкаПоСтрокамТЧ.Цена, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
			
			КонечнаяСкидка =  ВыборкаПоСтрокамТЧ.Количество *  ВыборкаПоСтрокамТЧ.Цена -  ВыборкаПоСтрокамТЧ.Сумма;

			Если КонечнаяСкидка <> 0 Тогда
				ОбластьТелоЧека.Параметры.ЗагСкидка = ?(КонечнаяСкидка > 0, НСтр("ru='Скидка:'"), НСтр("ru='Надбавка:'"));
				ОбластьТелоЧека.Параметры.Скидка    = Формат(?(КонечнаяСкидка > 0, КонечнаяСкидка, -КонечнаяСкидка), "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
			Иначе
				ОбластьТелоЧека.Параметры.ЗагСкидка = НСтр("ru=''");
				ОбластьТелоЧека.Параметры.Скидка    = НСтр("ru=''");
			КонецЕсли;
			
			ДокументЧекККМ.Вывести(ОбластьТелоЧека);
			
		КонецЦикла;
		ДокументЧекККМ.УдалитьСтроку(ДокументЧекККМ.КоличествоСтрок());
		
		ОбластьПодвалЧека.Параметры.Итог = Формат(ВыборкаПоДокументам.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		
		ОбластьПодвалЧека.Параметры.Сотрудник =  ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(ВыборкаПоДокументам.Ответственный);
		
		ДокументЧекККМ.Вывести(ОбластьПодвалЧека);
		
	КонецЦикла;
	
	СтрокаВозврат = "";
	
	КоличествоСтрок = ДокументЧекККМ.КоличествоСтрок();
	
	Для Индекс = 1 По КоличествоСтрок Цикл
		СтрокаВозврат = СтрокаВозврат + ДокументЧекККМ.ПолучитьСтроку(Индекс); 
		
		Если НЕ Индекс = КоличествоСтрок Тогда
			СтрокаВозврат = СтрокаВозврат + Символы.ПС; 
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрокаВозврат
	
КонецФункции

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Возвращает признак способа расчета по документу.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекККМ.
//
// Возвращаемое значение:
//  ПризнакСпособаРасчета - ПеречислениеСсылка.ПризнакиСпособаРасчета.
//
Функция ПризнакСпособаРасчета(ДокументСсылка) Экспорт
	
	СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ОперацияСДенежнымиСредствами");
	СтруктураРеквизитов.Вставить("ДокументРасчета");
	СтруктураРеквизитов.Вставить("СуммаДокумента");
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, СтруктураРеквизитов);
	
	СостояниеОплаты = СостояниеОплаты(ДокументСсылка);
	
	Если РеквизитыДокумента.ОперацияСДенежнымиСредствами Тогда
		
		Если ЗначениеЗаполнено(РеквизитыДокумента.ДокументРасчета) И
			ТипЗнч(РеквизитыДокумента.ДокументРасчета) = Тип("ДокументСсылка.ЧекККМ") Тогда
			
			ЧекРасчетаБезПередачиТоваров =
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыДокумента.ДокументРасчета, "ОперацияСДенежнымиСредствами");
			
			Если ЧекРасчетаБезПередачиТоваров Тогда
				// Оплата не отгруженного товара.
				Если СостояниеОплаты.Аванс Тогда
					СпособРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс;
				Иначе
					Если СостояниеОплаты.ПолнаяОплата Тогда
						СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная;
					Иначе
						СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаЧастичная;
					КонецЕсли;
				КонецЕсли;
			Иначе
				// Оплата ранее отгруженного товара.
				СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ОплатаКредита;
			КонецЕсли;
		Иначе
			Если СостояниеОплаты.Аванс Тогда
				// Прием аванса.
				СпособРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс;
			Иначе
				Если СостояниеОплаты.ПолнаяОплата Тогда
					// Прием полной предоплаты.
					СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная;
				Иначе
					// Прием частичной предоплаты.
					СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаЧастичная;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если СостояниеОплаты.БезОплаты Тогда
			// Отгрузка товаров в рассрочку
			СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаБезОплаты;
		Иначе
			Если СостояниеОплаты.ПолнаяОплата Тогда
				// Отгрузка товара и принимаем полностью за него оплату
				СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
			Иначе
				// Отгрузка товаров с неполной оплатой. Остаток в кредит.
				СпособРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСЧастичнойОплатой;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СпособРасчета;
КонецФункции

// Подготавливает данные в формате БПО
//
// Параметры:
//  ДанныеДляЧека - Структура - см. РозничныеПродажиКлиентСервер.СтруктураДанныхДляПробитияЧека();
//
// Возвращаемое значение:
//  Структура - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПодготовитьДанныеДляПробитияЧека(ДанныеДляЧека) Экспорт
	
	ДокументСсылка = ДанныеДляЧека.ЧекККМСсылка;
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	
	Если ДанныеДляЧека.ЭтоВозвратПродукцииИСМПБезМарки Тогда 
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЭтоВозвратПродукцииИСМПБезМарки");
	КонецЕсли;
	
	// Товары
	ТаблицаСоответствияТоваровСекциям = Новый Соответствие();
	ПодключаемоеОборудованиеРТ.ЗаполнитьСоответствиеСекцийДляТабличнойЧастиПоКассеККМ(
		ДокументОбъект.КассаККМ,
		ДокументОбъект.Товары,
		ТаблицаСоответствияТоваровСекциям);
	
	ДанныеДляПробитияЧекаККМ = ПродажиСервер.ДанныеДляПробитияЧекаККМ(ДокументОбъект);
	
	ДанныеДляЧека.ЕстьАлкогольнаяПродукцияЕГАИС = ДанныеДляПробитияЧекаККМ.ЕстьАлкогольнаяПродукцияЕГАИС;
	
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	ОбщиеПараметры.Вставить("ЕстьАлкогольнаяПродукцияЕГАИС", ДанныеДляПробитияЧекаККМ.ЕстьАлкогольнаяПродукцияЕГАИС);
	ОбщиеПараметры.Вставить("РабочееМестоКассира", ДанныеДляЧека.РабочееМестоКассира);
	
	// Подготовка таблицы товаров
	МассивТоваров = Новый Массив;
	МассивТоваровЕГАИС = Новый Массив;
	
	// Общие параметры чека
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ВидОперации");
	СтруктураРеквизитов.Вставить("КассаККМ");
	СтруктураРеквизитов.Вставить("Магазин");
	СтруктураРеквизитов.Вставить("Организация");
	СтруктураРеквизитов.Вставить("Контрагент");
	СтруктураРеквизитов.Вставить("Дата");
	СтруктураРеквизитов.Вставить("ЧекККМПродажа");
	СтруктураРеквизитов.Вставить("Телефон");
	СтруктураРеквизитов.Вставить("АдресЭП");
	СтруктураРеквизитов.Вставить("Ответственный");
	СтруктураРеквизитов.Вставить("ДоговорКонтрагента");
	СтруктураРеквизитов.Вставить("СистемаНалогообложения");
	СтруктураРеквизитов.Вставить("ЗаказПокупателя");
	СтруктураРеквизитов.Вставить("СуммаДокумента");
	СтруктураРеквизитов.Вставить("ОперацияСДенежнымиСредствами");
	СтруктураРеквизитов.Вставить("ДокументРасчета");
	СтруктураРеквизитов.Вставить("ЦенаВключаетНДС");
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, СтруктураРеквизитов);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Наименование");
	СтруктураРеквизитов.Вставить("НаименованиеПолное");
	СтруктураРеквизитов.Вставить("ИНН");
	СтруктураРеквизитов.Вставить("КПП");
	СтруктураРеквизитов.Вставить("СерияСвидетельстваПоНДС");
	СтруктураРеквизитов.Вставить("НомерСвидетельстваПоНДС");
	РеквизитыОрганизация =
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.Организация, СтруктураРеквизитов);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Код");
	СтруктураРеквизитов.Вставить("ЭлектронныйЧекSMSПередаютсяПрограммой1С");
	СтруктураРеквизитов.Вставить("ЭлектронныйЧекEmailПередаютсяПрограммой1С");
	СтруктураРеквизитов.Вставить("ПодключаемоеОборудование");
	СтруктураРеквизитов.Вставить("СпособФорматноЛогическогоКонтроля",
		"ПодключаемоеОборудование.СпособФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ДопустимоеРасхождениеФорматноЛогическогоКонтроля",
		"ПодключаемоеОборудование.ДопустимоеРасхождениеФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ТипОборудования",
		"ПодключаемоеОборудование.ТипОборудования");
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.КассаККМ, СтруктураРеквизитов);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Наименование");
	СтруктураРеквизитов.Вставить("ИНН");
	Кассир = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыДокумента.Ответственный, "ФизическоеЛицо");
	РеквизитыКассир =
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Кассир, СтруктураРеквизитов);
	
	ПодключаемоеОборудованиеРТ.ЗаполнитьДанныеПокупателя(ОбщиеПараметры, РеквизитыДокумента);
	
	ОбщиеПараметры.СистемаНалогообложения = РеквизитыДокумента.СистемаНалогообложения;
	
	ОбщиеПараметры.ТипРасчета =
		?(РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат,
			Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств,
			Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств);
	
	ОбщиеПараметры.ДокументОснование = ДокументСсылка;
	
	ПодключаемоеОборудованиеРТ.ЗаполнитьПараметрыПлатежногоДоговора(
		ОбщиеПараметры,
		РеквизитыДокумента.ДоговорКонтрагента,
		РеквизитыДокумента.СуммаДокумента);
		
	Если РеквизитыКассыККМ.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ Или
		РеквизитыКассыККМ.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков Тогда
		ЗаполнитьОплатуПриПробитииЧекаККМНаККТ(ДокументОбъект, ОбщиеПараметры);
	Иначе
		ЗаполнитьОплатуПриПробитииЧекаККМ(ДокументОбъект, ОбщиеПараметры);
	КонецЕсли;
	
	ДополнитьТоварамиПараметрыПриПробитииЧека(
		РеквизитыДокумента,
		ОбщиеПараметры,
		ДанныеДляПробитияЧекаККМ,
		ТаблицаСоответствияТоваровСекциям,
		ПризнакСпособаРасчета(ДокументСсылка));
		
	РаспределитьЗачетАванса(ОбщиеПараметры);
		
	ОбщиеПараметры.Электронно = ДанныеДляЧека.НеПечататьБумажныйЧек;
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.Телефон) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекSMSПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СSMS = Истина;
		КонецЕсли;
		Телефон = РеквизитыДокумента.Телефон;
		Если СтрНайти(Телефон, "+7") = 1 ИЛИ (СтрНайти(Телефон, "8") = 1 И СтрДлина(Телефон) = 11) Тогда
			ОбщиеПараметры.ПокупательНомер = Телефон;
		Иначе
			ОбщиеПараметры.ПокупательНомер = "+7" + Телефон;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.АдресЭП) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекEmailПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СEmail = Истина;
			СистемнаяУчетнаяЗапись = РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись();
			Если ЗначениеЗаполнено(СистемнаяУчетнаяЗапись) Тогда
				ОбщиеПараметры.ОтправительEmail = СистемнаяУчетнаяЗапись.АдресЭлектроннойПочты;
			КонецЕсли;
		КонецЕсли;
		ОбщиеПараметры.ПокупательEmail = РеквизитыДокумента.АдресЭП;
	КонецЕсли;
	
	// Параметры необходимые для чека ЕНВД на принтере чеков
	Если ДанныеДляЧека.ВариантЗаполненияКассира = 2 Тогда
		Должность = РегистрыСведений.ОтветственныеЛицаОрганизаций.ПолучитьДолжностьСотрудникаНаДату(Кассир, ТекущаяДатаСеанса());
		ОбщиеПараметры.Кассир = РеквизитыКассир.Наименование;
		Если ЗначениеЗаполнено(Должность) Тогда
			ОбщиеПараметры.Кассир = ОбщиеПараметры.Кассир + НСтр("ru=' ('") + Должность + НСтр("ru=')'")
		КонецЕсли;
	Иначе
		ОбщиеПараметры.Кассир = РеквизитыКассир.Наименование;
	КонецЕсли;
	ОбщиеПараметры.КассирИНН = РеквизитыКассир.ИНН;
	
	ОбщиеПараметры.НомерКассы = РеквизитыКассыККМ.Код;
	ОбщиеПараметры.НомерЧека = ДанныеДляЧека.НомерЧека;
	ОбщиеПараметры.Организация = РеквизитыДокумента.Организация;
	ОбщиеПараметры.ОрганизацияИНН = РеквизитыОрганизация.ИНН;
	ОбщиеПараметры.ОрганизацияКПП = РеквизитыОрганизация.КПП;
	ОбщиеПараметры.ОрганизацияНазвание =
		?(ПустаяСтрока(РеквизитыОрганизация.НаименованиеПолное),
			РеквизитыОрганизация.Наименование,
			РеквизитыОрганизация.НаименованиеПолное);
	
	ОбщиеПараметры.НомерСмены = 1;
	
	СведенияООрганизации =
		ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(РеквизитыДокумента.Организация, РеквизитыДокумента.Дата);
	АдресМагазина = ОбщегоНазначенияРТ.АдресМагазина(РеквизитыДокумента.Магазин);
	
	СерийныйНомер =
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыКассыККМ.ПодключаемоеОборудование, "СерийныйНомер");
	Если НЕ ЗначениеЗаполнено(СерийныйНомер) Тогда
		СерийныйНомер = "1";
	КонецЕсли;
	
	ОбщиеПараметры.ТорговыйОбъект = РеквизитыДокумента.Магазин;
	ОбщиеПараметры.АдресРасчетов = АдресМагазина;
	Если ДанныеДляЧека.ВариантЗаполненияМестаРасчетов = 1 Тогда
		ОбщиеПараметры.МестоРасчетов = Строка(РеквизитыДокумента.Магазин);
	Иначе
		ОбщиеПараметры.МестоРасчетов = Строка(РеквизитыДокумента.Магазин) + " " + АдресМагазина;
	КонецЕсли;
	ОбщиеПараметры.АдресМагазина = АдресМагазина;
	ОбщиеПараметры.НаименованиеМагазина = Строка(РеквизитыДокумента.Магазин);
	ОбщиеПараметры.СерийныйНомер = СерийныйНомер;
	ОбщиеПараметры.ИдентификаторФискальнойЗаписи = ДокументОбъект.ИдентификаторЧекаВОчереди;
	
	ОбщиеПараметры.КассаККМ = ДокументОбъект.КассаККМ;
	Если ДокументОбъект.КассаККМ.ИспользоватьБезПодключенияОборудования Тогда
		ОбщиеПараметры.АвтономнаяККТ = Истина;
	КонецЕсли;
	Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда
		ОбщиеПараметры.ШаблонЧека =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.КассаККМ, "ШаблонЧекаККМВозврат");
	Иначе
		ОбщиеПараметры.ШаблонЧека =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.КассаККМ, "ШаблонЧекаККМ");
	КонецЕсли;
	
	Если РеквизитыКассыККМ.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ
		Или РеквизитыКассыККМ.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков Тогда
		ПодключаемоеОборудованиеРТ.ДобавитьВнереализационнуюПрибыль(ДокументСсылка, ОбщиеПараметры);
		
		// При необходимости будет проведен формато-логический контроль
		
		ОбщиеПараметры.СпособФорматноЛогическогоКонтроля =
			РеквизитыКассыККМ.СпособФорматноЛогическогоКонтроля;
		ОбщиеПараметры.ДопустимоеРасхождениеФорматноЛогическогоКонтроля =
			РеквизитыКассыККМ.ДопустимоеРасхождениеФорматноЛогическогоКонтроля;
			
		Если ФорматноЛогическийКонтрольКлиентСервер.НуженФорматноЛогическийКонтроль(ОбщиеПараметры) Тогда
			ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ОбщиеПараметры);
		КонецЕсли;
	КонецЕсли;
	
	ЭтоТипПлатежнойСистемыККТСБП  = Ложь;
	ЭтоТипПлатежнойСистемыККТНСПК = Ложь;
	Если ОбщиеПараметры.Свойство("ТипПлатежнойСистемы") Тогда
		ЭтоТипПлатежнойСистемыККТСБП  =
			ОбщиеПараметры.ТипПлатежнойСистемы = Перечисления.ТипыПлатежнойСистемыККТ.СистемаБыстрыхПлатежей;
		ЭтоТипПлатежнойСистемыККТНСПК =
			ОбщиеПараметры.ТипПлатежнойСистемы = Перечисления.ТипыПлатежнойСистемыККТ.СертификатНСПК;
	КонецЕсли;
	
	Если ОбщиеПараметры.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств 
		И (ЭтоТипПлатежнойСистемыККТСБП ИЛИ ЭтоТипПлатежнойСистемыККТНСПК) 
		И ОбщиеПараметры.Свойство("QRКод") Тогда
		
		ЕстьШаблонЧекаПродажи = ОбщиеПараметры.Свойство("ШаблонЧека") 
			И ЗначениеЗаполнено(ОбщиеПараметры.ШаблонЧека);
		НеПечататьКодВозвратаСБП  = Ложь;
		НеПечататьКодВозвратаНСПК = Ложь;
		Если НЕ ЕстьШаблонЧекаПродажи И ПолучитьФункциональнуюОпцию("ИспользоватьНовоеРМК") Тогда 
			УстановитьНастройкиКодаВозврата(НеПечататьКодВозвратаСБП,НеПечататьКодВозвратаНСПК);
			НеПечататьКодВозвратаСБП = ЭтоТипПлатежнойСистемыККТСБП И НеПечататьКодВозвратаСБП;
			НеПечататьКодВозвратаНСПК = ЭтоТипПлатежнойСистемыККТНСПК И НеПечататьКодВозвратаНСПК; 
		КонецЕсли;
		Если ЕстьШаблонЧекаПродажи ИЛИ НеПечататьКодВозвратаСБП ИЛИ НеПечататьКодВозвратаНСПК Тогда
			ОбщиеПараметры.QRКод.ЗначениеКода = "";
		КонецЕсли;
	КонецЕсли;
	
	ОбщиеПараметры.СерияСвидетельстваПоНДС = РеквизитыОрганизация.СерияСвидетельстваПоНДС;
	ОбщиеПараметры.НомерСвидетельстваПоНДС = РеквизитыОрганизация.НомерСвидетельстваПоНДС;
	
	Возврат ОбщиеПараметры;
	
КонецФункции // ПодготовитьДанныеДляПробитияЧека()

//В случае если устновлена настройка "Отражать зачет аванса в чеке в виде скидки" производит
//распределение суммы Аванса в виде скидки
Процедура РаспределитьЗачетАванса(ОбщиеПараметры);
	
	Если Константы.СпособОтраженияЗачетаАванса.Получить() = Перечисления.ВариантыОтраженияЗачетаАванса.ОтражатьСуммуАвансаКакСкидкуБезОплаты Тогда 
		
		Для ОбратныйИндекс = - ОбщиеПараметры.ТаблицаОплат.ВГраница() По 0 Цикл
			ВариантОплаты = ОбщиеПараметры.ТаблицаОплат[- ОбратныйИндекс];
			Если ВариантОплаты.ТипОплаты = Перечисления.ТипыОплатыККТ.Предоплата Тогда 
				СуммаАванса = ВариантОплаты.Сумма;
				Для Каждого СтрокаТЧТовары Из ОбщиеПараметры.ПозицииЧека Цикл
					
					СуммаСкидкиПоСтроке = ?(СтрокаТЧТовары.Сумма < СуммаАванса, СтрокаТЧТовары.Сумма, СуммаАванса);
					СуммаАванса			= СуммаАванса - СуммаСкидкиПоСтроке;
					
					Если Не СуммаСкидкиПоСтроке = 0 Тогда 
						СтрокаТЧТовары.Сумма = СтрокаТЧТовары.Сумма - СуммаСкидкиПоСтроке;
						Если Окр(СтрокаТЧТовары.Цена * СтрокаТЧТовары.Количество, 2, 1) = СтрокаТЧТовары.Сумма Тогда
							СтрокаТЧТовары.ЦенаСоСкидками = СтрокаТЧТовары.Цена;
						Иначе
							СтрокаТЧТовары.ЦенаСоСкидками = Окр(СтрокаТЧТовары.Сумма / СтрокаТЧТовары.Количество, 2, 1);
						КонецЕсли;
						СтрокаТЧТовары.СуммаНДС 	= Окр(СтрокаТЧТовары.Сумма * СтрокаТЧТовары.СтавкаНДС / (СтрокаТЧТовары.СтавкаНДС + 1), 2);
						СтрокаТЧТовары.СуммаСкидок 	= СтрокаТЧТовары.СуммаСкидок + СуммаСкидкиПоСтроке;
					КонецЕсли;
					
				КонецЦикла;
				
				ОбщиеПараметры.ТаблицаОплат.Удалить(- ОбратныйИндекс);
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Возможность пустой части товары
// 
// Параметры:
//  Объект - ДокументОбъект.ЧекККМ
// 
// Возвращаемое значение
//  Булево
//
Функция ВозможностьПустойТЧТовары(Объект) Экспорт
	
	ЕстьВозможность = Ложь;
	ДокументРасчета = Объект.ДокументРасчета;
	ОперацияСДенежнымиСредствами = Объект.ОперацияСДенежнымиСредствами;
	
	Если ОперацияСДенежнымиСредствами И НЕ ЗначениеЗаполнено(ДокументРасчета) Тогда
		ЕстьВозможность = Истина;
		// Аванс
	ИначеЕсли ОперацияСДенежнымиСредствами 
		И ЗначениеЗаполнено(ДокументРасчета)
		И ТипЗнч(ДокументРасчета) = Тип("ДокументСсылка.ЧекККМ")
		И ДокументРасчета.Товары.Количество() = 0 Тогда
		// Возврат аванса выданного чеком
		ЕстьВозможность = Истина;
	ИначеЕсли ОперацияСДенежнымиСредствами 
		И ЗначениеЗаполнено(ДокументРасчета)
		И ТипЗнч(ДокументРасчета) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		И НЕ ЕстьДокументыВПКО(ДокументРасчета) Тогда
		// Возврат аванса выданного ПКО
		ЕстьВозможность = Истина;
	КонецЕсли;
	
	Возврат ЕстьВозможность;
	
КонецФункции // ВозможностьПустойТЧТовары()

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Магазин)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Если Константы.ИспользоватьСервисДоставкиКлиентам.Получить() Тогда
		СервисДоставкиРТ.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция формирует текст запроса по серийным номерам.
// Возвращаемое значение: ТекстЗапроса - Строка.
Функция ТекстЗапросаТаблицаСерийныхНомеров()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	Товары.Количество КАК Количество,
	|	Товары.Цена КАК Цена,
	|	Товары.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозврат
	|ПОМЕСТИТЬ ТабТоварыВСЕ
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И НЕ &ОперацияСДенежнымиСредствами
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура КАК Номенклатура,
	|	ТабТовары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	ТабТовары.Количество КАК Количество,
	|	ТабТовары.Цена КАК Цена,
	|	ТабТовары.Склад КАК Склад,
	|	ТабТовары.ЭтоВозврат КАК ЭтоВозврат
	|ПОМЕСТИТЬ ТабТовары
	|ИЗ
	|	ТабТоварыВСЕ КАК ТабТовары
	|ГДЕ
	|	ТабТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СерийныеНомера.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	СерийныеНомера.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	1 КАК Количество
	|ПОМЕСТИТЬ ТабСерийныеНомера
	|ИЗ
	|	Документ.ЧекККМ.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Ссылка = &Ссылка
	|	И НЕ &ОперацияСДенежнымиСредствами
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабСерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	ТабТовары.Номенклатура КАК Номенклатура,
	|	ТабТовары.Склад КАК Отправитель,
	|	ТабТовары.ЭтоВозврат КАК ЭтоВозврат
	|ПОМЕСТИТЬ СертификатыСНомерами
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабСерийныеНомера КАК ТабСерийныеНомера
	|		ПО ТабТовары.КлючСвязиСерийныхНомеров = ТабСерийныеНомера.КлючСвязиСерийныхНомеров
	|ГДЕ
	|	ТабТовары.Номенклатура.ИспользоватьСерийныеНомера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧекККМПогашениеПодарочныхСертификатов.ПодарочныйСертификат КАК ПодарочныйСертификат,
	|	ЧекККМПогашениеПодарочныхСертификатов.СерийныйНомер КАК СерийныйНомер,
	|	ЧекККМПогашениеПодарочныхСертификатов.СуммаПогашенияСертификата КАК СуммаПогашенияСертификата,
	|	ЧекККМПогашениеПодарочныхСертификатов.ПодарочныйСертификат.ЧастичноеПогашение КАК ЧастичноеПогашение,
	|	ЧекККМПогашениеПодарочныхСертификатов.ПодарочныйСертификат.ПроизвольныйНоминал КАК ПроизвольныйНоминал,
	|	ЛОЖЬ КАК ЭтоВозврат
	|ПОМЕСТИТЬ ПогашениеПодарочныхСертификатов
	|ИЗ
	|	Документ.ЧекККМ.ПогашениеПодарочныхСертификатов КАК ЧекККМПогашениеПодарочныхСертификатов
	|ГДЕ
	|	ЧекККМПогашениеПодарочныхСертификатов.Ссылка = &Ссылка
	|	И НЕ &ОперацияСДенежнымиСредствами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СертификатыСНомерами.Номенклатура,
	|	СертификатыСНомерами.СерийныйНомер,
	|	0,
	|	СертификатыСНомерами.Номенклатура.ЧастичноеПогашение,
	|	СертификатыСНомерами.Номенклатура.ПроизвольныйНоминал,
	|	ИСТИНА
	|ИЗ
	|	СертификатыСНомерами КАК СертификатыСНомерами
	|ГДЕ
	|	СертификатыСНомерами.ЭтоВозврат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПодарочныйСертификат,
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПогашениеПодарочныхСертификатов.ПодарочныйСертификат КАК ПодарочныйСертификат,
	|	ПогашениеПодарочныхСертификатов.СерийныйНомер КАК СерийныйНомер,
	|	ВЫБОР
	|		КОГДА ПогашениеПодарочныхСертификатов.ЭтоВозврат
	|			ТОГДА ЕСТЬNULL(ПодарочныеСертификатыОстатки.СуммаОстаток, 0)
	|		ИНАЧЕ ПогашениеПодарочныхСертификатов.СуммаПогашенияСертификата
	|	КОНЕЦ КАК СуммаПогашенияСертификата,
	|	ПогашениеПодарочныхСертификатов.ЧастичноеПогашение КАК ЧастичноеПогашение,
	|	ПогашениеПодарочныхСертификатов.ПроизвольныйНоминал КАК ПроизвольныйНоминал,
	|	ПогашениеПодарочныхСертификатов.ЭтоВозврат КАК ЭтоВозврат,
	|	ЕСТЬNULL(ПодарочныеСертификатыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	|ПОМЕСТИТЬ ПогашениеПодарочныхСертификатовОстатки
	|ИЗ
	|	ПогашениеПодарочныхСертификатов КАК ПогашениеПодарочныхСертификатов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПодарочныеСертификаты.Остатки(
	|				&Период,
	|				(ПодарочныйСертификат, НомерСертификата) В
	|					(ВЫБРАТЬ
	|						ПогашениеПодарочныхСертификатов.ПодарочныйСертификат,
	|						ПогашениеПодарочныхСертификатов.СерийныйНомер
	|					ИЗ
	|						ПогашениеПодарочныхСертификатов)) КАК ПодарочныеСертификатыОстатки
	|		ПО ПогашениеПодарочныхСертификатов.ПодарочныйСертификат = ПодарочныеСертификатыОстатки.ПодарочныйСертификат
	|			И ПогашениеПодарочныхСертификатов.СерийныйНомер = ПодарочныеСертификатыОстатки.НомерСертификата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СертификатыСНомерами.СерийныйНомер КАК СерийныйНомер,
	|	СертификатыСНомерами.Отправитель КАК Отправитель,
	|	СертификатыСНомерами.Номенклатура КАК Номенклатура,
	|	&АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации,
	|	1 КАК Количество,
	|	&Период КАК Период,
	|	NULL КАК Получатель
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	СертификатыСНомерами КАК СертификатыСНомерами
	|ГДЕ
	|	НЕ СертификатыСНомерами.ЭтоВозврат
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	ТабТовары.Склад,
	|	ТабТовары.Номенклатура,
	|	&АналитикаХозяйственнойОперации,
	|	ТабТовары.Количество,
	|	&Период,
	|	NULL
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|ГДЕ
	|	НЕ ТабТовары.Номенклатура.ИспользоватьСерийныеНомера
	|	И НЕ ТабТовары.ЭтоВозврат
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПогашениеПодарочныхСертификатовОстатки.СерийныйНомер,
	|	NULL,
	|	ПогашениеПодарочныхСертификатовОстатки.ПодарочныйСертификат,
	|	ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов),
	|	1,
	|	&Период,
	|	&СкладПродажи
	|ИЗ
	|	ПогашениеПодарочныхСертификатовОстатки КАК ПогашениеПодарочныхСертификатовОстатки
	|ГДЕ
	|	(ПогашениеПодарочныхСертификатовОстатки.СуммаПогашенияСертификата = ПогашениеПодарочныхСертификатовОстатки.СуммаОстаток
	|			ИЛИ НЕ ПогашениеПодарочныхСертификатовОстатки.ЧастичноеПогашение)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	NULL,
	|	ТабТовары.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов),
	|	ТабТовары.Количество,
	|	&Период,
	|	&СкладПродажи
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|ГДЕ
	|	НЕ ТабТовары.Номенклатура.ИспользоватьСерийныеНомера
	|	И ТабТовары.ЭтоВозврат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвижений.СерийныйНомер КАК СерийныйНомер,
	|	ТаблицаДвижений.Отправитель КАК Отправитель,
	|	ТаблицаДвижений.Номенклатура КАК Номенклатура,
	|	ТаблицаДвижений.АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации,
	|	&Организация КАК Организация,
	|	СУММА(ТаблицаДвижений.Количество) КАК Количество,
	|	ТаблицаДвижений.Период КАК Период,
	|	ТаблицаДвижений.Получатель КАК Получатель
	|ИЗ
	|	ТаблицаДвижений КАК ТаблицаДвижений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДвижений.Период,
	|	ТаблицаДвижений.Получатель,
	|	ТаблицаДвижений.Отправитель,
	|	ТаблицаДвижений.Номенклатура,
	|	ТаблицаДвижений.АналитикаХозяйственнойОперации,
	|	ТаблицаДвижений.СерийныйНомер";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует текст запроса по бонусам.
// Возвращаемое значение: ТекстЗапроса - Строка.
Функция ТекстЗапросаБонусы()
	
	ТекстЗапроса = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	&ПериодБонусов КАК Период,
	|	&Расход КАК ВидДвижения,
	|	Оплата.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	&КартаБонусов КАК ДисконтнаяКарта,
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА -Оплата.КоличествоБонусовВСкидках ИНАЧЕ Оплата.КоличествоБонусовВСкидках КОНЕЦ КАК Начислено,
	|	0 КАК КСписанию,
	|	&ПустаяДата КАК ДатаПервоначальногоНачисления
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК Оплата
	|ГДЕ
	|	Оплата.Ссылка = &Ссылка
	|	И Оплата.ВидОплаты = ЗНАЧЕНИЕ(Справочник.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой)
	|	И Оплата.КоличествоБонусовВСкидках <> 0 И НЕ &ОперацияСДенежнымиСредствами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.ДатаНачисления КАК Период,
	|	&Приход КАК ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	&КартаБонусов КАК ДисконтнаяКарта,
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА -БонусныеБаллы.КоличествоБонусныхБаллов ИНАЧЕ БонусныеБаллы.КоличествоБонусныхБаллов КОНЕЦ КАК Начислено,
	|	0 КАК КСписанию,
	|	&ПустаяДата КАК ДатаПервоначальногоНачисления
	|ПОМЕСТИТЬ БонусныеБаллы
	|ИЗ
	|	Документ.ЧекККМ.БонусныеБаллыКНачислению КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.Ссылка = &Ссылка
	|	И БонусныеБаллы.ДатаНачисления <> &ПустаяДата И НЕ &ОперацияСДенежнымиСредствами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БонусныеБаллы.ДатаСписания,
	|	&Приход,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	&КартаБонусов,
	|	0,
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА -БонусныеБаллы.КоличествоБонусныхБаллов ИНАЧЕ БонусныеБаллы.КоличествоБонусныхБаллов КОНЕЦ,
	|	БонусныеБаллы.ДатаНачисления
	|ИЗ
	|	Документ.ЧекККМ.БонусныеБаллыКНачислению КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.Ссылка = &Ссылка
	|	И БонусныеБаллы.ДатаСписания <> &ПустаяДата И НЕ &ОперацияСДенежнымиСредствами
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.ВидДвижения КАК ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.ДисконтнаяКарта КАК ДисконтнаяКарта,
	|	СУММА(БонусныеБаллы.Начислено) КАК Начислено,
	|	СУММА(БонусныеБаллы.КСписанию) КАК КСписанию,
	|	БонусныеБаллы.ДатаПервоначальногоНачисления КАК ДатаПервоначальногоНачисления
	|ИЗ
	|	БонусныеБаллы КАК БонусныеБаллы
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.ДисконтнаяКарта,
	|	БонусныеБаллы.ВидДвижения,
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ДисконтнаяКарта.КартаДляНакоплений = ЗНАЧЕНИЕ(Справочник.ИнформационныеКарты.ПустаяСсылка)
	|			ТОГДА БонусныеБаллы.ДисконтнаяКарта
	|		ИНАЧЕ БонусныеБаллы.ДисконтнаяКарта.КартаДляНакоплений
	|	КОНЕЦ,
	|	БонусныеБаллы.ДатаПервоначальногоНачисления";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентами()
	
	ТекстЗапроса = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ВЫБОР
	|		КОГДА &ДокументРасчета = НЕОПРЕДЕЛЕНО
	|				ИЛИ ЧекККМОплата.ВидОплаты.ТипОплаты = &ВРассрочку
	|			ТОГДА &Ссылка
	|		ИНАЧЕ &ДокументРасчета
	|	КОНЕЦ КАК ДокументРасчета,
	|	&Расход КАК ВидДвижения,
	|	ЧекККМОплата.Сумма КАК Сумма,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Контрагент,
	|	&ЗаказПокупателя КАК ЗаказПокупателя,
	|	&Магазин КАК Магазин,
	|	ЧекККМОплата.ВидОплаты КАК ВидОплаты
	|ПОМЕСТИТЬ ТаблицаРасчетыСКлиентами
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	&ИспользоватьРасчетыСКлиентами
	|	И ЧекККМОплата.Ссылка = &Ссылка
	|	И НЕ &ОперацияСДенежнымиСредствами
	|	И НЕ &ЭтоВозврат
	|	И (ЧекККМОплата.ВидОплаты.ТипОплаты = &ЗачетАванса
	|			ИЛИ ЧекККМОплата.ВидОплаты.ТипОплаты = &ВРассрочку)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Ссылка,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРасчета) = ТИП(Документ.РегистрацияБезналичнойОплаты)
	|			ТОГДА &ДокументРасчета
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ,
	|	&Расход,
	|	ЧекККМОплата.Сумма,
	|	&Организация,
	|	&Контрагент,
	|	&ЗаказПокупателя,
	|	&Магазин,
	|	ЧекККМОплата.ВидОплаты
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	&ИспользоватьРасчетыСКлиентами
	|	И ЧекККМОплата.Ссылка = &Ссылка
	|	И НЕ &ОперацияСДенежнымиСредствами
	|	И НЕ &ЭтоВозврат
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты = &ЗачетАванса
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты = &ВРассрочку
	|	И НЕ ЧекККМОплата.ВидОплаты = &ВидОплатыНаличные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Ссылка,
	|	ВЫБОР
	|		КОГДА &ДокументРасчета = НЕОПРЕДЕЛЕНО ИЛИ НЕ &ОперацияСДенежнымиСредствами
	|			ТОГДА &Ссылка
	|		ИНАЧЕ &ДокументРасчета
	|	КОНЕЦ,
	|	&Приход,
	|	ЧекККМОплата.Сумма,
	|	&Организация,
	|	&Контрагент,
	|	&ЗаказПокупателя,
	|	&Магазин,
	|	ЧекККМОплата.ВидОплаты
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	&ИспользоватьРасчетыСКлиентами
	|	И ЧекККМОплата.Ссылка = &Ссылка
	|	И НЕ &ЭтоВозврат
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты = &ЗачетАванса
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты = &ВРассрочку
	|	И НЕ ЧекККМОплата.ВидОплаты = &ВидОплатыНаличные
	|	И НЕ ТИПЗНАЧЕНИЯ(&ДокументРасчета) = ТИП(Документ.РегистрацияБезналичнойОплаты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Ссылка,
	|	ВЫБОР
	|		КОГДА &ДокументРасчета = НЕОПРЕДЕЛЕНО
	|			ТОГДА &Ссылка
	|		ИНАЧЕ &ДокументРасчета
	|	КОНЕЦ,
	|	&Расход,
	|	ЧекККМОплата.Сумма,
	|	&Организация,
	|	&Контрагент,
	|	&ЗаказПокупателя,
	|	&Магазин,
	|	ЧекККМОплата.ВидОплаты
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	&ИспользоватьРасчетыСКлиентами
	|	И ЧекККМОплата.Ссылка = &Ссылка
	|	И &ЭтоВозврат
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты = &ЗачетАванса
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты = &ВРассрочку
	|	И НЕ ЧекККМОплата.ВидОплаты = &ВидОплатыНаличные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Ссылка,
	|	&Ссылка,
	|	&Приход,
	|	ЧекККМОплата.Сумма,
	|	&Организация,
	|	&Контрагент,
	|	&ЗаказПокупателя,
	|	&Магазин,
	|	ЧекККМОплата.ВидОплаты
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	&ИспользоватьРасчетыСКлиентами
	|	И ЧекККМОплата.Ссылка = &Ссылка
	|	И НЕ &ОперацияСДенежнымиСредствами
	|	И &ЭтоВозврат
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты = &ЗачетАванса
	|	И НЕ ЧекККМОплата.ВидОплаты = &ВидОплатыНаличные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасчетыСКлиентами.Период КАК Период,
	|	ТаблицаРасчетыСКлиентами.Регистратор КАК Регистратор,
	|	ТаблицаРасчетыСКлиентами.ДокументРасчета КАК ДокументРасчета,
	|	ТаблицаРасчетыСКлиентами.ВидДвижения КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ТаблицаРасчетыСКлиентами.ВидОплаты = &ВидОплатыНаличные
	|			ТОГДА &ВыручкаНаличными
	|		ИНАЧЕ ТаблицаРасчетыСКлиентами.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ТаблицаРасчетыСКлиентами.Организация КАК Организация,
	|	ТаблицаРасчетыСКлиентами.Контрагент КАК Контрагент,
	|	ТаблицаРасчетыСКлиентами.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаРасчетыСКлиентами.Магазин КАК Магазин
	|ИЗ
	|	ТаблицаРасчетыСКлиентами КАК ТаблицаРасчетыСКлиентами";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаАкцизныеМарки()
	
	ТекстЗапроса = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ЭтоВозврат
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ 							   КАК ВидДвижения,
	|	&Период 						   КАК Период,
	|	ТаблицаАкцизныеМарки.Ссылка        КАК Регистратор,
	|	&ОрганизацияЕГАИС                  КАК ОрганизацияЕГАИС,
	|	ТаблицаАкцизныеМарки.Справка2      КАК Справка2,
	|	Справки2ЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаАкцизныеМарки.АкцизнаяМарка) КАК Количество,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаАкцизныеМарки.АкцизнаяМарка) КАК СвободныйОстаток
	|ИЗ
	|	Документ.ЧекККМ.АкцизныеМарки КАК ТаблицаАкцизныеМарки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|			ПО Справки2ЕГАИС.Ссылка = ТаблицаАкцизныеМарки.Справка2
	|ГДЕ
	|	ТаблицаАкцизныеМарки.Ссылка = &Ссылка
	|	И ТаблицаАкцизныеМарки.Справка2 <> ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаАкцизныеМарки.Ссылка,
	|	ТаблицаАкцизныеМарки.Справка2,
	|	Справки2ЕГАИС.АлкогольнаяПродукция
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДополнитьТоварамиПараметрыПриПробитииЧека(РеквизитыДокумента, ОбщиеПараметры, ДанныеДляПробитияЧекаККМ, ТаблицаСоответствияТоваровСекциям, ПризнакСпособаРасчета)
	
	ИспользуетсяКомиссионнаяТорговля = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссионнуюТорговлю");
	ИспользуетсяУчетИмпортныхТоваров = ПолучитьФункциональнуюОпцию("ИспользоватьУчетИмпортныхТоваров");
	ИспользоватьАгентскиеПлатежиИРазделениеВыручки =
		ПолучитьФункциональнуюОпцию("ИспользоватьАгентскиеПлатежиИРазделениеВыручки");
	ИспользоватьУчетТоваровФСС = Ложь; //ПолучитьФункциональнуюОпцию("ИспользоватьУчетТоваровФСС");
	
	ТаблицаТоваровЧека  = ДанныеДляПробитияЧекаККМ.ТаблицаТоваровЧека;
	ТаблицаТоваровГосИС = ДанныеДляПробитияЧекаККМ.ТаблицаТоваровГосИС;
	
	//ИнтеграцияИСМПТК_РозничноеВыбытие
	ТаблицаМарки = Новый ТаблицаЗначений();
	Если Константы.ВестиУчетМаркируемойПродукцииИСМПТК.Получить() Тогда
		ТаблицаМарки = ОбщиеПараметры.ДокументОснование.КодыМаркировкиИСМПТК.Выгрузить();
	КонецЕсли;
	//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
	
	ЭтоАванс = ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс
		ИЛИ ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная
		ИЛИ ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаЧастичная;
	
	Если ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс Тогда
		
		СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека();
		
		НаименованиеПозиции = СтрШаблон(НСтр("ru = 'Оплата от: %1'"), СокрЛП(РеквизитыДокумента.Контрагент));
		Если ОбщиеПараметры.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств Тогда
			НаименованиеПозиции = СтрШаблон(НСтр("ru = 'Возврат от: %1'"), СокрЛП(РеквизитыДокумента.Контрагент));
		КонецЕсли;
		
		СтрокаПозицииЧека.Наименование = НаименованиеПозиции;
		СтрокаПозицииЧека.Количество = 1;
		СтрокаПозицииЧека.Цена = РеквизитыДокумента.СуммаДокумента;
		СтрокаПозицииЧека.ЦенаСоСкидками = РеквизитыДокумента.СуммаДокумента;
		СтрокаПозицииЧека.Сумма = РеквизитыДокумента.СуммаДокумента;
		СтрокаПозицииЧека.НомерСекции = 2;
		
		ОсвобожденОтНДС = Ложь;
		СНООрганизации = РегистрыСведений.ПрименениеСистемНалогообложения.СистемаНалогообложенияОрганизации(РеквизитыДокумента.Организация);
		Если СНООрганизации <> Неопределено Тогда
			ОсвобожденОтНДС = СНООрганизации.ОсвобожденОтНДС;
		КонецЕсли;
		
		Если ОбщиеПараметры.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН 
			И НЕ ОсвобожденОтНДС Тогда
			
			СтрокаПозицииЧека.СтавкаНДС = 12;
			
		Иначе
			СтрокаПозицииЧека.СтавкаНДС = Неопределено;
		КонецЕсли;
		
		СтрокаПозицииЧека.ОсвобожденныйОборотНДС = Ложь;
		СтрокаПозицииЧека.ПризнакСпособаРасчета = ПризнакСпособаРасчета;
		СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата;
		
		ЕдиницаИзмеренияПоУмолчанию = Новый Структура("КодЕдиницыИзмерения, НаименованиеЕдиницыИзмерения", 796, "шт");
		СтрокаПозицииЧека.КодЕдиницыИзмерения    = ЕдиницаИзмеренияПоУмолчанию.КодЕдиницыИзмерения;
		СтрокаПозицииЧека.НаименованиеЕдиницыИзмерения = ЕдиницаИзмеренияПоУмолчанию.НаименованиеЕдиницыИзмерения;
		
		ПодключаемоеОборудованиеРТ.ЗаполнитьПараметрыПлатежногоДоговораВСтроке(ОбщиеПараметры, СтрокаПозицииЧека);
		
		ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
		
	Иначе
		
		КэшСНО = Новый Соответствие;
		Для Каждого СтрокаТЧ Из ТаблицаТоваровЧека Цикл
			
			СтрокаТаблицыТоваров = Новый СписокЗначений();
			НаименованиеТовара = Строка(СтрокаТЧ.Номенклатура) + ?(ЗначениеЗаполнено(СтрокаТЧ.Характеристика), " (" + Строка(СтрокаТЧ.Характеристика) + ")", "") 
							   + ?(ЗначениеЗаполнено(СтрокаТЧ.НаименованиеЕдиницыИзмерения), " (" + Строка(СтрокаТЧ.НаименованиеЕдиницыИзмерения) + ")", "");
			
			НомерСекции = ТаблицаСоответствияТоваровСекциям.Получить(СтрокаТЧ.НомерСтроки);
			
			МассивСтрокГосИС = Новый Массив;
			Если СтрокаТЧ.ПродукцияГИСМ Тогда
				
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
				СтруктураПоиска.Вставить("Характеристика", СтрокаТЧ.Характеристика);
				
				МассивСтрокГосИС = ТаблицаТоваровГосИС.НайтиСтроки(СтруктураПоиска);
			Иначе
				МассивСтрокГосИС = ТаблицаТоваровГосИС.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаТЧ.КлючСвязи));
			КонецЕсли;
			
			СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека();
			СтрокаПозицииЧека.Наименование = НаименованиеТовара;
			СтрокаПозицииЧека.Количество = ?(СтрокаТЧ.КоличествоУпаковок = 0, 1, СтрокаТЧ.КоличествоУпаковок);
			СтрокаПозицииЧека.Цена = ?(СтрокаТЧ.Цена = 0, СтрокаТЧ.Сумма, СтрокаТЧ.Цена);
			СтрокаПозицииЧека.Сумма = СтрокаТЧ.Сумма;
			СтрокаПозицииЧека.СуммаСкидок = СтрокаТЧ.СуммаСкидок;
			
			СтрокаПозицииЧека.Вставить("РезультатРаспределенияШтрихкодов");
			
			Если Окр(СтрокаПозицииЧека.Цена * СтрокаПозицииЧека.Количество, 2, 1) = СтрокаПозицииЧека.Сумма Тогда
				СтрокаПозицииЧека.ЦенаСоСкидками = СтрокаПозицииЧека.Цена;
			Иначе
				СтрокаПозицииЧека.ЦенаСоСкидками = Окр(СтрокаПозицииЧека.Сумма / СтрокаПозицииЧека.Количество, 2, 1);
			КонецЕсли;
			СтрокаПозицииЧека.НомерСекции = НомерСекции;
			
			СтрокаПозицииЧека.СуммаНДС = СтрокаТЧ.СуммаНДС;
			СтрокаПозицииЧека.КодЕдиницыИзмерения = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаТЧ.КодЕдиницыИзмерения);
			СтрокаПозицииЧека.НаименованиеЕдиницыИзмерения = СтрокаТЧ.НаименованиеЕдиницыИзмерения;
			
			Если ИспользоватьАгентскиеПлатежиИРазделениеВыручки Тогда
				ПодключаемоеОборудованиеРТ.ЗаполнитьПараметрыПлатежногоДоговораВСтроке(
				ОбщиеПараметры,
				СтрокаПозицииЧека,
				СтрокаТЧ);
			КонецЕсли;
			
			НуженПересчетСуммыНДС = Не ОбщиеПараметры.РабочееМестоКассира;
			ДатаКорректировкиСтавкиНДС = РеквизитыДокумента.Дата;
			Если РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда 
				ЧекПродажи = РеквизитыДокумента.ЧекККМПродажа;
				Если ЗначениеЗаполнено(ЧекПродажи) И ЧекПродажи.Дата < УчетНДС.ДатаПереходногоПериода() Тогда
					//Возврат товара за 2018 год.
					ДатаКорректировкиСтавкиНДС = ЧекПродажи.Дата;
					НуженПересчетСуммыНДС = Ложь;
				КонецЕсли;
			КонецЕсли;
			СтрокаТЧ.СтавкаНДС = УчетНДС.СкорректироватьСтавкуНДС(СтрокаТЧ.СтавкаНДС, ДатаКорректировкиСтавкиНДС);
			
			Если СтрокаТЧ.ЭтоПодарочныйСертификат Тогда
				СтрокаПозицииЧека.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс;
				СтрокаПозицииЧека.СтавкаНДС = ПодключаемоеОборудованиеРТ.СтавкаНДСВФорматеБПО(СтрокаТЧ.СтавкаНДС, Истина);
			Иначе
				СтрокаПозицииЧека.ПризнакСпособаРасчета = ПризнакСпособаРасчета;
				СтрокаПозицииЧека.СтавкаНДС = ПодключаемоеОборудованиеРТ.СтавкаНДСВФорматеБПО(СтрокаТЧ.СтавкаНДС, ЭтоАванс);
			КонецЕсли;
			СтрокаПозицииЧека.ОсвобожденныйОборотНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ.СтавкаНДС, "ДляОсвобожденногоОборота");
			
			Если ИспользуетсяУчетИмпортныхТоваров Тогда
				СтрокаПозицииЧека.КодСтраныПроисхожденияТовара = СтрокаТЧ.КодСтраныПроисхождения;
				СтрокаПозицииЧека.НомерТаможеннойДекларации = СтрокаТЧ.РегистрационныйНомерДекларации;
			КонецЕсли;
			
			СтрокаПозицииЧека.КодЕдиницыИзмерения = СтрокаТЧ.ЕдиницаИзмерения.Код;
			СтрокаПозицииЧека.ЕдиницаИзмерения = СтрокаТЧ.ЕдиницаИзмеренияПредметаРасчета;
			СтрокаПозицииЧека.НомерСтрокиТовара = СтрокаТЧ.НомерСтроки;
			
			СтрокаПозицииЧека.КодВидаНоменклатурнойКлассификации = СтрокаТЧ.КодВидаНоменклатурнойКлассификации;
			
			Если ИспользоватьУчетТоваровФСС И Не ПустаяСтрока(СтрокаТЧ.КодТовараТРУ) Тогда
				СтрокаПозицииЧека.NTIN = СтрокаТЧ.КодТовараТРУ; // Для тега 1162 и 1163
				Если ОбщиеПараметры.ТипПлатежнойСистемы = Перечисления.ТипыПлатежнойСистемыККТ.СертификатНСПК
					И ОбщиеПараметры.Электронно Тогда
					СтрокаПозицииЧека.Наименование = СтрокаПозицииЧека.Наименование + Символы.ПС +
												   НСтр("ru='(Корзина №: '") + ОбщиеПараметры.ИдентификаторОплатыПлатежнойСистемы + НСтр("ru=')'");
				КонецЕсли;
			КонецЕсли;
			
			ПродолжатьЗаполнениеМаркировки = Ложь; //ИнтеграцияИСМПТК_РозничноеВыбытие
			
			Если РеквизитыДокумента.ОперацияСДенежнымиСредствами Тогда
				СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата;
			Иначе
				СтрокаПозицииЧека.ПризнакПредметаРасчета = СтрокаТЧ.ПризнакПредметаРасчета;
				
				//ИнтеграцияИСМПТК_РозничноеВыбытие
				Если Константы.ВестиУчетМаркируемойПродукцииИСМПТК.Получить() 
					И Не ТаблицаМарки.Количество() = 0 Тогда
					//Дополнять структуру документа для пробития данными по маркировке имеет смысл только когда включен учет нужной ТГ и указан корректный код маркировки в строке товаров.
					//В остальных случаях игнорируем наличие или отсутствие кода маркировки.
					ПродолжатьЗаполнениеМаркировки = Истина;
					//1. Проверяем, что учет ТГ включен
					Если СтрокаТЧ.ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная")
						И Константы.ВестиУчетМаркируемойОбувиИСМПТК.Получить() Тогда 
						СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ТипМаркировки = Перечисления.ТипыМаркировкиККТ.ОбувныеТовары;
						
					ИначеЕсли СтрокаТЧ.ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты")
						И Константы.ВестиУчетМаркируемыхЛекарствИСМПТК.Получить() Тогда 
						СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ТипМаркировки = Перечисления.ТипыМаркировкиККТ.ЛекарственныеПрепараты;
						
					ИначеЕсли (СтрокаТЧ.ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Табачная")
						ИЛИ СтрокаТЧ.ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.АльтернативныйТабак"))
						И Константы.ВестиУчетМаркируемогоТабакаИСМПТК.Получить() Тогда 
						СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ТипМаркировки = Перечисления.ТипыМаркировкиККТ.ТабачнаяПродукция;
						
					Иначе
						//Если учет ТГ не включен, данные маркировки не обрабатываем
						ПродолжатьЗаполнениеМаркировки = Ложь;
					КонецЕсли;
					
					//Возможно, для номеклатуры не включена особенность учета, но код маркировки указан корректно. 
					//Поэтому проверяем наличие КМ по товару
					ЕстьСвязанныеКоды = Не ТаблицаМарки.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаТЧ.КлючСвязи)).Количество() = 0;
					Если Не ПродолжатьЗаполнениеМаркировки И ЕстьСвязанныеКоды Тогда 
						ПродолжатьЗаполнениеМаркировки = Истина;
						СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ТипМаркировки = Перечисления.ТипыМаркировкиККТ.ПустаяСсылка();
					КонецЕсли;
				КонецЕсли; 	
				//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
				
				Если СтрокаТЧ.АлкогольнаяПродукция И СтрокаПозицииЧека.Количество > 1 И Не СтрокаТЧ.ПродаетсяВРозлив Тогда
					ДанныеМаркировки = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьШтриховойКодТовара(СтрокаТЧ.Штрихкод);
					Если ДанныеМаркировки.Разобран Тогда
						СтрокаПозицииЧека.Штрихкод = ДанныеМаркировки.EAN;
					КонецЕсли;
				Иначе
					
					Штрихкод = СтрокаТЧ.Штрихкод;
					
					Если РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ПроверитьКорректностьGTIN(Штрихкод) Тогда
						СтрокаПозицииЧека.Штрихкод = Штрихкод;
					Иначе
						СтрокаПозицииЧека.Штрихкод = "";
					КонецЕсли;
					
				КонецЕсли;
				
				//ЗаполнитьСтрокуПоДаннымРазбораШтрихкода(СтрокаПозицииЧека, СтрокаТЧ); //не маркировка КЗ
				
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТЧ, "РезультатРаспределенияШтрихкодов")
					И ЗначениеЗаполнено(СтрокаТЧ.РезультатРаспределенияШтрихкодов) Тогда
					СтрокаПозицииЧека.РезультатРаспределенияШтрихкодов = СтрокаТЧ.РезультатРаспределенияШтрихкодов;
					
					ПолныйКодМаркировки = СтрокаТЧ.РезультатРаспределенияШтрихкодов.ПолныйКодМаркировки;
					Если ЗначениеЗаполнено(ПолныйКодМаркировки) Тогда
						Если ОбщегоНазначенияРМК.ПризнакИспользованияКМПоНоменклатуре(СтрокаТЧ.Номенклатура.ВидНоменклатуры) Тогда
							СтрокаПозицииЧека.ШтрихкодBase64 = ПолныйКодМаркировки;
							СтрокаПозицииЧека.КонтрольнаяМарка = ПолныйКодМаркировки;
						Иначе
							Если СтрокаТЧ.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Пиво Тогда
								СтрокаПозицииЧека.КонтрольнаяМарка = ПолныйКодМаркировки;
							Иначе
								СтрокаПозицииЧека.Штрихкод = СтрокаТЧ.Штрихкод;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Если СтрокаТЧ.РезультатРаспределенияШтрихкодов.ЧастичноеВыбытие
						И Не СтрокаТЧ.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Пиво Тогда
						
						СтрокаПозицииЧека.ДробноеКоличество.Числитель   = СтрокаТЧ.РезультатРаспределенияШтрихкодов.Количество;
						СтрокаПозицииЧека.ДробноеКоличество.Знаменатель = СтрокаТЧ.РезультатРаспределенияШтрихкодов.ЕмкостьПотребительскойУпаковки;
						
						Если СтрокаТЧ.Количество > 1 Тогда
							
							Упаковка = СтрокаТЧ.Упаковка;
							Если НЕ ЗначениеЗаполнено(Упаковка) Тогда
								Упаковка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ.Номенклатура, "ЕдиницаИзмерения");
							КонецЕсли;
							
							УпаковкаНаименование = "ед";
							Если ЗначениеЗаполнено(Упаковка) Тогда
								УпаковкаНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Упаковка, "Наименование");
							КонецЕсли;
							УпаковкаНаименование = УпаковкаНаименование + ".";
							
							Валюта = ОбщегоНазначенияРТКлиентСервер.Валюта();
							
							ОписаниеЧастичногоВыбытия = СтрШаблон(
									НСтр("ru = ' (%1 %2, цена: %3 %4/%5)'"),
									СтрокаТЧ.Количество,
									УпаковкаНаименование,
									СтрокаТЧ.Цена,
									Валюта,
									УпаковкаНаименование);
							
							СтрокаПозицииЧека.Наименование = СтрокаПозицииЧека.Наименование
									+ ОписаниеЧастичногоВыбытия;
						КонецЕсли;
						
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаТЧ.РезультатРаспределенияШтрихкодов.РазрешительныйРежимИдентификаторЗапросаГИСМТ) Тогда
						СтрокаПозицииЧека.ЗапросПроверкиКода.ИдентификаторЗапроса
								= СтрокаТЧ.РезультатРаспределенияШтрихкодов.РазрешительныйРежимИдентификаторЗапросаГИСМТ;
						СтрокаПозицииЧека.ЗапросПроверкиКода.ВременнаяМетка
								= СтрокаТЧ.РезультатРаспределенияШтрихкодов.РазрешительныйРежимДатаЗапросаГИСМТ;
					КонецЕсли;
					
					ПризнакиПодакцизногоТовара = Новый Массив;
					ПризнакиПодакцизногоТовара.Добавить(Перечисления.ПризнакиПредметаРасчета.ПодакцизныйТовар);
					ПризнакиПодакцизногоТовара.Добавить(Перечисления.ПризнакиПредметаРасчета.ПодакцизныйТоварМаркируемыйСИНеИмеющийКМ);
					ПризнакиПодакцизногоТовара.Добавить(Перечисления.ПризнакиПредметаРасчета.ПодакцизныйТоварМаркируемыйСИИмеющийКМ);
					
					Если ЗначениеЗаполнено(РеквизитыДокумента.Контрагент)
						И ПризнакиПодакцизногоТовара.Найти(СтрокаПозицииЧека.ПризнакПредметаРасчета) <> Неопределено Тогда
						ТипКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыДокумента.Контрагент, "ЮрФизЛицо");
						
						Если ТипКонтрагента = Перечисления.ЮрФизЛицо.ЮрЛицо
							ИЛИ ТипКонтрагента = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
							СтрокаПозицииЧека.СуммаАкциза = 0;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			НеобходимоРазбитьСтроку = Ложь; //СтрокаТЧ.ПродукцияГИСМ;
			
			Если ИспользуетсяКомиссионнаяТорговля Тогда
				Если СтрокаТЧ.Номенклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга Тогда
					Если ЗначениеЗаполнено(СтрокаТЧ.Договор) Тогда
						Если СтрокаТЧ.Договор.ПризнакАгента = Перечисления.ПризнакиАгента.Комиссионер Тогда
							СтрокаПозицииЧека.ПризнакАгентаПоПредметуРасчета = Перечисления.ПризнакиАгента.Комиссионер;
							СтрокаПозицииЧека.ДанныеПоставщика.ИНН = СтрокаТЧ.ИННПоставщикаУслуг;
							СтрокаПозицииЧека.ДанныеПоставщика.Телефон = СтрокаТЧ.ТелефонПоставщика;
							СтрокаПозицииЧека.ДанныеПоставщика.Наименование = СокрЛП(СтрокаТЧ.Поставщик);
							
							УчетНДСУКомитента = ПодключаемоеОборудованиеРТ.КэшируемыеСНО(СтрокаТЧ.Договор, КэшСНО);
							ПодключаемоеОборудованиеРТ.ПерезаполнитьНДСКомитента(
									СтрокаПозицииЧека,
									СтрокаТЧ.Номенклатура,
									УчетНДСУКомитента,
									ЭтоАванс);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаПозицииЧека.Сумма =
			?(РеквизитыДокумента.ЦенаВключаетНДС,
			СтрокаПозицииЧека.Сумма,
			СтрокаПозицииЧека.Сумма + СтрокаПозицииЧека.СуммаНДС);
			
			Если НуженПересчетСуммыНДС Тогда
				Если СтрокаТЧ.НДСПоСтрочно Или СтрокаТЧ.ПродукцияГИСМ Тогда
					СтрокаПозицииЧека.СуммаНДС =
							ОбработкаТабличнойЧастиТоварыКлиентСервер.СуммаНДСПоСтрочно(
									СтрокаПозицииЧека.Сумма,
									СтрокаПозицииЧека.Количество,
									СтрокаПозицииЧека.СтавкаНДС);
				Иначе
					СтрокаПозицииЧека.СуммаНДС =
							Окр(ОбработкаТабличнойЧастиТоварыКлиентСервер.СуммаНДС(СтрокаПозицииЧека.Сумма, СтрокаПозицииЧека.СтавкаНДС), 2);
				КонецЕсли;
			КонецЕсли;
			
			//ИнтеграцияИСМПТК_РозничноеВыбытие	
			Если ПродолжатьЗаполнениеМаркировки Тогда 
				
				МассивСтрокКМ = ТаблицаМарки.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаТЧ.КлючСвязи));
				РазвернутыеСтроки = РазвернутьСтрокуПозицииЧекаИСМПТК(СтрокаПозицииЧека, МассивСтрокКМ);
				
				Для Каждого РазвернутаяСтрока Из РазвернутыеСтроки Цикл
					ОбщиеПараметры.ПозицииЧека.Добавить(РазвернутаяСтрока);
				КонецЦикла;
			Иначе
				
				Если НеобходимоРазбитьСтроку Тогда
					// Распределение средствами ГОСИС не выполнено.
					РазвернутыеСтроки = РазвернутьСтрокуПозицииЧека(СтрокаПозицииЧека, МассивСтрокГосИС);
					
					Для Каждого РазвернутаяСтрока Из РазвернутыеСтроки Цикл
						
						Если ИспользоватьУчетТоваровФСС И Не ПустаяСтрока(РазвернутаяСтрока.NTIN) Тогда
							СтрокаТекста = СтрШаблон(НСтр("ru = 'ТРУ (КОД): %1'"), РазвернутаяСтрока.NTIN);
							ТекстоваяСтрокаЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека(СтрокаТекста);
							ОбщиеПараметры.ПозицииЧека.Добавить(ТекстоваяСтрокаЧека);
						КонецЕсли;
						ОбщиеПараметры.ПозицииЧека.Добавить(РазвернутаяСтрока);
					КонецЦикла;
				Иначе
					
					Если ИспользоватьУчетТоваровФСС И Не ПустаяСтрока(СтрокаПозицииЧека.NTIN) Тогда
						СтрокаТекста = СтрШаблон(НСтр("ru = 'ТРУ (КОД): %1'"), СтрокаПозицииЧека.NTIN);
						ТекстоваяСтрокаЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека(СтрокаТекста);
						ОбщиеПараметры.ПозицииЧека.Добавить(ТекстоваяСтрокаЧека);
					КонецЕсли;
					ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
				КонецЕсли; 
			КонецЕсли;
			//Конец ИнтеграцияИСМПТК_РозничноеВыбытие
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуПоДаннымРазбораШтрихкода(СтрокаПозицииЧека, СтрокаДляДобавленияВЧек)
	
	Если НЕ (СтрокаДляДобавленияВЧек.ПродукцияИСМП И ЗначениеЗаполнено(СтрокаПозицииЧека.Штрихкод)) Тогда
		Возврат;
	КонецЕсли;
	
	ТипМаркировкиККТ = ИнтеграцияИСКлиентСервер.ТипМаркировкиККТПоВидуПродукции(СтрокаДляДобавленияВЧек.ВидПродукцииИС);
	Если Не ЗначениеЗаполнено(ТипМаркировкиККТ) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(СтрокаПозицииЧека.Штрихкод, СтрокаДляДобавленияВЧек.ВидПродукцииИС);
	Если ДанныеРазбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ТипМаркировки                          = ТипМаркировкиККТ;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ГлобальныйИдентификаторТорговойЕдиницы = ДанныеРазбора.СоставКодаМаркировки.GTIN;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.СерийныйНомер                          = ДанныеРазбора.СоставКодаМаркировки.СерийныйНомер;
	
КонецПроцедуры

Процедура ЗаполнитьОплатуПриПробитииЧекаККМ(ДокументОбъект, ОбщиеПараметры)
	
	// Подготовка таблицы оплат
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЧекККМОплата.Сумма,
	|	ВЫРАЗИТЬ(ЧекККМОплата.ВидОплаты КАК Справочник.ВидыОплатЧекаККМ) КАК ВидОплаты
	|ПОМЕСТИТЬ ТаблицаВЗапросе
	|ИЗ
	|	&Оплата КАК ЧекККМОплата
	|ГДЕ
	|	НЕ ЧекККМОплата.ВидОплаты = ЗНАЧЕНИЕ(Справочник.ВидыОплатЧекаККМ.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВЗапросе.Сумма,
	|	ТаблицаВЗапросе.ВидОплаты.ТипОплаты КАК ТипОплаты
	|ПОМЕСТИТЬ ТаблицаСТипамиОплат
	|ИЗ
	|	ТаблицаВЗапросе КАК ТаблицаВЗапросе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТаблицаСТипамиОплат.Сумма) КАК Сумма,
	|	ТаблицаСТипамиОплат.ТипОплаты
	|ИЗ
	|	ТаблицаСТипамиОплат КАК ТаблицаСТипамиОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСТипамиОплат.ТипОплаты
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаСТипамиОплат.Сумма) > 0";
	
	Запрос.УстановитьПараметр("Оплата", ДокументОбъект.Оплата.Выгрузить());
	
	Результат = Запрос.Выполнить();
	ТаблицаПоТипамОплат = Результат.Выгрузить();
	
	// Наличные
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.Наличные, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Наличные);
		СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Платежная карта
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Электронно);
		СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Мобильный платеж
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.МобильныйПлатеж, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.МобильныйПлатеж);
		СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	СуммаПостоплата = 0;
	
	// Платежная система
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ПлатежнаяСистема, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Электронно);
		СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Банковский платеж
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.БанковскийПлатеж, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Электронно);
		СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Банковский кредит
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.БанковскийКредит, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СуммаПостоплата = СуммаПостоплата + СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	// В рассрочку
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ВРассрочку, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СуммаПостоплата = СуммаПостоплата + СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	Если СуммаПостоплата > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Постоплата);
		СтрокаОплаты.Вставить("Сумма", СуммаПостоплата);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли; 
	
	ЗачетАванса = 0;
	
	// Подарочный сертификат
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ПодарочныйСертификат, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		ЗачетАванса = СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	// Зачет аванс
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ЗачетАванса, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		ЗачетАванса = ЗачетАванса + СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	Если ЗачетАванса > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Предоплата);
		СтрокаОплаты.Вставить("Сумма", ЗачетАванса);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Встречное предоставление
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ВстречноеПредоставление, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.ВстречноеПредоставление);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Встречное предоставление'"));
		СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьОплатуПриПробитииЧекаККМ()

Процедура ЗаполнитьОплатуПриПробитииЧекаККМНаККТ(ДокументОбъект, ОбщиеПараметры)
	
	// Подготовка таблицы оплат
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЧекККМОплата.Сумма КАК Сумма,
	|	ЧекККМОплата.СсылочныйНомер КАК СсылочныйНомер,
	|	ЧекККМОплата.СуммаБонусовВСкидках КАК СуммаБонусов,
	|	ВЫРАЗИТЬ(ЧекККМОплата.ВидОплаты КАК Справочник.ВидыОплатЧекаККМ) КАК ВидОплаты
	|ПОМЕСТИТЬ ТаблицаВЗапросе
	|ИЗ
	|	&Оплата КАК ЧекККМОплата
	|ГДЕ
	|	НЕ ЧекККМОплата.ВидОплаты = ЗНАЧЕНИЕ(Справочник.ВидыОплатЧекаККМ.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВЗапросе.Сумма КАК Сумма,
	|	ТаблицаВЗапросе.СуммаБонусов КАК СуммаБонусов,
	|	ТаблицаВЗапросе.ВидОплаты.ТипОплаты КАК ТипОплаты,
	|	ВЫБОР
	|		КОГДА СоответствиеНастроекИнтеграции.Интеграция ЕСТЬ NULL
	|			ТОГДА ТаблицаВЗапросе.ВидОплаты.ПлатежнаяСистема
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыПлатежнойСистемыККТ.СистемаБыстрыхПлатежей)
	|	КОНЕЦ КАК ПлатежнаяСистема,
	|	ТаблицаВЗапросе.СсылочныйНомер КАК ИдентификаторОплаты
	|ПОМЕСТИТЬ ТаблицаСТипамиОплат
	|ИЗ
	|	ТаблицаВЗапросе КАК ТаблицаВЗапросе
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНастроекИнтеграцииРМК КАК СоответствиеНастроекИнтеграции
	|		ПО (СоответствиеНастроекИнтеграции.Организация = &Организация)
	|			И (СоответствиеНастроекИнтеграции.ТорговыйОбъект = &Магазин)
	|			И (СоответствиеНастроекИнтеграции.СпособОплаты = ТаблицаВЗапросе.ВидОплаты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТаблицаСТипамиОплат.Сумма) КАК Сумма,
	|	СУММА(ТаблицаСТипамиОплат.СуммаБонусов) КАК СуммаБонусов,
	|	ТаблицаСТипамиОплат.ПлатежнаяСистема КАК ПлатежнаяСистема,
	|	ТаблицаСТипамиОплат.ТипОплаты КАК ТипОплаты
	|ИЗ
	|	ТаблицаСТипамиОплат КАК ТаблицаСТипамиОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСТипамиОплат.ТипОплаты,
	|	ТаблицаСТипамиОплат.ПлатежнаяСистема
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаСТипамиОплат.Сумма) >= 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСТипамиОплат.ПлатежнаяСистема КАК ПлатежнаяСистема,
	|	ТаблицаСТипамиОплат.ИдентификаторОплаты КАК ИдентификаторОплаты,
	|	ТаблицаСТипамиОплат.СуммаБонусов КАК СуммаБонусов,
	|	ТаблицаСТипамиОплат.Сумма КАК Сумма
	|ИЗ
	|	ТаблицаСТипамиОплат КАК ТаблицаСТипамиОплат
	|ГДЕ
	|	ТаблицаСТипамиОплат.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.ПлатежнаяСистема)
	|	И ТаблицаСТипамиОплат.ИдентификаторОплаты <> """"";
	
	Запрос.УстановитьПараметр("Оплата", ДокументОбъект.Оплата.Выгрузить());
	Запрос.УстановитьПараметр("Магазин", ДокументОбъект.Магазин);
	Запрос.УстановитьПараметр("Организация", ДокументОбъект.Организация);
	
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаПоТипамОплат = Результат[Результат.ВГраница()-1].Выгрузить();
	ТаблицаДанныеQRКода = Результат[Результат.ВГраница()].Выгрузить();
	
	//Бонусы
	Если ДокументОбъект.СуммаДокумента = 0 Тогда
		СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.Бонусы, "ТипОплаты");
		Если НЕ СтрокаТаблицы = Неопределено Тогда
			СтрокаОплаты = Новый Структура();
			СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.ВстречноеПредоставление);
			СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Встречное предоставление'"));
			СтрокаОплаты.Вставить("Сумма", 0);
			ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
		КонецЕсли;
	КонецЕсли;
	
	// Наличные
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.Наличные, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		Если СтрокаТаблицы.Сумма > 0 Тогда
			СтрокаОплаты = Новый Структура();
			СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Наличные);
			СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Наличные'"));
			СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
			ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
		КонецЕсли;
	КонецЕсли;
	
	СуммаЭлектронно = 0;
	
	// Платежная карта
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СуммаЭлектронно = СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	Если СуммаЭлектронно > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Электронно);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Электронно'"));
		СтрокаОплаты.Вставить("Сумма", СуммаЭлектронно);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	СуммаЭлектронно = 0;
	
	// Платежная система
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ПлатежнаяСистема, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СуммаЭлектронно = СтрокаТаблицы.Сумма;
		
		// формирование структуры вывода QR-кода
		Если ТаблицаДанныеQRКода.Количество() Тогда
			
			ДанныеQRКода = ТаблицаДанныеQRКода[0];
			
			Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа
				ИЛИ ДанныеQRКода.ПлатежнаяСистема = Перечисления.ТипыПлатежнойСистемыККТ.СертификатНСПК Тогда
			
				ОбщиеПараметры.ИдентификаторОплатыПлатежнойСистемы = ДанныеQRКода.ИдентификаторОплаты;
				ОбщиеПараметры.ТипПлатежнойСистемы = ДанныеQRКода.ПлатежнаяСистема;
				ОбщиеПараметры.QRКод.ЗначениеКода = ДанныеQRКода.ИдентификаторОплаты;
			
			КонецЕсли;
			
	КонецЕсли;
		
	КонецЕсли;
	
	Если СуммаЭлектронно > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Электронно);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Электронно'"));
		СтрокаОплаты.Вставить("Сумма", СуммаЭлектронно);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	СуммаЭлектронно = 0;
	
	// Банковский платеж
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.БанковскийПлатеж, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СуммаЭлектронно = СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	Если СуммаЭлектронно > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Электронно);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Электронно'"));
		СтрокаОплаты.Вставить("Сумма", СуммаЭлектронно);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	СуммаКредита = 0;
	
	// В рассрочку и кредитом
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ВРассрочку, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СуммаКредита = СуммаКредита + СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	// Банковский кредит
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.БанковскийКредит, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СуммаКредита = СуммаКредита + СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	Если СуммаКредита > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Постоплата);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Постоплата'"));
		СтрокаОплаты.Вставить("Сумма", СуммаКредита);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	ЗачетАванса = 0;
	
	// Подарочный сертификат
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ПодарочныйСертификат, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		ЗачетАванса = СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	// Зачет аванс
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ЗачетАванса, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		ЗачетАванса = ЗачетАванса + СтрокаТаблицы.Сумма;
	КонецЕсли;
	
	Если ЗачетАванса > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Предоплата);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Зачет аванс'"));
		СтрокаОплаты.Вставить("Сумма", ЗачетАванса);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Встречное предоставление
	СтрокаТаблицы = ТаблицаПоТипамОплат.Найти(Перечисления.ТипыОплатЧекаККМ.ВстречноеПредоставление, "ТипОплаты");
	Если НЕ СтрокаТаблицы = Неопределено Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.ВстречноеПредоставление);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Встречное предоставление'"));
		СтрокаОплаты.Вставить("Сумма", СтрокаТаблицы.Сумма);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	
КонецПроцедуры // ЗаполнитьОплатуПриПробитииЧекаККМНаККТ()

///////////////////////////////////////////////////////////////////////////////
// Печать

// Функция формирует табличный документ с печатной формой.
//
// Возвращаемое значение:
//  ТабличныйДокумент - печатная форма.
//
Функция ПечатьГарантийныйТалон(МассивОбъектов, ОбъектыПечати)

	КолонкаКодов       = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	ВыводитьКоды       = ЗначениеЗаполнено(КолонкаКодов);
	ВыводитьУпаковки   = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Чек ККМ'");
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЧекаККМ_ТоварныйЧек";
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.Магазин КАК Магазин,
	|	Документ.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозврат,
	|	ВЫБОР КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|		ТОГДА ПРЕДСТАВЛЕНИЕ(Документ.Организация)
	|		ИНАЧЕ &ТекстИП +
	|		    ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Фамилия, """") = """"
	|				ТОГДА """"
	|			ИНАЧЕ
	|				ФИОФизЛицСрезПоследнихИП.Фамилия + &ТекстПробел +
	|				ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Имя, """") = """"
	|						ТОГДА """"
	|						ИНАЧЕ ВЫРАЗИТЬ(ФИОФизЛицСрезПоследнихИП.Имя КАК СТРОКА(1)) + &ТекстТочка + &ТекстПробел
	|				КОНЕЦ
	|				+
	|				ВЫБОР	КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Отчество, """") = """"
	|						ТОГДА """"
	|						ИНАЧЕ ВЫРАЗИТЬ(ФИОФизЛицСрезПоследнихИП.Отчество КАК СТРОКА(1)) + &ТекстТочка
	|				КОНЕЦ
	|			КОНЕЦ
	|			+ """"
	|	КОНЕЦ КАК НаименованиеОрганизации,
	|	ВЫБОР КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрганизацияЮридическоеЛицо,
	|	Документ.Организация КАК Организация,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Магазин) КАК МагазинПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Организация) КАК ОрганизацияПредставление,
	|	Документ.Ответственный.ФизическоеЛицо КАК Ответственный
	|ИЗ
	|	Документ.ЧекККМ КАК Документ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних() КАК ФИОФизЛицСрезПоследнихИП
	|ПО
	|	Документ.Организация = ФИОФизЛицСрезПоследнихИП.ФизЛицо
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.Проведен
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка, Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.Представление КАК Товар,
	|	" + ?(ВыводитьКоды, "ТаблицаТовары.Номенклатура." + КолонкаКодов +" КАК КолонкаКодов,", "") + "
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.Номенклатура.ВидНоменклатуры.СрокГарантии КАК СрокГарантии,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекККМ.Серии КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|	И ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
	|	И ТаблицаТовары.Номенклатура.ВидНоменклатуры.СрокГарантии > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	УстановитьТекстовыеПараметрыЗапроса(Запрос);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЧекККМ.ПФ_MXL_ГарантийныйТалон");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	Если Результаты[1].Пустой() Тогда
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = НСтр("ru = 'Нет товаров с гарантийным сроком'");
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
	ОбластьИНН       = Макет.ПолучитьОбласть("ИНН");
	ОбластьМагазин   = Макет.ПолучитьОбласть("Магазин");
	
	ОбластьШапкаНомера = Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");
	ОбластьШапкаКодов  = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
	ОбластьШапкаДанных = Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");
	
	ОбластьКолонкаТовар = Макет.Область("Товар");
	Если Не ВыводитьКоды Тогда
		ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки
		                                  + Макет.Область("КолонкаКодов").ШиринаКолонки;
	КонецЕсли;
	ОбластьСтрокаНомера = Макет.ПолучитьОбласть("Строка|НомерСтроки");
	ОбластьСтрокаКодов  = Макет.ПолучитьОбласть("Строка|КолонкаКодов");
	ОбластьСтрокаДанных = Макет.ПолучитьОбласть("Строка|Данные");
	
	// Вывести Итого.
	ОбластьИтогоНомера = Макет.ПолучитьОбласть("Итого|НомерСтроки");
	ОбластьИтогоКодов  = Макет.ПолучитьОбласть("Итого|КолонкаКодов");
	ОбластьИтогоДанных = Макет.ПолучитьОбласть("Итого|Данные");
	
	ОбластьПодписей      = Макет.ПолучитьОбласть("Подписи");
	
	ВыборкаПоДокументам = Результаты[0].Выбрать();
	ВыборкаПоТабличнымЧастям = Результаты[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ВыборкаПоДокументам.ЭтоВозврат Тогда
			СинонимДокумента   = НСтр("ru='Гарантийный талон (возврат)'");
		Иначе
			СинонимДокумента   = НСтр("ru='Гарантийный талон'");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ВыборкаПоДокументам);
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, СинонимДокумента);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		СведенияОПоставщике = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ВыборкаПоДокументам.Организация, ВыборкаПоДокументам.Дата);
		
		Если ВыборкаПоДокументам.ОрганизацияЮридическоеЛицо Тогда
			ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ПолноеНаименование");
		Иначе
			ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ВыборкаПоДокументам.НаименованиеОрганизации;
		КонецЕсли;
		ОбластьПоставщик.Параметры.Поставщик = ВыборкаПоДокументам.Организация;
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		

		ОбластьИНН.Параметры.ИНН = СведенияОПоставщике.ИНН;
		ТабличныйДокумент.Вывести(ОбластьИНН);

		ОбластьМагазин.Параметры.ПредставлениеМагазина = ВыборкаПоДокументам.МагазинПредставление;
		ОбластьМагазин.Параметры.Магазин = ВыборкаПоДокументам.Магазин;
		ТабличныйДокумент.Вывести(ОбластьМагазин);
		
		// Шапка
		
		ТабличныйДокумент.Вывести(ОбластьШапкаНомера);
		Если ВыводитьКоды Тогда
			ОбластьШапкаКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			ТабличныйДокумент.Присоединить(ОбластьШапкаКодов);
		КонецЕсли;
		ТабличныйДокумент.Присоединить(ОбластьШапкаДанных);
		
		// СТРОКИ ТЧ
		Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамТЧ.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;

			ОбластьСтрокаНомера.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ТабличныйДокумент.Вывести(ОбластьСтрокаНомера);

			Если ВыводитьКоды Тогда
				
				ОбластьСтрокаКодов.Параметры.Артикул = ВыборкаПоСтрокамТЧ["КолонкаКодов"];
				ТабличныйДокумент.Присоединить(ОбластьСтрокаКодов);
				
			КонецЕсли;

			ОбластьСтрокаДанных.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ОбластьСтрокаДанных.Параметры.Товар = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(ВыборкаПоСтрокамТЧ.НоменклатураПредставление,ВыборкаПоСтрокамТЧ.ХарактеристикаПредставление);
			
			ТабличныйДокумент.Присоединить(ОбластьСтрокаДанных);
			
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьИтогоНомера);
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьИтогоКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьИтогоДанных);
		
		// ПОДПИСИ
		ТабличныйДокумент.Вывести(ОбластьПодписей);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Функция формирует табличный документ с печатной формой КМ3.
//
// Возвращаемое значение:
//  ТабличныйДокумент - печатная форма.
//
Функция ПечатьКМ3(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Чек ККМ'");
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПФ_MXL_КМ3";
	
	Запрос = Новый Запрос;

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокЧек.Ссылка КАК Ссылка,
	|	ДокЧек.Номер КАК Номер,
	|	ДокЧек.Дата КАК ДатаДокумента,
	|	ДокЧек.КассаККМ,
	|	ДокЧек.КассаККМ.Представление КАК Покупатель,
	|	ДокЧек.Ответственный.ФизическоеЛицо КАК КассирККМ,
	|	ДокЧек.КассаККМ.Владелец КАК Организация,
	|	ДокЧек.КассаККМ.Владелец КАК Руководители,
	|	ДокЧек.КассаККМ.Владелец.Представление КАК Поставщик,
	|	ДокЧек.КассаККМ.СерийныйНомер КАК СерийныйНомерККМ,
	|	ДокЧек.КассаККМ.РегистрационныйНомер КАК РегистрационныйНомерККМ,
	|	ДокЧек.ЧекККМПродажа.НомерЧекаККМ КАК НомерЧека,
	|	ДокЧек.СуммаДокумента КАК СуммаДокумента,
	|	ДокЧек.КассаККМ.ПодключаемоеОборудование.Наименование КАК ПредставлениеККМ,
	|	ДокЧек.ВидОперации
	|ИЗ
	|	Документ.ЧекККМ КАК ДокЧек
	|ГДЕ
	|	ДокЧек.Ссылка В(&МассивОбъектов)
	|	И ДокЧек.Проведен
	|	И ДокЧек.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ДатаДокумента";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КМ3");
	
	ВыборкаПоДокументам = Результаты[0].Выбрать();
	
	Если Результаты[0].Пустой() Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ОтсутствиеДанных");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьАкта");
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ВыборкаПоДокументам);
		ОбластьМакета.Параметры.Заполнить(ВыборкаПоДокументам);
		
		СведенияОбОрганизации = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ВыборкаПоДокументам.Организация, ВыборкаПоДокументам.ДатаДокумента);
		
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны");
		ОбластьМакета.Параметры.СуммаПрописью            = ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(ВыборкаПоДокументам.СуммаДокумента);
		ОбластьМакета.Параметры.НомерДокумента           = "";

		ОбластьМакета.Параметры.ОрганизацияПоОКПО     = СведенияОбОрганизации.КодПоОКПО;
		ОбластьМакета.Параметры.ОрганизацияИНН        = СведенияОбОрганизации.ИНН;
		ОбластьМакета.Параметры.ВидДеятельностиПоОКДП = "";
		
		Руководители          = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(ВыборкаПоДокументам.Руководители, ВыборкаПоДокументам.ДатаДокумента);
		Руководитель          = Руководители.Руководитель;
		СтаршийКассир         = Руководители.Кассир;
		ДолжностьРуководителя = Руководители.РуководительДолжность;
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.КассирККМ) Тогда
			СтруктураФИОКассираККМ  = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(ВыборкаПоДокументам.КассирККМ, ВыборкаПоДокументам.ДатаДокумента);
			ФИОКассираОперациониста = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(Ложь,
			                           СтруктураФИОКассираККМ.Фамилия,
			                           СтруктураФИОКассираККМ.Имя,
			                           СтруктураФИОКассираККМ.Отчество);
		КонецЕсли;
		
		ОбластьМакета.Параметры.ФИОКассираОрганизации   = СтаршийКассир;
		ОбластьМакета.Параметры.ФИОРуководителя         = Руководитель;
		ОбластьМакета.Параметры.ФИОКассираОперациониста = ФИОКассираОперациониста;
		ОбластьМакета.Параметры.ДолжностьРуководителя   = ДолжностьРуководителя;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует табличный документ с печатной формой узкого чека для печати на принтерах с чековой лентой.
//
Функция ПечатьТоварногоЧекаДляПД(МассивОбъектов, ОбъектыПечати)

	ВыводитьУпаковки   = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Чек ККМ'");
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЧекаККМ_ТоварныйЧекДляПД";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.Магазин КАК Магазин,
	|	Документ.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозврат,
	|	ВЫБОР
	|		КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(Документ.Организация)
	|		ИНАЧЕ &ТекстИП + ВЫБОР
	|				КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Фамилия, """") = """"
	|					ТОГДА """"
	|				ИНАЧЕ ФИОФизЛицСрезПоследнихИП.Фамилия + &ТекстПробел + ВЫБОР
	|						КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Имя, """") = """"
	|							ТОГДА """"
	|						ИНАЧЕ ФИОФизЛицСрезПоследнихИП.Имя + &ТекстПробел
	|					КОНЕЦ + ВЫБОР
	|						КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследнихИП.Отчество, """") = """"
	|							ТОГДА """"
	|						ИНАЧЕ ФИОФизЛицСрезПоследнихИП.Отчество
	|					КОНЕЦ
	|			КОНЕЦ + """"
	|	КОНЕЦ КАК НаименованиеОрганизации,
	|	ВЫБОР
	|		КОГДА Документ.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрганизацияЮридическоеЛицо,
	|	Документ.Организация КАК Организация,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Магазин) КАК МагазинПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Организация) КАК ОрганизацияПредставление,
	|	Документ.Ответственный.ФизическоеЛицо КАК Ответственный,
	|	Документ.КассаККМ,
	|	Документ.КассаККМ.ШиринаЛенты КАК ШиринаЛенты,
	|	Документ.Организация.ИНН КАК ИНН
	|ИЗ
	|	Документ.ЧекККМ КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(, ) КАК ФИОФизЛицСрезПоследнихИП
	|		ПО Документ.Организация = ФИОФизЛицСрезПоследнихИП.ФизЛицо
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОплата.ВидОплаты.ТипОплаты КАК ТипОплаты,
	|	ТаблицаОплата.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаОплата.ВидОплаты.ТипОплаты) КАК ПредставлениеТипОплаты,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|					И ТаблицаОплата.ВидОплаты.ТипОплаты <> ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.Наличные)
	|				ТОГДА ТаблицаОплата.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаБезналичныхОплат,
	|	СУММА(ТаблицаОплата.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ТаблицаОплата
	|ГДЕ
	|	ТаблицаОплата.Ссылка В(&МассивОбъектов)
	|	И ТаблицаОплата.Ссылка.Проведен
	|	И (НЕ(ТаблицаОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|				И ТаблицаОплата.ВидОплаты.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.Наличные)))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОплата.Ссылка,
	|	ТаблицаОплата.ВидОплаты.ТипОплаты
	|
	|ИМЕЮЩИЕ
	|	(НЕ СУММА(ТаблицаОплата.Сумма) = 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.Представление КАК Товар,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Упаковка.ЕдиницаИзмерения)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|	И ТаблицаТовары.Ссылка.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	УстановитьТекстовыеПараметрыЗапроса(Запрос);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЧекККМ.ПФ_MXL_ТоварныйЧекДляПД");
	
	ВыборкаПоДокументам = Результаты[0].Выбрать();
	ВыборкаПоТабличнойЧастиОплата = Результаты[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоТабличнымЧастям = Результаты[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ШиринаЛенты = ВыборкаПоДокументам.ШиринаЛенты;
		Если ШиринаЛенты = 0 Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У кассы %1 документ (%2) не задана ширина ленты'"),
				ВыборкаПоДокументам.КассаККМ,
				ВыборкаПоДокументам.Ссылка
			);
			
			ОбщегоНазначения.СообщитьПользователю(
				Текст);
				
			Продолжить;
		КонецЕсли;
		
		ОбластьШапкаЧека         = Макет.ПолучитьОбласть("ШапкаЧека|_"         + ШиринаЛенты);
		ОбластьТелоЧека          = Макет.ПолучитьОбласть("ТелоЧека|_"          + ШиринаЛенты);
		ОбластьТелоЧекаСкидка    = Макет.ПолучитьОбласть("ТелоЧекаСкидка|_"    + ШиринаЛенты);
		ОбластьТелоЧекаОтступ    = Макет.ПолучитьОбласть("ТелоЧекаОтступ|_"    + ШиринаЛенты);
		ОбластьПодвалЧека        = Макет.ПолучитьОбласть("ПодвалЧека|_"        + ШиринаЛенты);
		ОбластьПодвалЧекаОплата  = Макет.ПолучитьОбласть("ПодвалЧекаОплата|_"  + ШиринаЛенты);
		ОбластьПодвалЧекаОстаток = Макет.ПолучитьОбласть("ПодвалЧекаОстаток|_" + ШиринаЛенты);
		
		ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ВыборкаПоДокументам.ЭтоВозврат Тогда
			СинонимДокумента   = НСтр("ru='Чек (возврат)'");
		Иначе
			СинонимДокумента   = НСтр("ru='Чек'");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ВыборкаПоДокументам);
		ОбластьШапкаЧека.Параметры.Заголовок = ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, СинонимДокумента);
		ОбластьШапкаЧека.Параметры.Организация    = ВыборкаПоДокументам.НаименованиеОрганизации;
		ОбластьШапкаЧека.Параметры.ИНН            = ВыборкаПоДокументам.ИНН;
		
		
		
		ТабличныйДокумент.Вывести(ОбластьШапкаЧека);
		
		ВсегоНаименований = 0;
		Сумма             = 0;
		ВсегоСкидок       = 0;
		ВсегоБезСкидок    = 0;
		
		// СТРОКИ ТЧ
		Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамТЧ.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;

			ОбластьТелоЧека.Параметры.НаименованиеТовара = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(ВыборкаПоСтрокамТЧ.НоменклатураПредставление,ВыборкаПоСтрокамТЧ.ХарактеристикаПредставление)
														 + "(" + ВыборкаПоСтрокамТЧ.ЕдиницаИзмерения + ")";
			ОбластьТелоЧека.Параметры.КоличествоЦена     = Формат( ВыборкаПоСтрокамТЧ.Количество, "ЧЦ=17; ЧДЦ=3; ЧРД=.; ЧН=; ЧГ=0")
														 + " х "
														 + Формат( ВыборкаПоСтрокамТЧ.Цена, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");

			КонечнаяСкидка =  ВыборкаПоСтрокамТЧ.Количество *  ВыборкаПоСтрокамТЧ.Цена -  ВыборкаПоСтрокамТЧ.Сумма;
			ТабличныйДокумент.Вывести(ОбластьТелоЧека);

			Если КонечнаяСкидка <> 0 Тогда
				ОбластьТелоЧекаСкидка.Параметры.ЗагСкидка = ?(КонечнаяСкидка > 0, "Скидка:", "Надбавка:");
				ОбластьТелоЧекаСкидка.Параметры.Скидка    = Формат(?(КонечнаяСкидка > 0, КонечнаяСкидка, -КонечнаяСкидка), "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				ТабличныйДокумент.Вывести(ОбластьТелоЧекаСкидка);
			КонецЕсли;

			ТабличныйДокумент.Вывести(ОбластьТелоЧекаОтступ);
		КонецЦикла;
		
		// ПОДВАЛ
		ОбластьПодвалЧека.Параметры.Итог = Формат(ВыборкаПоДокументам.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		ТабличныйДокумент.Вывести(ОбластьПодвалЧека);
		
		
		СуммаБезналичныхОплат = 0;
		СуммаОплат = 0;
		
		// Оплата
		Если ВыборкаПоТабличнойЧастиОплата.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			ВыборкаПоОплата = ВыборкаПоТабличнойЧастиОплата.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоОплата.Следующий() Цикл
				ОбластьПодвалЧекаОплата.Параметры.ТипОплаты = Строка(ВыборкаПоОплата.ПредставлениеТипОплаты) + ":";
				ОбластьПодвалЧекаОплата.Параметры.Оплата = Формат(ВыборкаПоОплата.Сумма, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				ТабличныйДокумент.Вывести(ОбластьПодвалЧекаОплата);
				СуммаБезналичныхОплат = СуммаБезналичныхОплат + ВыборкаПоОплата.СуммаБезналичныхОплат;
				СуммаОплат = СуммаОплат + ВыборкаПоОплата.Сумма;
			КонецЦикла;
			
			Если ВыборкаПоДокументам.ЭтоВозврат Тогда
				СуммаНаличных = ВыборкаПоДокументам.СуммаДокумента - СуммаБезналичныхОплат;
				Если СуммаНаличных > 0  Тогда
					ОбластьПодвалЧекаОплата.Параметры.ТипОплаты = "Наличные: ";
					ОбластьПодвалЧекаОплата.Параметры.Оплата = Формат(СуммаНаличных, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
					ТабличныйДокумент.Вывести(ОбластьПодвалЧекаОплата);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		
		// ПОДПИСИ
		Если ВыборкаПоДокументам.ЭтоВозврат Тогда
			ОбластьПодвалЧекаОстаток.Параметры.Сдача = "0.00";
		Иначе
			ОбластьПодвалЧекаОстаток.Параметры.Сдача = Формат(СуммаОплат - ВыборкаПоДокументам.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		КонецЕсли;
		
		ОбластьПодвалЧекаОстаток.Параметры.Сотрудник = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(ВыборкаПоДокументам.Ответственный);
		ТабличныйДокумент.Вывести(ОбластьПодвалЧекаОстаток);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции

Процедура УстановитьТекстовыеПараметрыЗапроса(Запрос)
	
	Запрос.УстановитьПараметр("ТекстИП"    , НСтр("ru = 'ИП'") + " ");
	Запрос.УстановитьПараметр("ТекстПробел", " ");
	Запрос.УстановитьПараметр("ТекстТочка" , ".");
	
КонецПроцедуры

Функция ПечатьОписиНоменклатуры(МассивОбъектов, ОбъектыПечати)
	
	КолонкаКодов       = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	ВыводитьКоды       = ЗначениеЗаполнено(КолонкаКодов);
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Организация КАК Организация,
	|	ТаблицаДокументов.Магазин,
	|	ТаблицаДокументов.Ссылка
	|ИЗ
	|	Документ.ЧекККМ КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СтруктураПечати = Новый Структура;
	СтруктураПечати.Вставить("ИмяРеквизита1", "Организация");
	СтруктураПечати.Вставить("ИмяРеквизита2", "Магазин");
	СтруктураПечати.Вставить("КоличествоРеквизитов", 2);
	СтруктураПечати.Вставить("ВыводитьКоды", ВыводитьКоды);
	СтруктураПечати.Вставить("КолонкаКодов", КолонкаКодов);
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураПечати.Вставить("Документ", Выборка.Ссылка);
		СтруктураПечати.Вставить("Реквизит1", Выборка.Организация);
		СтруктураПечати.Вставить("Реквизит2", Выборка.Магазин);
		
		УправлениеПечатьюРТ.ПечатьОписиНоменклатуры(ТабличныйДокумент, ОбъектыПечати, СтруктураПечати, ПервыйДокумент);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует печатную форму документа.
//
Функция ПечатьЗаявленияНаВозврат(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	Перем ЗаявлениеБезПроведения;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Заявление на возврат'");
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратТоваровОтПокупателя_ЗаявлениеНаВозврат";
	
	Если ПараметрыПечати.Свойство("ЗаявлениеБезПроведения", ЗаявлениеБезПроведения) И ЗаявлениеБезПроведения Тогда
		Запрос = Новый Запрос;
		ПодготовитьЗапросИзРМК(Запрос, МассивОбъектов, ПараметрыПечати);
		СдвигВременныхТаблиц = 2;
	Иначе
		Запрос = Новый Запрос(ТекстЗапросаЗаявления());
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Запрос.УстановитьПараметр("ВидОперации"   , Перечисления.ВидыОперацийЧекККМ.Возврат);
		СдвигВременныхТаблиц = 0;
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратТоваровОтПокупателя.ПФ_MXL_ЗаявлениеНаВозврат");
	
	ОбластьШапка         = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТоваров = Макет.ПолучитьОбласть("Товар");
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	ВыборкаПоДокументам = Результаты[0 + СдвигВременныхТаблиц].Выбрать();
	ВыборкаПоТабличнымЧастям = Результаты[1 + СдвигВременныхТаблиц].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СоответствиеПолучателей = ПараметрыПечати.СоответствиеПолучателей;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка.Параметры.Кому = СоответствиеПолучателей.Получить(ВыборкаПоДокументам.Покупатель);
		
		ОбластьШапка.Параметры.Заполнить(ВыборкаПоДокументам);
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ЧекДатаПоДокументу) Тогда
			ОбластьШапка.Параметры.ЧекДата = Формат(ВыборкаПоДокументам.ЧекДатаПоДокументу, "ДЛФ = DD")
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ДатаРожденияПоДокументу) Тогда
			ОбластьШапка.Параметры.ДатаРождения = Формат(ВыборкаПоДокументам.ДатаРожденияПоДокументу, "ДЛФ = DD")
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ФизЛицо) Тогда
			
			ФИОФизЛицаРП = "";
			ФИОФизЛицаТП = "";
			
			Если ПараметрыПечати.Свойство("СоответствиеФизЛиц") Тогда
				СоответствиеФизЛицВИменительномПадеже = ПараметрыПечати.СоответствиеФизЛиц.ВИменительномПадеже;
				СоответствиеФизЛицВРодительномПадеже  = ПараметрыПечати.СоответствиеФизЛиц.ВРодительномПадеже;
				СоответствиеФизЛицВТворительномПадеже = ПараметрыПечати.СоответствиеФизЛиц.ВТворительномПадеже;
				
				ФИОФизЛицаРП = СоответствиеФизЛицВРодительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
				ФИОФизЛицаТП = СоответствиеФизЛицВТворительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
				ФИОФизЛицаИП = СоответствиеФизЛицВТворительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
			Иначе
				
				Если Не ТипЗнч(ВыборкаПоДокументам.ФизЛицо) = Тип("Строка")Тогда
					ФизЛицоФИО = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(, Новый Структура("ФизЛицо", ВыборкаПоДокументам.ФизЛицо)); 
					ФИОФизЛицаИП = ФизЛицоФИО.Фамилия + " " + ФизЛицоФИО.Имя + " " + ФизЛицоФИО.Отчество;
					
					Если СокрЛП(ФИОФизЛицаИП) = "" Тогда
						ФИОФизЛицаИП = ВыборкаПоДокументам.ФизЛицо.Наименование;
					КонецЕсли;
					
				ИначеЕсли ТипЗнч(ВыборкаПоДокументам.ФизЛицо) = Тип("Строка") Тогда
					ФИОФизЛицаИП = ВыборкаПоДокументам.ФизЛицо;
				Иначе
					ФИОФизЛицаИП = ""
				КонецЕсли;
				
				ФИОФизЛицаРП = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 2);
				ФИОФизЛицаТП = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 5);
			КонецЕсли;
			
			
			ОбластьШапка.Параметры.ОтКого = НСтр("ru = 'от'") + " " + ФИОФизЛицаРП;
			ОбластьШапка.Параметры.ФИОПокупателя = ФИОФизЛицаТП;
			
		КонецЕсли;
		
		ОбластьШапка.Параметры.Заполнить(ПараметрыПечати);
		
		Если ВыборкаПоСтрокамТЧ.Количество() > 1 Тогда
			ОбластьШапка.Параметры.О1 = "и";
			ОбластьШапка.Параметры.О2 = "ы";
			ОбластьШапка.Параметры.О3 = "ы";
		Иначе
			ОбластьШапка.Параметры.О1 = "";
			ОбластьШапка.Параметры.О2 = "";
			ОбластьШапка.Параметры.О3 = "";
		КонецЕсли;
		
		ОбластьШапка.Параметры.Количество = ВыборкаПоСтрокамТЧ.Количество();
		
		ДлинноеПодчеркивание = "______________________________________________________";
		
		СреднееПодчеркивание = "________________";
		
		КороткоеПодчеркивание = "________";
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ОтКого) 
			ИЛИ ОбластьШапка.Параметры.ОтКого = "от " Тогда
			ОбластьШапка.Параметры.ОтКого = "от " + ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ФИОПокупателя) Тогда
			ОбластьШапка.Параметры.ФИОПокупателя = ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ДатаРождения) Тогда
			ОбластьШапка.Параметры.ДатаРождения = СреднееПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ПаспортныеДанные) Тогда
			ОбластьШапка.Параметры.ПаспортныеДанные = ДлинноеПодчеркивание + ДлинноеПодчеркивание + ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ЧекНомер) Тогда
			ОбластьШапка.Параметры.ЧекНомер = КороткоеПодчеркивание;
		Иначе
			ОбластьШапка.Параметры.ЧекНомер = СокрЛП(ОбластьШапка.Параметры.ЧекНомер);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ЧекДата) Тогда
			ОбластьШапка.Параметры.ЧекДата = СреднееПодчеркивание;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		
		Счетчик = 0;
		// СТРОКИ ТЧ
		Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
			
			Счетчик = Счетчик + 1;
			
			ОбластьСтрокаТоваров.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			
			ОбластьСтрокаТоваров.Параметры.Номер            = Счетчик;
			ОбластьСтрокаТоваров.Параметры.Номенклатура     = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(ВыборкаПоСтрокамТЧ.НоменклатураПредставление,ВыборкаПоСтрокамТЧ.ХарактеристикаПредставление);;
			
			ТабличныйДокумент.Вывести(ОбластьСтрокаТоваров);
		КонецЦикла;
		
		ОбластьПодвал.Параметры.СуммаДокумента = ВыборкаПоДокументам.СуммаДокумента;
		ОбластьПодвал.Параметры.Дата = Формат(ВыборкаПоДокументам.Дата, "ДЛФ = DD");;
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует печатную форму документа.
//
Функция ПечатьЗаявленияОбОшибочноПробитомЧеке(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	Перем ЗаявлениеБезПроведения;
	
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Заявление об ошибочно пробитом чеке'");

	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратТоваровОтПокупателя_ЗаявлениеОбОшибочноПробитомЧеке";
	
	Если ПараметрыПечати.Свойство("ЗаявлениеБезПроведения", ЗаявлениеБезПроведения) И ЗаявлениеБезПроведения Тогда
		Запрос = Новый Запрос;
		ПодготовитьЗапросИзРМК(Запрос, МассивОбъектов, ПараметрыПечати);
		СдвигВременныхТаблиц = 2;
	Иначе
		Запрос = Новый Запрос(ТекстЗапросаЗаявления());
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		СдвигВременныхТаблиц = 0;
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратТоваровОтПокупателя.ПФ_MXL_ЗаявлениеОбОшибочноПробитомЧеке");
	
	ОбластьШапка         = Макет.ПолучитьОбласть("Шапка");
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	ВыборкаПоДокументам = Результаты[0 + СдвигВременныхТаблиц].Выбрать();
	
	СоответствиеПолучателей = ПараметрыПечати.СоответствиеПолучателей;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка.Параметры.Кому = СоответствиеПолучателей.Получить(ВыборкаПоДокументам.Покупатель);
		
		ОбластьШапка.Параметры.Заполнить(ВыборкаПоДокументам);
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ЧекДатаПоДокументу) Тогда
			ОбластьШапка.Параметры.ЧекДата = Формат(ВыборкаПоДокументам.ЧекДатаПоДокументу, "ДЛФ = DD")
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ДатаРожденияПоДокументу) Тогда
			ОбластьШапка.Параметры.ДатаРождения = Формат(ВыборкаПоДокументам.ДатаРожденияПоДокументу, "ДЛФ = DD")
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ФизЛицо) Тогда
			
			ФИОФизЛицаРП = "";
			ФИОФизЛицаТП = "";
			
			Если ПараметрыПечати.Свойство("СоответствиеФизЛиц") Тогда
				СоответствиеФизЛицВИменительномПадеже = ПараметрыПечати.СоответствиеФизЛиц.ВИменительномПадеже;
				СоответствиеФизЛицВРодительномПадеже  = ПараметрыПечати.СоответствиеФизЛиц.ВРодительномПадеже;
				СоответствиеФизЛицВТворительномПадеже = ПараметрыПечати.СоответствиеФизЛиц.ВТворительномПадеже;
				
				ФИОФизЛицаРП = СоответствиеФизЛицВРодительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
				ФИОФизЛицаТП = СоответствиеФизЛицВТворительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
				ФИОФизЛицаИП = СоответствиеФизЛицВИменительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
			Иначе
				
				Если Не ТипЗнч(ВыборкаПоДокументам.ФизЛицо) = Тип("Строка")Тогда
					ФизЛицоФИО = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(, Новый Структура("ФизЛицо", ВыборкаПоДокументам.ФизЛицо)); 
					ФИОФизЛицаИП = ФизЛицоФИО.Фамилия + " " + ФизЛицоФИО.Имя + " " + ФизЛицоФИО.Отчество;
					
					Если СокрЛП(ФИОФизЛицаИП) = "" Тогда
						ФИОФизЛицаИП = ВыборкаПоДокументам.ФизЛицо.Наименование;
					КонецЕсли;
					
				ИначеЕсли ТипЗнч(ВыборкаПоДокументам.ФизЛицо) = Тип("Строка") Тогда
					ФИОФизЛицаИП = ВыборкаПоДокументам.ФизЛицо;
				Иначе
					ФИОФизЛицаИП = ""
				КонецЕсли;
				
				ФИОФизЛицаРП = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 2);
				ФИОФизЛицаТП = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 5);
			КонецЕсли;
			
			ОбластьШапка.Параметры.ОтКого = НСтр("ru = 'от'") + " " + ФИОФизЛицаРП;
			ОбластьШапка.Параметры.ФИОПродавца = ФИОФизЛицаТП;
			
			ОбластьПодвал.Параметры.ФИОКассира = ФИОФизЛицаИП;
		КонецЕсли;
		
		ОбластьШапка.Параметры.Заполнить(ПараметрыПечати);
		
		ДлинноеПодчеркивание = "______________________________________________________";
		
		СреднееПодчеркивание = "________________";
		
		КороткоеПодчеркивание = "________";
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ОтКого) 
			ИЛИ ОбластьШапка.Параметры.ОтКого = "от " Тогда
			ОбластьШапка.Параметры.ОтКого = "от " + ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ФИОПродавца) Тогда
			ОбластьШапка.Параметры.ФИОПродавца = ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ДатаРождения) Тогда
			ОбластьШапка.Параметры.ДатаРождения = СреднееПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ПаспортныеДанные) Тогда
			ОбластьШапка.Параметры.ПаспортныеДанные = ДлинноеПодчеркивание + ДлинноеПодчеркивание + ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ЧекНомер) Тогда
			ОбластьШапка.Параметры.ЧекНомер = КороткоеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ЧекДата) Тогда
			ОбластьШапка.Параметры.ЧекДата = СреднееПодчеркивание;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ОбластьПодвал.Параметры.Заполнить(ПараметрыПечати);
		ОбластьПодвал.Параметры.Дата = Формат(ВыборкаПоДокументам.Дата, "ДЛФ = DD");;
		
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует печатную форму документа.
//
Функция ПечатьЗаявленияНаУтерянныйЧек(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	Перем ЗаявлениеБезПроведения;
	
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Заявление на возврат'");
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратТоваровОтПокупателя_ЗаявлениеНаУтерянныйЧек";
	
	Если ПараметрыПечати.Свойство("ЗаявлениеБезПроведения", ЗаявлениеБезПроведения) И ЗаявлениеБезПроведения Тогда
		Запрос = Новый Запрос;
		ПодготовитьЗапросИзРМК(Запрос, МассивОбъектов, ПараметрыПечати);
		СдвигВременныхТаблиц = 2;
	Иначе
		Запрос = Новый Запрос(ТекстЗапросаЗаявления());
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		СдвигВременныхТаблиц = 0;
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратТоваровОтПокупателя.ПФ_MXL_ЗаявлениеНаУтерянныйЧек");
	
	ОбластьШапка         = Макет.ПолучитьОбласть("Шапка");
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	ВыборкаПоДокументам = Результаты[0 + СдвигВременныхТаблиц].Выбрать();
	ВыборкаПоТабличнымЧастям = Результаты[1 + СдвигВременныхТаблиц].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СоответствиеПолучателей = ПараметрыПечати.СоответствиеПолучателей;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка.Параметры.Кому = СоответствиеПолучателей.Получить(ВыборкаПоДокументам.Покупатель);
		
		ОбластьШапка.Параметры.Заполнить(ВыборкаПоДокументам);
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ДатаРожденияПоДокументу) Тогда
			ОбластьШапка.Параметры.ДатаРождения = Формат(ВыборкаПоДокументам.ДатаРожденияПоДокументу, "ДЛФ = DD")
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.ФизЛицо) Тогда
			
			ФИОФизЛицаРП = "";
			ФИОФизЛицаТП = "";
			
			Если ПараметрыПечати.Свойство("СоответствиеФизЛиц") Тогда
				СоответствиеФизЛицВИменительномПадеже = ПараметрыПечати.СоответствиеФизЛиц.ВИменительномПадеже;
				СоответствиеФизЛицВРодительномПадеже  = ПараметрыПечати.СоответствиеФизЛиц.ВРодительномПадеже;
				СоответствиеФизЛицВТворительномПадеже = ПараметрыПечати.СоответствиеФизЛиц.ВТворительномПадеже;
				
				ФИОФизЛицаРП = СоответствиеФизЛицВРодительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
				ФИОФизЛицаТП = СоответствиеФизЛицВТворительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
				ФИОФизЛицаИП = СоответствиеФизЛицВТворительномПадеже.Получить(ВыборкаПоДокументам.Ссылка);
			Иначе
				
				Если Не ТипЗнч(ВыборкаПоДокументам.ФизЛицо) = Тип("Строка")Тогда
					ФизЛицоФИО = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(, Новый Структура("ФизЛицо", ВыборкаПоДокументам.ФизЛицо)); 
					ФИОФизЛицаИП = ФизЛицоФИО.Фамилия + " " + ФизЛицоФИО.Имя + " " + ФизЛицоФИО.Отчество;
					
					Если СокрЛП(ФИОФизЛицаИП) = "" Тогда
						ФИОФизЛицаИП = ВыборкаПоДокументам.ФизЛицо.Наименование;
					КонецЕсли;
					
				ИначеЕсли ТипЗнч(ВыборкаПоДокументам.ФизЛицо) = Тип("Строка") Тогда
					ФИОФизЛицаИП = ВыборкаПоДокументам.ФизЛицо;
				Иначе
					ФИОФизЛицаИП = ""
				КонецЕсли;
				
				ФИОФизЛицаРП = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 2);
				ФИОФизЛицаТП = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(ФИОФизЛицаИП, 5);
			КонецЕсли;
			
			ОбластьШапка.Параметры.ОтКого = НСтр("ru = 'от'") + " "+ ФИОФизЛицаРП;
			ОбластьШапка.Параметры.ФИОПокупателя = ФИОФизЛицаТП;
			
		КонецЕсли;
		
		ОбластьШапка.Параметры.Заполнить(ПараметрыПечати);
		
		Если ВыборкаПоСтрокамТЧ.Количество() >1 Тогда
			ОбластьШапка.Параметры.О1 = "и";
			ОбластьШапка.Параметры.О2 = "ы";
			ОбластьШапка.Параметры.О4 = "ы";
		Иначе
			ОбластьШапка.Параметры.О1 = "";
			ОбластьШапка.Параметры.О2 = "";
			ОбластьШапка.Параметры.О4 = "";
		КонецЕсли;
		
		ДлинноеПодчеркивание = "______________________________________________________";
		
		СреднееПодчеркивание = "________________";
		
		КороткоеПодчеркивание = "________";
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ОтКого) 
			ИЛИ ОбластьШапка.Параметры.ОтКого = "от " Тогда
			ОбластьШапка.Параметры.ОтКого = "от " + ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ФИОПокупателя) Тогда
			ОбластьШапка.Параметры.ФИОПокупателя = ДлинноеПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ДатаРождения) Тогда
			ОбластьШапка.Параметры.ДатаРождения = СреднееПодчеркивание;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбластьШапка.Параметры.ПаспортныеДанные) Тогда
			ОбластьШапка.Параметры.ПаспортныеДанные = ДлинноеПодчеркивание + ДлинноеПодчеркивание + ДлинноеПодчеркивание;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ОбластьПодвал.Параметры.Дата = Формат(ВыборкаПоДокументам.Дата, "ДЛФ = DD");;
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ЕстьДокументыВПКО(ПриходныйКассовыйОрдер)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка = &Ссылка
	|	И ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом В(&МассивПустыхДокументов)";
	
	МассивПустыхДокументов = Новый Массив;
	МассивПустыхДокументов.Добавить(Документы.РеализацияТоваров.ПустаяСсылка());
	МассивПустыхДокументов.Добавить(Документы.ВозвратТоваровПоставщику.ПустаяСсылка());
	МассивПустыхДокументов.Добавить(Документы.ЧекККМ.ПустаяСсылка());
	МассивПустыхДокументов.Добавить(Документы.РасходныйКассовыйОрдер.ПустаяСсылка());
	МассивПустыхДокументов.Добавить(Документы.РеализацияТоваров.ПустаяСсылка());
	МассивПустыхДокументов.Добавить(Документы.ВводОстатковРасчетовСКлиентами.ПустаяСсылка());
	МассивПустыхДокументов.Добавить(Неопределено);
	
	Запрос.УстановитьПараметр("Ссылка", ПриходныйКассовыйОрдер);
	Запрос.УстановитьПараметр("МассивПустыхДокументов", МассивПустыхДокументов);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		Возврат Истина
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция РазвернутьСтрокуПозицииЧека(СтрокаПозицииЧека, МассивСтрокГосИС = Неопределено)
	
	МассивРезультат = Новый Массив;
	
	Количество = СтрокаПозицииЧека.Количество;
	
	Коэффициенты = Новый Массив(Количество); // Служебный массив для распределения сумм
	
	// Построчное заполнение сведений о маркированной продукции.
	Индекс = 0;
	РазбиратьСтрокуМаркированногоТовара = Ложь;
	МаксимальныйИндексГОСИС = 0;
	
	Если НЕ МассивСтрокГосИС = Неопределено И МассивСтрокГосИС.Количество()>0 Тогда
		РазбиратьСтрокуМаркированногоТовара = Истина;
		МаксимальныйИндексГОСИС = МассивСтрокГосИС.Количество()-1;
	КонецЕсли;
	
	Для Каждого СтрокаКоэффициента Из Коэффициенты Цикл
		
		Коэффициенты[Индекс] = 1;
		
		НоваяСтрокаПозицииЧека = ОбщегоНазначения.СкопироватьРекурсивно(СтрокаПозицииЧека);
		НоваяСтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры = Неопределено;
		
		НоваяСтрокаПозицииЧека.Количество = 1;
		
		Если РазбиратьСтрокуМаркированногоТовара Тогда
			
			Если Индекс > МаксимальныйИндексГОСИС Тогда
				// Для ситуаций, когда разрешены продажи остатков без марки (фото, шины), а товар указан как маркированный.
				МассивРезультат.Добавить(НоваяСтрокаПозицииЧека);
		
				Индекс = Индекс + 1;
				Продолжить;
			КонецЕсли;
			
			ТекущаяСтрока    = МассивСтрокГосИС[Индекс];
			КодАкцизнойМарки = ТекущаяСтрока.КодАкцизнойМарки;
			
			НоваяСтрокаПозицииЧека.Штрихкод = КодАкцизнойМарки;
			ЗаполнитьСтрокуПоДаннымРазбораШтрихкода(НоваяСтрокаПозицииЧека, ТекущаяСтрока)
		КонецЕсли;
	
		МассивРезультат.Добавить(НоваяСтрокаПозицииЧека);
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	// Распределение суммы
	Если СтрокаПозицииЧека.Сумма > 0 Тогда
		РаспределенныеСуммы = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаПозицииЧека.Сумма, Коэффициенты);
		
		Индекс = 0;
		
		Для Каждого СтрокаРаспределения Из РаспределенныеСуммы Цикл
			МассивРезультат[Индекс].Сумма = СтрокаРаспределения;
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЕсли;
	
	// Перезаполнение суммы скидок
	Если НЕ СтрокаПозицииЧека.СуммаСкидок = 0 Тогда
		РаспределенныеСуммы = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаПозицииЧека.СуммаСкидок, Коэффициенты);
		
		Индекс = 0;
		
		Для Каждого СтрокаРаспределения Из РаспределенныеСуммы Цикл
			МассивРезультат[Индекс].СуммаСкидок = СтрокаРаспределения;
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЕсли;
	
	// Перезаполнение цены, суммы НДС
	Для Каждого СтрокаРезультата Из МассивРезультат Цикл
		
		Если СтрокаРезультата.Цена= СтрокаРезультата.Сумма Тогда
			СтрокаРезультата.ЦенаСоСкидками = СтрокаРезультата.Цена;
		Иначе
			СтрокаРезультата.ЦенаСоСкидками = СтрокаРезультата.Сумма;
		КонецЕсли;
		
		СтрокаРезультата.СуммаНДС = СтрокаРезультата.СуммаНДС / Количество;
	КонецЦикла;
	
	Возврат МассивРезультат;
КонецФункции

Функция СостояниеОплаты(ДокументСсылка)
	
	СостояниеОплаты = Новый Структура;
	СостояниеОплаты.Вставить("Аванс", Ложь);
	СостояниеОплаты.Вставить("ПолнаяОплата", Ложь);
	СостояниеОплаты.Вставить("БезОплаты", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ЧекККМТовары.Сумма), 0) КАК СуммаТоваров,
	|	0 КАК СуммаОплат
	|ПОМЕСТИТЬ ТоварыИОплаты
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ЧекККМТовары
	|ГДЕ
	|	ЧекККМТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ЕСТЬNULL(СУММА(ЧекККМОплата.Сумма), 0)
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	ЧекККМОплата.Ссылка = &Ссылка
	|	И НЕ ЧекККМОплата.ВидОплаты.ТипОплаты В (&ТипыОплатыКредитом)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СУММА(ТоварыИОплаты.СуммаТоваров) = 0
	|				И НЕ СУММА(ТоварыИОплаты.СуммаОплат) = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Аванс,
	|	ВЫБОР
	|		КОГДА НЕ СУММА(ТоварыИОплаты.СуммаТоваров) = 0
	|				И СУММА(ТоварыИОплаты.СуммаОплат) = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БезОплаты,
	|	ВЫБОР
	|		КОГДА СУММА(ТоварыИОплаты.СуммаТоваров) <= СУММА(ТоварыИОплаты.СуммаОплат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПолнаяОплата
	|ИЗ
	|	ТоварыИОплаты КАК ТоварыИОплаты";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	ТипыОплатыКредитом = Новый Массив;
	ТипыОплатыКредитом.Добавить(Перечисления.ТипыОплатЧекаККМ.ВРассрочку);
	ТипыОплатыКредитом.Добавить(Перечисления.ТипыОплатЧекаККМ.БанковскийКредит);
	
	Запрос.УстановитьПараметр("ТипыОплатыКредитом", ТипыОплатыКредитом);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат СостояниеОплаты;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(СостояниеОплаты, Выборка);
	
	Возврат СостояниеОплаты;
КонецФункции

Функция СоответствиеДанныхОбОрганизациях(МассивОбъектов, СоответствиеПолучателей, ПараметрыПечати)
	
	Если НЕ ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
		
		ПараметрыПечати = Новый Структура;
		
	КонецЕсли;
	
	Если ПараметрыПечати.Свойство("СоответствиеПолучателей") Тогда
		Возврат ПараметрыПечати;
	КонецЕсли;
	
	ЗаявлениеБезПроведения = Ложь;
	
	Если НЕ ПараметрыПечати.Свойство("ЗаявлениеБезПроведения", ЗаявлениеБезПроведения) Тогда
		ЗаявлениеБезПроведения = Ложь;
	КонецЕсли;
	
	Если ЗаявлениеБезПроведения = Истина Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Организация КАК Организация
		|ИЗ
		|	(ВЫБРАТЬ
		|		&Организация КАК Организация) КАК ВложенныйЗапрос";
		
		Запрос.УстановитьПараметр("Организация", МассивОбъектов[0].Организация);
		
	Иначе
		Если СоответствиеПолучателей.Количество() > 0 Тогда
			
			ПараметрыПечати.Вставить("СоответствиеПолучателей", СоответствиеПолучателей);
			Возврат ПараметрыПечати;
			
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЧекККМ.Организация КАК Организация
		|ИЗ
		|	Документ.ЧекККМ КАК ЧекККМ
		|ГДЕ
		|	ЧекККМ.Ссылка В(&МассивОбъектов)";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СтруктураРезультатаЗапроса = Новый Структура;
	СоответствиеОрганизацияРуководитель = Новый Соответствие;
	СоответствиеОрганизацияДолжность    = Новый Соответствие;
	СоответствиеОрганизацияНаименование = Новый Соответствие;
	МассивОрганизаций = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Руководители = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(Выборка.Организация, ТекущаяДатаСеанса(),,Истина);

		СоответствиеОрганизацияРуководитель.Вставить(Выборка.Организация, Руководители.Руководитель);
		СоответствиеОрганизацияДолжность.Вставить(Выборка.Организация   , Руководители.РуководительДолжность);
		СоответствиеОрганизацияНаименование.Вставить(Выборка.Организация, ?(СокрЛП(Выборка.Организация.НаименованиеПолное) = "", Выборка.Организация.Наименование, Выборка.Организация.НаименованиеПолное));
		МассивОрганизаций.Добавить(Выборка.Организация);
		
	КонецЦикла;
	
	СтруктураРезультатаЗапроса.Вставить("СоответствиеОрганизацияРуководитель", СоответствиеОрганизацияРуководитель);
	СтруктураРезультатаЗапроса.Вставить("СоответствиеОрганизацияДолжность"   , СоответствиеОрганизацияДолжность);
	СтруктураРезультатаЗапроса.Вставить("СоответствиеОрганизацияНаименование", СоответствиеОрганизацияНаименование);
	СтруктураРезультатаЗапроса.Вставить("МассивОрганизаций"                 , МассивОрганизаций);
	
	СоответствиеПолучателей = Новый Соответствие;
	
	Для каждого ЭлементОрганизации Из СтруктураРезультатаЗапроса.МассивОрганизаций Цикл
		
		Кому = "";
		Руководитель          = СтруктураРезультатаЗапроса.СоответствиеОрганизацияРуководитель.Получить(ЭлементОрганизации);
		РуководительДолжность = СтруктураРезультатаЗапроса.СоответствиеОрганизацияДолжность.Получить(ЭлементОрганизации);
		Наименование          = СтруктураРезультатаЗапроса.СоответствиеОрганизацияНаименование.Получить(ЭлементОрганизации);
		
		Если Не Руководитель = Неопределено Тогда
			Должность       = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(РуководительДолжность, 3);
			ФИОРуководителя = ОбщегоНазначенияРТКлиентСерверПовтИсп.ПолучитьСклонениеФИО(Руководитель, 3);
			
			Кому = Должность + " " + Наименование + Символы.ПС+ ФИОРуководителя;
		КонецЕсли;
		
		СоответствиеПолучателей.Вставить(ЭлементОрганизации, Кому);
		
	КонецЦикла;
	
	ПараметрыПечати.Вставить("СоответствиеПолучателей", СоответствиеПолучателей);
	Возврат ПараметрыПечати;
	
	
КонецФункции // СоответствиеДанныхОбОрганизациях()

Функция ТекстЗапросаЗаявления()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.Организация КАК Покупатель,
	|	Документ.Контрагент КАК Поставщик,
	|	Документ.Ответственный.ФизическоеЛицо КАК Получил,
	|	Документ.Организация.Префикс КАК Префикс,
	|	Документ.ЦенаВключаетНДС КАК УчитыватьНДС,
	|	Документ.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Контрагент) КАК ПоставщикПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Организация) КАК ПокупательПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Ответственный.ФизическоеЛицо) КАК ПолучилПредставление,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	Документ.Магазин КАК Магазин,
	|	Документ.ЧекККМПродажа.Дата КАК ЧекДатаПоДокументу,
	|	Документ.ЧекККМПродажа.НомерЧекаККМ КАК ЧекНомер,
	|	Документ.ДатаРождения КАК ДатаРожденияПоДокументу,
	|	Документ.ФизЛицо КАК ФизЛицо,
	|	Документ.ПоДокументу КАК ПаспортныеДанные
	|ИЗ
	|	Документ.ЧекККМ КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.ВидОперации = &ВидОперации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Упаковка.ЕдиницаИзмерения)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
	|	КОНЕЦ КАК ПредставлениеЕдиницыИзмеренияУпаковки,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Ссылка.ЧекККМПродажа КАК ОснованиеВозврата,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Возврат ТекстЗапроса;
КонецФункции // ТекстЗапросаЗаявления()

Процедура ПодготовитьЗапросИзРМК(Запрос, МассивОбъектов, ПараметрыПечати)
	
	Если МассивОбъектов.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	ОбъектРМК = МассивОбъектов[0];
	ЗапросТаблицаЧека = Новый Запрос;
	ЗапросТаблицаЧека.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.Организация КАК Покупатель,
	|	Документ.Контрагент КАК Поставщик,
	|	Документ.Ответственный.ФизическоеЛицо КАК Получил,
	|	Документ.Организация.Префикс КАК Префикс,
	|	Документ.ЦенаВключаетНДС КАК УчитыватьНДС,
	|	Документ.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Контрагент) КАК ПоставщикПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Организация) КАК ПокупательПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Документ.Ответственный.ФизическоеЛицо) КАК ПолучилПредставление,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	Документ.Магазин КАК Магазин,
	|	Документ.ЧекККМПродажа.Дата КАК ЧекДатаПоДокументу,
	|	Документ.ЧекККМПродажа.Номер КАК ЧекНомер,
	|	Документ.ДатаРождения КАК ДатаРожденияПоДокументу,
	|	Документ.ФизЛицо КАК ФизЛицо,
	|	Документ.ПоДокументу КАК ПаспортныеДанные
	|ИЗ
	|	Документ.ЧекККМ КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &ПустойЧек";
	ЗапросТаблицаЧека.Параметры.Вставить("ПустойЧек", Документы.ЧекККМ.ПустаяСсылка());
	
	РезультатТаблицыЧека = ЗапросТаблицаЧека.Выполнить();
	ТаблицаЧека = РезультатТаблицыЧека.Выгрузить();
	ТаблицаЧека.Очистить();
	СтрокаТаблицыЧека = ТаблицаЧека.Добавить();
	
	Ссылка = Новый УникальныйИдентификатор;
	СтрокаТаблицыЧека.Ссылка = Ссылка;
	СтрокаТаблицыЧека.Номер  = Строка(ОбъектРМК.ЧекККМПродажа.НомерЧекаККМ);
	СтрокаТаблицыЧека.Дата   = ТекущаяДатаСеанса();
	СтрокаТаблицыЧека.Покупатель   = ОбъектРМК.Организация;
	СтрокаТаблицыЧека.Поставщик    = ОбъектРМК.Контрагент;
	СтрокаТаблицыЧека.Получил      = ОбъектРМК.Ответственный.ФизическоеЛицо;
	СтрокаТаблицыЧека.Префикс      = ОбъектРМК.Организация.Префикс;
	СтрокаТаблицыЧека.УчитыватьНДС = ОбъектРМК.ЦенаВключаетНДС;
	СтрокаТаблицыЧека.ЦенаВключаетНДС = ОбъектРМК.ЦенаВключаетНДС;
	СтрокаТаблицыЧека.ПоставщикПредставление = ОбъектРМК.Контрагент.Наименование;
	СтрокаТаблицыЧека.ПокупательПредставление = ОбъектРМК.Организация.Наименование;
	СтрокаТаблицыЧека.ПолучилПредставление = ОбъектРМК.Ответственный.ФизическоеЛицо.Наименование;
	СтрокаТаблицыЧека.Магазин = ОбъектРМК.ЧекККМПродажа.Магазин;
	СтрокаТаблицыЧека.ЧекДатаПоДокументу = ОбъектРМК.ЧекККМПродажа.Дата;
	СтрокаТаблицыЧека.ЧекНомер = Строка(ОбъектРМК.ЧекККМПродажа.НомерЧекаККМ);
	Если ПараметрыПечати.Свойство("ПерсональныеДанные") Тогда
		ПараметрыПечати.ПерсональныеДанные.Свойство("ПоДокументу", СтрокаТаблицыЧека.ПаспортныеДанные);
		ПараметрыПечати.ПерсональныеДанные.Свойство("ФизЛицо", СтрокаТаблицыЧека.ФизЛицо);
		ПараметрыПечати.ПерсональныеДанные.Свойство("ДатаРождения", СтрокаТаблицыЧека.ДатаРожденияПоДокументу);
	КонецЕсли;
	Если ПараметрыПечати.Свойство("СуммаДокумента") Тогда
		ПараметрыПечати.Свойство("СуммаДокумента", СтрокаТаблицыЧека.СуммаДокумента);
	КонецЕсли;
	
	ЗапросТаблицаТоваров = Новый Запрос;
	ЗапросТаблицаТоваров.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Упаковка.ЕдиницаИзмерения)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
	|	КОНЕЦ КАК ПредставлениеЕдиницыИзмеренияУпаковки,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Ссылка.ЧекККМПродажа КАК ОснованиеВозврата,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ПустойЧек";
	ЗапросТаблицаТоваров.Параметры.Вставить("ПустойЧек", Документы.ЧекККМ.ПустаяСсылка());
	РезультатТаблицыТоваров = ЗапросТаблицаТоваров.Выполнить();
	ТаблицаТоваров = РезультатТаблицыТоваров.Выгрузить();
	ТаблицаТоваров.Очистить();
	
	Для каждого СтрокаРМК Из ОбъектРМК.Товары Цикл
		
		СтрокаТаблицыТоваров = ТаблицаТоваров.Добавить();
		СтрокаТаблицыТоваров.НомерСтроки = СтрокаРМК.НомерСтроки;
		СтрокаТаблицыТоваров.Номенклатура = СтрокаРМК.Номенклатура;
		СтрокаТаблицыТоваров.НоменклатураПредставление = СтрокаРМК.Номенклатура.НаименованиеПолное;
		СтрокаТаблицыТоваров.ХарактеристикаПредставление = СтрокаРМК.Характеристика.Наименование;
		СтрокаТаблицыТоваров.ПредставлениеБазовойЕдиницыИзмерения = СтрокаРМК.Номенклатура.ЕдиницаИзмерения;
		Если ЗначениеЗаполнено(СтрокаРМК.Упаковка) Тогда
			СтрокаТаблицыТоваров.ПредставлениеЕдиницыИзмеренияУпаковки = СтрокаРМК.Упаковка.ЕдиницаИзмерения.Наименование;
		Иначе
			СтрокаТаблицыТоваров.ПредставлениеЕдиницыИзмеренияУпаковки = СтрокаРМК.Номенклатура.ЕдиницаИзмерения.Наименование;
		КонецЕсли;
		СтрокаТаблицыТоваров.Количество = СтрокаРМК.Количество;
		СтрокаТаблицыТоваров.КоличествоУпаковок =  СтрокаРМК.КоличествоУпаковок;
		СтрокаТаблицыТоваров.Цена = СтрокаРМК.Цена;
		СтрокаТаблицыТоваров.Сумма = СтрокаРМК.Сумма;
		СтрокаТаблицыТоваров.СуммаНДС =  СтрокаРМК.СуммаНДС;
		СтрокаТаблицыТоваров.ОснованиеВозврата = ОбъектРМК.ЧекККМПродажа;
		СтрокаТаблицыТоваров.Ссылка = Ссылка;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.Покупатель КАК Покупатель,
	|	Документ.Поставщик КАК Поставщик,
	|	Документ.Получил КАК Получил,
	|	Документ.Префикс КАК Префикс,
	|	Документ.УчитыватьНДС КАК УчитыватьНДС,
	|	Документ.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	Документ.ПоставщикПредставление КАК ПоставщикПредставление,
	|	Документ.ПокупательПредставление КАК ПокупательПредставление,
	|	Документ.ПолучилПредставление КАК ПолучилПредставление,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	Документ.Магазин КАК Магазин,
	|	Документ.ЧекДатаПоДокументу КАК ЧекДатаПоДокументу,
	|	Документ.ЧекНомер КАК ЧекНомер,
	|	Документ.ДатаРожденияПоДокументу КАК ДатаРожденияПоДокументу,
	|	Документ.ФизЛицо КАК ФизЛицо,
	|	Документ.ПаспортныеДанные КАК ПаспортныеДанные
	|ПОМЕСТИТЬ ТаблицаЧекаВЗапросе
	|ИЗ
	|	&ТаблицаЧека КАК Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.НоменклатураПредставление КАК НоменклатураПредставление,
	|	ТаблицаТовары.ХарактеристикаПредставление КАК ХарактеристикаПредставление,
	|	ТаблицаТовары.ПредставлениеБазовойЕдиницыИзмерения КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ТаблицаТовары.ПредставлениеЕдиницыИзмеренияУпаковки КАК ПредставлениеЕдиницыИзмеренияУпаковки,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.ОснованиеВозврата КАК ОснованиеВозврата,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаТоварыВЗапросе
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЧекаВЗапросе.Ссылка КАК Ссылка,
	|	ТаблицаЧекаВЗапросе.Номер КАК Номер,
	|	ТаблицаЧекаВЗапросе.Дата КАК Дата,
	|	ТаблицаЧекаВЗапросе.Покупатель КАК Покупатель,
	|	ТаблицаЧекаВЗапросе.Поставщик КАК Поставщик,
	|	ТаблицаЧекаВЗапросе.Получил КАК Получил,
	|	ТаблицаЧекаВЗапросе.Префикс КАК Префикс,
	|	ТаблицаЧекаВЗапросе.УчитыватьНДС КАК УчитыватьНДС,
	|	ТаблицаЧекаВЗапросе.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ТаблицаЧекаВЗапросе.ПоставщикПредставление КАК ПоставщикПредставление,
	|	ТаблицаЧекаВЗапросе.ПокупательПредставление КАК ПокупательПредставление,
	|	ТаблицаЧекаВЗапросе.ПолучилПредставление КАК ПолучилПредставление,
	|	ТаблицаЧекаВЗапросе.СуммаДокумента КАК СуммаДокумента,
	|	ТаблицаЧекаВЗапросе.Магазин КАК Магазин,
	|	ТаблицаЧекаВЗапросе.ЧекДатаПоДокументу КАК ЧекДатаПоДокументу,
	|	ТаблицаЧекаВЗапросе.ЧекНомер КАК ЧекНомер,
	|	ТаблицаЧекаВЗапросе.ДатаРожденияПоДокументу КАК ДатаРожденияПоДокументу,
	|	ТаблицаЧекаВЗапросе.ФизЛицо КАК ФизЛицо,
	|	ТаблицаЧекаВЗапросе.ПаспортныеДанные КАК ПаспортныеДанные
	|ИЗ
	|	ТаблицаЧекаВЗапросе КАК ТаблицаЧекаВЗапросе
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоварыВЗапросе.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоварыВЗапросе.Номенклатура КАК Номенклатура,
	|	ТаблицаТоварыВЗапросе.НоменклатураПредставление КАК НоменклатураПредставление,
	|	ТаблицаТоварыВЗапросе.ХарактеристикаПредставление КАК ХарактеристикаПредставление,
	|	ТаблицаТоварыВЗапросе.ПредставлениеБазовойЕдиницыИзмерения КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ТаблицаТоварыВЗапросе.ПредставлениеЕдиницыИзмеренияУпаковки КАК ПредставлениеЕдиницыИзмеренияУпаковки,
	|	ТаблицаТоварыВЗапросе.Количество КАК Количество,
	|	ТаблицаТоварыВЗапросе.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТоварыВЗапросе.Цена КАК Цена,
	|	ТаблицаТоварыВЗапросе.Сумма КАК Сумма,
	|	ТаблицаТоварыВЗапросе.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоварыВЗапросе.ОснованиеВозврата КАК ОснованиеВозврата,
	|	ТаблицаТоварыВЗапросе.Ссылка КАК Ссылка
	|ИЗ
	|	ТаблицаТоварыВЗапросе КАК ТаблицаТоварыВЗапросе
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("ТаблицаЧека", ТаблицаЧека);
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
КонецПроцедуры

// Функция формирует текст запроса по подарочным сертификатам.
// Возвращаемое значение: ТекстЗапроса - Строка.
Функция ТекстЗапросаТаблицаПодарочныхСертификатов()
	
	ТекстЗапроса = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура КАК ПодарочныйСертификат,
	|	ТабСерийныеНомера.СерийныйНомер КАК НомерСертификата,
	|	ТабТовары.Номенклатура.Номинал КАК Номинал,
	|	ТабТовары.Цена КАК Цена,
	|	ТабТовары.Номенклатура.ЧастичноеПогашение КАК ЧастичноеПогашение,
	|	ТабТовары.Номенклатура.ПроизвольныйНоминал КАК ПроизвольныйНоминал,
	|	ТабТовары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	ТабТовары.ЭтоВозврат КАК ЭтоВозврат
	|ПОМЕСТИТЬ ТаблицаСведенийОПодарочныхСертификатах
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабСерийныеНомера КАК ТабСерийныеНомера
	|		ПО ТабТовары.КлючСвязиСерийныхНомеров = ТабСерийныеНомера.КлючСвязиСерийныхНомеров
	|ГДЕ
	|	(ТабТовары.Номенклатура.ЧастичноеПогашение
	|			ИЛИ ТабТовары.Номенклатура.ПроизвольныйНоминал)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаСведенийОПодарочныхСертификатах.ПодарочныйСертификат КАК ПодарочныйСертификат,
	|	ТаблицаСведенийОПодарочныхСертификатах.НомерСертификата КАК НомерСертификата,
	|	ВЫБОР
	|		КОГДА ТаблицаСведенийОПодарочныхСертификатах.ПроизвольныйНоминал
	|			ТОГДА ЕСТЬNULL(ТаблицаСведенийОПодарочныхСертификатах.Цена, 0)
	|		ИНАЧЕ ТаблицаСведенийОПодарочныхСертификатах.Номинал
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ТаблицаДвиженийПодарочныхСертификатов
	|ИЗ
	|	ТаблицаСведенийОПодарочныхСертификатах КАК ТаблицаСведенийОПодарочныхСертификатах 
	|ГДЕ 
	|	Не ТаблицаСведенийОПодарочныхСертификатах.ЭтоВозврат
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ПогашениеПодарочныхСертификатовОстатки.ПодарочныйСертификат,
	|	ПогашениеПодарочныхСертификатовОстатки.СерийныйНомер,
	|	ВЫБОР
	|		КОГДА ПогашениеПодарочныхСертификатовОстатки.ЧастичноеПогашение
	|			ТОГДА ПогашениеПодарочныхСертификатовОстатки.СуммаПогашенияСертификата
	|		ИНАЧЕ ЕСТЬNULL(ПогашениеПодарочныхСертификатовОстатки.СуммаОстаток, 0)
	|	КОНЕЦ
	|ИЗ
	|	ПогашениеПодарочныхСертификатовОстатки КАК ПогашениеПодарочныхСертификатовОстатки
	|ГДЕ (ПогашениеПодарочныхСертификатовОстатки.ЧастичноеПогашение
	|			ИЛИ ПогашениеПодарочныхСертификатовОстатки.ПроизвольныйНоминал)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийПодарочныхСертификатов.Период КАК Период,
	|	ТаблицаДвиженийПодарочныхСертификатов.ВидДвижения КАК ВидДвижения,
	|	ТаблицаДвиженийПодарочныхСертификатов.ПодарочныйСертификат КАК ПодарочныйСертификат,
	|	ТаблицаДвиженийПодарочныхСертификатов.НомерСертификата КАК НомерСертификата,
	|	СУММА(ТаблицаДвиженийПодарочныхСертификатов.Сумма) КАК Сумма
	|ИЗ
	|	ТаблицаДвиженийПодарочныхСертификатов КАК ТаблицаДвиженийПодарочныхСертификатов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДвиженийПодарочныхСертификатов.Период,
	|	ТаблицаДвиженийПодарочныхСертификатов.ВидДвижения,
	|	ТаблицаДвиженийПодарочныхСертификатов.ПодарочныйСертификат,
	|	ТаблицаДвиженийПодарочныхСертификатов.НомерСертификата";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьНастройкиКодаВозврата(НеПечататьКодВозвратаСБП,НеПечататьКодВозвратаНСПК)
	
	РабочееМесто = МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента();
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	НастройкиРабочегоМестаКассира.НеПечататьКодВозвратаСБП КАК НеПечататьКодВозвратаСБП,
	|	НастройкиРабочегоМестаКассира.НеПечататьКодВозвратаНСПК КАК НеПечататьКодВозвратаНСПК
	|ИЗ
	|	Справочник.НастройкиРабочегоМестаКассира КАК НастройкиРабочегоМестаКассира
	|ГДЕ
	|	НЕ НастройкиРабочегоМестаКассира.ПометкаУдаления
	|	И НастройкиРабочегоМестаКассира.РабочееМесто = &РабочееМесто");
	Запрос.УстановитьПараметр("РабочееМесто", РабочееМесто);
	
	ВыборкаНастроек = Запрос.Выполнить().Выбрать();
	КоличествоНастроек = ВыборкаНастроек.Количество();
	
	Если КоличествоНастроек = 1 И ВыборкаНастроек.Следующий() Тогда 
	НеПечататьКодВозвратаСБП  = ВыборкаНастроек.НеПечататьКодВозвратаСБП;
	НеПечататьКодВозвратаНСПК = ВыборкаНастроек.НеПечататьКодВозвратаНСПК;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ДействияПриОбменеЕГАИС

// Статус после подготовки к передаче данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречисленияСсылка.СтатусыИнформированияЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//
Функция СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция) Экспорт
	
	Возврат ЧекиЕГАИС.СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция);
	
КонецФункции

// Статус после передачи данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречисленияСсылка.СтатусыИнформированияЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//
Функция СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	Возврат ЧекиЕГАИС.СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки);
	
КонецФункции

// Статус после получения данных из ЕГАИС.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Документ, для которого требуется обновить статус.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция обмена с ЕГАИС.
//  ДополнительныеПараметры - Неопределено, Структура со свойствами:
//   * СтатусОбработки - Перечисление.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция, на которую получена квитанция.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречисленияСсылка.СтатусыИнформированияЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//
Функция СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	Возврат ЧекиЕГАИС.СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры);
	
КонецФункции

// Обновить статус после подготовки к передаче данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
// 
// Возвращаемое значение:
//  Перечисления.СтатусыИнформированияЕГАИС - Новый статус.
//
Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	Возврат ЧекиЕГАИС.ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры);
	
КонецФункции

// Обновить статус после передачи данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения
// 
// Возвращаемое значение:
//  Перечисления.СтатусыИнформированияЕГАИС - Новый статус.
//
Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры = Неопределено) Экспорт
	
	Возврат ЧекиЕГАИС.ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры);
	
КонецФункции

// Обновить статус после получения данных из ЕГАИС.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Документ, для которого требуется обновить статус.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция обмена с ЕГАИС.
//  ДополнительныеПараметры - Неопределено, Структура со свойствами:
//   * СтатусОбработки - Перечисление.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция, на которую получена квитанция.
// 
// Возвращаемое значение:
//  Перечисления.СтатусыИнформированияЕГАИС - Новый статус.
//
Функция ОбновитьСтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	Возврат ЧекиЕГАИС.ОбновитьСтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры);
	
КонецФункции

// Получить последовательность операций в течении жизненного цикла документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Документ, для которого требуется обновить статус.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. функцию ИнтеграцияЕГАИС.ПустаяТаблицаПоследовательностьОпераций().
//
Функция ПоследовательностьОпераций(ДокументСсылка) Экспорт
	
	Возврат ЧекиЕГАИС.ПоследовательностьОпераций(ДокументСсылка);
	
КонецФункции

// Опеределить необходимость перезаписи движений.
//
// Параметры:
//  ПредыдущийСтатус - ПеречислениеСсылка.СтатусыИнформированияЕГАИС - Предыдущий статус.
//  НовыйСтатус - ПеречислениеСсылка.СтатусыИнформированияЕГАИС - Предыдущий статус.
// 
// Возвращаемое значение:
//  Булево - Необходимость перезаписи движений.
//
Функция ОбновлятьДвижения(ПредыдущийСтатус, НовыйСтатус) Экспорт
	Возврат Ложь;
КонецФункции

// Определить необходимость перерасчета статуса оформления документов.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекЕГАИС - Документ, по которому требуется рассчитать статус оформления.
//  ПредыдущийСтатус - ПеречислениеСсылка.СтатусыИнформированияЕГАИС - Предыдущий статус.
//  НовыйСтатус - ПеречислениеСсылка.СтатусыИнформированияЕГАИС - Предыдущий статус.
// 
// Возвращаемое значение:
//  Булево - Необходимость перерасчета статуса оформления.
//
Функция РассчитатьСтатусОформления(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус) Экспорт
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#Область Статусы

// Возвращает статус по умолчанию.
// 
// Возвращаемое значение:
//  Перечисления.СтатусыИнформированияЕГАИС - Статус по-умолчанию.
//
Функция СтатусПоУмолчанию() Экспорт
	
	Возврат ЧекиЕГАИС.СтатусПоУмолчанию();
	
КонецФункции

// Возвращает дальнейшее действие по умолчанию.
// 
// Возвращаемое значение:
//  Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие по-умолчанию.
//
Функция ДальнейшееДействиеПоУмолчанию() Экспорт
	
	Возврат ЧекиЕГАИС.ДальнейшееДействиеПоУмолчанию();
	
КонецФункции

#КонецОбласти

#Область СообщенияЕГАИС

// Сообщение к передаче XML
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
// 
// Возвращаемое значение:
//  Строка - Текст сообщения XML
//
Функция СообщениеКПередачеXML(ДокументСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные Тогда
		
		Возврат ЧекЕГАИСXML(ДокументСсылка, ДополнительныеПараметры);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию Тогда
		
		Возврат ЧекЕГАИСОтменаXML(ДокументСсылка, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецФункции

Функция ЧекЕГАИСXML(ДокументСсылка, ДополнительныеПараметры)
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЕГАИСПрисоединенныеФайлы.Документ      КАК Ссылка,
		|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
		|ПОМЕСТИТЬ Версии
		|ИЗ
		|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
		|ГДЕ
		|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
		|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
		|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
		|СГРУППИРОВАТЬ ПО
		|	ЕГАИСПрисоединенныеФайлы.Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Шапка.Номер                                  КАК Номер,
		|	Шапка.Дата                                   КАК Дата,
		|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)           КАК ПоследнийНомерВерсии,
		|	Шапка.Ссылка                                 КАК ДокументОснование,
		|	КлассификаторОрганизацийЕГАИС.Ссылка         КАК ОрганизацияЕГАИС,
		|	КлассификаторОрганизацийЕГАИС.Код            КАК ИдентификаторФСРАР,
		|	КлассификаторОрганизацийЕГАИС.ФорматОбмена   КАК ФорматОбмена,
		|	КлассификаторОрганизацийЕГАИС.ИНН            КАК ИНН,
		|	КлассификаторОрганизацийЕГАИС.КПП            КАК КПП,
		|	КлассификаторОрганизацийЕГАИС.ТорговыйОбъект КАК ТорговыйОбъект,
		|	Шапка.Ответственный                          КАК Ответственный,
		|	&НомерСмены                                  КАК НомерСмены,
		|	&НомерЧека                                   КАК НомерЧекаККМ,
		|	ВЫБОР
		|		КОГДА &СерийныйНомер = НЕОПРЕДЕЛЕНО
		|			ТОГДА Шапка.КассаККМ.СерийныйНомер
		|		ИНАЧЕ &СерийныйНомер
		|	КОНЕЦ                                        КАК СерийныйНомерККМ
		|ПОМЕСТИТЬ Шапка
		|ИЗ
		|	Документ.ЧекККМ КАК Шапка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
		|		ПО Шапка.Ссылка = Версии.Ссылка
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|		ПО Шапка.Магазин = КлассификаторОрганизацийЕГАИС.ТорговыйОбъект
		|			И Шапка.Организация = КлассификаторОрганизацийЕГАИС.Контрагент
		|ГДЕ
		|	Шапка.Ссылка = &Ссылка
		|;
		|
		|//#РезультатЗапроса#////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	*
		|ИЗ
		|	Шапка
		|",
		"Шапка");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Товары.КлючСвязи                                    КАК ИдентификаторСтроки,
		|	Товары.НомерСтроки                                  КАК НомерСтроки,
		|	Товары.Количество                                   КАК Количество,
		|	ВЫБОР
		|		КОГДА Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
		|			ТОГДА ВЫБОР
		|					КОГДА Товары.Количество <> 0
		|						ТОГДА Товары.Сумма / Товары.Количество
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Количество <> 0
		|					ТОГДА -Товары.Сумма / Товары.Количество
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ                                               КАК Цена,
		|	Товары.Штрихкод                                     КАК Штрихкод,
		|	Товары.Номенклатура                                 КАК Номенклатура,
		|	Товары.Характеристика                               КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
		|	Товары.Номенклатура.ОбъемДАЛ * 10 КАК Объем,
		|	Товары.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ
		|ИЗ
		|	Документ.ЧекККМ.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|	И ЕСТЬNULL(Товары.Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
		|	И Товары.Номенклатура.АлкогольнаяПродукция
		|",
		"Товары");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТабличнаяЧасть.КлючСвязи                      КАК ИдентификаторСтроки,
		|	Шапка.ОрганизацияЕГАИС                        КАК ОрганизацияЕГАИС,
		|	ТабличнаяЧасть.Справка2                       КАК Справка2,
		|	ТабличнаяЧасть.Справка2.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
		|	ТабличнаяЧасть.АкцизнаяМарка                  КАК ШтрихкодУпаковки,
		|	ТабличнаяЧасть.ШтрихкодУпаковки               КАК ШтрихкодРодительскойУпаковки,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеКоличество     КАК ЧастичноеВыбытиеКоличество,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеВариантУчета   КАК ЧастичноеВыбытиеВариантУчета,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеНоменклатура   КАК ЧастичноеВыбытиеНоменклатура,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеХарактеристика КАК ЧастичноеВыбытиеХарактеристика,
		|	ТабличнаяЧасть.ВыбытиеБутылки                 КАК ВыбытиеБутылки
		|
		|ПОМЕСТИТЬ ВТВложенныеШтрихкодыИсходныеДанные
		|ИЗ
		|	Документ.ЧекККМ.АкцизныеМарки КАК ТабличнаяЧасть
		|		ЛЕВОЕ СОЕДИНЕНИЕ Шапка КАК Шапка
		|		ПО ИСТИНА
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	ШтрихкодУпаковки,
		|	ШтрихкодРодительскойУпаковки
		|");
	
	ПараметрыФормированияТекстаЗапроса = ШтрихкодированиеЕГАИС.ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов();
	ПараметрыФормированияТекстаЗапроса.ДокументСсылка = Неопределено;
	ТекстыЗапроса.Добавить(
		ШтрихкодированиеЕГАИС.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
		"ВложенныеШтрихкоды");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка",   ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Перечисления.ВидыДокументовЕГАИС.ЧекККМ);
	Запрос.УстановитьПараметр("ПустыеЗначенияНоменклатуры", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ПараметрыЧека = ИнтеграцияЕГАИСРТ.ПараметрыЧека(ДокументСсылка);
		НомерЧека     = ПараметрыЧека.НомерЧека;
		НомерСмены    = ПараметрыЧека.НомерСмены;
		СерийныйНомер = ПараметрыЧека.СерийныйНомер;
	Иначе
		НомерЧека     = ДополнительныеПараметры.НомерЧека;
		НомерСмены    = ДополнительныеПараметры.НомерСмены;
		СерийныйНомер = ДополнительныеПараметры.СерийныйНомер;
	КонецЕсли;
	
	Если НомерЧека = 0 Тогда
		НомерЧека = 1;
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Передача данных в ЕГАИС'"),
			УровеньЖурналаРегистрации.Ошибка,,
			ДокументСсылка,
			НСтр("ru = 'Номер чека = 0. Проверьте версию драйвера кассового аппарата.'"));
	КонецЕсли;
	
	Если НомерСмены = 0 Тогда
		НомерСмены = 1;
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Передача данных в ЕГАИС'"),
			УровеньЖурналаРегистрации.Ошибка,,
			ДокументСсылка,
			НСтр("ru = 'Номер Кассовой смены = 0. Проверьте версию драйвера кассового аппарата.'"));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НомерЧека",     НомерЧека);
	Запрос.УстановитьПараметр("НомерСмены",    НомерСмены);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	РезультатыЗапроса = ИнтеграцияИС.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
	
	Возврат ЧекиЕГАИС.ЧекXML(ДокументСсылка, РезультатыЗапроса, МенеджерВременныхТаблиц);
	
КонецФункции

Функция ЧекЕГАИСОтменаXML(ДокументСсылка, ДополнительныеПараметры)
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЕГАИСПрисоединенныеФайлы.Документ      КАК Ссылка,
		|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
		|ПОМЕСТИТЬ Версии
		|ИЗ
		|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
		|ГДЕ
		|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
		|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
		|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
		|СГРУППИРОВАТЬ ПО
		|	ЕГАИСПрисоединенныеФайлы.Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Шапка.Номер                           КАК Номер,
		|	Шапка.Дата                            КАК Дата,
		|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)    КАК ПоследнийНомерВерсии,
		|	Шапка.Ссылка                          КАК ДокументОснование,
		|	КлассификаторОрганизацийЕГАИС.Ссылка  КАК ОрганизацияЕГАИС,
		|	КлассификаторОрганизацийЕГАИС.Код            КАК ИдентификаторФСРАР,
		|	КлассификаторОрганизацийЕГАИС.ФорматОбмена   КАК ФорматОбмена,
		|	КлассификаторОрганизацийЕГАИС.ИНН            КАК ИНН,
		|	КлассификаторОрганизацийЕГАИС.КПП            КАК КПП,
		|	КлассификаторОрганизацийЕГАИС.ТорговыйОбъект КАК ТорговыйОбъект,
		|	Шапка.Ответственный                          КАК Ответственный,
		|	&НомерСмены                                  КАК НомерСмены,
		|	&НомерЧека                                   КАК НомерЧекаККМ,
		|	ВЫБОР
		|		КОГДА &СерийныйНомер = НЕОПРЕДЕЛЕНО
		|			ТОГДА Шапка.КассаККМ.СерийныйНомер
		|		ИНАЧЕ &СерийныйНомер
		|	КОНЕЦ                                        КАК СерийныйНомерККМ
		|ПОМЕСТИТЬ Шапка
		|ИЗ
		|	Документ.ЧекККМ КАК Шапка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
		|		ПО Шапка.Ссылка = Версии.Ссылка
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|		ПО Шапка.Магазин = КлассификаторОрганизацийЕГАИС.ТорговыйОбъект
		|			И Шапка.Организация = КлассификаторОрганизацийЕГАИС.Контрагент
		|ГДЕ
		|	Шапка.Ссылка = &Ссылка
		|;
		|
		|//#РезультатЗапроса#////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	*
		|ИЗ
		|	Шапка
		|",
		"Шапка");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Товары.КлючСвязи                  КАК ИдентификаторСтроки,
		|	Товары.НомерСтроки                КАК НомерСтроки,
		|	Товары.Количество                 КАК Количество,
		|	ВЫБОР КОГДА Товары.Количество <> 0 ТОГДА
		|		-Товары.Сумма / Товары.Количество
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК Цена,
		|	Товары.Штрихкод                   КАК Штрихкод,
		|	Товары.Номенклатура               КАК Номенклатура,
		|	Товары.Характеристика             КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
		|	Товары.Номенклатура.ОбъемДАЛ * 10 КАК Объем,
		|	Товары.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ
		|ИЗ
		|	Документ.ЧекККМ.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|	И ЕСТЬNULL(Товары.Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
		|	И Товары.Номенклатура.АлкогольнаяПродукция
		|",
		"Товары");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Шапка.ОрганизацияЕГАИС                        КАК ОрганизацияЕГАИС,
		|	ТабличнаяЧасть.КлючСвязи                      КАК ИдентификаторСтроки,
		|	ТабличнаяЧасть.Справка2.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
		|	ТабличнаяЧасть.Справка2                       КАК Справка2,
		|	ТабличнаяЧасть.ШтрихкодУпаковки               КАК ШтрихкодРодительскойУпаковки,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеКоличество     КАК ЧастичноеВыбытиеКоличество,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеВариантУчета   КАК ЧастичноеВыбытиеВариантУчета,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеНоменклатура   КАК ЧастичноеВыбытиеНоменклатура,
		|	ТабличнаяЧасть.ЧастичноеВыбытиеХарактеристика КАК ЧастичноеВыбытиеХарактеристика,
		|	ТабличнаяЧасть.ВыбытиеБутылки                 КАК ВыбытиеБутылки,
		|	ТабличнаяЧасть.АкцизнаяМарка                  КАК ШтрихкодУпаковки
		|ПОМЕСТИТЬ ВТВложенныеШтрихкодыИсходныеДанные
		|ИЗ
		|	Документ.ЧекККМ.АкцизныеМарки КАК ТабличнаяЧасть
		|		ЛЕВОЕ СОЕДИНЕНИЕ Шапка КАК Шапка
		|		ПО ИСТИНА
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	ШтрихкодУпаковки,
		|	ШтрихкодРодительскойУпаковки
		|");
	
	ПараметрыФормированияТекстаЗапроса = ШтрихкодированиеЕГАИС.ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов();
	ПараметрыФормированияТекстаЗапроса.ДокументСсылка = Неопределено;
	ТекстыЗапроса.Добавить(
		ШтрихкодированиеЕГАИС.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
		"ВложенныеШтрихкоды");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка",   ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Перечисления.ВидыДокументовЕГАИС.ЧекККМОтменаПередачи);
	Запрос.УстановитьПараметр("ПустыеЗначенияНоменклатуры", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ПараметрыЧека = ИнтеграцияЕГАИСРТ.ПараметрыЧека(ДокументСсылка);
		НомерЧека     = ПараметрыЧека.НомерЧека;
		НомерСмены    = ПараметрыЧека.НомерСмены;
		СерийныйНомер = ПараметрыЧека.СерийныйНомер;
	Иначе
		НомерЧека     = ДополнительныеПараметры.НомерЧека;
		НомерСмены    = ДополнительныеПараметры.НомерСмены;
		СерийныйНомер = ДополнительныеПараметры.СерийныйНомер;
	КонецЕсли;
	
	Если НомерЧека = 0 Тогда
		НомерЧека = 1;
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Передача данных в ЕГАИС'"),
			УровеньЖурналаРегистрации.Ошибка,,
			ДокументСсылка,
			НСтр("ru = 'Номер чека = 0. Проверьте версию драйвера кассового аппарата.'"));
	КонецЕсли;
	
	Если НомерСмены = 0 Тогда
		НомерСмены = 1;
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Передача данных в ЕГАИС'"),
			УровеньЖурналаРегистрации.Ошибка,,
			ДокументСсылка,
			НСтр("ru = 'Номер Кассовой смены = 0. Проверьте версию драйвера кассового аппарата.'"));
	КонецЕсли;
		
	Запрос.УстановитьПараметр("НомерЧека",     НомерЧека);
	Запрос.УстановитьПараметр("НомерСмены",    НомерСмены);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	РезультатыЗапроса = ИнтеграцияИС.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
	
	Возврат ЧекиЕГАИС.ЧекXML(ДокументСсылка, РезультатыЗапроса,
		МенеджерВременныхТаблиц, Перечисления.ВидыДокументовЕГАИС.ЧекККМОтменаПередачи);
	
КонецФункции

Процедура ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса) Экспорт
	
	ПриИзмененииСтатусаЧека(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса);
	
КонецПроцедуры

Процедура ПриИзмененииСтатусаЧека(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса) Экспорт
	
	ПолноеИмя = ДокументСсылка.Метаданные().ПолноеИмя();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
	ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидОперации");
	Если ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
		ДобавитьДвижения = ИнтеграцияЕГАИС.СтатусТребуетДобавленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокСвободныйОстаток(), ПредыдущийСтатус, НовыйСтатус)
		               ИЛИ ИнтеграцияЕГАИС.СтатусТребуетДобавленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокКоличество(), ПредыдущийСтатус, НовыйСтатус);
		УдалитьДвижения = ИнтеграцияЕГАИС.СтатусТребуетУдаленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокСвободныйОстаток(), ПредыдущийСтатус, НовыйСтатус)
		               ИЛИ ИнтеграцияЕГАИС.СтатусТребуетУдаленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокКоличество(), ПредыдущийСтатус, НовыйСтатус);
	Иначе
		УдалитьДвижения = ИнтеграцияЕГАИС.СтатусТребуетДобавленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокСвободныйОстаток(), ПредыдущийСтатус, НовыйСтатус)
		               Или ИнтеграцияЕГАИС.СтатусТребуетДобавленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокКоличество(), ПредыдущийСтатус, НовыйСтатус);
		ДобавитьДвижения = ИнтеграцияЕГАИС.СтатусТребуетУдаленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокСвободныйОстаток(), ПредыдущийСтатус, НовыйСтатус)
		               Или ИнтеграцияЕГАИС.СтатусТребуетУдаленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокКоличество(), ПредыдущийСтатус, НовыйСтатус);
	КонецЕсли;
	
	ЗапросОрганизация = Новый Запрос;
	ЗапросОрганизация.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК ОрганизацияЕГАИС,
	|	ЧекККМАкцизныеМарки.АкцизнаяМарка КАК АкцизнаяМарка,
	|	ЧекККМАкцизныеМарки.Справка2 КАК Справка2,
	|	ЧекККМАкцизныеМарки.Справка2.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ЧекККМАкцизныеМарки.ЧастичноеВыбытиеКоличество КАК ЧастичноеВыбытиеКоличество,
	|	ЧекККМАкцизныеМарки.ВыбытиеБутылки КАК ВыбытиеБутылки
	|ИЗ
	|	Документ.ЧекККМ КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО Шапка.Магазин = КлассификаторОрганизацийЕГАИС.ТорговыйОбъект
	|			И Шапка.Организация = КлассификаторОрганизацийЕГАИС.Контрагент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМАкцизныеМарки
	|		ПО Шапка.Ссылка = ЧекККМАкцизныеМарки.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ.Товары КАК ЧекККМТовары
	|		ПО (ЧекККМАкцизныеМарки.Ссылка = ЧекККМТовары.Ссылка)
	|			И (ЧекККМАкцизныеМарки.КлючСвязи = ЧекККМТовары.КлючСвязи)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (ЧекККМТовары.Номенклатура = СпрНоменклатура.Ссылка)
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|	И СпрНоменклатура.АлкогольнаяПродукция";
	ЗапросОрганизация.УстановитьПараметр("Ссылка", ДокументСсылка);
	АкцизныеМарки = ЗапросОрганизация.Выполнить().Выгрузить();
	// Для списания из регистра №3
	Если ДобавитьДвижения Тогда
		
		Для Каждого СтрокаТЧ Из АкцизныеМарки Цикл
			
			Если ЗначениеЗаполнено(СтрокаТЧ.АкцизнаяМарка) Тогда
				ТребуетсяРезервирование = ИнтеграцияЕГАИС.СтатусТребуетДобавленияДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокСвободныйОстаток(), ПредыдущийСтатус, НовыйСтатус)
				                        И ИнтеграцияЕГАИС.НетДвижений(ЧекиЕГАИС.СтатусыДвиженийАкцизныхМарокКоличество(), ПредыдущийСтатус, НовыйСтатус);
				
				ДанныеЗаписиСтатуса = РегистрыСведений.АкцизныеМаркиЕГАИС.ПоляЗаписиСтатусаУпаковки();
				ДанныеЗаписиСтатуса.ОрганизацияЕГАИС     = СтрокаТЧ.ОрганизацияЕГАИС;
				ДанныеЗаписиСтатуса.АкцизнаяМарка        = СтрокаТЧ.АкцизнаяМарка;
				ДанныеЗаписиСтатуса.Справка2             = СтрокаТЧ.Справка2;
				ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТЧ.Справка2.АлкогольнаяПродукция;
				
				Если ЗначениеЗаполнено(СтрокаТЧ.ЧастичноеВыбытиеКоличество)
					И Не СтрокаТЧ.ВыбытиеБутылки Тогда
					ДанныеЗаписиСтатуса.Статус = Перечисления.СтатусыАкцизныхМарок.ВскрытаяБутылка;
				ИначеЕсли ТребуетсяРезервирование Тогда
					ДанныеЗаписиСтатуса.Статус = Перечисления.СтатусыАкцизныхМарок.ВРезерве;
				Иначе
					ДанныеЗаписиСтатуса.Статус = Перечисления.СтатусыАкцизныхМарок.Реализована;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаТЧ.ЧастичноеВыбытиеКоличество)
					И Не ТребуетсяРезервирование Тогда
					ДанныеЗаписиСтатуса.КоличествоЧастичногоВыбытия = СтрокаТЧ.ЧастичноеВыбытиеКоличество;
				КонецЕсли;
				
				ДанныеЗаписиСтатуса.Основание = ДокументСсылка;
				РегистрыСведений.АкцизныеМаркиЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписиСтатуса);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// Для восстановления записи в регистре №3
	Если УдалитьДвижения Тогда
		
		Для Каждого СтрокаТЧ Из АкцизныеМарки Цикл
			
			Если ЗначениеЗаполнено(СтрокаТЧ.АкцизнаяМарка) Тогда
				ДанныеЗаписиСтатуса = РегистрыСведений.АкцизныеМаркиЕГАИС.ПоляЗаписиСтатусаУпаковки();
				ДанныеЗаписиСтатуса.ОрганизацияЕГАИС     = СтрокаТЧ.ОрганизацияЕГАИС;
				ДанныеЗаписиСтатуса.АкцизнаяМарка        = СтрокаТЧ.АкцизнаяМарка;
				ДанныеЗаписиСтатуса.Справка2             = СтрокаТЧ.Справка2;
				ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
				ДанныеЗаписиСтатуса.Статус               = Перечисления.СтатусыАкцизныхМарок.ВНаличии;
				
				ДанныеЗаписиСтатуса.Основание = ДокументСсылка;
				РегистрыСведений.АкцизныеМаркиЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписиСтатуса);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СканированиеМаркируемойПродукции

Функция ОбработатьДанныеШтрихкода(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды = Неопределено) Экспорт
	
	// Подготовка параметров штрихкода для обработки.
	ПараметрыШтрихкодаДляОбработки = ШтрихкодированиеИСРТ.ПараметрыШтрихкодаДляОбработки(Форма, ПараметрыСканирования, ДанныеШтрихкода);
	
	Объект              = ПараметрыШтрихкодаДляОбработки.Объект;
	РезультатОбработки  = ПараметрыШтрихкодаДляОбработки.РезультатОбработки;
	ПараметрыЗаполнения = ПараметрыШтрихкодаДляОбработки.ПараметрыЗаполнения;
	НастройкиКэша       = ПараметрыШтрихкодаДляОбработки.НастройкиКэша;
	
	Если ДанныеШтрихкода.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
		
		ДеревоУпаковок = Неопределено;
		Если ВложенныеШтрихкоды <> Неопределено Тогда
			ДеревоУпаковок = ВложенныеШтрихкоды.ДеревоУпаковок;
		ИначеЕсли ДанныеШтрихкода.Свойство("АдресДереваУпаковок")
			И Не ПустаяСтрока(ДанныеШтрихкода.АдресДереваУпаковок) Тогда
			ДеревоУпаковок = ПолучитьИзВременногоХранилища(ДанныеШтрихкода.АдресДереваУпаковок);
		КонецЕсли;
		
		Если НЕ ДеревоУпаковок = Неопределено Тогда
			ШтрихкодированиеИСРТ.ОбработатьДанныеШтрихкодовДереваУпаковок(ДеревоУпаковок, ПараметрыШтрихкодаДляОбработки, Форма);
		КонецЕсли;
		
	Иначе
		
		// Подготовка данных штрихкода для обработки.
		ДанныеШтрихкодаДляОбработки = ШтрихкодированиеИСРТ.ДанныеШтрихкодаДляОбработки();
		ЗаполнитьЗначенияСвойств(ДанныеШтрихкодаДляОбработки, ДанныеШтрихкода);
		
		ДанныеШтрихкодаДляОбработки.Количество = ИнтеграцияИСРТ.КоличествоПродукцииПоДаннымШтрихкода(ДанныеШтрихкода, ПараметрыСканирования);
		ШтрихкодированиеИСРТ.ОбработатьДанныеШтрихкодаПотребительскойУпаковки(ДанныеШтрихкодаДляОбработки, ПараметрыШтрихкодаДляОбработки, Форма);
		
	КонецЕсли;
	
	ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(Форма, НастройкиКэша);
	ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(Форма, НастройкиКэша);
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(Форма);
	
	Возврат РезультатОбработки;
	
КонецФункции

#Область АлкогольнаяПродукция

Функция ТаблицаАлкогольнойПродукцииКОпределениюСправок2(ДокументОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТабличнаяЧасть.НоменклатураЕГАИС КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТабличнаяЧасть.Количество КАК Количество
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК ТабличнаяЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка) КАК Справка2,
	|	СУММА(ТабличнаяЧасть.Количество) КАК Количество,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ) КАК Маркируемая
	|ИЗ
	|	ВТТовары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукции
	|		ПО (СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС = ВидыАлкогольнойПродукции.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (СправочникНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
	|ГДЕ
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ)
	|	И НЕ ВидыНоменклатуры.ПродаетсяВРозлив
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия,
	|	ТабличнаяЧасть.АлкогольнаяПродукция,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ)";
	
	ТаблицаТовары = Неопределено;
	Если ТипЗнч(ДокументОбъект) = Тип("ДанныеФормыКоллекция") Тогда 
		ТаблицаТовары = ДокументОбъект.Выгрузить();
	Иначе 
		ТаблицаТовары = ДокументОбъект.Товары.Выгрузить();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ШтрихкодыУпаковок(ДокументСсылка, ЗаполнитьСправки2ИзРегистра = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	АкцизныеМаркиЕГАИС.АкцизнаяМарка КАК ШтрихкодУпаковки,
	|	ВЫБОР
	|		КОГДА АкцизныеМаркиЕГАИС.Справка2 = ЧекККМАкцизныеМарки.Справка2
	|			ТОГДА АкцизныеМаркиЕГАИС.Справка2
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ОшибкаЧтенияСтатуса)
	|	КОНЕЦ КАК Статус,
	|	АкцизныеМаркиЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ЧекККМАкцизныеМарки.Справка2 КАК Справка2
	|ИЗ
	|	Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМАкцизныеМарки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО ЧекККМАкцизныеМарки.Ссылка.Организация = КлассификаторОрганизацийЕГАИС.Контрагент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
	|		ПО ЧекККМАкцизныеМарки.АкцизнаяМарка = АкцизныеМаркиЕГАИС.АкцизнаяМарка
	|ГДЕ
	|	ЧекККМАкцизныеМарки.Ссылка = &Ссылка";
	
	Если ЗаполнитьСправки2ИзРегистра Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЧекККМАкцизныеМарки.Справка2", "АкцизныеМаркиЕГАИС.Справка2");
	КонецЕсли; 
	
	Возврат Новый Структура("МаркированныеТовары", Запрос.Выполнить().Выгрузить());
КонецФункции

#КонецОбласти

#КонецОбласти

//ИнтеграцияИСМПТК_РозничноеВыбытие
#Область МаркировкаИСМПТК

Функция РазвернутьСтрокуПозицииЧекаИСМПТК(СтрокаПозицииЧека, МассивСтрокКМ = Неопределено)
	
	МассивРезультат = Новый Массив;
	
	Количество = СтрокаПозицииЧека.Количество;
	Коэффициенты = Новый Массив(); // Служебный массив для распределения сумм
	
	// Построчное заполнение сведений о маркированной продукции.
	Индекс = 0;
	РазбиратьСтрокуМаркированногоТовара = Ложь;
	МаксимальныйИндекс = 0;
	ВсегоКодов = МассивСтрокКМ.Количество();
	
	Если НЕ МассивСтрокКМ = Неопределено И ВсегоКодов>0 Тогда
		РазбиратьСтрокуМаркированногоТовара = Истина;
		МаксимальныйИндекс = ВсегоКодов-1;
	КонецЕсли;
	
	Счетчик = 0;
	Пока Счетчик < Количество Цикл
		//Разбивам 1 к 1, остаток без КМ списываем одной общей строкой 
		Если РазбиратьСтрокуМаркированногоТовара Тогда
			
			Если Индекс > МаксимальныйИндекс Тогда 
				//Закончились КМ, остаток группируем 
				Коэффициенты.Добавить(Количество-Индекс);
				НоваяСтрокаПозицииЧека = ОбщегоНазначения.СкопироватьРекурсивно(СтрокаПозицииЧека);
				НоваяСтрокаПозицииЧека.Количество = СтрокаПозицииЧека.Количество - Индекс;
				МассивРезультат.Добавить(НоваяСтрокаПозицииЧека);
				Прервать;
			Иначе			
				Коэффициенты.Добавить(1);
				НоваяСтрокаПозицииЧека = ОбщегоНазначения.СкопироватьРекурсивно(СтрокаПозицииЧека);
				НоваяСтрокаПозицииЧека.Количество = 1;
				
				ТекущаяСтрока    = МассивСтрокКМ[Индекс];
				ДанныеМаркировки = РозничноеВыбытиеИСМПТК.РазобратьКодМаркировкиТовараДляПробитияЧека(ТекущаяСтрока.КодМаркировки);
				
				Если ДанныеМаркировки.Разобран 
					И Не ЗначениеЗаполнено(ДанныеМаркировки.ОписаниеОшибки) Тогда
					
					//Если удалось разбрать КМ по формату - заполняем данные для пробития по его структуре
					НоваяСтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ГлобальныйИдентификаторТорговойЕдиницы = ДанныеМаркировки.ГлобальныйИдентификаторТорговойЕдиницы; //GTIN
					НоваяСтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.СерийныйНомер = ДанныеМаркировки.СерийныйНомер;
					
					//Если код соответствует формату, преобразуем его для восстановления исходного значения:
					//на текущем этапе КМ в строке нормализован для использования в платформе, нужно преобразовать его
					//в изначальный вид путем добавления спецсимволов GS1. 
					Если ДанныеМаркировки.ДанныеШтрихкода.Количество() = 0 Тогда
						//Но если данный код принадлежит табачной пачке, он не нормализовался, т.к. изначально имеет формат без спецсимволов.
						//Для такого КМ оставляем без изменений представление и значение.
						СтрокаКМПредставление = ТекущаяСтрока.КодМаркировки;
						СтрокаКМСпецсимволы   = ТекущаяСтрока.КодМаркировки;
					Иначе
						СтрокаКМПредставление = "";
						СтрокаКМСпецсимволы   = "";
						РозничноеВыбытиеИСМПТК.ПолныйШтрихкодИзНормализованного(ДанныеМаркировки.ДанныеШтрихкода, СтрокаКМПредставление, СтрокаКМСпецсимволы);
					КонецЕсли;
					
					НоваяСтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.Вставить("КодМаркировки", СтрокаКМПредставление); //представление
					//Кодируем полный КМ в Base64, чтобы не возникало ошибки при передаче между клиент-сервером
					НоваяСтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.Вставить("КодМаркировкиBase64", РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодВBase64(СтрокаКМСпецсимволы));
					НоваяСтрокаПозицииЧека.Штрихкод = ДанныеМаркировки.EAN;
				КонецЕсли;
			КонецЕсли;
			МассивРезультат.Добавить(НоваяСтрокаПозицииЧека);
			Индекс = Индекс + 1;
		Иначе
			Коэффициенты.Добавить(Количество - Индекс);
			НоваяСтрокаПозицииЧека = ОбщегоНазначения.СкопироватьРекурсивно(СтрокаПозицииЧека);
			МассивРезультат.Добавить(НоваяСтрокаПозицииЧека);
			Прервать;
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	// Распределение суммы
	Если СтрокаПозицииЧека.Сумма > 0 Тогда
		РаспределенныеСуммы = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаПозицииЧека.Сумма, Коэффициенты);
		
		Индекс = 0;
		
		Для Каждого СтрокаРаспределения Из РаспределенныеСуммы Цикл
			МассивРезультат[Индекс].Сумма = СтрокаРаспределения;
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЕсли;
	
	// Перезаполнение суммы скидок
	Если НЕ СтрокаПозицииЧека.СуммаСкидок = 0 Тогда
		РаспределенныеСуммы = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаПозицииЧека.СуммаСкидок, Коэффициенты);
		
		Индекс = 0;
		
		Для Каждого СтрокаРаспределения Из РаспределенныеСуммы Цикл
			МассивРезультат[Индекс].СуммаСкидок = СтрокаРаспределения;
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЕсли;
	
	// Перезаполнение цены, суммы НДС
	Для Каждого СтрокаРезультата Из МассивРезультат Цикл
		
		Если СтрокаРезультата.Цена= СтрокаРезультата.Сумма Тогда
			СтрокаРезультата.ЦенаСоСкидками = СтрокаРезультата.Цена;
		Иначе
			СтрокаРезультата.ЦенаСоСкидками = СтрокаРезультата.Сумма;
		КонецЕсли;
		
		СтрокаРезультата.СуммаНДС = (СтрокаРезультата.СуммаНДС / Количество) * СтрокаРезультата.Количество;
	КонецЦикла;
	
	Возврат МассивРезультат;
	
КонецФункции

#КонецОбласти
//Конец ИнтеграцияИСМПТК_РозничноеВыбытие

#КонецЕсли

#Область ОбновлениеИнформационнойБазы

Процедура ПерезаполнитьСистемуНалогообложения(Параметры) Экспорт

	МетаданныеДокумента = Метаданные.Документы.ЧекККМ;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
		ОбновлениеИнформационнойБазыРТ.ЗаполнитьСистемыНалогообложенияДокументов(ПолноеИмяОбъекта, Выборка, Параметры);
		
	#КонецЕсли
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКЗаполнениюСНО(Параметры) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ЧекККМ;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		ДокументыДляОбработки = ОбновлениеИнформационнойБазыРТ.ДокументыДляЗаполненияСНО(ПолноеИмяОбъекта);
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДокументыДляОбработки);
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти