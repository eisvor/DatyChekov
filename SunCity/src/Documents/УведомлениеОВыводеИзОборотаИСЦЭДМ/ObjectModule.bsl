#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения.ДокументОснование;
	Иначе
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения;
	КонецЕсли;	
	
	Если ДокументОснованиеДляЗаполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("Массив") 
		И ДокументОснованиеДляЗаполнения.Количество() <> 0 Тогда
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения[0]);
	Иначе
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения);
	КонецЕсли;
	
	//Если создается документ без основания
	Если ТипДокумента = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	ПараметрыЗаполнения = РаботаСДокументамиИСМПТК.ПолучитьПараметрыЗаполненияДокументовИСМПТПоУмолчанию();
	ПараметрыЗаполнения.Вставить("МассивДокументов", ДокументОснованиеДляЗаполнения);
	
	ТипДокументаИСЦЭДМ = Документы.УведомлениеОВыводеИзОборотаИСЦЭДМ.ПустаяСсылка();
	МассивДанныхДляЗаполнения = РаботаСДокументамиИСМПТК.ПодготовитьДанныеДляЗаполненияДокументаИСЦЭДМ_Общая(ПараметрыЗаполнения, ТипДокументаИСЦЭДМ);
	
	Если ТипЗнч(МассивДанныхДляЗаполнения) = Тип("Массив") И МассивДанныхДляЗаполнения.Количество() > 0 Тогда
		
		Для Каждого СтруктураДокумента Из МассивДанныхДляЗаполнения Цикл
			
			Если МассивДанныхДляЗаполнения.Найти(СтруктураДокумента) > 0 Тогда
				НовыйДокументВИО = Документы.УведомлениеОВыводеИзОборотаИСЦЭДМ.СоздатьДокумент();
			Иначе
				НовыйДокументВИО = ЭтотОбъект;
			КонецЕсли; 
		
			//Заполняем шапку документа
			ЗаполнитьЗначенияСвойств(НовыйДокументВИО, СтруктураДокумента.Реквизиты[0]);
			
			СтруктураПереопределяемыхРеквизитов = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВыводеИСЦЭДМ(ПараметрыЗаполнения.МассивДокументов);
			ЗаполнитьЗначенияСвойств(НовыйДокументВИО, СтруктураПереопределяемыхРеквизитов);
			
			Если ОбщегоНазначенияИСМПТКВызовСервера.ПроверитьЗначениеКонстанты("ВестиУчетПоСкладамИСЦЭДМ") Тогда
				СкладЦЭДМПоСоответствию = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(НовыйДокументВИО.Склад, НовыйДокументВИО.Организация);
				Если Не СкладЦЭДМПоСоответствию = Неопределено Тогда
					НовыйДокументВИО.СкладЦЭДМ = СкладЦЭДМПоСоответствию.СкладЦЭДМ;
					НовыйДокументВИО.ПоставщикСкладЦЭДМИдентификационныйНомер = СкладЦЭДМПоСоответствию.ИдентификаторСклада;
				КонецЕсли;
			КонецЕсли;
			
			НовыйДокументВИО.Статус = Перечисления.СтатусыДокументовИСЦЭДМ.НеОпределен;  
			
			Для Каждого СтрокаТЧ Из СтруктураДокумента.Товары Цикл
				НоваяСтрокаТЧ = НовыйДокументВИО.Товары.Добавить();
				НоваяСтрокаТЧ.Номенклатура = СтрокаТЧ.Номенклатура;
				НоваяСтрокаТЧ.КоличествоИсточник = СтрокаТЧ.КоличествоИсточник;
				НоваяСтрокаТЧ.ВидПродукции = СтрокаТЧ.ВидПродукции;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Автор) Тогда
		ЭтотОбъект.Автор = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Статус) Тогда
		ЭтотОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСЦЭДМ.НеОпределен");
		ЭтотОбъект.ЭтапОформления = ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Номер) Тогда
		ЭтотОбъект.Номер = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьНомерНаПечать(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.ДокументОснование = Неопределено; 
	ЭтотОбъект.Идентификатор = Неопределено;
	ЭтотОбъект.НомерИСЦЭДМ = Неопределено;
	ЭтотОбъект.ДатаВИСЦЭДМ = Неопределено;
	ЭтотОбъект.Статус = Перечисления.СтатусыДокументовИСЦЭДМ.НеОпределен;
	ЭтотОбъект.Автор = Неопределено;
	ЭтотОбъект.Номер = Неопределено;
	ЭтотОбъект.ЦеныПереданыУспешно = Неопределено;
	ЭтотОбъект.ДокументОснованиеНаименование = Неопределено; 
	ЭтотОбъект.ДокументОснованиеДата  = Неопределено;
	ЭтотОбъект.ДокументОснованиеНомер = Неопределено;
	ЭтотОбъект.ЭтапОформления = Перечисления.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе;
	
	ЭтотОбъект.Марки.Очистить();
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.Ошибки.Очистить();
	ЭтотОбъект.Итоги.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//В общем случае не проверяем заполненность реквизитов при записи
	//проверка необходимых реквизитов проходит при отправке документа
	//в случае необходимости можно указать реквизиты проверки отдельно
	//в процедуре ДокументВИО_ОбработкаПроверкиЗаполнения
	ПроверяемыеРеквизиты.Очистить();
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	РаботаСДокументамиИСМПТКПереопределяемый.ДокументВИО_ОбработкаПроверкиЗаполненияИСЦЭДМ(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	РаботаСДокументамиИСМПТКПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
		
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли


