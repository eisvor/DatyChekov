
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура        = Параметры.Номенклатура;
	Характеристика      = Параметры.Характеристика;
	ТребуемоеКоличество = Параметры.ТребуемоеКоличествоНомеров;
	ВидПродукции        = Параметры.ВидПродукции;
	ЦветСтиляПояснения  = ЦветаСтиля.ПоясняющийТекст;
	ШрифтПояснения      = Новый Шрифт(,,,,Истина);
	
	КоличествоСимволовСерии = 0;
	Если ВидПродукции = Перечисления.ВидыПродукцииИСМПТК.Обувная
		ИЛИ ВидПродукции = Перечисления.ВидыПродукцииИСМПТК.ЛегкаяПромышленность
		ИЛИ ВидПродукции = Перечисления.ВидыПродукцииИСМПТК.БезалкогольныеНапитки Тогда 
		КоличествоСимволовСерии = 12;
	ИначеЕсли ВидПродукции = Перечисления.ВидыПродукцииИСМПТК.МолочнаяПродукция Тогда
		КоличествоСимволовСерии = 5;
	КонецЕсли;
	
	НомерСтроки = 0;
	Для Каждого СерийныйНомер Из Параметры.СерийныеНомера Цикл
		
		НомерСтроки = НомерСтроки + 1;
		НоваяСтрока = СерийныеНомера.Добавить();
		НоваяСтрока.СерийныйНомер = СерийныйНомер;
		НоваяСтрока.НомерСтроки = НомерСтроки;
		
	КонецЦикла;
	
	ОбновитьПоясняющийТекст(ЭтотОбъект);
	СформироватьМаскуДляВводСерийногоНомера();
	
	СобытияФормИСМПТКПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СобытияФормИСМПТККлиентПереопределяемый.ПриОткрытии(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СерийныеНомераПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьПоясняющийТекст(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СерийныеНомераПослеУдаления(Элемент)
	
	ОбновитьНумерациюТаблицыСерийныеНомера(ЭтотОбъект);
	ОбновитьПоясняющийТекст(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СерийныеНомераПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	ОчиститьСообщения();
	Если ТребуемоеКоличество <= СерийныеНомера.Количество() Тогда
		Отказ = Истина;
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(НСтр("ru = 'Все серии уже указаны.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	СписокСерийныхНомеров = Новый Массив;
	Для Каждого Строка Из СерийныеНомера Цикл
		Если Не ПустаяСтрока(Строка.СерийныйНомер) Тогда
			СписокСерийныхНомеров.Добавить(Строка.СерийныйНомер);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(СписокСерийныхНомеров);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПоясняющийТекст(Форма)
	
	ШаблонСообщения = НСтр("ru = 'Серий указано: %КоличествоУказано% из %КоличествоНужноУказать%.'");
	ТекстСообщения  = СтрЗаменить(ШаблонСообщения, "%КоличествоУказано%", 	   Форма.СерийныеНомера.Количество());
	ТекстСообщения  = СтрЗаменить(ШаблонСообщения, "%КоличествоНужноУказать%", Форма.ТребуемоеКоличество);
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения, Форма.ШрифтПояснения, Форма.ЦветСтиляПояснения));
	
	Форма.ПоясняющийТекст = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНумерациюТаблицыСерийныеНомера(Форма)
	
	Для Каждого СтрокаТаблицы Из Форма.СерийныеНомера Цикл
		СтрокаТаблицы.НомерСтроки = Форма.СерийныеНомера.Индекс(СтрокаТаблицы) + 1
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СерийныеНомераПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.СерийныеНомера.ТекущиеДанные;
	Если ОтменаРедактирования Тогда
		ТекущиеДанные.СерийныйНомер = "";
	КонецЕсли;
	
	СерийныйНомерБезПробелов = СтрЗаменить(ТекущиеДанные.СерийныйНомер, " ", "");
	Если СтрДлина(СерийныйНомерБезПробелов) < КоличествоСимволовСерии
		И Не ПустаяСтрока(СерийныйНомерБезПробелов) Тогда
		
		Отказ = Истина;
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Требуемое количество символов серийного номера: %1. Использование пробелов недопустимо.'"),
			КоличествоСимволовСерии);
		
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
		
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		Элементы.СерийныеНомера.ТекущиеДанные.НомерСтроки = СерийныеНомера.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьМаскуДляВводСерийногоНомера()
	
	Маска = "";
	Для К = 1 По КоличествоСимволовСерии Цикл
		Маска = Маска + "N";
	КонецЦикла;
	
	Элементы.СерийныеНомераСерийныйНомер.Маска = Маска;
	
КонецПроцедуры

#КонецОбласти