
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаказКодов") Тогда
		ЗаказКодовМаркировки = Параметры.ЗаказКодов;
	КонецЕсли;
	
	//Выставляем значение по умолчанию из общих настроек ИС МПТ
	ОтображатьДанныеПоНоменклатуре = Константы.ОтображатьДанныеПоНоменклатуреВДокументахСУЗИСМПТК.Получить();

	ЗаполнитьТаблицуКодовМаркировки();
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТаблицаКодовЗаказа.Количество() = 1 Тогда
		УстановитьОтметку();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьКодыМаркировки(Команда)
	
	ДокументМассив = Новый Массив;
	ДокументМассив.Добавить(ЗаказКодовМаркировки);
	
	МассивСтрокаКодов = ТаблицаКодовЗаказа.НайтиСтроки(Новый Структура("Выбран", Истина));
	Если Не МассивСтрокаКодов.Количество() = 0 Тогда 
		МассивКодов = Новый Массив;
		Для Каждого СтрокаТЧ Из МассивСтрокаКодов Цикл 
			МассивКодов.Добавить(СтрокаТЧ.GTIN);
		КонецЦикла;
		ИнтеграцияИСМПТККлиент.ПолучитьКМПоЗаказуНаЭмиссию(ДокументМассив, Новый Структура("ЗапускатьФоновоеЗадание, РазмерБлокаКодов, МассивКодов", Ложь, РазмерБлокаКодов, МассивКодов));
	Иначе 
		ТекстСообщения = НСтр("ru = 'Не указан GTIN для получения кодов!'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоКодовКПолучениюПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьДанныеПоНоменклатуреПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();
			
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	УстановитьОтметку();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыбор(Команда)
	
	УстановитьОтметку(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуКодовМаркировки()
	
	ТаблицаКодовЗаказа.Очистить();
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество КАК КоличествоВЗаказе,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Номенклатура КАК Номенклатура,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Характеристика КАК Характеристика,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN КАК GTIN,
	|	ЕСТЬNULL(СтатусыЗаказовИСМПТК.Идентификатор, """") КАК ИдентификаторБлока
	|ИЗ
	|	Документ.ЗаказКодовМаркировкиСУЗИСМПТК.Товары КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаказовИСМПТК КАК СтатусыЗаказовИСМПТК
	|		ПО ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = СтатусыЗаказовИСМПТК.ЗаказНаЭмиссию
	|			И ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN = СтатусыЗаказовИСМПТК.GTIN
	|ГДЕ
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = &ЗаказКодовМаркировки";
	
	Запрос.УстановитьПараметр("ЗаказКодовМаркировки", ЗаказКодовМаркировки);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл 
			НоваяСтрока = ТаблицаКодовЗаказа.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			СтатусПоКодамGTIN = ИнтеграцияИСМПТКВызовСервера.ПолучитьСтатусGTINПоЗаказуНаЭмиссию(ЗаказКодовМаркировки, Выборка.GTIN);
			Если Не ЗначениеЗаполнено(СтатусПоКодамGTIN) И Не ЗначениеЗаполнено(Выборка.ИдентификаторБлока) Тогда
				НоваяСтрока.КоличествоПолучено = 0;
				НоваяСтрока.КоличествоДоступно = Выборка.КоличествоВЗаказе;
			ИначеЕсли Не ЗначениеЗаполнено(СтатусПоКодамGTIN) И ЗначениеЗаполнено(Выборка.ИдентификаторБлока) Тогда
				НоваяСтрока.КоличествоПолучено = Выборка.КоличествоВЗаказе;
				НоваяСтрока.КоличествоДоступно = 0;
			ИначеЕсли ЗначениеЗаполнено(СтатусПоКодамGTIN) Тогда
				НоваяСтрока.КоличествоПолучено = СтатусПоКодамGTIN["quantity"];
				НоваяСтрока.КоличествоДоступно = Выборка.КоличествоВЗаказе - СтатусПоКодамGTIN["quantity"];
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ЗаполнитьТаблицуКодовМаркировки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗаказНаЭмиссию" Тогда 
		ЗаполнитьТаблицуКодовМаркировки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПолучитьКодыМаркировки", 		  "Доступность", ?(ЭтаФорма.РазмерБлокаКодов = 0, Ложь, Истина));
	
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаКодовЗаказаНоменклатура",   "Видимость", ОтображатьДанныеПоНоменклатуре);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаКодовЗаказаХарактеристика", "Видимость", ОтображатьДанныеПоНоменклатуре);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметку(Отметка = Истина)
	
	Для Каждого СтрокаТЧ Из ТаблицаКодовЗаказа Цикл 
		СтрокаТЧ.Выбран = Отметка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКодовЗаказаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКодовЗаказаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти
