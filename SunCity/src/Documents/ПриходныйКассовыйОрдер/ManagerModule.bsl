#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.АктОРасхожденияхПриПриемкеТоваров - документ для инициализации данных.
//  СтруктураДополнительныеСвойства - Структура - структура дополнительных свойств.
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДанныеДокумента.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ТекстЗапроса = ТекстЗапросаТаблицаДенежныеСредстваНаличные() + ПолучитьРазделитьЗапросов()
		+ ТекстЗапросаТаблицаДенежныеСредстваКПоступлениюНаличные() + ПолучитьРазделитьЗапросов()
		+ ТекстЗапросаТаблицаРасчетыСПоставщиками() + ПолучитьРазделитьЗапросов()
		+ ТекстЗапросаТаблицаДенежныеСредстваККМ() + ПолучитьРазделитьЗапросов()
		+ ТекстЗапросаТаблицаРасчетыСКлиентами();
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Контрагент", Реквизиты.Контрагент);
	
	ИспользоватьРасчетыСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетыСКлиентами");
	Запрос.УстановитьПараметр("ИспользоватьРасчетыСКлиентами", ИспользоватьРасчетыСКлиентами);
	
	МассивПустыхДокументовРасчета = Новый Массив;
	МассивПустыхДокументовРасчета.Добавить(Документы.РеализацияТоваров.ПустаяСсылка());
	МассивПустыхДокументовРасчета.Добавить(Документы.ЧекККМ.ПустаяСсылка());
	МассивПустыхДокументовРасчета.Добавить(Неопределено);
	
	Запрос.УстановитьПараметр("МассивПустыхДокументовРасчета", МассивПустыхДокументовРасчета);
	Запрос.УстановитьПараметр("СтатьяДвиженияДенежныхСредств", ЗначениеНастроекПовтИсп.ПредопределеннаяСтатьяДвиженияДенежныхСредств(Реквизиты.ХозяйственнаяОперация));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваНаличные",               МассивРезультатов[0].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваКПоступлениюНаличные",   МассивРезультатов[1].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками",                   МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваККМ",                    МассивРезультатов[3].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСКлиентами",                      МассивРезультатов[4].Выгрузить());
	
КонецПроцедуры

// Подготавливает данные для пробития чека.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка - документ для пробития.
//  РаспределениеВыручкиПоСекциям - Соответствие - соответсвие распределения выручки.
//  НомерЧека - Число - номер чека.
//
// Возвращаемое значение:
//  Структура - данные для пробития чека.
//
Функция ПодготовитьДанныеДляПробитияЧека(ДокументСсылка, РаспределениеВыручкиПоСекциям, НомерЧека) Экспорт
	
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	// Общие параметры чека
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("КассаККМ");
	СтруктураРеквизитов.Вставить("Касса");
	СтруктураРеквизитов.Вставить("ШаблонПКО", "Касса.ШаблонПКО");
	СтруктураРеквизитов.Вставить("Организация");
	СтруктураРеквизитов.Вставить("Контрагент");
	СтруктураРеквизитов.Вставить("Дата");
	СтруктураРеквизитов.Вставить("Ответственный");
	СтруктураРеквизитов.Вставить("ДоговорКонтрагента");
	СтруктураРеквизитов.Вставить("СистемаНалогообложения");
	СтруктураРеквизитов.Вставить("СуммаДокумента");
	СтруктураРеквизитов.Вставить("Телефон");
	СтруктураРеквизитов.Вставить("АдресЭП");
	СтруктураРеквизитов.Вставить("ПринятоОт");
	СтруктураРеквизитов.Вставить("Основание");
	СтруктураРеквизитов.Вставить("ЗаказПокупателя");
	СтруктураРеквизитов.Вставить("ХозяйственнаяОперация");
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, СтруктураРеквизитов);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("НаименованиеПолное");
	СтруктураРеквизитов.Вставить("ИНН");
	СтруктураРеквизитов.Вставить("КПП");
	СтруктураРеквизитов.Вставить("СерияСвидетельстваПоНДС");
	СтруктураРеквизитов.Вставить("НомерСвидетельстваПоНДС");
	РеквизитыОрганизация = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.Организация, СтруктураРеквизитов);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ЭлектронныйЧекSMSПередаютсяПрограммой1С");
	СтруктураРеквизитов.Вставить("ЭлектронныйЧекEmailПередаютсяПрограммой1С");
	СтруктураРеквизитов.Вставить("СерийныйНомер");
	СтруктураРеквизитов.Вставить("Магазин");
	СтруктураРеквизитов.Вставить("Код");
	СтруктураРеквизитов.Вставить("СпособФорматноЛогическогоКонтроля", "ПодключаемоеОборудование.СпособФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ДопустимоеРасхождениеФорматноЛогическогоКонтроля", "ПодключаемоеОборудование.ДопустимоеРасхождениеФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ТипОборудования", "ПодключаемоеОборудование.ТипОборудования");
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.КассаККМ, СтруктураРеквизитов);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Наименование");
	СтруктураРеквизитов.Вставить("ИНН");
	РеквизитыКассир = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.Ответственный.ФизическоеЛицо, СтруктураРеквизитов);

	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ИНН", "Контрагент.ИНН");
	СтруктураРеквизитов.Вставить("КонтрагентЮрФизЛицо", "Контрагент.ЮрФизЛицо");
	РеквизитыКонтрагент = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, СтруктураРеквизитов);
	Если РеквизитыКонтрагент.КонтрагентЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		//для контрагентов - физических лиц в качестве ИИН используется ИИН Физ.лица
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("ИНН", "Контрагент.ФизЛицо.ИНН");
		РеквизитыКонтрагент = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, СтруктураРеквизитов);
	КонецЕсли;
	
	Если РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		ОбщиеПараметры.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	Иначе
		ОбщиеПараметры.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств;
	КонецЕсли;
	
	ОбщиеПараметры.Электронно = Ложь;
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.Телефон) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекSMSПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СSMS = Истина;
		КонецЕсли;
		Телефон = РеквизитыДокумента.Телефон;
		Если СтрНайти(Телефон, "+7") = 1 ИЛИ (СтрНайти(Телефон, "8") = 1 И СтрДлина(Телефон) = 11) Тогда
			ОбщиеПараметры.ПокупательНомер = Телефон;
		Иначе
			ОбщиеПараметры.ПокупательНомер = "+7" + Телефон;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.АдресЭП) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекEmailПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СEmail = Истина;
			СистемнаяУчетнаяЗапись = РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись();
			Если ЗначениеЗаполнено(СистемнаяУчетнаяЗапись) Тогда
				ОбщиеПараметры.ОтправительEmail = СистемнаяУчетнаяЗапись.АдресЭлектроннойПочты;
			КонецЕсли;
		КонецЕсли;
		ОбщиеПараметры.ПокупательEmail = РеквизитыДокумента.АдресЭП;
	КонецЕсли;
	
	// Параметры необходимые для чека ЕНВД на принтере чеков
	ОбщиеПараметры.ДокументОснование = ДокументСсылка;
	
	ОбщиеПараметры.Кассир          = РеквизитыКассир.Наименование;
	ОбщиеПараметры.КассирИНН       = РеквизитыКассир.ИНН;
	
	ОбщиеПараметры.Организация    = РеквизитыДокумента.Организация;
	ОбщиеПараметры.ОрганизацияНазвание = РеквизитыОрганизация.НаименованиеПолное;
	ОбщиеПараметры.ОрганизацияИНН = РеквизитыОрганизация.ИНН;
	ОбщиеПараметры.ОрганизацияКПП = РеквизитыОрганизация.КПП;
	ОбщиеПараметры.НомерКассы     = РеквизитыКассыККМ.Код;
	ОбщиеПараметры.НомерЧека      = НомерЧека;
	
	ОбщиеПараметры.НомерСмены = 1;
	
	СведенияООрганизации = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(РеквизитыДокумента.Организация, РеквизитыДокумента.Дата);
	АдресМагазина = ОбщегоНазначенияРТ.АдресМагазина(РеквизитыКассыККМ.Магазин);
	
	СерийныйНомер = РеквизитыКассыККМ.СерийныйНомер;
	Если НЕ ЗначениеЗаполнено(СерийныйНомер) Тогда
		СерийныйНомер = "1";
	КонецЕсли;
	
	ОбщиеПараметры.АдресРасчетов = АдресМагазина;
	ОбщиеПараметры.МестоРасчетов = Строка(РеквизитыКассыККМ.Магазин) + " " + АдресМагазина;
	ОбщиеПараметры.АдресМагазина = АдресМагазина;
	ОбщиеПараметры.НаименованиеМагазина = Строка(РеквизитыКассыККМ.Магазин);
	
	ОбщиеПараметры.СерийныйНомер = СерийныйНомер;
	ОбщиеПараметры.СистемаНалогообложения = РеквизитыДокумента.СистемаНалогообложения;
	
	ПодключаемоеОборудованиеРТ.ЗаполнитьДанныеПокупателя(ОбщиеПараметры, РеквизитыДокумента);
	
	ПодключаемоеОборудованиеРТ.ЗаполнитьПараметрыПлатежногоДоговора(ОбщиеПараметры, 
																	РеквизитыДокумента.ДоговорКонтрагента,
																	РеквизитыДокумента.СуммаДокумента);
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.ДоговорКонтрагента) Тогда
		НомерСекции = РаспределениеВыручкиПоСекциям.СоответствиеДоговоровСекциям.Получить(РеквизитыДокумента.ДоговорКонтрагента);
	Иначе
		НомерСекции = РаспределениеВыручкиПоСекциям.НомерСекцииДляПриходныхКассовыхОрдеров;
	КонецЕсли;
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("НомерСекции", НомерСекции);
	
	РасшифровкаПлатежа = РасшифровкаПлатежа(ДокументСсылка);
	
	СуммаДокументовРасчетов = 0;
	
	ДобавленыДанныеПоЗаказу = Ложь;
	
	Для Каждого СтрокаПлатежа Из РасшифровкаПлатежа Цикл
		ПараметрыДокумента.Вставить("ПризнакСпособаРасчета", СтрокаПлатежа.ПризнакСпособаРасчета);
		Если ЗначениеЗаполнено(СтрокаПлатежа.ДокументРасчетовСКонтрагентом)
			И ОбщегоНазначенияРТ.ЕстьТЧОбъекта("Товары", СтрокаПлатежа.ДокументРасчетовСКонтрагентом.Метаданные()) Тогда
			ПодключаемоеОборудованиеРТ.ДобавитьСтрокиДляФискализацииТоваров(СтрокаПлатежа.ДокументРасчетовСКонтрагентом,
				ПараметрыДокумента, ОбщиеПараметры, СуммаДокументовРасчетов);
		ИначеЕсли ЗначениеЗаполнено(РеквизитыДокумента.ЗаказПокупателя) И НЕ ДобавленыДанныеПоЗаказу Тогда
			ПодключаемоеОборудованиеРТ.ДобавитьСтрокиДляФискализацииТоваров(РеквизитыДокумента.ЗаказПокупателя,
				ПараметрыДокумента, ОбщиеПараметры, СуммаДокументовРасчетов);
			ДобавленыДанныеПоЗаказу = Истина;
		Иначе // Это аванс
			СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
			
			СтрокаПозицииЧека.Наименование = НСтр("ru = 'Оплата от:'") + " " + РеквизитыДокумента.ПринятоОт + Символы.ПС
				+ НСтр("ru = 'Основание:'") + " " + РеквизитыДокумента.Основание;
			СтрокаПозицииЧека.Количество     = 1;
			СтрокаПозицииЧека.Цена           = СтрокаПлатежа.Сумма;
			СтрокаПозицииЧека.ЦенаСоСкидками = СтрокаПлатежа.Сумма;
			СтрокаПозицииЧека.Сумма          = СтрокаПлатежа.Сумма;
			СтрокаПозицииЧека.НомерСекции    = НомерСекции;
			Если ОбщиеПараметры.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
				ЗначСтавкиНДС = УчетНДС.СтавкаНДСПоУмолчанию(ДокументСсылка.Дата);
				СтрокаПозицииЧека.СтавкаНДС = ПодключаемоеОборудованиеРТ.СтавкаНДСВФорматеБПО(ЗначСтавкиНДС, Истина);
				СтрокаПозицииЧека.ОсвобожденныйОборотНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначСтавкиНДС, "ДляОсвобожденногоОборота");
			Иначе
				СтрокаПозицииЧека.СтавкаНДС      = Неопределено;
			КонецЕсли;
			СтрокаПозицииЧека.ПризнакСпособаРасчета = СтрокаПлатежа.ПризнакСпособаРасчета;
			СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата;
			
			ЕдиницаИзмеренияПоУмолчанию = Новый Структура("КодЕдиницыИзмерения, НаименованиеЕдиницыИзмерения", 796, "шт");
			СтрокаПозицииЧека.КодЕдиницыИзмерения    = ЕдиницаИзмеренияПоУмолчанию.КодЕдиницыИзмерения;
			СтрокаПозицииЧека.НаименованиеЕдиницыИзмерения = ЕдиницаИзмеренияПоУмолчанию.НаименованиеЕдиницыИзмерения;
			
			ПодключаемоеОборудованиеРТ.ЗаполнитьПараметрыПлатежногоДоговораВСтроке(ОбщиеПараметры, СтрокаПозицииЧека);
			
			ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
			СуммаДокументовРасчетов = СуммаДокументовРасчетов + СтрокаПлатежа.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ОбщиеПараметры.КассаККМ 		= РеквизитыДокумента.КассаККМ;
	ОбщиеПараметры.ШаблонЧека 		= РеквизитыДокумента.ШаблонПКО;
	ОбщиеПараметры.ТорговыйОбъект 	= РеквизитыКассыККМ.Магазин;
	
	Если РеквизитыКассыККМ.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ Тогда
		// При необходимости будет проведен формато-логический контроль
		ОбщиеПараметры.СпособФорматноЛогическогоКонтроля = РеквизитыКассыККМ.СпособФорматноЛогическогоКонтроля;
		ОбщиеПараметры.ДопустимоеРасхождениеФорматноЛогическогоКонтроля = РеквизитыКассыККМ.ДопустимоеРасхождениеФорматноЛогическогоКонтроля;
		Если ФорматноЛогическийКонтрольКлиентСервер.НуженФорматноЛогическийКонтроль(ОбщиеПараметры) Тогда
			ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ОбщиеПараметры);
		КонецЕсли;
	КонецЕсли;
	
	СтрокаОплаты = Новый Структура();
	СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Наличные);
	СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Наличные'"));
	СтрокаОплаты.Вставить("Сумма", РеквизитыДокумента.СуммаДокумента);
	ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);

	// Постооплата в РК не используется
	Если ПолучитьФункциональнуюОпцию("ОтключенныйФункционал") Тогда
	РазницаСумм = СуммаДокументовРасчетов - РеквизитыДокумента.СуммаДокумента;
	Если РазницаСумм > 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты",Перечисления.ТипыОплатыККТ.Постоплата);
		СтрокаОплаты.Вставить("Наименование", НСтр("ru = 'Постоплата'"));
		СтрокаОплаты.Вставить("Сумма", РазницаСумм);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	ИначеЕсли РазницаСумм < 0 Тогда
		СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
		
		СтрокаПозицииЧека.Наименование = НСтр("ru = 'Оплата от:'") + " " + РеквизитыДокумента.ПринятоОт + Символы.ПС
			+ НСтр("ru = 'Основание:'") + " " + РеквизитыДокумента.Основание;
		СтрокаПозицииЧека.Количество     = 1;
		СтрокаПозицииЧека.Цена           = - РазницаСумм;
		СтрокаПозицииЧека.ЦенаСоСкидками = - РазницаСумм;
		СтрокаПозицииЧека.Сумма          = - РазницаСумм;
		СтрокаПозицииЧека.НомерСекции    = НомерСекции;
		Если ОбщиеПараметры.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
			ЗначСтавкиНДС = УчетНДС.СтавкаНДСПоУмолчанию(ДокументСсылка.Дата);
			СтрокаПозицииЧека.СтавкаНДС = ПодключаемоеОборудованиеРТ.СтавкаНДСВФорматеБПО(ЗначСтавкиНДС, Истина);	
		Иначе
			СтрокаПозицииЧека.СтавкаНДС      = Неопределено;
		КонецЕсли;
		СтрокаПозицииЧека.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.Аванс;
		СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата;
		
		ПодключаемоеОборудованиеРТ.ЗаполнитьПараметрыПлатежногоДоговораВСтроке(ОбщиеПараметры, СтрокаПозицииЧека);
		
		ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	КонецЕсли;
	КонецЕсли;

    Если СуммаДокументовРасчетов <> РеквизитыДокумента.СуммаДокумента Тогда
		// суммы не совпадают, поэтому отражаем в чеке одной строкой
		ОбщиеПараметры.ПозицииЧека.Очистить();
		
		Для Каждого СтрокаПлатежа Из РасшифровкаПлатежа Цикл
			СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
			СтрокаПозицииЧека.Наименование = НСтр("ru = 'Оплата от:'") + " " + РеквизитыДокумента.ПринятоОт;
			Если ЗначениеЗаполнено(РеквизитыДокумента.Основание) Тогда
				СтрокаПозицииЧека.Наименование = СтрокаПозицииЧека.Наименование + Символы.ПС
				+ НСтр("ru = 'Основание:'") + " " + РеквизитыДокумента.Основание;
			КонецЕсли; 
			СтрокаПозицииЧека.Количество     = 1;
			СтрокаПозицииЧека.Цена           = СтрокаПлатежа.Сумма;
			СтрокаПозицииЧека.ЦенаСоСкидками = СтрокаПлатежа.Сумма;
			СтрокаПозицииЧека.Сумма          = СтрокаПлатежа.Сумма;
			СтрокаПозицииЧека.НомерСекции    = НомерСекции;
			Если ОбщиеПараметры.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
				ЗначСтавкиНДС = УчетНДС.СтавкаНДСПоУмолчанию(ДокументСсылка.Дата);
				СтрокаПозицииЧека.СтавкаНДС = ПодключаемоеОборудованиеРТ.СтавкаНДСВФорматеБПО(ЗначСтавкиНДС, Истина);
				СтрокаПозицииЧека.ОсвобожденныйОборотНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначСтавкиНДС, "ДляОсвобожденногоОборота");
			Иначе
				СтрокаПозицииЧека.СтавкаНДС      = Неопределено;
			КонецЕсли;
			СтрокаПозицииЧека.ПризнакСпособаРасчета = СтрокаПлатежа.ПризнакСпособаРасчета;
			СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата;
			
			ЕдиницаИзмеренияПоУмолчанию = Новый Структура("КодЕдиницыИзмерения, НаименованиеЕдиницыИзмерения", 796, "шт");
			СтрокаПозицииЧека.КодЕдиницыИзмерения    = ЕдиницаИзмеренияПоУмолчанию.КодЕдиницыИзмерения;
			СтрокаПозицииЧека.НаименованиеЕдиницыИзмерения = ЕдиницаИзмеренияПоУмолчанию.НаименованиеЕдиницыИзмерения;
			
			ПодключаемоеОборудованиеРТ.ЗаполнитьПараметрыПлатежногоДоговораВСтроке(ОбщиеПараметры, СтрокаПозицииЧека);
			
			ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
		КонецЦикла;
		
	КонецЕсли; 
     
	ОбщиеПараметры.СерияСвидетельстваПоНДС = РеквизитыОрганизация.СерияСвидетельстваПоНДС;
	ОбщиеПараметры.НомерСвидетельстваПоНДС = РеквизитыОрганизация.НомерСвидетельстваПоНДС;
	ОбщиеПараметры.ПолучательИИН = РеквизитыКонтрагент.ИНН;
	
	Возврат ОбщиеПараметры;
	
КонецФункции // ПодготовитьДанныеДляПробитияЧека()

// Заполняет массивы реквизитов, зависимых от хозяйственной операции документа.
// Параметры:
//           ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хоз. операции документа для которого
//                                                                              необходимо получить массив реквизитов.
//           МассивВсехРеквизитов - Неопределено - Выходной параметр с типом Массив в который будут помещены имена всех
//                                                 реквизитов документов.
//           МассивРеквизитовОперации - Неопределено - Выходной параметр с типом Массив в который будут помещены имена
//                                                     реквизитов по виду операции документа.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Организация");
	МассивВсехРеквизитов.Добавить("Касса");
	МассивВсехРеквизитов.Добавить("Контрагент");
	МассивВсехРеквизитов.Добавить("КонтрагентПрочийДоход");
	МассивВсехРеквизитов.Добавить("ДокументОснование");
	МассивВсехРеквизитов.Добавить("БанковскийСчет");
	МассивВсехРеквизитов.Добавить("КассаККМ");
	МассивВсехРеквизитов.Добавить("СуммаДокумента");
	МассивВсехРеквизитов.Добавить("ОтчетОРозничныхПродажах");
	МассивВсехРеквизитов.Добавить("ПробиватьЧекиПоКассеККМ");
	МассивВсехРеквизитов.Добавить("ДоговорКонтрагента");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.Сумма");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ПризнакСпособаРасчета");
	МассивВсехРеквизитов.Добавить("РазбитьПлатеж");
	МассивВсехРеквизитов.Добавить("ГруппаЗаказПокупателя");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Сумма");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РазбитьПлатеж");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("КассаККМ");
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Сумма");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ПризнакСпособаРасчета");
		МассивРеквизитовОперации.Добавить("РазбитьПлатеж");
		МассивРеквизитовОперации.Добавить("ПробиватьЧекиПоКассеККМ");
		МассивРеквизитовОперации.Добавить("ОтчетОРозничныхПродажах");
		МассивРеквизитовОперации.Добавить("ГруппаЗаказПокупателя");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("КассаККМ");
		МассивРеквизитовОперации.Добавить("КонтрагентПрочийДоход");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Сумма");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РазбитьПлатеж");
		МассивРеквизитовОперации.Добавить("ПробиватьЧекиПоКассеККМ");
		МассивРеквизитовОперации.Добавить("ОтчетОРозничныхПродажах");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("КассаККМ");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		МассивРеквизитовОперации.Добавить("ДоговорКонтрагента");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("БанковскийСчет");
		МассивРеквизитовОперации.Добавить("ДоговорКонтрагента");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВознаграждениеОтКомитента Тогда
		
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("КассаККМ");
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Сумма");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ПризнакСпособаРасчета");
		МассивРеквизитовОперации.Добавить("РазбитьПлатеж");
		МассивРеквизитовОперации.Добавить("ПробиватьЧекиПоКассеККМ");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаВыручки Тогда
		
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("Касса");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		МассивРеквизитовОперации.Добавить("СуммаДокумента");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполненяет признак способа расчета в строке расшифровки платежа.
//
// Параметры:
//  Объект - ДокументОбъект.ПриходныйКассовыйОрдер, ДанныеФормыСтруктуры - объект для заполнения признака;
//  СтрокаРасшифровки - СтрокаТабличнойЧасти, ДанныеФормыЭлементКоллекции - строка расшифровки платежа для заполнения.
//
Процедура ЗаполнитьПризнакСпособаРасчетаСтрокиРасшифровки(Объект, СтрокаРасшифровки) Экспорт
	
	ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПустаяСсылка();
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВознаграждениеОтКомитента Тогда
		
		Если ЗначениеЗаполнено(СтрокаРасшифровки.ДокументРасчетовСКонтрагентом)
			И (ТипЗнч(СтрокаРасшифровки.ДокументРасчетовСКонтрагентом) = Тип("ДокументСсылка.РеализацияТоваров")
			ИЛИ ТипЗнч(СтрокаРасшифровки.ДокументРасчетовСКонтрагентом) = Тип("ДокументСсылка.ЧекККМ")) Тогда
			ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ОплатаКредита;
		ИначеЕсли ЗначениеЗаполнено(Объект.ЗаказПокупателя) Тогда
			СуммаЗаказа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаказПокупателя, "СуммаДокумента");
			Если СтрокаРасшифровки.Сумма >= СуммаЗаказа Тогда
				ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная;
			Иначе
				ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаЧастичная;
			КонецЕсли;
		Иначе
			ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаРасшифровки.ПризнакСпособаРасчета = ПризнакСпособаРасчета;
	
КонецПроцедуры

// Заполняет Магазин по данным объекта.
//
// Параметры:
//  Объект - ДокументОбъект.ПриходныйКассовыйОрдер, ДанныеФормыСтруктуры - объект для заполнения признака;
//
Процедура ЗаполнитьМагазин(Объект, Магазин) Экспорт
	
	Источник = Неопределено;
	Если ЗначениеЗаполнено(Объект.Касса) Тогда
		Источник = Объект.Касса;
	ИначеЕсли ЗначениеЗаполнено(Объект.КассаККМ) Тогда
		Источник = Объект.КассаККМ;
	КонецЕсли;
	
	Если НЕ Источник = Неопределено Тогда
		Магазин = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "Магазин");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет реквизиты, используемые для печати.
//
// Параметры:
//  Объект - ДокументОбъект.ПриходныйКассовыйОрдер, ДанныеФормыСтруктуры - объект для заполнения признака;
//  ДанныеЗаполнения - Неопределено, Структура - данные, используемые для заполнения;
//
Процедура ЗаполнитьРеквитыПечатнойФормы(Объект, ДанныеЗаполнения = Неопределено) Экспорт
	
	ДатаСведений = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	ЕстьРасчетыСКонтрагентами = ДенежныеСредстваСервер.ЕстьРасчетыСКонтрагентами(Объект.ХозяйственнаяОперация);
	
	// Инициализация данных заполнения в случае отсутствия.
	Если ДанныеЗаполнения = Неопределено Тогда
		
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Основание");
		ДанныеЗаполнения.Вставить("ВТомЧислеНДС");
		
		Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ
			И ЗначениеЗаполнено(Объект.ДокументОснование) И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ") Тогда
			ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументОснование,
				"Представление, Номер, Дата, Ответственный.ФизическоеЛицо");
			
			ДанныеЗаполнения.Вставить("ПринятоОт", ДанныеОснования.ОтветственныйФизическоеЛицо);
			ДанныеЗаполнения.Вставить("Основание", ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(ДанныеОснования));
		КонецЕсли;
		
	КонецЕсли;
	
	// Основание.
	Если ДанныеЗаполнения.Свойство("Основание") Тогда
		Если ЗначениеЗаполнено(ДанныеЗаполнения.Основание) Тогда
			Объект.Основание = ДанныеЗаполнения.Основание;
		Иначе
			Объект.Основание = Строка(Объект.ХозяйственнаяОперация);
			Если ЕстьРасчетыСКонтрагентами И ЗначениеЗаполнено(Объект.Контрагент) Тогда
				НаименованиеКонтрагента = Справочники.Контрагенты.ПолучитьНаименованиеКонтрагента(Объект.Контрагент);
				Объект.Основание = Объект.Основание + СтрШаблон(": ""%1""", НаименованиеКонтрагента);
				
				// Дополним представлением расчетых документов.
				КоллекцияПредставлений = ДенежныеСредстваСервер.ПредставлениеРасчетныхДокументов(Объект);
				Если КоллекцияПредставлений.Количество() = 1 Тогда
					Объект.Основание = Объект.Основание + СтрШаблон(" по документу %1", КоллекцияПредставлений[0]);
				ИначеЕсли КоллекцияПредставлений.Количество() > 1 Тогда
					Объект.Основание = Объект.Основание + СтрШаблон(" по документам: %1", СтрСоединить(КоллекцияПредставлений, "; "));
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(Объект.ПринятоОт) И НЕ ДанныеЗаполнения.Свойство("ПринятоОт") Тогда
					ДанныеКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Контрагент, "ЮрФизЛицо, ФизЛицо");
					Если ДанныеКонтрагента.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
						ДанныеЗаполнения.Вставить("ПринятоОт", ДанныеКонтрагента.ФизЛицо);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// В том числе НДС.
	Если ДанныеЗаполнения.Свойство("ВТомЧислеНДС") Тогда
		Объект.ВТомЧислеНДС = ДенежныеСредстваСервер.ТекстСуммаНДСПлатежа(Объект);
	КонецЕсли;
		
	// Принято от.
	Если ДанныеЗаполнения.Свойство("ПринятоОт") И ЗначениеЗаполнено(ДанныеЗаполнения.ПринятоОт) Тогда
		Если ТипЗнч(ДанныеЗаполнения.ПринятоОт) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ДанныеФизЛица = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(ДанныеЗаполнения.ПринятоОт, ДатаСведений);
			ФИО = ДанныеФизЛица.Фамилия + " " + ДанныеФизЛица.Имя + " " + ДанныеФизЛица.Отчество;
		Иначе
			ФИО = ДанныеЗаполнения.ПринятоОт;
		КонецЕсли;
		Объект.ПринятоОт = ПолучитьСклоненияСтроки(ФИО, "Л=ru_RU", "ПД=Родительный")[0];
	КонецЕсли;
	
КонецПроцедуры

// Получает реквизиты документа для обмена с конфигурацией "Бухгалтерия предприятия".
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - документ для которого необходимо получить реквизиты.
//
// Возвращаемое значение:
//	Структура - Структура реквизитов документа.
//
Функция РеквизитыДокументаДляОбменаСБухгалтерией(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет КАК КорреспондирующийСчет,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|	И ДанныеДокумента.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет <> """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.НомерСтроки");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КорреспондирующийСчет = Выборка.КорреспондирующийСчет;
		СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДвиженияДенежныхСредств;
	Иначе
		КорреспондирующийСчет = "";
		СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
	КонецЕсли;                              	
	
	Если Найти(КорреспондирующийСчет, "66") <> 0
		ИЛИ Найти(КорреспондирующийСчет, "67") <> 0 Тогда
		ВидОперации = "РасчетыПоКредитамИЗаймам";
	Иначе
		ВидОперации = "ПрочееПоступление";  		
	КонецЕсли;
		
	СтруктураРеквизитов = Новый Структура("ВидОперации, Счет, СтатьяДвиженияДенежныхСредств",
		ВидОперации,
		КорреспондирующийСчет,
		СтатьяДвиженияДенежныхСредств);
	
	Возврат СтруктураРеквизитов;
		
КонецФункции

// Формирует структуру шаблона чека по умолчанию из макета документа Приходный кассовый ордер.
//
// Параметры:
//  ИерархическиШаблон - Строка - иерархический шаблон.
//  ИмяОбъекта - Строка - наименование объекта.
//  ПервичнаяСтруктура - Булево - признак первичной структуры.
//  Загружать - Булево - признак загрузки.
//
// Возвращаемое значение:
//  ДокументСсылка.ПриходныйКассовыйОрдер - документ для получения макета.
//
Функция СформироватьПервичнуюСтруктуруШаблона(ИерархическийШаблон, ИмяОбъекта, ПервичнаяСтруктура, Загружать) Экспорт
	
	// Загрузим ИерархическийШаблон из макета.
	Если Загружать Тогда
		ТекстовыйДокумент = Документы.ПриходныйКассовыйОрдер.ПолучитьМакет("ШаблонФискальногоЧекаПоУмолчанию");
		Попытка
			Шаблон = ЗначениеИзСтрокиВнутр(ТекстовыйДокумент.ПолучитьТекст());
			Если ТипЗнч(Шаблон) <> Тип("ДеревоЗначений") Тогда
				Загружать = Ложь;
			КонецЕсли;
		Исключение
			Загружать = Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Если Загружать Тогда
		ИерархическийШаблон = Шаблон.Скопировать();
	Иначе
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Шапка'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаШапка";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Табличная часть ""Расшифровка платежа"" (Шапка)'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаТЧ";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Табличная часть ""Расшифровка платежа"" (Подвал)'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаТЧ";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
		
		НоваяГруппа = ИерархическийШаблон.Строки.Добавить();
		НоваяГруппа.Элемент = НСтр("ru = 'Подвал'");
		НоваяГруппа.Идентификатор = УправлениеШаблонами.ПолучитьИдентификатор();
		НоваяГруппа.ТипЭлемента = "ОбластьЧека";
		НоваяГруппа.ИмяМакета   = "ПоляШаблонаШапка";
		НоваяГруппа.ИмяОбъекта = ИмяОбъекта;
	КонецЕсли;
	Возврат Документы.ПриходныйКассовыйОрдер.ПустаяСсылка();
	
КонецФункции

// Выполняет добавление фискальных строк в дерево шаблона перед формированием
//  по нему представления чека ПКО.
//
// Параметры:
//  КопияШаблона          - КоллекцияСтрокДереваЗначений - коллекция строк в которую необходимо добавить фискальные строки.
//  ШиринаЧека            - Число - Ширина чека в символах.
//  ОднаФискальнаяСтрока  - Булево - Определяется наличие режима одна фискальная строка.
//
// Возвращаемое значение:
//  КоллекцияСтрокДереваЗначений   - Коллекция строк дерева значений.
//
Функция СформироватьФискальныеСтроки(КопияШаблона, ШиринаЧека, ОднаФискальнаяСтрока) Экспорт

	// Шапка
	
	Если ШиринаЧека < 18 Тогда
		Возврат КопияШаблона;
	КонецЕсли;
	
	ФР_Строка_0_0 = КопияШаблона.Строки.Найти("Шапка",,Ложь);
	Если ФР_Строка_0_0 <> Неопределено Тогда 
		
		// Первая фискальная строка
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР1");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР1");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 0, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , НСтр("ru = 'Касса № 01'"));
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР2");
		ПараметрыФункции.Вставить("Ширина"          , 10);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		Если ШиринаЧека >= 25 Тогда
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.Касса.Организация.ИНН%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаКассаОрганизацияИНН");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР3");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека - 10);
			ПараметрыФункции.Вставить("РазмещениеТекста", 1); // Обрезать
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Префикс"         , "ИНН: ");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		КонецЕсли;
		
		// Вторая фискальная строка
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР4");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР4");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 1, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.Date%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаDate");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР5");
		ПараметрыФункции.Вставить("Ширина"          , 16);
		ПараметрыФункции.Вставить("РазмещениеТекста", 2); // Забивать
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПараметрыФункции.Вставить("Префикс"         , "Дата  ");
		ПараметрыФункции.Вставить("Формат"         , "ДЛФ=D");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		// Третья фискальная строка
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР7");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР7");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ВставитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, 2, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.Date%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаDate");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР8");
		ПараметрыФункции.Вставить("Ширина"          , 11);
		ПараметрыФункции.Вставить("РазмещениеТекста", 2); // Забивать
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПараметрыФункции.Вставить("Префикс"         , "Время ");
		ПараметрыФункции.Вставить("Формат"          , "ДФ=чч:мм");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		Если ШиринаЧека >= 23 Тогда
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.НомерЧекаККМ%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаНомерЧекаККМ");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР9");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека-11);
			ПараметрыФункции.Вставить("РазмещениеТекста", 2); // Забивать
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Префикс"         , "ЧЕК.№ ");
			ПараметрыФункции.Вставить("Формат"          , "ЧЦ=6; ЧВН=; ЧГ=0");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		КонецЕсли;
		
	КонецЕсли;
	
	// Подвал
	
	ФР_Строка_0_0 = КопияШаблона.Строки.Найти("Подвал",,Ложь);
	Если ФР_Строка_0_0 <> Неопределено И ОднаФискальнаяСтрока Тогда
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР11");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР11");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "Всего:");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР12");
		ПараметрыФункции.Вставить("Ширина"          , 6);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.СуммаДокумента%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаСуммаДокумента");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР13");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека - 6);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
	КонецЕсли;
	
	Если ФР_Строка_0_0 <> Неопределено Тогда
		
		Пробелы50 = "**************************************************";
		Пробелы = "";
		МинимальноеКоличествоСтрок = Цел(ШиринаЧека/50);
		Для Счетчик = 0 По МинимальноеКоличествоСтрок Цикл
			Пробелы = Пробелы + Пробелы50;
		КонецЦикла;
		Пробелы = Лев(Пробелы, ШиринаЧека);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , Пробелы);
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР14");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ФР15");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "Таблица");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР15");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ФР_Строка_1_0 = ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "ИТОГ");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР16");
		ПараметрыФункции.Вставить("Ширина"          , 4);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Лево");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%Ссылка.СуммаДокумента%%");
		ПараметрыФункции.Вставить("ИмяКолонки"      , "СсылкаСуммаДокумента");
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР17");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека - 4);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_1_0.Строки, ПараметрыФункции);
		
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("ИмяЭлемента"     , Пробелы);
		ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаТекста");
		ПараметрыФункции.Вставить("Идентификатор"   , "ФР18");
		ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
		ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
		ПараметрыФункции.Вставить("Выравнивание"    , "Право");
		ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
		
	КонецЕсли;
	
	// Табличная часть
	
	Если НЕ ОднаФискальнаяСтрока Тогда
		
		ФР_Строка_0_0 = КопияШаблона.Строки.Найти("Табличная часть ""Расшифровка платежа"" (Подвал)",,Ложь);
		
		Если ФР_Строка_0_0 <> Неопределено Тогда
			
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("ИмяЭлемента"     , "%%РасшифровкаПлатежа.СуммаПлатежа%%");
			ПараметрыФункции.Вставить("ИмяКолонки"      , "РасшифровкаПлатежаСуммаПлатежа");
			ПараметрыФункции.Вставить("ТипЭлемента"     , "СтрокаДанных");
			ПараметрыФункции.Вставить("Идентификатор"   , "ФР19");
			ПараметрыФункции.Вставить("Ширина"          , ШиринаЧека);
			ПараметрыФункции.Вставить("РазмещениеТекста", 0); // Переносить
			ПараметрыФункции.Вставить("Формат"          , "ЧДЦ=2"); // Переносить
			ПараметрыФункции.Вставить("Выравнивание"    , "Право");
			ПараметрыФункции.Вставить("Префикс"         , "=");
			ПечатьФискальныхЧеков.ДобавитьСтрокуВКоллекциюСтрокДерева(ФР_Строка_0_0.Строки, ПараметрыФункции);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КопияШаблона;
	
КонецФункции

// Функция получает структуру массивов (макет) для чека ПКО.
//
// Параметры:
//  Ссылка      - ДокументСсылка.ПриходныйКассовыйОрдер - Ссылка на документ ПКО.
//  ШаблонЧека  - ДеревоЗначений - Шаблон чека
//  ШиринаЧека  - Число - Ширина чека в символах.
//  Параметры   - Структура - Вспомогательные параметры
//
// Возвращаемое значение:
//  Структура - Структура массивов (макет) текстовых строк.
//
Функция МакетФискальногоЧека(Ссылка, ШаблонЧека, ШиринаЧека, Параметры) Экспорт
	
	ТаблицаРасшифровкаПлатежа = Новый ТаблицаЗначений;
	
	Значение = Неопределено;
	Если Параметры.Свойство("КэшТаблицыРезультатаКомпоновки", Значение) Тогда
		ТаблицыРезультатаКомпоновки        = Значение;
	Иначе
		// Значение по умолчанию
		ТаблицыРезультатаКомпоновки = УправлениеШаблонами.ВыполнитьКомпоновкуДанныхДляИерархическогоШаблона(ШаблонЧека, Ссылка, Параметры.СхемаКомпоновкиДанных, Параметры.КомпоновщикМакета);
		Параметры.Вставить("КэшТаблицыРезультатаКомпоновки", ТаблицыРезультатаКомпоновки);
	КонецЕсли;
	
	Для каждого Таблицы Из ТаблицыРезультатаКомпоновки.Строки Цикл
		
		Если Таблицы.ЧастьЗапроса = "РасшифровкаПлатежа" Тогда
			ТаблицаРасшифровкаПлатежа = Таблицы.Строки;
		КонецЕсли;
		
	КонецЦикла;
	
	Структура = УправлениеШаблонами.ИдентификаторыПользовательскихПолей(ШаблонЧека);
	
	// Механизм кэширования макетов.
	Если Параметры.КэшМакетов = Неопределено Тогда
		Структура.Вставить("КэшМакетов", Новый Соответствие);
	Иначе
		Структура.Вставить("КэшМакетов", Параметры.КэшМакетов);
	КонецЕсли;
	
	СтруктураМассивы = Новый Структура;
	
	// Подготовка Шапки и Подвала
	СтрокаШапка = ШаблонЧека.Строки.Найти("Шапка",,Ложь);
	Если СтрокаШапка <> Неопределено Тогда
		Если ТаблицаРасшифровкаПлатежа.Количество() > 0 Тогда
			УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(ТаблицаРасшифровкаПлатежа.Родитель, Структура, СтрокаШапка);
		КонецЕсли;
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(СтрокаШапка, Структура);
		
		МассивШапка = Новый Массив;
		Для каждого Строка2Уровня Из СтрокаШапка.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивШапка.Добавить(Соответствие);
		КонецЦикла;
		СтруктураМассивы.Вставить("Шапка",МассивШапка);
		
	КонецЕсли;
	
	СтрокаПодвал = ШаблонЧека.Строки.Найти("Подвал",,Ложь);
	Если СтрокаШапка <> Неопределено Тогда
		Если ТаблицаРасшифровкаПлатежа.Количество() > 0 Тогда
			УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(ТаблицаРасшифровкаПлатежа.Родитель, Структура, СтрокаПодвал);
		КонецЕсли;
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(СтрокаПодвал, Структура);
		
		МассивПодвал = Новый Массив;
		Для каждого Строка2Уровня Из СтрокаПодвал.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивПодвал.Добавить(Соответствие);
		КонецЦикла;
		
		СтруктураМассивы.Вставить("Подвал",МассивПодвал);
	КонецЕсли;
	
	// Итоговая строка идет последней, ее не включаем в ТЧ.
	КоличествоСтрокРезультата = ТаблицаРасшифровкаПлатежа.Количество() - 1;
	Для Счетчик = 0 По КоличествоСтрокРезультата Цикл
		
		ТелоШапка = ШаблонЧека.Строки.Найти("Табличная часть ""Расшифровка платежа"" (Шапка)",,Ложь);
		УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(ТаблицаРасшифровкаПлатежа[Счетчик], Структура, ТелоШапка);
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(ТелоШапка, Структура);
		
		МассивТелоШапка = Новый Массив;
		Для каждого Строка2Уровня Из ТелоШапка.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивТелоШапка.Добавить(Соответствие);
		КонецЦикла;
		
		ТелоПодвал = ШаблонЧека.Строки.Найти("Табличная часть ""Расшифровка платежа"" (Подвал)",,Ложь);
		УправлениеШаблонами.ЗаполнитьСтруктуруПараметровШаблонаПоСтрокеТЧ(ТаблицаРасшифровкаПлатежа[Счетчик], Структура, ТелоПодвал);
		УправлениеШаблонами.ЗаполнитьСоставныеСтрокиПоСтруктуре(ТелоПодвал, Структура);
		
		МассивТелоПодвал = Новый Массив;
		Для каждого Строка2Уровня Из ТелоПодвал.Строки Цикл
			Значение = УправлениеШаблонами.МассивСтрокДляСтрокиДереваШаблона(Строка2Уровня, Структура);
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(Строка2Уровня, Значение);
			МассивТелоПодвал.Добавить(Соответствие);
		КонецЦикла;
		
		СтруктураМассивы.Вставить("ТелоШапка_"+Счетчик,МассивТелоШапка);
		СтруктураМассивы.Вставить("ТелоПодвал_"+Счетчик,МассивТелоПодвал);
		
	КонецЦикла;
	
	СтруктураМассивы.Вставить("РезультатКомпоновкиДанных", ТаблицаРасшифровкаПлатежа);
	СтруктураМассивы.Вставить("КоличествоСтрокТабличнойЧасти", Счетчик);
	СтруктураМассивы.Вставить("ШиринаЧека", ШиринаЧека);
	
	// Механизм кэширования макетов.
	Кэш = Структура.КэшМакетов;
	
	Возврат СтруктураМассивы;
	
КонецФункции

// Получает ссылку на документ Приходный кассовый ордер по хозяйственной операции для настройки шаблона чека.
//
Функция ДокументДляШаблонаЧека() Экспорт
	Перем ДокументДляПечатиШаблона;
	
	ДокументДляПечатиШаблона = Документы.ПриходныйКассовыйОрдер.ПустаяСсылка();
	
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ПриходныйКассовыйОрдер.Ссылка
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДокументДляПечатиШаблона = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ДокументДляПечатиШаблона;
	
КонецФункции

// Возвращает массив типов для ограничения типов элемента формы ДокументОснование.
// Параметры:
//           ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хоз. операции документа для которого
//                                                                              необходимо получить массив типов.
// Возвращаемое значение:
//           Массив - Массив типов ограничивающих типы для реквизита формы ДокументОснование.
//
Функция ОграничениеТипаДокументаОснованияРасходаИзКассы(ХозяйственнаяОперация) Экспорт
	
	Перем МассивТиповОграничений;
	МассивТиповОграничений = Новый Массив();
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации Тогда
		
		МассивТиповОграничений.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ Тогда
		
		МассивТиповОграничений.Добавить(Тип("ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ"));
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		
		МассивТиповОграничений.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		
	КонецЕсли;
	
	Возврат МассивТиповОграничений;
	
КонецФункции

// Определяет отбор на хозяйственную операцию, устанавливаемый на список выбора документа Приходный кассовый ордер в
// форме элемента справочника хранилище шаблонов.
//
// Возвращаемое значение:
//  Массив - связей параметров выбора на хозяйственную операцию.
//
Функция СвязьПараметровВыбораНаХозяйственнуюОперацию() Экспорт
	
	МассивПараметровВыбора = Новый Массив();
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента));
	Возврат Новый ФиксированныйМассив(МассивПараметровВыбора);
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приходный кассовый ордер
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПКО";
	КомандаПечати.Представление = НСтр("ru = 'Приходный кассовый ордер'");
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// Параметры:
//  МассивОбъектов - Массив - список объектов, для которых была выполнена процедура Печать;
//  ПараметрыПечати - Структура - произвольные параметры, переданные при вызове команды печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - содержит сформированные табличные документы и дополнительную информацию;
//  ОбъектыПечати - СписокЗначений - соответствие между объектами и именами областей в табличных документах, где
//                                   значение - Объект, представление - имя области с объектом в табличных документах;
//  ПараметрыВывода - Структура - параметры, связанные с выводом табличных документов:
//   * ПараметрыОтправки - Структура - для заполнения письма при отправке печатной формы по электронной почте.
//                    см. РаботаСПочтовымиСообщениямиКлиент.РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПКО") Тогда
	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПКО",
				ПараметрыПечати.Представление, 
				СформироватьПечатнуюФормуКО1(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет данные по Документу расчетов и Систему налогообложение в Форма.Объект
//
// Параметры:
//  Форма			 - УправляемаяФорма - форма, в которой произошло изменение данных
//  ТекущаяСтрока	 - Строка РасшифровкиДанных - изменяемая строка
//
Процедура РасшифровкаПлатежаДокументРасчетовСКонтрагентомПриИзменении(Форма, ТекущаяСтрока) Экспорт
	
	РасшифровкаПлатежаДокументРасчетовСКонтрагентом = ТекущаяСтрока.ДокументРасчетовСКонтрагентом;
	
	Если ЗначениеЗаполнено(РасшифровкаПлатежаДокументРасчетовСКонтрагентом) Тогда
		
		ДенежныеСредстваСервер.ЗаполнитьСистемуНалогообложенияЭлементаФормы(ТекущаяСтрока);
		
		Если НЕ (Форма.СуммаУстановленаВручную И Форма.Объект.РасшифровкаПлатежа.Количество() < 1) Тогда
			
			Если ТипЗнч(РасшифровкаПлатежаДокументРасчетовСКонтрагентом) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
				ТекущаяСтрока.Сумма = ДенежныеСредстваВызовСервера.ПолучитьСуммаВознаграждения(РасшифровкаПлатежаДокументРасчетовСКонтрагентом);
				Форма.Объект.СуммаДокумента = ТекущаяСтрока.Сумма;
			ИначеЕсли НЕ ТипЗнч(РасшифровкаПлатежаДокументРасчетовСКонтрагентом) = Тип("ДокументСсылка.ВводОстатковРасчетовСКлиентами") Тогда
				ТекущаяСтрока.Сумма = ДенежныеСредстваВызовСервера.ПолучитьСуммуДокумента(РасшифровкаПлатежаДокументРасчетовСКонтрагентом);
				Форма.Объект.СуммаДокумента = ТекущаяСтрока.Сумма;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ТекущаяСтрока.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ПустаяСсылка();
		
	КонецЕсли;
	
	Если Форма.ЭтоФискальнаяОперация И Форма.Объект.ПробиватьЧекиПоКассеККМ Тогда
		
		Если ТекущаяСтрока = Форма.Объект.РасшифровкаПлатежа[0] Тогда
			
			ДенежныеСредстваСервер.ЗаполнитьСистемуНалогообложения(Форма.Объект);
			
		КонецЕсли;
		
	Иначе
		
		Форма.Объект.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры

#Область СтандарныеПодсистемы

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Касса.Магазин)";

КонецПроцедуры

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваНаличные()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Касса КАК Касса,
	|	ДанныеДокумента.Касса.Магазин КАК Магазин,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧастьРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств ЕСТЬ NULL 
	|				ИЛИ ТабличнаяЧастьРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|			ТОГДА &СтатьяДвиженияДенежныхСредств
	|		ИНАЧЕ ТабличнаяЧастьРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(ТабличнаяЧастьРасшифровкаПлатежа.Сумма, ДанныеДокумента.СуммаДокумента) КАК Сумма
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТабличнаяЧастьРасшифровкаПлатежа
	|		ПО (ТабличнаяЧастьРасшифровкаПлатежа.Ссылка = ДанныеДокумента.Ссылка)
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТабличнаяЧастьРасшифровкаПлатежа.НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентами()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Касса.Магазин КАК Магазин,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ЕСТЬNULL(ТабличнаяЧастьРасшифровкаПлатежа.Сумма, ДанныеДокумента.СуммаДокумента) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТабличнаяЧастьРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом, ДанныеДокумента.Ссылка) В (&МассивПустыхДокументовРасчета)
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(ТабличнаяЧастьРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом, ДанныеДокумента.Ссылка)
	|	КОНЕЦ КАК ДокументРасчета,
	|	ВЫБОР
	|		КОГДА НЕ ТабличнаяЧастьРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом В (&МассивПустыхДокументовРасчета)
	|				И (ТабличнаяЧастьРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ЧекККМ
	|					ИЛИ ТабличнаяЧастьРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.РеализацияТоваров)
	|			ТОГДА ТабличнаяЧастьРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом.ЗаказПокупателя
	|		ИНАЧЕ ДанныеДокумента.ЗаказПокупателя
	|	КОНЕЦ КАК ЗаказПокупателя
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТабличнаяЧастьРасшифровкаПлатежа
	|		ПО (ТабличнаяЧастьРасшифровкаПлатежа.Ссылка = ДанныеДокумента.Ссылка)
	|ГДЕ
	|	&ИспользоватьРасчетыСКлиентами
	|	И ДанныеДокумента.Ссылка = &Ссылка
	|	И (ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|	ИЛИ ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВознаграждениеОтКомитента))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТабличнаяЧастьРасшифровкаПлатежа.НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваККМ()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.КассаККМ КАК КассаККМ,
	|	ДанныеДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(ТабличнаяЧастьРасшифровкаПлатежа.Сумма, ДанныеДокумента.СуммаДокумента) КАК Сумма
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТабличнаяЧастьРасшифровкаПлатежа
	|		ПО (ТабличнаяЧастьРасшифровкаПлатежа.Ссылка = ДанныеДокумента.Ссылка)
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|	И ДанныеДокумента.ПробиватьЧекиПоКассеККМ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ДанныеДокумента.КассаККМ,
	|	ДанныеДокумента.ДоговорКонтрагента,
	|	ЕСТЬNULL(ТабличнаяЧастьРасшифровкаПлатежа.Сумма, ДанныеДокумента.СуммаДокумента)
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТабличнаяЧастьРасшифровкаПлатежа
	|		ПО (ТабличнаяЧастьРасшифровкаПлатежа.Ссылка = ДанныеДокумента.Ссылка)
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|	И ДанныеДокумента.ПробиватьЧекиПоКассеККМ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКПоступлениюНаличные()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата                   	КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 	КАК ВидДвижения,
	|	ДанныеДокумента.Организация            	КАК Организация,
	|	ДанныеДокумента.Касса                  	КАК Касса,
	|	ДанныеДокумента.ДокументОснование      	КАК ДокументПередачи,
	|	ДанныеДокумента.ХозяйственнаяОперация  	КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СуммаДокумента         	КАК Сумма,
	|	ВЫРАЗИТЬ(ДанныеДокумента.ДокументОснование КАК Документ.РасходныйКассовыйОрдер).Касса КАК КассаОтправитель
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата                     КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	ДанныеДокумента.Организация              КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)  КАК Касса,
	|	ДанныеДокумента.ДокументОснование        КАК ДокументПередачи,
	|	ДанныеДокумента.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СуммаДокумента           КАК Сумма,
	|	ДанныеДокумента.КассаККМ                 КАК КассаОтправитель
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом.Магазин ЕСТЬ НЕ NULL 
	|			ТОГДА РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом.Магазин
	|		ИНАЧЕ РасшифровкаПлатежа.Ссылка.Касса.Магазин
	|	КОНЕЦ КАК Магазин,
	|	&Контрагент КАК Поставщик,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом КАК Документ.ВозвратТоваровПоставщику).ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	|						ТОГДА ВЫБОР
	|								КОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом КАК Документ.ВозвратТоваровПоставщику).ДокументОснование.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|									ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом КАК Документ.ВозвратТоваровПоставщику).ДокументОснование.ЗаказПоставщику
	|								ИНАЧЕ ВЫРАЗИТЬ(РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом КАК Документ.ВозвратТоваровПоставщику).ДокументОснование
	|							КОНЕЦ
	|					ИНАЧЕ РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом
	|				КОНЕЦ
	|		ИНАЧЕ РасшифровкаПлатежа.ДокументРасчетовСКонтрагентом
	|	КОНЕЦ КАК ДокументРасчета,
	|	РасшифровкаПлатежа.Сумма КАК Сумма,
	|	РасшифровкаПлатежа.Сумма КАК КОплате,
	|	0 КАК КПоступлению,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная) КАК ФормаОплаты,
	|	ЛОЖЬ КАК Взаимозачет
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
	|	И РасшифровкаПлатежа.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьРазделитьЗапросов()

	СтрокаРазделителя = 
	"
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат СтрокаРазделителя

КонецФункции // ПолучитьРазделитьЗапросов()
 

///////////////////////////////////////////////////////////////////////////////
// Печать

Функция СформироватьПечатнуюФормуКО1(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	СинонимДокумента   = НСтр("ru='Приходный кассовый ордер'");
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриходныйКассовыйОрдер_ПФ_MXL_КО1";
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КО1");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст ="ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПриходныйКассовыйОрдер.Номер КАК НомерДокумента,
	|	ПриходныйКассовыйОрдер.Дата КАК ДатаДокумента,
	|	ПриходныйКассовыйОрдер.Организация КАК Организация,
	|	ПриходныйКассовыйОрдер.Организация КАК Руководители,
	|	ПриходныйКассовыйОрдер.Контрагент,
	|	ПриходныйКассовыйОрдер.Контрагент.Представление КАК Контрагент,
	|	ВЫРАЗИТЬ(ПриходныйКассовыйОрдер.ПринятоОт КАК СТРОКА(1000)) КАК ПринятоОт,
	|	ВЫРАЗИТЬ(ПриходныйКассовыйОрдер.Основание КАК СТРОКА(1000)) КАК Основание,
	|	ВЫРАЗИТЬ(ПриходныйКассовыйОрдер.Приложение КАК СТРОКА(1000)) КАК Приложение,
	|	ПриходныйКассовыйОрдер.СуммаДокумента КАК Сумма,
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ПриходныйКассовыйОрдер.ХозяйственнаяОперация
    |ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.Ссылка В(&МассивОбъектов)";
	
	Результаты = Запрос.Выполнить();
 	ВыборкаПоДокументам = Результаты.Выбрать();
	
	ПервыйДокумент = Истина;
	//области макета документа
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
        ЕстьРасчетыСКонтрагентами = ДенежныеСредстваСервер.ЕстьРасчетыСКонтрагентами(ВыборкаПоДокументам.ХозяйственнаяОперация);
        // Выводим шапку ПКО
		СведенияОбОрганизации = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ВыборкаПоДокументам.Организация, ВыборкаПоДокументам.ДатаДокумента);
		ТекстРНН_БИН 		  = "";
		
		ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаПоДокументам);	
		ОбластьМакетаШапка.Параметры.ПредставлениеОрганизации 	= ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеПолноеПоУчредительнымДокументам,");
		ОбластьМакетаШапка.Параметры.ВалютаПредставление      	= "тенге";
		ОбластьМакетаШапка.Параметры.Сумма		      			= Формат(ВыборкаПоДокументам.Сумма,"ЧЦ=15; ЧДЦ=2");
        
		СуммаПрописью = ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(ВыборкаПоДокументам.Сумма);
        ОбластьМакетаШапка.Параметры.СуммаПрописью     = СуммаПрописью;
		
		ОбластьМакетаШапка.Параметры.ОрганизацияРНН_БИН	= ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ИНН,", Ложь, "ru");
		ОбластьМакетаШапка.Параметры.ДатаДокумента     	= Формат(ВыборкаПоДокументам.ДатаДокумента, "ДФ = 'дд.ММ.гггг'")+" года";
		ОбластьМакетаШапка.Параметры.НомерДокумента    	= ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоДокументам.НомерДокумента, Ложь, Истина);
		
		Руководители = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(ВыборкаПоДокументам.Организация, КонецДня(ВыборкаПоДокументам.ДатаДокумента),);
		Бухгалтер    = Руководители.ГлавныйБухгалтер;
		Кассир       = Руководители.Кассир;
		
		ОбластьМакетаШапка.Параметры.ФИОГлавногоБухгалтера = ?(НЕ ЗначениеЗаполнено(Бухгалтер), "Не предусмотрен", Бухгалтер);
		ОбластьМакетаШапка.Параметры.ФИОКассира            = Кассир;
		
		ТекстПринятоОт = ВыборкаПоДокументам.ПринятоОт;
        Если НЕ ЗначениеЗаполнено(ТекстПринятоОт) Тогда
			Если ВыборкаПоДокументам.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы Тогда
				
				ТекстВыдать = ?(ЗначениеЗаполнено(ВыборкаПоДокументам.Организация), ВыборкаПоДокументам.Организация.Наименование, "");
				
			ИначеЕсли ЕстьРасчетыСКонтрагентами  Тогда
				
				ТекстПринятоОт = ?(ВыборкаПоДокументам.Контрагент.НаименованиеПолное = "", ВыборкаПоДокументам.Контрагент.Наименование, ВыборкаПоДокументам.Контрагент.НаименованиеПолное);			
			КонецЕсли;
		КонецЕсли;
		
		ОбластьМакетаШапка.Параметры.ПринятоОт  = ТекстПринятоОт;
		ОбластьМакетаШапка.Параметры.Основание  = ВыборкаПоДокументам.Основание;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	//для получения табличного документа
	//формируется документ на сервере
	//некоторые функции изменены для адаптации к Казахстану
	//предлогается перенос данных функций в отдельный модуль, чтобы российский можно было поставить на поддержку
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция РасшифровкаПлатежа(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Сумма КАК Сумма,
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.ПризнакСпособаРасчета КАК ПризнакСпособаРасчета
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка = &ДокументСсылка";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

Процедура ПерезаполнитьСистемуНалогообложения(Параметры) Экспорт

	МетаданныеДокумента = Метаданные.Документы.ПриходныйКассовыйОрдер;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
	ОбновлениеИнформационнойБазыРТ.ЗаполнитьСистемыНалогообложенияДокументов(ПолноеИмяОбъекта, Выборка, Параметры);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКЗаполнениюСНО(Параметры) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ПриходныйКассовыйОрдер;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	ДокументыДляОбработки = ОбновлениеИнформационнойБазыРТ.ДокументыДляЗаполненияСНО(ПолноеИмяОбъекта);
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДокументыДляОбработки);
	
КонецПроцедуры

Процедура ПерезаполнитьБанковскийСчетОрганизации(Параметры) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ПриходныйКассовыйОрдер;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
		МодульОбновлениеИнформационнойБазыРТ = ОбщегоНазначения.ОбщийМодуль("ОбновлениеИнформационнойБазыРТ");
		МодульОбновлениеИнформационнойБазыРТ.ЗаполнитьБанковскийСчетОрганизацииОбъектов(ПолноеИмяОбъекта, "БанковскийСчет", Выборка, Параметры);
		
	#КонецЕсли
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКЗаполнениюБСО(Параметры) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ПриходныйКассовыйОрдер;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		МодульОбновлениеИнформационнойБазыРТ = ОбщегоНазначения.ОбщийМодуль("ОбновлениеИнформационнойБазыРТ");
		ДокументыДляОбработки = МодульОбновлениеИнформационнойБазыРТ.ОбъектыДляЗаполненияБанковскогоСчетаОрганизации(ПолноеИмяОбъекта, "БанковскийСчет");
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДокументыДляОбработки);
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
