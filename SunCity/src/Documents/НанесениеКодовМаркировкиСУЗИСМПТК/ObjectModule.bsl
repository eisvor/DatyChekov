#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		СтандартнаяОбработка = Ложь;
		ТекстСообщения = НСтр("ru = 'Документ ""%НаименованиеНового%"" не может быть введен без основания! Воспользуйтесь вводом на основании документа ""%НаименованиеОснования%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НаименованиеНового%",	Метаданные.Документы.НанесениеКодовМаркировкиСУЗИСМПТК.Представление());
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НаименованиеОснования%", Метаданные.Документы.ЗаказКодовМаркировкиСУЗИСМПТК.Представление());
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из ЭтотОбъект.Товары Цикл
		
		Если ПустаяСтрока(СтрокаТовары.ИдентификаторСтроки) Тогда
			СтрокаТовары.ИдентификаторСтроки = Новый УникальныйИдентификатор();
		КонецЕсли;
		
	КонецЦикла;
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения.ДокументОснование;
	Иначе
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения;
	КонецЕсли;	
	
	Если (ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("Массив") 
			И ДокументОснованиеДляЗаполнения.Количество() = 0)
		ИЛИ Не ЗначениеЗаполнено(ДокументОснованиеДляЗаполнения) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыЗаполнения = РаботаСДокументамиИСМПТК.ПолучитьПараметрыЗаполненияДокументовИСМПТПоУмолчанию();
	ПараметрыЗаполнения.Вставить("МассивДокументов", ДокументОснованиеДляЗаполнения);
	ПараметрыЗаполнения.Вставить("МассивВидовДокумента"	, Новый Массив);
	
	ТипДокументаИСМПТ = Документы.НанесениеКодовМаркировкиСУЗИСМПТК.ПустаяСсылка();
	МассивДанныхДляЗаполнения = РаботаСДокументамиИСМПТК.ПодготовитьДанныеДляЗаполненияДокументаСУЗ(ПараметрыЗаполнения, ТипДокументаИСМПТ);
	
	Если ТипЗнч(МассивДанныхДляЗаполнения) = Тип("Массив") И МассивДанныхДляЗаполнения.Количество() > 0 Тогда
		
		Для Каждого СтруктураДокумента Из МассивДанныхДляЗаполнения Цикл
			
			Если СтруктураДокумента.Товары.Количество() = 0 Тогда
				ТекстСообщения = НСтр("ru = 'Сначала необходимо получить коды по заказу!'");
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
				Возврат;
			КонецЕсли;
				
			Если МассивДанныхДляЗаполнения.Найти(СтруктураДокумента) > 0 Тогда
				НовыйДокументЗаказ = Документы.НанесениеКодовМаркировкиСУЗИСМПТК.СоздатьДокумент();
			Иначе
				НовыйДокументЗаказ = ЭтотОбъект;
			КонецЕсли; 
			
			//Заполняем шапку документа
			ЗаполнитьЗначенияСвойств(НовыйДокументЗаказ, СтруктураДокумента.Реквизиты[0]);
			
			НовыйДокументЗаказ.ТипИспользования = ПредопределенноеЗначение("Перечисление.ТипыИспользованияСУЗИСМПТК.НанесениеПодтверждено");
			НовыйДокументЗаказ.Состояние = Перечисления.СостоянияДокументовСУЗИСМПТК.Черновик;  
			НовыйДокументЗаказ.Статус 	 = Перечисления.СтатусыОбработкиОтчетовСУЗИСМПТК.Черновик;  		
			
			//Заполняем табличную часть
			Для Каждого СтрокаТЧ Из СтруктураДокумента.Товары Цикл
				НоваяСтрокаТЧ = НовыйДокументЗаказ.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаТЧ);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Состояние     = РаботаСДокументамиИСМПТК.СостояниеДокументаСУЗ(ЭтотОбъект);
	ЭтотОбъект.Ответственный = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь(); 
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		ЭтотОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиОтчетовСУЗИСМПТК.Черновик");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	//Дополнительно нужно заполнить признак в документе-основания
	Если ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК")
		И ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование) Тогда
		
		ЗаказКМ = ЭтотОбъект.ДокументОснование.ПолучитьОбъект();
		ЗаказКМ.ВыполненоНанесениеКМ = Истина;
		ЗаказКМ.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.Номер = Неопределено;
	ЭтотОбъект.OrderID = Неопределено;
	ЭтотОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиОтчетовСУЗИСМПТК.Черновик");
	ЭтотОбъект.Состояние = Неопределено;
	ЭтотОбъект.ДокументОснование = Неопределено;
	ЭтотОбъект.Ответственный = Неопределено;
	ЭтотОбъект.ПричинаОтклонения = Неопределено;
	ЭтотОбъект.СрокГодности = Неопределено;
	ЭтотОбъект.НомерПроизводственнойСерии = Неопределено;
	
	ЭтотОбъект.Товары.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//В общем случае не проверяем заполненность реквизитов при записи
	//проверка необходимых реквизитов проходит при отправке документа
	//в случае необходимости можно указать реквизиты проверки отдельно
	//в процедуре ДокументНанесениеКодовМаркировкиСУЗИСМПТК_ОбработкаПроверкиЗаполнения
	ПроверяемыеРеквизиты.Очистить();
	МассивНепроверяемыхРеквизитов = Новый Массив;
	РаботаСДокументамиИСМПТКПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование) Тогда
		ЗаказКМ = ЭтотОбъект.ДокументОснование.ПолучитьОбъект();
		ЗаказКМ.ВыполненоНанесениеКМ = Ложь;
		ЗаказКМ.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
КонецПроцедуры

#КонецЕсли