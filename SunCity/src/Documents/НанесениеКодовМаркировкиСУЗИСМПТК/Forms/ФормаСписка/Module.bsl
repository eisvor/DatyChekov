
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		Организации.Добавить(Параметры.Отбор.Организация);
		Организация = Организации.Получить(0).Значение;
		ОрганизацииПредставление = ОбщегоНазначенияИСМПТК.ЗначениеРеквизитаОбъекта(Организация, "Представление");
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		ЭтаФорма.Заголовок = Параметры.Заголовок;
	КонецЕсли;	
	
	УстановитьУсловноеОформление();
	
	СобытияФормИСМПТКПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СобытияФормИСМПТККлиентПереопределяемый.ПриОткрытии(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияЗаписьНанесениеКодовМаркировки() Тогда
		
		Элементы.Список.Обновить();
		УстановитьВидимостьКнопокИзмененияСтатуса(Элементы.Список.ТекущиеДанные.Статус);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьКодыМаркировкиВФайл(Команда)
	
	ТекстВопроса = НСтр("ru = 'По связанному заказу будут выгружены доступные коды маркировки из Пула кодов.'")
				 + Символы.ПС
				 + НСтр("ru = 'Продолжить?'");
		
	ВыгрузитьКодыМаркировки = Новый ОписаниеОповещения("ВыгрузитьКодыМаркировки", ЭтаФорма);
	ПоказатьВопрос(ВыгрузитьКодыМаркировки, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ОбщегоНазначенияИСМПТКПереопределяемый.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.Дата", Элементы.Дата.Имя);
			
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКнопокИзмененияСтатуса(Статус = Неопределено)
	
	НеОтправлен = Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиОтчетовСУЗИСМПТК.Черновик");
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументНанесениеКодовМаркировкиСУЗИСМПТКОтправить", "Видимость", НеОтправлен);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументНанесениеКодовМаркировкиСУЗИСМПТКПроверить", "Видимость", Ложь);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументНанесениеКодовМаркировкиСУЗИСМПТКОбновить",  "Видимость", Не НеОтправлен);
			
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументНанесениеКодовМаркировкиСУЗИСМПТКОбновить", "Доступность", Ложь);
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьКнопокИзмененияСтатуса(ТекущиеДанные.Статус);
	
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументНанесениеКодовМаркировкиСУЗИСМПТКОбновить", "Доступность", Истина);
	
	Если ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиОтчетовСУЗИСМПТК.Отправлен") Тогда
		//Разрешаем печатать и выгружать коды в файл только когда они нанесены (документ успешно принят сервером)
		ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокСохранитьКодыМаркировкиВФайл", "Видимость", Истина);
	Иначе
		ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокСохранитьКодыМаркировкиВФайл", "Видимость", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#Область ВыгрузкаВФайл

&НаКлиенте
Процедура ВыгрузитьКодыМаркировкиИзФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если Не ВыбранныеФайлы = Неопределено Тогда
		
		НовыйТекстовыйДокумент = Новый ТекстовыйДокумент;
		
		ОтчетыОбИспользовании = Элементы.Список.ВыделенныеСтроки;
		Заказы = ПолучитьСписокЗаказовПоВыбраннымДокументам(ОтчетыОбИспользовании);
		
		СписокКодов = ИнтеграцияИСМПТКВызовСервера.ПолучитьКодыМаркировкиЗаказов(Заказы);
		
		Если Не СписокКодов = Неопределено Тогда
			
			Для Каждого СтрокаТЧ Из СписокКодов Цикл
				НовыйТекстовыйДокумент.ДобавитьСтроку(РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.КодМаркировки));
			КонецЦикла;
			
			НовыйТекстовыйДокумент.Записать(ВыбранныеФайлы[0]);
			
			ИнтеграцияИСМПТКВызовСервера.ОтметитьВыгрузкуКодовМаркировкиЗаказа(СписокКодов);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьСписокЗаказовПоВыбраннымДокументам(ОтчетыОбИспользовании);
	
	Если ОтчетыОбИспользовании.Количество() = 0 Тогда
		Возврат ПредопределенноеЗначение("Документ.ЗаказКодовМаркировкиСУЗИСМПТК.ПустаяСсылка");
	КонецЕсли;
	
	ЗаказыКМ = Новый Массив();
	Для Каждого Нанесение Из ОтчетыОбИспользовании Цикл
		
		Если Не ЗначениеЗаполнено(Нанесение.ДокументОснование) Тогда
			ТекстСообщения = НСтр("ru = 'У отчета %1 не выбран документ-основание (Заказ кодов маркировки). Невозможно обновить сведения в Пуле кодов.'")
						   + Символы.ПС
						   + НСтр("ru = 'Документ исключен из списка выгрузки в файл.'");
			ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, Нанесение.Ссылка);
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения); 
			Продолжить;			
		КонецЕсли;
		
		ЗаказыКМ.Добавить(Нанесение.ДокументОснование);		
		
	КонецЦикла;
	
	Возврат ЗаказыКМ;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьКодыМаркировки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогВыбораФайла.Заголовок = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияЗаголовокВыборФайла();
		ДиалогВыбораФайла.Фильтр	= "CSV (Comma-Separated Values)(*.csv)|*.csv";
		ДиалогВыбораФайла.ИндексФильтра = 0;
		ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
		ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
		ДиалогВыбораФайла.МножественныйВыбор = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ВыгрузитьКодыМаркировкиИзФайлаЗавершение", ЭтотОбъект);
		ДиалогВыбораФайла.Показать(Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ОбщегоНазначенияИСМПТККлиентПереопределяемый.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	
	ОбщегоНазначенияИСМПТКПереопределяемый.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
