#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
			ДокументОснованиеДляЗаполнения = ДанныеЗаполнения.ДокументОснование;
		Иначе
	        ДокументОснованиеДляЗаполнения = Неопределено;
		КонецЕсли;
	Иначе
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ДокументОснованиеДляЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	МассивДокументов = Новый Массив();
	МассивДокументов.Добавить(ДокументОснованиеДляЗаполнения);
	
	ПараметрыЗаполнения = РаботаСДокументамиИСМПТК.ПолучитьПараметрыЗаполненияДокументовИСМПТПоУмолчанию();
	ПараметрыЗаполнения.Вставить("МассивДокументов", МассивДокументов);
		
	ТипДокументаИСМПТ = Документы.УведомлениеОВвозеИзТретьихСтранИСМПТК.ПустаяСсылка();
	МассивДанныхДляЗаполнения = РаботаСДокументамиИСМПТК.ПодготовитьДанныеДляЗаполненияДокументаИСМПТ(ПараметрыЗаполнения, ТипДокументаИСМПТ);
	
	Если ТипЗнч(МассивДанныхДляЗаполнения) = Тип("Массив") И МассивДанныхДляЗаполнения.Количество() > 0 Тогда
		
		Для Каждого СтруктураДокумента Из МассивДанныхДляЗаполнения Цикл
			
			Если МассивДанныхДляЗаполнения.Найти(СтруктураДокумента) > 0 Тогда
				НовыйУведомлениеОВвозе = Документы.УведомлениеОВвозеИзТретьихСтранИСМПТК.СоздатьДокумент();
			Иначе
				НовыйУведомлениеОВвозе = ЭтотОбъект;
			КонецЕсли; 
		
			//Заполняем шапку документа
			ЗаполнитьЗначенияСвойств(НовыйУведомлениеОВвозе, СтруктураДокумента.Реквизиты[0]);
			
			СтруктураПереопределяемыхРеквизитов = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВвозеИмпортИСМПТ(МассивДокументов);
			ЗаполнитьЗначенияСвойств(НовыйУведомлениеОВвозе, СтруктураПереопределяемыхРеквизитов);
			
			НовыйУведомлениеОВвозе.Состояние = Перечисления.СостоянияДокументовИСМПТК.Сформирован;
			НовыйУведомлениеОВвозе.Статус    = Перечисления.СтатусыДокументовИСМПТК.НеОпределен;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Состояние = РаботаСДокументамиИСМПТК.СостояниеДокументаИСМПТ(ЭтотОбъект);
		
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Автор) Тогда
		ЭтотОбъект.Автор = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь();
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Статус) Тогда
		ЭтотОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.НеОпределен");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Номер) Тогда
		ЭтотОбъект.Номер = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьНомерНаПечать(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.ДокументОснование = Неопределено; 
	ЭтотОбъект.Идентификатор = Неопределено;
	ЭтотОбъект.НомерИСМПТ = Неопределено;
	ЭтотОбъект.ДатаВИСМПТ = Неопределено;
	ЭтотОбъект.Статус = Перечисления.СтатусыДокументовИСМПТК.НеОпределен; 
	ЭтотОбъект.Состояние = Неопределено;
	ЭтотОбъект.Автор = Неопределено;
	ЭтотОбъект.Номер = Неопределено;
	ЭтотОбъект.РегНомерНаБумажномНосителе = Неопределено;
	ЭтотОбъект.ДатаВыпискиНаБумажномНосителе = Неопределено;
	ЭтотОбъект.НомерДокументаТаможенногоОформления = Неопределено;	
	ЭтотОбъект.ДатаДокументаТаможенногоОформления = Неопределено;
	ЭтотОбъект.КодРешенияТаможенногоОргана = Неопределено;
	ЭтотОбъект.ДатаВремяПринятияРешения = Неопределено;
	
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.Марки.Очистить();
	ЭтотОбъект.Ошибки.Очистить();
	ЭтотОбъект.ДокументыСоответствия.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//В общем случае не проверяем заполненность реквизитов при записи
	//проверка необходимых реквизитов проходит при отправке документа
	//в случае необходимости можно указать реквизиты проверки отдельно
	//в процедуре УведомлениеОВвозеТретьиСтраны_ОбработкаПроверкиЗаполнения
	ПроверяемыеРеквизиты.Очистить();
	МассивНепроверяемыхРеквизитов = Новый Массив();
	РаботаСДокументамиИСМПТКПереопределяемый.УведомлениеОВвозеТретьиСтраны_ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	РаботаСДокументамиИСМПТКПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	
КонецПроцедуры

#КонецЕсли