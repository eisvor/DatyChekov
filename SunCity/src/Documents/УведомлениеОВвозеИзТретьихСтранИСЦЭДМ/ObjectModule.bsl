#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения.ДокументОснование;
	Иначе
		ДокументОснованиеДляЗаполнения = ДанныеЗаполнения;
	КонецЕсли;	
	
	Если ДокументОснованиеДляЗаполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("Массив") 
		И ДокументОснованиеДляЗаполнения.Количество() <> 0 Тогда
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения[0]);
	Иначе
		ТипДокумента = ТипЗнч(ДокументОснованиеДляЗаполнения);
	КонецЕсли;
	
	//Если создается документ без основания
	Если ТипДокумента = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = РаботаСДокументамиИСМПТК.ПолучитьПараметрыЗаполненияДокументовИСМПТПоУмолчанию();
	ПараметрыЗаполнения.Вставить("МассивДокументов", ДокументОснованиеДляЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОПередачеКИОтНерезидентаРКИСЦЭДМ") Тогда
		
		ИсключаемыеПоля = "Автор, Комментарий, НомерИСЦЭДМ, ДатаВИСЦЭДМ, Идентификатор, Номер, Дата, ДатаВыпискиНаБумажномНосителе, РегНомерНаБумажномНосителе"; 
		
		НовыйДокумент = ЭтотОбъект;
		НовыйДокумент.Организация = ДанныеЗаполнения.Получатель;
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ДанныеЗаполнения,, ИсключаемыеПоля);
		
		Если ОбщегоНазначенияИСМПТКВызовСервера.ПроверитьЗначениеКонстанты("ВестиУчетПоСкладамИСЦЭДМ") 
			И ЗначениеЗаполнено(ДанныеЗаполнения.СкладЦЭДМ) Тогда
			НовыйДокумент.СкладЦЭДМ = ДанныеЗаполнения.СкладЦЭДМ;
			НовыйДокумент.ПолучательСкладЦЭДМИдентификационныйНомер = ДанныеЗаполнения.СкладЦЭДМ.ИИНБИНСклада;
			СкладУчетныйПоСоответствию = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьДанныеСкладаПоИдентификаторуЦЭДМ(
			НовыйДокумент.ПолучательСкладЦЭДМИдентификационныйНомер, НовыйДокумент.Организация);
			Если Не СкладУчетныйПоСоответствию = Неопределено Тогда
				НовыйДокумент.Склад = СкладУчетныйПоСоответствию.Склад;
			КонецЕсли;
		КонецЕсли;                                               
		
		ТЧМарки = ДанныеЗаполнения.Марки.Выгрузить();
		НовыйДокумент.Марки.Загрузить(ТЧМарки);
		ТЧИтоги = НовыйДокумент.Марки.Выгрузить(, "Номенклатура,GTIN,Количество");
		ТЧИтоги.Свернуть("Номенклатура,GTIN", "Количество");
		
		НовыйДокумент.Итоги.Загрузить(ТЧИтоги);
		НовыйДокумент.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий");
			
	Иначе
		
		ТипДокументаИСЦЭДМ = Документы.УведомлениеОВвозеИзТретьихСтранИСЦЭДМ.ПустаяСсылка();
		МассивДанныхДляЗаполнения = РаботаСДокументамиИСМПТК.ПодготовитьДанныеДляЗаполненияДокументаИСЦЭДМ_Общая(ПараметрыЗаполнения, ТипДокументаИСЦЭДМ);
		
		Если ТипЗнч(МассивДанныхДляЗаполнения) = Тип("Массив") И МассивДанныхДляЗаполнения.Количество() > 0 Тогда
			
			Для Каждого СтруктураДокумента Из МассивДанныхДляЗаполнения Цикл
				
				Если МассивДанныхДляЗаполнения.Найти(СтруктураДокумента) > 0 Тогда
					НовыйДокумент = Документы.УведомлениеОВвозеИзТретьихСтранИСЦЭДМ.СоздатьДокумент();
				Иначе
					НовыйДокумент = ЭтотОбъект;
				КонецЕсли; 
				
				РеквизитыДокумента = СтруктураДокумента.Реквизиты[0];
				ЗаполнитьЗначенияСвойств(НовыйДокумент, РеквизитыДокумента);
				//Заполняем шапку документа дополнительными сведениями
				СтруктураПереопределяемыхРеквизитов = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВвозеИмпортИСЦЭДМ(ПараметрыЗаполнения.МассивДокументов);
				ЗаполнитьЗначенияСвойств(НовыйДокумент, СтруктураПереопределяемыхРеквизитов);
				
				НовыйДокумент.СтранаОтправления = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьСтрануОтправленияВвозИзЕАЭС(РеквизитыДокумента.Контрагент);
				НовыйДокумент.Организация = РеквизитыДокумента.Поставщик;
				НовыйДокумент.Получатель = РеквизитыДокумента.Поставщик; //Организация из документа-основания
				НовыйДокумент.ПолучательНаименование = РеквизитыДокумента.ПоставщикНаименование;
				НовыйДокумент.ПолучательИдентификационныйНомер = РеквизитыДокумента.ПоставщикИдентификационныйНомер;
				
				Если ОбщегоНазначенияИСМПТКВызовСервера.ПроверитьЗначениеКонстанты("ВестиУчетПоСкладамИСЦЭДМ") Тогда
					СкладЦЭДМПоСоответствию = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(РеквизитыДокумента.Склад, НовыйДокумент.Организация);
					Если Не СкладЦЭДМПоСоответствию = Неопределено Тогда
						НовыйДокумент.СкладЦЭДМ = СкладЦЭДМПоСоответствию.СкладЦЭДМ;
						НовыйДокумент.ПолучательСкладЦЭДМИдентификационныйНомер = СкладЦЭДМПоСоответствию.ИдентификаторСклада;
					КонецЕсли;
				КонецЕсли;
				
				Для Каждого СтрокаТЧ Из СтруктураДокумента.Товары Цикл
					НоваяСтрокаТЧ = НовыйДокумент.Товары.Добавить();
					НоваяСтрокаТЧ.Номенклатура = СтрокаТЧ.Номенклатура;
					НоваяСтрокаТЧ.КоличествоИсточник = СтрокаТЧ.КоличествоИсточник;
					НоваяСтрокаТЧ.ЕдиницаИзмерения   = СтрокаТЧ.ЕдиницаИзмерения;
				КонецЦикла;
				
				НовыйДокумент.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий");
				НовыйДокумент.СтранаОтправления = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьСтрануОтправленияВвозИзЕАЭС(РеквизитыДокумента.Контрагент);			
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Автор) Тогда
		ЭтотОбъект.Автор = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь(); 
	КонецЕсли;
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		ЭтотОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСЦЭДМ.НеОпределен");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.Идентификатор = Неопределено;
	ЭтотОбъект.НомерИСЦЭДМ = Неопределено;
	ЭтотОбъект.ДатаВИСЦЭДМ = Неопределено;
	ЭтотОбъект.Статус = Перечисления.СтатусыДокументовИСЦЭДМ.НеОпределен;
	ЭтотОбъект.ЦеныПереданыУспешно = Неопределено;
	ЭтотОбъект.НомерДокументаТаможенногоОформления = Неопределено;
	ЭтотОбъект.ДатаДокументаТаможенногоОформления = Неопределено;
	ЭтотОбъект.КодРешенияТаможенногоОргана = Неопределено;
	ЭтотОбъект.ДатаВремяПринятияРешения = Неопределено;
	ЭтотОбъект.ДатаВыпискиНаБумажномНосителе = Неопределено;
	ЭтотОбъект.РегНомерНаБумажномНосителе = Неопределено;
	ЭтотОбъект.ДокументОснование = Неопределено;
	ЭтотОбъект.Автор = Неопределено;
	ЭтотОбъект.Номер = Неопределено;
	
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.Марки.Очистить();
	ЭтотОбъект.Ошибки.Очистить(); 
	ЭтотОбъект.Итоги.Очистить();
	ЭтотОбъект.ДокументыСоответствия.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Очистить();
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	РаботаСДокументамиИСМПТКПереопределяемый.ДокументВвозИмпорт_ОбработкаПроверкиЗаполненияИСЦЭДМ(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	РаботаСДокументамиИСМПТКПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
		
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли


