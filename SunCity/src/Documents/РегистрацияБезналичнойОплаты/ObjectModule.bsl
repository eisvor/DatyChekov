#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.РегистрацияБезналичнойОплаты.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДенежныеСредстваСервер.ОтразитьДенежныеСредстваБезналичные(ДополнительныеСвойства, Движения, Отказ);
	ДенежныеСредстваСервер.ОтразитьРасчетыСКлиентами(ДополнительныеСвойства, Движения, Отказ);
	ДенежныеСредстваСервер.ОтразитьРасчетыСПоставщиками(ДополнительныеСвойства, Движения, Отказ);
	ДенежныеСредстваСервер.ОтразитьДенежныеСредстваКВыплате(ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ЗакупкиСервер.ОбновитьСостояниеОплаты(ЭтотОбъект);
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		Если НЕ Отказ Тогда
			ОбменССайтомРТ.ЗаписатьСостояниеОплатыЗаказаПокупателя(ДокументОснование, Ссылка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Или ДанныеЗаполнения = NULL Тогда
		ВызватьИсключение НСтр("ru='Команда не может быть выполнена для указанного объекта!
								   |Финансы -> Безналичная оплата -> Создать'");
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ПроверитьВозможностьВводаНаОснованииБезЗаявки(Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
		ЗаполнитьПоЗаказуПоступлению(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ПроверитьВозможностьВводаНаОснованииБезЗаявки(Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
		ЗаполнитьПоЗаказуПоступлению(ДанныеЗаполнения, Истина);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ЗаполнитьПоВозвратуТоваров(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ЗаполнитьПоЗаявкеНаРасходованиеДС(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ЗаполнитьПоЗаказуПокупателя(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ЗаполнитьПоДокументуРасчетовСКлиентом(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ЗаполнитьПоДокументуРасчетовСКлиентом(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ЗаполнитьПоОтчетуКомитентуОПродажах(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РегистрацияБезналичнойОплаты") Тогда
		
		Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
			ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
			ЗаполнитьВозвратПокупателюАванса(ДанныеЗаполнения);
		ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику Тогда
			ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
			ЗаполнитьВозвратОтПоставщика(ДанныеЗаполнения);
		Иначе
			ТекстИсключения = НСтр("ru = 'Ввод на основании возможен только для поступления оплаты от покупателя или оплаты поставщику'");
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		Если ДанныеЗаполнения.Свойство("ДокументОснование") И ДанныеЗаполнения.Свойство("Организация") Тогда
			Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				ДанныеЗаполненияПрочие = Новый Структура;
				ФинансыСервер.ЗаполнениеРасшифровкиПлатежаПоЗаказуПокупателя(ЭтотОбъект, ДанныеЗаполнения.ДокументОснование, ДанныеЗаполненияПрочие, ДанныеЗаполнения.Организация);
			КонецЕсли;
		КонецЕсли;
		
		ПроверитьВозможностьВводаНаОснованииБезЗаявки(ЭтотОбъект.ХозяйственнаяОперация);
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	ОбщегоНазначенияРТ.ПроверитьИспользованиеОрганизации(,,Организация);
	ДенежныеСредстваСервер.ЗаполнитьПризнакСпособаРасчета(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем МассивВсехРеквизитов;
	Перем МассивРеквизитовОперации;
	
	Документы.ПриходныйКассовыйОрдер.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ХозяйственнаяОперация,
		МассивВсехРеквизитов,
		МассивРеквизитовОперации);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ОбщегоНазначенияРТКлиентСервер.ЗаполнитьМассивНепроверяемыхРеквизитов(
		МассивВсехРеквизитов,
		МассивРеквизитовОперации,
		МассивНепроверяемыхРеквизитов);
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы 
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("Магазин");
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетБезналичныхДенежныхСредств")
		ИЛИ Дата < Константы.ДатаНачалаУчетаБезналичныхДенежныхСредств.Получить() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчетОрганизации");
	КонецЕсли;
	
	ПродажиСервер.ПроверитьСистемуНалогообложения(ЭтотОбъект, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ЗакупкиСервер.ОбновитьСостояниеОплаты(ЭтотОбъект);
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		Если НЕ Отказ Тогда
			ОбменССайтомРТ.ЗаписатьСостояниеОплатыЗаказаПокупателя(ДокументОснование, Ссылка, Ссылка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(Проведен, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ОбщегоНазначенияРТ.УстановитьНовоеЗначениеРеквизита(
		ЭтотОбъект,
		ОбработкаТабличнойЧастиТоварыКлиентСервер.СуммаДокумента(РасшифровкаПлатежа, Истина),
		"СуммаДокумента");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	ОплатаВыполнена = Ложь;
	ПробитЧек       = Ложь;
	СменаЗакрыта    = Ложь;
	
	НомерЧекаККМ            = 0;
	ОтчетОРозничныхПродажах = Документы.ОтчетОРозничныхПродажах.ПустаяСсылка();
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация   = ЗначениеНастроекПовтИсп.ОрганизацияПоУмолчанию(Организация,Ответственный);
	Контрагент    = ЗначениеНастроекПовтИсп.ПоставщикПоУмолчанию(Ответственный, Контрагент);
	Магазин       = ЗначениеНастроекПовтИсп.МагазинПоУмолчанию(Магазин);
	КассаККМ      = ДенежныеСредстваСервер.КассаККМ(Организация, Магазин, ПараметрыСеанса.РабочееМестоКлиента);
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту Тогда 
		ДенежныеСредстваСервер.ЗаполнитьСистемуНалогообложения(ЭтотОбъект);
	КонецЕсли;
	
	Если РасшифровкаПлатежа.Количество() > 0 Тогда
		СуммаДокумента = РасшифровкаПлатежа.Итог("Сумма");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуПоступлению(ДанныеЗаполнения, ПоПоступлению = Ложь)

	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику;
	СтатьяДвиженияДенежныхСредств = ЗначениеНастроекПовтИсп.СтатьяДвиженияДенежныхСредств(ХозяйственнаяОперация);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Магазин, Контрагент, Организация");
	
	СуммаНеОплаченногоЭтапа = ЗакупкиСервер.ПолучитьСуммуНеОплаченногоЭтапа(ДанныеЗаполнения, Перечисления.ФормыОплаты.Безналичная);

	Если СуммаНеОплаченногоЭтапа = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Нет задолженности по запланированным оплатам на дату: %Дата% (безналичная форма оплаты) по документу ""%ДокументОснование%"".
									|Перепланируйте этапы оплат или введите сумму вручную.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Дата%", Формат(ТекущаяДатаСеанса(),"ДЛФ=D"));
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДокументОснование%", ДанныеЗаполнения);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "РасшифровкаПлатежа");
	КонецЕсли;
	
	НоваяСтрока = РасшифровкаПлатежа.Добавить();
	НоваяСтрока.ДокументРасчетовСКонтрагентом = ДанныеЗаполнения;
	НоваяСтрока.Сумма = СуммаНеОплаченногоЭтапа;
	НоваяСтрока.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуПокупателя(ДанныеЗаполнения)
	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента;
	ДокументОснование     = ДанныеЗаполнения;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Магазин, Контрагент, Организация");
	
	ДанныеЗаполненияПрочие = Новый Структура;
	ФинансыСервер.ЗаполнениеРасшифровкиПлатежаПоЗаказуПокупателя(ЭтотОбъект, ДанныеЗаполнения, ДанныеЗаполненияПрочие);
	
КонецПроцедуры

Процедура ЗаполнитьПоДокументуРасчетовСКлиентом(ДанныеЗаполнения)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Магазин, Контрагент, Организация");
	
	ДанныеПоДокументуРасчета = ПродажиСервер.ДанныеПоДокументуРасчета(ДанныеЗаполнения);
	
	НеОплаченнаяСумма = ДанныеПоДокументуРасчета.Сумма;
	ХозяйственнаяОперация = ДанныеПоДокументуРасчета.ХозяйственнаяОперация;
	СтатьяДвиженияДенежныхСредств = ЗначениеНастроекПовтИсп.СтатьяДвиженияДенежныхСредств(ХозяйственнаяОперация);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		ДокументОснование = ДанныеЗаполнения.ЗаказПокупателя;
	КонецЕсли;
	
	НоваяСтрока = РасшифровкаПлатежа.Добавить();
	НоваяСтрока.ДокументРасчетовСКонтрагентом = ДанныеЗаполнения;
	НоваяСтрока.Сумма = НеОплаченнаяСумма;
	НоваяСтрока.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
	
КонецПроцедуры

Процедура ЗаполнитьПоВозвратуТоваров(ДанныеЗаполнения)
	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика;
	СтатьяДвиженияДенежныхСредств = ЗначениеНастроекПовтИсп.СтатьяДвиженияДенежныхСредств(ХозяйственнаяОперация);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Магазин, Контрагент, Организация");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетыСПоставщикамиОстатки.ДокументРасчета,
		|	РасчетыСПоставщикамиОстатки.КОплатеОстаток КАК КПоступлению
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками.Остатки(, ДокументРасчета = &Ссылка) КАК РасчетыСПоставщикамиОстатки
		|ГДЕ
		|	РасчетыСПоставщикамиОстатки.СуммаОстаток > 0";

	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);

	Результат = Запрос.Выполнить();
	
		
	Если Результат.Пустой() Тогда
		
		ТекстОшибки = НСтр("ru = 'Нет задолженности поставщика по документу ""%ДокументОснование%"" .'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДокументОснование%", ДанныеЗаполнения);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "РасшифровкаПлатежа");
		
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		НоваяСтрока = РасшифровкаПлатежа.Добавить();
		НоваяСтрока.ДокументРасчетовСКонтрагентом = ДанныеЗаполнения;
		НоваяСтрока.Сумма = Выборка.КПоступлению;
		НоваяСтрока.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаявкеНаРасходованиеДС(ДанныеЗаполнения)

	Если ДанныеЗаполнения.Статус <> Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.Утверждена Тогда
		
		Текст = НСтр("ru = 'Регистрацию безналичной оплаты возможно вводить на основании заявки со статусом ""Утверждена""'");
		ВызватьИсключение Текст;
		
	КонецЕсли;
	
	Если НЕ (ДанныеЗаполнения.ФормаОплатыЗаявки = Перечисления.ФормыОплаты.Безналичная ИЛИ ДанныеЗаполнения.ФормаОплатыЗаявки = Перечисления.ФормыОплаты.ПустаяСсылка()) Тогда
		
		Текст = НСтр("ru = 'Регистрацию безналичной оплаты возможно вводить на основании заявки с формой оплаты ""Безналичные"" или ""Любая""'");
		ВызватьИсключение Текст;
		
	КонецЕсли;
	
	ДокументОснование = ДанныеЗаполнения;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Магазин, Контрагент, ХозяйственнаяОперация, Организация");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.НомерСтроки,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТЧДок
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа
	|ГДЕ
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.Ссылка = &РаспоряжениеНаРасходованиеДенежныхСредств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДенежныеСредстваКВыплатеОстатки.ДокументРасчета КАК ДокументРасчетовСКонтрагентом,
	|	ДенежныеСредстваКВыплатеОстатки.СтатьяДвиженияДенежныхСредств,
	|	-ДенежныеСредстваКВыплатеОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате.Остатки(, РаспоряжениеНаРасходованиеДенежныхСредств = &РаспоряжениеНаРасходованиеДенежныхСредств) КАК ДенежныеСредстваКВыплатеОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТЧДок КАК ТЧДок
	|		ПО ДенежныеСредстваКВыплатеОстатки.ДокументРасчета = ТЧДок.ДокументРасчетовСКонтрагентом
	|			И ДенежныеСредстваКВыплатеОстатки.СтатьяДвиженияДенежныхСредств = ТЧДок.СтатьяДвиженияДенежныхСредств
	|ГДЕ
	|	ДенежныеСредстваКВыплатеОстатки.СуммаОстаток < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТЧДок.НомерСтроки";
	
	Запрос.УстановитьПараметр("РаспоряжениеНаРасходованиеДенежныхСредств", ДанныеЗаполнения);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Текст = НСтр("ru = 'Заявка на расходование ДС полностью оплачена.
							|Ввод документов оплаты не требуется.'");
		ВызватьИсключение Текст;
	Иначе
		РасшифровкаПлатежа.Загрузить(Результат.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоОтчетуКомитентуОПродажах(ДанныеЗаполнения)
	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику;
	СтатьяДвиженияДенежныхСредств = ЗначениеНастроекПовтИсп.СтатьяДвиженияДенежныхСредств(ХозяйственнаяОперация);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Магазин, Контрагент, Организация");
	
	СуммаНеОплаченногоЭтапа = ЗакупкиСервер.ПолучитьСуммуНеОплаченногоЭтапа(ДанныеЗаполнения, Перечисления.ФормыОплаты.Безналичная);
	
	Если СуммаНеОплаченногоЭтапа = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Нет задолженности по запланированным оплатам на дату: %Дата% (безналичная форма оплаты) по документу ""%ДокументОснование%"".
		|Перепланируйте этапы оплат или введите сумму вручную.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Дата%", Формат(ТекущаяДатаСеанса(),"ДЛФ=D"));
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДокументОснование%", ДанныеЗаполнения);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "РасшифровкаПлатежа");
	КонецЕсли;
	
	СуммаНеОплаченногоЭтапа = СуммаНеОплаченногоЭтапа - ДанныеЗаполнения.СуммаВознаграждения;
	
	НоваяСтрока = РасшифровкаПлатежа.Добавить();
	НоваяСтрока.ДокументРасчетовСКонтрагентом = ДанныеЗаполнения;
	НоваяСтрока.Сумма                         = ?(СуммаНеОплаченногоЭтапа<0,0,СуммаНеОплаченногоЭтапа);
	НоваяСтрока.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
	
КонецПроцедуры

Процедура ЗаполнитьВозвратПокупателюАванса(ДанныеЗаполнения)
	
	РеквизитыИсключаемыеИзЗаполнения = "Дата, Номер, ПробитЧек, НомерЧекаККМ, ХозяйственнаяОперация, Ответственный, РасшифровкаПлатежа,
		|ДатаВходящегоДокумента, НомерВходящегоДокумента, НазначениеПлатежа, Проведен";
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,,РеквизитыИсключаемыеИзЗаполнения);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыСКлиентамиОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки КАК РасчетыСКлиентамиОстатки
	|ГДЕ
	|	РасчетыСКлиентамиОстатки.ДокументРасчета = &ДокументРасчета";
	
	Запрос.УстановитьПараметр("ДокументРасчета", ДанныеЗаполнения);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту;
	Ответственный = Пользователи.ТекущийПользователь();
	СтатьяДвиженияДенежныхСредств = ЗначениеНастроекПовтИсп.СтатьяДвиженияДенежныхСредств(ХозяйственнаяОперация);
	
	СтрокаРасчета = РасшифровкаПлатежа.Добавить();
	СтрокаРасчета.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
	Если Выборка.Следующий() Тогда
		СтрокаРасчета.ДокументРасчетовСКонтрагентом = ДанныеЗаполнения;
		СтрокаРасчета.Сумма = Выборка.СуммаОстаток;
	Иначе
		СтрокаРасчета.Сумма = ДанныеЗаполнения.СуммаДокумента;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьВозвратОтПоставщика(ДанныеЗаполнения)
	
	РеквизитыИсключаемыеИзЗаполнения = "Дата, Номер, ПробитЧек, НомерЧекаККМ, ХозяйственнаяОперация, Ответственный, РасшифровкаПлатежа,
		|ДатаВходящегоДокумента, НомерВходящегоДокумента, НазначениеПлатежа, Проведен";
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,,РеквизитыИсключаемыеИзЗаполнения);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(, ) КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	РасчетыСПоставщикамиОстатки.ДокументРасчета = &ДокументРасчета";
	
	Запрос.УстановитьПараметр("ДокументРасчета", ДанныеЗаполнения);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика;
	Ответственный = Пользователи.ТекущийПользователь();
	СтатьяДвиженияДенежныхСредств = ЗначениеНастроекПовтИсп.СтатьяДвиженияДенежныхСредств(ХозяйственнаяОперация);
	
	СтрокаРасчета = РасшифровкаПлатежа.Добавить();
	СтрокаРасчета.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
	Если Выборка.Следующий() Тогда
		СтрокаРасчета.ДокументРасчетовСКонтрагентом = ДанныеЗаполнения;
		СтрокаРасчета.Сумма = Выборка.СуммаОстаток;
	Иначе
		СтрокаРасчета.Сумма = ДанныеЗаполнения.СуммаДокумента;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()
	
	Массив = Новый Массив;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Массив.Добавить(Движения.ДенежныеСредстваКВыплате);
	КонецЕсли;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);
	
КонецПроцедуры

Процедура ПроверитьВозможностьВводаНаОснованииБезЗаявки(Операция)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаРасходованиеДС")
		И НЕ УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(ПланыВидовХарактеристик.ПраваПользователей.РазрешитьОформлениеРКОРБОБезЗаявки, Ложь) Тогда
		
		ХозОперация = ?(ЗначениеЗаполнено(Операция), Операция, Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
		
		Если НЕ (ХозОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику ИЛИ ХозОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы) Тогда
			Возврат;
		КонецЕсли;
		
		ТекстОшибки = НСтр("ru='Регистрацию безналичной оплаты с операцией ""%1"" необходимо вводить на основании
								|заявки на расходование денежных средств.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ХозОперация);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
