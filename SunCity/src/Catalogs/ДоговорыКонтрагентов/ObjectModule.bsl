
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ПризнакАгента = Перечисления.ПризнакиАгента.Комиссионер Тогда
		
		Если НЕ ЗначениеЗаполнено(УчетНДСУКомитента) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = '""Выделение НДС"" не заполнено'");
			ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект, 
			"УчетНДСУКомитента");
		КонецЕсли;
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("СтавкаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("УчетНДСУКомитента");
		МассивНепроверяемыхРеквизитов.Добавить("СпособРасчетаКомиссионногоВознаграждения");
		Если НЕ ЗначениеЗаполнено(ПроцентКомиссионногоВознаграждения) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("УслугаКомиссионногоВознаграждения");
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		Организация   = ЗначениеНастроекПовтИсп.ОрганизацияПоУмолчанию(Организация, ТекущийПользователь);
		БанковскийСчет = ЗначениеНастроекПовтИсп.БанковскийСчетОрганизацииПоУмолчанию(Организация, , БанковскийСчет);
		ТипДоговора = Перечисления.ТипыДоговоровПлатежныхАгентов.Прямой;
		ИспользоватьКомиссионнуюТорговлю = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссионнуюТорговлю");
		
		Если ИспользоватьКомиссионнуюТорговлю Тогда
			ПризнакАгента = Перечисления.ПризнакиАгента.Комиссионер;
		Иначе
			ПризнакАгента = Перечисления.ПризнакиАгента.ПлатежныйАгент;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	ИННПоставщикаУслуг = ОбщегоНазначенияРТ.ПолучитьИННКонтрагента(Агент);
КонецПроцедуры

#КонецОбласти

#КонецЕсли
