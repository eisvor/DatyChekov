#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает упаковку с наименьшим коэффициентом.
//
// Параметры:
//	Номенклатура  - номенклатура-владелец упаковки (если используется упаковки, индивидуальные для каждой номенклатуры)
//                 или номенклатура, из которой берется вид номенклатуры (если используются упаковки, общие для вида
//                 номенклатуры).
//
// Возвращаемое значение:
//   СправочникСсылка.УпаковкиНоменклатуры или Неопределенно.
//
Функция ПолучитьУпаковкуПоУмолчанию(Номенклатура) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УпаковкиНоменклатуры.Ссылка КАК Упаковка
	|ИЗ
	|	Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (УпаковкиНоменклатуры.Владелец = ВЫБОР
	|				КОГДА СпрНоменклатура.НаборУпаковок = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры)
	|					ТОГДА СпрНоменклатура.Ссылка
	|				КОГДА СпрНоменклатура.НаборУпаковок <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ПустаяСсылка)
	|					ТОГДА СпрНоменклатура.НаборУпаковок
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|	И (НЕ УпаковкиНоменклатуры.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УпаковкиНоменклатуры.Коэффициент";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() 	Тогда
		Упаковка = Выборка.Упаковка;
	Иначе
		Упаковка = Справочники.УпаковкиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Упаковка;
	
КонецФункции

// Формирует наименование по основным реквизитам
//
// Параметры:
//  ЕдиницаИзмерения - СправочникСсылка.ЕдиницыИзмерения - единица измерения упаковки.
//  Коэффициент - Число - коэффициент упаковки.
//  ЕдиницаИзмеренияВладельца - -СправочникСсылка.ЕдиницыИзмерения - единица измерения номенклатуры.
//
// Возвращаемое значение:
//  Строка - наименование упаковки.
//
Функция СформироватьНаименование(ЕдиницаИзмерения, Коэффициент, ЕдиницаИзмеренияВладельца) Экспорт
	Возврат СокрЛП(СокрЛП(ЕдиницаИзмерения) + " (" + Формат(Коэффициент,"ЧРД=.") + Строка(ЕдиницаИзмеренияВладельца) + ")");
КонецФункции

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	Результат = Новый Массив;
	Результат.Добавить("Коэффициент");
	Результат.Добавить("ЕдиницаИзмерения");
	Возврат Результат;
КонецФункции

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Функция возвращает упаковку.
//
// Параметры:
//	Владелец - номенклатура-владелец упаковки (если используется упаковки, индивидуальные для каждой номенклатуры)
//                 или номенклатура, из которой берется вид номенклатуры (если используются упаковки, общие для вида
//                 номенклатуры).
//	Коэффициент - коэффициент упаковки
//
// Возвращаемое значение:
//   СправочникСсылка.УпаковкиНоменклатуры или Неопределенно.
//
Функция Упаковка(Владелец, Коэффициент) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	УпаковкиНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.Владелец = &Владелец
	|	И УпаковкиНоменклатуры.Коэффициент = &Коэффициент
	|	И НЕ УпаковкиНоменклатуры.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УпаковкиНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.Владелец В
	|			(ВЫБРАТЬ
	|				НаборыУпаковок.Ссылка
	|			ИЗ
	|				Справочник.НаборыУпаковок КАК НаборыУпаковок
	|			ГДЕ
	|				НаборыУпаковок.Ссылка В
	|					(ВЫБРАТЬ
	|						Номенклатура.НаборУпаковок
	|					ИЗ
	|						Справочник.Номенклатура КАК Номенклатура
	|					ГДЕ
	|						Номенклатура.Ссылка = &Владелец))
	|	И УпаковкиНоменклатуры.Коэффициент = &Коэффициент
	|	И НЕ УпаковкиНоменклатуры.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Коэффициент", Коэффициент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.УпаковкиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)

	Перем ЗначениеИзСтруктуры;

	Если Не Параметры.Свойство("Номенклатура", ЗначениеИзСтруктуры) Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;

	ДанныеВыбора = Новый СписокЗначений;

	ОбработкаТабличнойЧастиТоварыВызовСервера.СписокДляВыбораУпаковок(ЗначениеИзСтруктуры, ДанныеВыбора, Истина, Параметры.СтрокаПоиска);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
