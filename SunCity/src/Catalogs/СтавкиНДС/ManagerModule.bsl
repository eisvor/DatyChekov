// Функция возвращает ставку НДС - Без НДС.
//
Функция ПолучитьСтавкуНДСБезНДС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтавкиНДС.Ссылка КАК СтавкаНДС
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|ГДЕ
	|	СтавкиНДС.ДляОсвобожденногоОборота
	|	И СтавкиНДС.Ставка = 0";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтавкаНДС;
	Иначе
		//создаем ставку без НДС
		СтавкаБезНДС = СоздатьСтавкуНДСБезНДС();
		Возврат СтавкаБезНДС;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьСтавкуНДСБезНДС()

// Функция возвращает ставку НДС - Ноль.
//
Функция ПолучитьСтавкуНДСНоль() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтавкиНДС.Ссылка КАК СтавкаНДС
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|ГДЕ
	|	(НЕ СтавкиНДС.ДляОсвобожденногоОборота)
	|	И СтавкиНДС.Ставка = 0";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтавкаНДС;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Функция возвращает ставку НДС - Ноль.
//
Функция ПолучитьСтавкуНДСПоЗначению(Ставка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтавкиНДС.Ссылка КАК СтавкаНДС
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|ГДЕ
	|	(НЕ СтавкиНДС.ДляОсвобожденногоОборота)
	|	И СтавкиНДС.Ставка = &СтавкаНДС";
	
	Запрос.УстановитьПараметр("СтавкаНДС", Ставка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтавкаНДС;
	Иначе
		Возврат ПредопределенноеЗначение("Справочник.СтавкиНДС.ПустаяСсылка");
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Функция создает ставку НДС - Без НДС.
//
Функция СоздатьСтавкуНДСБезНДС()
	
	СсылкаСтавкаБезНДС = Неопределено;
	
	Попытка
		
		ОбъектСтавкаБезНДС                          = Справочники.СтавкиНДС.СоздатьЭлемент();
		ОбъектСтавкаБезНДС.Наименование             = "Без НДС";
		ОбъектСтавкаБезНДС.ДляОсвобожденногоОборота = Истина;
		ОбъектСтавкаБезНДС.Записать();
		СсылкаСтавкаБезНДС = ОбъектСтавкаБезНДС.Ссылка;
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ТекстСообщенияОбОшибке = НСтр("ru = 'Ошибка при создании ставки НДС: %1'");
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									ТекстСообщенияОбОшибке,
									КраткоеПредставлениеОшибки(Инфо));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке);
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Создание ставки НДС.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			,
			ПодробноеПредставлениеОшибки(Инфо));
		
	КонецПопытки;
	
	Возврат СсылкаСтавкаБезНДС;
	
КонецФункции
