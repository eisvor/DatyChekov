#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда
		
		Если НЕ ПодключаемоеОборудование.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ Тогда
			ЭлектронныйЧекSMSПередаютсяПрограммой1С   = Ложь;
			ЭлектронныйЧекEmailПередаютсяПрограммой1С = Ложь;
		КонецЕсли;
		
		Если ТипКассы = Перечисления.ТипыКассККМ.ККМOffline
			ИЛИ ТипКассы = Перечисления.ТипыКассККМ.ККМED
			ИЛИ ТипКассы = Перечисления.ТипыКассККМ.СетевоеОборудование Тогда
			Возврат;
		КонецЕсли;

		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КассыККМ.Ссылка.Наименование КАК Наименование
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	КассыККМ.Магазин = &Магазин
		|	И НЕ(КассыККМ.ТипКассы = ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.ККМOffline)
		|				ИЛИ КассыККМ.ТипКассы = ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.ККМED))
		|	И КассыККМ.Владелец = &Владелец
		|	И КассыККМ.РабочееМесто = &РабочееМесто
		|	И НЕ КассыККМ.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Магазин", Магазин);
		Запрос.УстановитьПараметр("Владелец", Владелец);
		Запрос.УстановитьПараметр("РабочееМесто", РабочееМесто);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() Тогда
		
			Отказ = Истина;
			СтрокаСообщения = НСтр("ru = 'Касса с такими же параметрами уже существует
										 |" + Выборка.Наименование + " :
										 |Магазин      - " + Магазин + "
										 |Организация  - " + Владелец + "
										 |РабочееМесто - " + РабочееМесто + "'"); 
			
			ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения)
		КонецЕсли;
		
		ТипОборудования = ПодключаемоеОборудование.ТипОборудования;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		ДанныеЗаполнения.Свойство("Владелец", Владелец);
		ДанныеЗаполнения.Свойство("Магазин", Магазин);
		ДанныеЗаполнения.Свойство("ТипКассы", ТипКассы);
	КонецЕсли;
	
	ИспользоватьБезПодключенияОборудования = НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПодключаемоеОборудование");
	
	Если ИспользоватьБезПодключенияОборудования Тогда
		Если Не ЗначениеЗаполнено(ТипКассы) Тогда
			ТипКассы = Перечисления.ТипыКассККМ.АвтономнаяККМ;
		КонецЕсли;
	ИначеЕсли ТипКассы = Перечисления.ТипыКассККМ.ПустаяСсылка() Тогда
		ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор;
	КонецЕсли;
	
	Если ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор Тогда
		НастройкаРаспределенияВыручкиПоСекциям = Справочники.НастройкиРаспределенияВыручкиПоСекциямФР.РаспределениеПоУмолчанию;
	КонецЕсли;
	
	ОбщегоНазначенияРТ.ПроверитьИспользованиеОрганизации(,,Владелец);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если ИспользоватьБезПодключенияОборудования Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТипОборудования");
		МассивНепроверяемыхРеквизитов.Добавить("ПодключаемоеОборудование");
	Иначе
		Если ЭтотОбъект.ТипКассы = Перечисления.ТипыКассККМ.СетевоеОборудование Тогда
			МассивНепроверяемыхРеквизитов.Добавить("РабочееМесто");
			МассивНепроверяемыхРеквизитов.Добавить("Магазин");
		КонецЕсли;
		
		Если ТипКассы = Перечисления.ТипыКассККМ.ККМOffline Тогда
			ПроверяемыеРеквизиты.Добавить("ОфлайнОборудование");
		ИначеЕсли ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор Тогда
			ПроверяемыеРеквизиты.Добавить("ПодключаемоеОборудование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
			Если ЗначениеЗаполнено(ТипОборудования) Тогда
				Если ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ
					ИЛИ ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков Тогда
					УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонЧекаККМ", Перечисления.ТипыШаблонов.ЧекККТ, Отказ);
					УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонЧекаККМВозврат", Перечисления.ТипыШаблонов.ЧекККТ, Отказ);
				ИначеЕсли ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ФискальныйРегистратор Тогда
					УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонЧекаККМ", Перечисления.ТипыШаблонов.ФискальныйЧек, Отказ);
					УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонЧекаККМВозврат", Перечисления.ТипыШаблонов.ФискальныйЧек, Отказ);
				КонецЕсли;
			Иначе
				ТекстСообщения = НСтр("ru = 'У выбранного оборудования не определен тип'");
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ПодключаемоеОборудование",
					"Объект",
					Отказ);
			КонецЕсли;
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("ТипОборудования");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если Не Отказ Тогда
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ЭтотОбъект.Ссылка, "ИдентификаторНСПК, КлючКассыНСПК");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли

