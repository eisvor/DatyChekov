
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресСКД", адресСКД) Тогда
		ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, адресСКД);
	КонецЕсли;
		
	КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиВыборДоступныеПоляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПодготовитьДанныеВыбора()
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, АдресСхемыКомпоновкиДанных, АдресНастроекКомпоновкиДанных = Неопределено)
	
	Попытка
		
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		Если ЗначениеЗаполнено(АдресНастроекКомпоновкиДанных) Тогда
			НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных);
		КонецЕсли;
		
		КомпоновщикНастроек.Инициализировать(
			Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
		Если ЗначениеЗаполнено(АдресНастроекКомпоновкиДанных) Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновкиДанных);
		Иначе
			КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		КонецЕсли;
		
		КомпоновщикНастроек.Восстановить();
		
	Исключение
		
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
		ИмяСобытия = НСтр("ru = 'Не удалось инициализировать компоновщик настроек.'", КодОсновногоЯзыка);
		ЗаписьЖурналаРегистрации(ИмяСобытия,
		УровеньЖурналаРегистрации.Ошибка, , ,
		ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДанныеВыбора()
	
	СтруктураСтроки = Неопределено;
	
	ТекущаяСтрока = Элементы.КомпоновщикНастроекНастройкиВыборДоступныеПоляВыбора.ТекущаяСтрока;
	
	Если НЕ ТекущаяСтрока = Неопределено Тогда
	
		ПолеВыбора = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.
			ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
		
		Если НЕ ПолеВыбора = Неопределено Тогда
		
			ИмяЭлемента = Строка(ПолеВыбора.Поле);
			
			СтруктураСтроки = ОбщегоНазначенияРМККлиент.ДанныеВыбораПараметраШаблонаЧека(ПолеВыбора,
				ИмяЭлемента);
			Если НЕ СтруктураСтроки = Неопределено Тогда
				
				ПодготовленноеИмяЭлемента = СтрЗаменить(ИмяЭлемента,".","");
				СтруктураСтроки.Вставить("ИмяПоля", ПодготовленноеИмяЭлемента);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураСтроки = Неопределено Тогда
		Закрыть();
	Иначе
		Закрыть(СтруктураСтроки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
