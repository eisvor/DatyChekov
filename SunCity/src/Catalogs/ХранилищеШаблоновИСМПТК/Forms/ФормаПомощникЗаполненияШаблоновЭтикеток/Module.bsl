
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормИСМПТКПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ЗаполнятьШаблонЛогистическойУпаковки = Ложь;
	ЗаполнятьКоличествоЭкземпляров = ?(Параметры.РежимИспользования = "УказаниеКоличества", Истина, Ложь);
	ЗаполнятьШаблонПотребительскойУпаковки = ?(Параметры.РежимИспользования = "ЗаполнениеШаблона", Истина, Ложь);
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ШаблонЛогистическойЭтикетки",   ШаблонЛогистическойЭтикетки);
	Настройки.Вставить("ШаблонПотребительскойЭтикетки", ШаблонПотребительскойЭтикетки);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	ШаблонЛогистическойЭтикетки   = Настройки.Получить("ШаблонЛогистическойЭтикетки");
	ШаблонПотребительскойЭтикетки = Настройки.Получить("ШаблонПотребительскойЭтикетки");	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(ПараметрыЗакрытияФормы());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаполнятьКоличествоЭкземпляровПриИзменении(Элемент)
	
	Если ЗаполнятьКоличествоЭкземпляров Тогда
		КоличествоЭкземпляровЛогистическойЭтикетки = 1;
	Иначе
		КоличествоЭкземпляровЛогистическойЭтикетки = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьШаблонПотребительскойУпаковкиПриИзменении(Элемент)
	
	Если Не ЗаполнятьШаблонПотребительскойУпаковки Тогда
		ШаблонПотребительскойЭтикетки = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗаполнятьШаблонЛогистическойУпаковкиПриИзменении(Элемент)
	
	Если Не ЗаполнятьШаблонЛогистическойУпаковки Тогда
		ШаблонЛогистическойЭтикетки = Неопределено;
	КонецЕсли;
	
КонецПроцедуры 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПотребительская",		 "Видимость", ЗаполнятьШаблонПотребительскойУпаковки);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЛогистическаяЭтикетка", "Видимость", ЗаполнятьШаблонЛогистическойУпаковки);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаКоличествоЭкземпляров", "Видимость", ЗаполнятьКоличествоЭкземпляров);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСохранитьЗначения",		 "Видимость", ЗаполнятьШаблонПотребительскойУпаковки);
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаВосстановитьЗначения", 	 "Видимость", ЗаполнятьШаблонПотребительскойУпаковки);
		
КонецПроцедуры

Функция ПараметрыЗакрытияФормы()
	
	СтруктураРезультат = Новый Структура();
	
	Если ЗаполнятьШаблонПотребительскойУпаковки Тогда
		СтруктураРезультат.Вставить("ШаблонПотребительскойЭтикетки", ШаблонПотребительскойЭтикетки);
	КонецЕсли;
	
	Если ЗаполнятьШаблонЛогистическойУпаковки Тогда
		СтруктураРезультат.Вставить("ШаблонЛогистическойЭтикетки", ШаблонЛогистическойЭтикетки);
	КонецЕсли;
	
	Если ЗаполнятьКоличествоЭкземпляров Тогда
		СтруктураРезультат.Вставить("КоличествоПечать", КоличествоЭкземпляровЛогистическойЭтикетки);
	КонецЕсли;
	
	Возврат СтруктураРезультат;
	
КонецФункции

&НаКлиенте
Процедура ШаблонЭтикеткиНачалоВыбора_Потребительская(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура();
	
	ОткрытьФорму("Справочник.ХранилищеШаблоновИСМПТК.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЭтикеткиНачалоВыбора_Логистическая(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияОтборов = Новый Структура();
	ЗначенияОтборов.Вставить("Назначение", ПредопределенноеЗначение("Перечисление.НазначенияШаблоновЭтикетокИСМПТК.ЭтикеткаДляШтрихкодовУпаковок"));
	
	ПараметрыФормы = Новый Структура("Отбор", ЗначенияОтборов);
	
	ОткрытьФорму("Справочник.ХранилищеШаблоновИСМПТК.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

#КонецОбласти