#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	
	
КонецПроцедуры

// Возвращает настройки распределения.
//
// Параметры:
//  Настройки - СправочникСсылка.ВариантыРаспределенияТоваров - вариант распределения товаров.
//  Магазин - СправочникСсылка.Магазины - магазин для определения настроек распределения.
//
Функция НастройкиРаспределения(ВариантРаспределения, Магазин = Неопределено) Экспорт
	
	ИндивидуальныеНастройкиДляМагазинов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантРаспределения, "ИндивидуальныеНастройкиДляМагазинов");
	
	Если ИндивидуальныеНастройкиДляМагазинов И НЕ ТипЗнч(Магазин) = Тип("СправочникСсылка.Магазины") Тогда
		ОписаниеОшибки = НСтр("ru = 'Для варианта распределения %1 необходимо указать магазин'");
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, ВариантРаспределения);
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиВариантовРаспределенияТоваров.НастройкиВарианта КАК НастройкиВарианта
	|ИЗ
	|	РегистрСведений.НастройкиВариантовРаспределенияТоваров КАК НастройкиВариантовРаспределенияТоваров
	|ГДЕ
	|	НастройкиВариантовРаспределенияТоваров.ВариантРаспределенияТоваров = &ВариантРаспределения";
	Запрос.УстановитьПараметр("ВариантРаспределения", ВариантРаспределения);
	
	Если ИндивидуальныеНастройкиДляМагазинов Тогда
		Запрос.Текст = Запрос.Текст + " И НастройкиВариантовРаспределенияТоваров.Магазин = &Магазин";
		Запрос.УстановитьПараметр("Магазин", Магазин);
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.НастройкиВарианта.Получить();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает настройки распределения.
//
// Параметры:
//  Магазин - СправочникСсылка.ВариантыРаспределенияТоваров - вариант распределения товаров.
//  ВариантРаспределения - СправочникСсылка.ВариантыРаспределенияТоваров - вариант распределения.
//  ТоварыФакт - ТаблицаЗначений - товары фактические для распределения.
//  СерийныеНомераФакт - ТаблицаЗначений - таблица серийных номеров.
//  ТоварыПеремещение - ТаблицаЗначений - таблица товаров к перемещению.
//  СерийныеНомераПеремещение - ТаблицаЗначений - таблица серийных номеров к перемещению.
//  ФормаРаспределения - УправляемаяФорма - форма распределения.
//  НастройкиВарианта - Структура - настройки варианта рапсределения.
//  ИдентификаторыВыделенныхСтрок - УникальныйИдентификатор - идентификатор выделенных строк.
//
Процедура ВыполнитьРаспределениеТоваров(Магазин, ВариантРаспределения, ТоварыФакт, СерийныеНомераФакт, ТоварыПеремещение,
	СерийныеНомераПеремещение, ФормаРаспределения, ИдентификаторыВыделенныхСтрок, НастройкиВарианта = Неопределено) Экспорт
	
	Если НастройкиВарианта = Неопределено Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Настройки.НастройкиВарианта КАК НастройкиВарианта
		|ИЗ
		|	РегистрСведений.НастройкиВариантовРаспределенияТоваров КАК Настройки
		|ГДЕ
		|	Настройки.ВариантРаспределенияТоваров = &ВариантРаспределенияТоваров
		|	И (Настройки.Магазин = &Магазин
		|			ИЛИ НЕ Настройки.ВариантРаспределенияТоваров.ИндивидуальныеНастройкиДляМагазинов)";
		Запрос.УстановитьПараметр("ВариантРаспределенияТоваров", ВариантРаспределения);
		Запрос.УстановитьПараметр("Магазин", Магазин);
		Результат = Запрос.Выполнить().Выбрать();
		
		Если Результат.Следующий() Тогда
			НастройкиВарианта = Результат.НастройкиВарианта;
		Иначе
			ТекстОшибки = НСтр("ru = 'При чтении настроек распределения произошла ошибка, выполнение распределения невозможно. Обратитесь к Администратору'");
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
	КонецЕсли;
	
	ВнешняяОбработка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантРаспределения, "ВнешняяОбработка");
	ВнешняяОбработкаОбъект = ДополнительныеОтчетыИОбработки.ОбъектВнешнейОбработки(ВнешняяОбработка);
	ВнешняяОбработкаОбъект.ВыполнитьРаспределение(ТоварыФакт, СерийныеНомераФакт, ТоварыПеремещение, СерийныеНомераПеремещение, НастройкиВарианта, ИдентификаторыВыделенныхСтрок, ФормаРаспределения);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
