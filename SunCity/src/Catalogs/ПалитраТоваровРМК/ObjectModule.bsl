#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВложенныеПалитрыИзменены = Ложь;
	Если СоставВложенныхПалитрИзменен() Тогда
		ВложенныеПалитрыИзменены = Истина;
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ВложенныеПалитрыИзменены", ВложенныеПалитрыИзменены);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоставВложенныхПалитрИзменен()
	
	РезультатФункции = Ложь;
	
	ЗапросПалитрДо = Новый Запрос;
	ЗапросПалитрДо.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПалитраТоваровРМКСостав.Палитра КАК Палитра
	|ИЗ
	|	Справочник.ПалитраТоваровРМК.Состав КАК ПалитраТоваровРМКСостав
	|ГДЕ
	|	ПалитраТоваровРМКСостав.Ссылка = &ПалитраСсылка
	|	И НЕ ПалитраТоваровРМКСостав.Палитра = ЗНАЧЕНИЕ(Справочник.ПалитраТоваровРМК.ПустаяСсылка)";
	ЗапросПалитрДо.УстановитьПараметр("ПалитраСсылка", Ссылка);
	
	ТаблицаПалитрДо = ЗапросПалитрДо.Выполнить().Выгрузить();
	МассивВложенныхПалитр = Новый Массив;
	Для Каждого СтрокаПалитры Из Состав Цикл
		Если ЗначениеЗаполнено(СтрокаПалитры.Палитра)
				И МассивВложенныхПалитр.Найти(СтрокаПалитры.Палитра) = Неопределено Тогда
				
			МассивВложенныхПалитр.Добавить(СтрокаПалитры.Палитра);
				
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаПалитрДо.Количество() = МассивВложенныхПалитр.Количество() Тогда
		КоличествоСтрок = ТаблицаПалитрДо.Количество();
		ИндексСтроки = 0;
		Пока ИндексСтроки < КоличествоСтрок И Не РезультатФункции Цикл
			ВложеннаяПалитра = МассивВложенныхПалитр[ИндексСтроки];
			Если ТаблицаПалитрДо.Найти(ВложеннаяПалитра, "Палитра") = Неопределено Тогда
				РезультатФункции = Истина;
			КонецЕсли;
			ИндексСтроки = ИндексСтроки + 1;
		КонецЦикла;
	Иначе
		РезультатФункции = Истина;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти

#КонецЕсли