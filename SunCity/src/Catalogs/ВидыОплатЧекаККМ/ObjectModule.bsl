#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ТипОплаты <> Перечисления.ТипыОплатЧекаККМ.БанковскийКредит Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("БанкКредитор");
		МассивНепроверяемыхРеквизитов.Добавить("ПроцентБанковскойКомиссии");
	КонецЕсли;
	
	Если ТипОплаты <> Перечисления.ТипыОплатЧекаККМ.ПлатежнаяСистема Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("ПлатежнаяСистема");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

// Обработчик события "ПередЗаписью" объекта.
//
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если ТипОплаты <> Перечисления.ТипыОплатЧекаККМ.БанковскийКредит Тогда
			БанкКредитор = Справочники.Контрагенты.ПустаяСсылка();
			ПроцентБанковскойКомиссии = 0;
		КонецЕсли;
		
		Если ЭтоНовый() И ТипОплаты = Перечисления.ТипыОплатЧекаККМ.ПлатежнаяСистема
			И ПлатежнаяСистема = Перечисления.ТипыПлатежнойСистемыККТ.СертификатНСПК Тогда
			
			ВидОплатыНСПК = Неопределено;
			ЭлектронныеСертификатыНСПКРТ.ЗаполнитьВидОплатыПлатежнаяСистемаНСПК(ВидОплатыНСПК);
			Если ЗначениеЗаполнено(ВидОплатыНСПК) Тогда
				Отказ = Истина;
				СтрокаСообщения = НСтр("ru = 'Вид оплаты с такими параметрами уже существует:
				|ТипОплаты      - Платежная система
				|ПлатежнаяСистема  - Сертификат НСПК'");
				
				ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения)
			КонецЕсли;
		
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
