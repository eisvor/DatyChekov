#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВидНоменклатуры = Справочники.СерииНоменклатуры.ЗаполнитьВидНоменклатуры(ВладелецСерии);
	Если НЕ ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыШаблона = ЗначениеНастроекПовтИсп.ПараметрыСерийНоменклатуры(ВидНоменклатуры, ВладелецСерии);
	
	Наименование = "";
	Номер = СокрЛП(Номер);
	
	Если ПараметрыШаблона.ИспользоватьНомерСерии Тогда
		Наименование = Номер;
	КонецЕсли;
	
	Если ПараметрыШаблона.ИспользоватьСрокГодностиСерии Тогда
		Если ПараметрыШаблона.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
			ГоденДо = НачалоЧаса(ГоденДо);
		Иначе
			ГоденДо = НачалоДня(ГоденДо);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Наименование) Тогда
			СтрокаСтрокаГодности = НСтр("ru = ' до %ГоденДо%'");
		Иначе
			СтрокаСтрокаГодности = НСтр("ru = 'До %ГоденДо%'");
		КонецЕсли;
		СтрокаСтрокаГодности = СтрЗаменить(СтрокаСтрокаГодности,"%ГоденДо%",
										   Формат(ГоденДо,ПараметрыШаблона.ФорматнаяСтрокаСрокаГодности));
		
		Наименование = Наименование + СтрокаСтрокаГодности;
		
		Наименование = СокрЛП(Наименование);
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПараметрыШаблона = ЗначениеНастроекПовтИсп.ПараметрыСерийНоменклатуры(ВидНоменклатуры, ВладелецСерии);

	Если Не ПараметрыШаблона.ИспользоватьНомерСерии Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Номер");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьСрокГодностиСерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоденДо");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьДатуПроизводстваСерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПроизводства");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьПроизводителяЕГАИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводительЕГАИС");
	КонецЕсли;
		
	Если Не ПараметрыШаблона.ИспользоватьСправку2ЕГАИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Справка2ЕГАИС");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьПроизводителяВЕТИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводительВЕТИС");
	КонецЕсли;
		
	Если Не ПараметрыШаблона.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗаписьСкладскогоЖурналаВЕТИС");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьИдентификаторПартииВЕТИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПартииВЕТИС");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Регистрация ШтрихкодаУпаковки для продукции из натурального меха.
	Если НЕ ДополнительныеСвойства.Свойство("НеФормироватьКИЗ")
		И ТипЗнч(ВладелецСерии) = Тип("СправочникСсылка.Номенклатура") Тогда
		ИнтеграцияИСРТ.ЗарегистрироватьШтрихкодУпаковкиПоСерии(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
