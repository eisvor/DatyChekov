#Область ПрограммныйИнтерфейс

&НаКлиенте
Перем ОтключитьЗаполнениеПоИНН;

&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Обработка заполнения реквизитов по ИНН.
&НаКлиенте
Процедура ЗаполнитьРеквизитыПоИННЗавершение(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаполнениеРеквизитовПоИНН();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросЗаписатьНесколькоСНО(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Если Записать() Тогда
			ОткрытьФормуПрименениеСистемНалогообложения();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, "СтраницаКонтактнаяИнформация",ПоложениеЗаголовкаЭлементаФормы.Лево);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ЗаполнитьДанныеФИО();
		
		ЗаполнитьДанныеОтветственныхЛиц();
		
		ЗаполнитьПараметрыПодключенияНСПК();
		
	Иначе
		УправлениеЭлементамиФормыНаСервере();
	КонецЕсли;
	
	ПодготовитьФормуНаСервере();
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	ИспользованиеСПАРКРазрешено = СПАРКРиски.ИспользованиеРазрешено();
	
	Если ИспользованиеСПАРКРазрешено
		И ПоказатьИнформациюСпарк(ЭтотОбъект) Тогда
		ПараметрыПроцедуры 		 = Новый Структура("ВариантОтображения", "Многострочный");
		ВидКонтрагентаСПАРКРиски = СПАРКРискиКлиентСерверРТ.ВидКонтрагентаСПАРКРиски(Объект.ЮрФизЛицо);
		Если Параметры.Ключ.Пустая() Тогда
			// Если это копирование, то использовать ИНН, а не ссылку для получения индексов.
			СПАРКРиски.ПриСозданииНаСервере(
				ЭтотОбъект,
				Объект,
				Объект.ИНН,
				ВидКонтрагентаСПАРКРиски,
				ПараметрыПроцедуры);
		Иначе
			СПАРКРиски.ПриСозданииНаСервере(
				ЭтотОбъект,
				Объект,
				Объект.Ссылка,
				ВидКонтрагентаСПАРКРиски,
				ПараметрыПроцедуры);
		КонецЕсли;
	Иначе
		Элементы.ГруппаИндексыСПАРКРиски.Видимость = Ложь;
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСправочника();
	ПараметрыПриСозданииНаСервере.Форма                 = ЭтотОбъект;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Элементы.КомандыЭДО;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСправочника(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если ИспользованиеСПАРКРазрешено И ПоказатьИнформациюСпарк(ЭтотОбъект) Тогда
		СПАРКРискиКлиент.ПриОткрытии(ЭтотОбъект, Объект);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриОткрытии = ОбменСКонтрагентамиКлиент.ПараметрыПриОткрытии();
	ПараметрыПриОткрытии.Форма                            = ЭтотОбъект;
	ПараметрыПриОткрытии.МестоРазмещенияКоманд            = Элементы.КомандыЭДО;
	ПараметрыПриОткрытии.ЕстьОбработчикОбновитьКомандыЭДО = Истина;
	
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ПараметрыПриОткрытии);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	ПодготовитьФормуНаСервере();
	УправлениеЭлементамиФормыНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьДанныеОтветственныхЛиц(ТекущийОбъект);
	ЗаписатьДанныеФИО(ТекущийОбъект);
	ЗаписатьСНО(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ЮрФизЛицо <>  Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Фамилия");
	КонецЕсли;
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация

	Если НЕ ЗначениеЗаполнено(СистемаНалогообложения) Тогда
		ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Поле ""%1"" не заполнено.'"), Нстр("ru='Система налогообложения'")),,, "СистемаНалогообложения");
		//Отказ = Истина;
	ИначеЕсли СистемаНалогообложения = "УСН" И НЕ ЗначениеЗаполнено(УСНОбъектНалогообложения) Тогда
		ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Поле ""%1"" не заполнено.'"), Нстр("ru='Объект налогообложения'")),,, "УСНОбъектНалогообложения");
	//	Отказ = Истина;
	ИначеЕсли Ложь И СистемаНалогообложения = "ЕНВД" И ТекущаяДата() >= Дата('2021.01.01') Тогда //Отключенный функционал
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'С 01.01.2021 нельзя использовать систему налогообложения ЕНВД.'"));
		Отказ = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация

	ОбновитьИнтерфейс = ТекущийОбъект.ЭтоНовый() И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если ИспользованиеСПАРКРазрешено Тогда
		СПАРКРиски.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
	Если СистемаНалогообложения = "ЕНВД" И ТекущаяДата() < Дата('2021.01.01') Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ЕНВДВОрганизациях.Установить(Истина);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	ЗаписатьПараметрыПодключенияНСПК();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УправлениеЭлементамиФормыНаСервере();
	
	Если ОбновитьИнтерфейс Тогда
		
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОтветственныхЛиц" Тогда
		
		ЗаполнитьДанныеОтветственныхЛиц();
		
	ИначеЕсли ИмяСобытия = "ЗагруженАдресныйКлассификатор" Тогда
		
		Элементы.ДекорацияАдресныйКлассификаторНеЗагружен.Видимость = АдресныйКлассификаторПуст();
		
	ИначеЕсли ИмяСобытия = "ИзменениеСНООрганизации" И Параметр = Объект.Ссылка Тогда
		
		УправлениеЭлементамиСНО();
		
	Иначе
		
		// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСправочника();
		ПараметрыОповещенияЭДО.Форма                            = ЭтотОбъект;
		ПараметрыОповещенияЭДО.МестоРазмещенияКоманд            = Элементы.КомандыЭДО;
		ПараметрыОповещенияЭДО.ЕстьОбработчикОбновитьКомандыЭДО = Истина;
		
		ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСправочника(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
		// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	СПАРКРискиКлиент.ОбработкаОповещения(ЭтотОбъект, Объект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	ЮрФизЛицоПриИзмененииНаСервере();
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если ИспользованиеСПАРКРазрешено Тогда
		ЭтотОбъект.ИндексыСПАРКРиски = Неопределено;
		ОбновитьОтображениеИндексыСПАРК();
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияАдресныйКлассификаторНеЗагруженОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЗагрузитьАдресныйКлассификатор" Тогда
		СтандартнаяОбработка = Ложь;
		АдресныйКлассификаторКлиент.ЗагрузитьАдресныйКлассификатор();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИндексыСПАРКРискиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СПАРКРискиКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	
	Если СистемаНалогообложения = "УСН" Тогда
		Элементы.УСНОбъектНалогообложения.Видимость = Истина;
		Элементы.ОсвобожденОтНДС.Видимость 			= Ложь;
	ИначеЕсли СистемаНалогообложения = "ОСН" ИЛИ СистемаНалогообложения = "ЕСН" Тогда
		Элементы.УСНОбъектНалогообложения.Видимость = Ложь;
		Элементы.ОсвобожденОтНДС.Видимость 			= Истина;
	Иначе
		Элементы.УСНОбъектНалогообложения.Видимость = Ложь;
		Элементы.ОсвобожденОтНДС.Видимость 			= Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИзменитьСНОНажатие(Элемент)
		
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Отбор 			= Новый Структура("Организация", Объект.Ссылка);		
		ПараметрыФормы 	= Новый Структура("Отбор", Отбор);	
		
		// &ЗамерПроизводительности 	
        ОценкаПроизводительностиРТКлиент.НачатьЗамер(
                 Истина, "РегистрСведений.ПрименениеСистемНалогообложения.Форма.ФормаСпискаСНООрганизации");
		
		ОткрытьФорму("РегистрСведений.ПрименениеСистемНалогообложения.Форма.ФормаСпискаСНООрганизации", ПараметрыФормы, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если ИспользованиеСПАРКРазрешено Тогда
		ЭтотОбъект.ИндексыСПАРКРиски = Неопределено;
		ОбновитьОтображениеИндексыСПАРК();
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСНОНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ОткрытьФормуПрименениеСистемНалогообложения();
		
	Иначе
		
		ТекстВопроса = НСтр("ru='Для настройки нескольких систем налогообложения необходимо записать объект. Записать?'");
								
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросЗаписатьНесколькоСНО", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОтветственныеЛицаОрганизацииНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.ОтветственныеЛицаОрганизаций.ФормаСписка");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	
	МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.ПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	
	МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент,, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	
	МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.Очистка(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	
	МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОбновитьКонтактнуюИнформацию(Результат) Экспорт
	
	МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
	МодульУправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьСведенияОбОрганизацииГИСМ(Команда)
	
		Возврат;
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура управляет видимостью и доступностью элементов на форме.
//
&НаСервере
Процедура УправлениеЭлементамиФормыНаСервере()
	
	ЭтоЮридическоеЛицо =  Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо
		ИЛИ Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент;
		
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КПП", "Видимость", ЭтоЮридическоеЛицо);
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РеквизитыИП", "Видимость", Не ЭтоЮридическоеЛицо);
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюПерейти", "Доступность", ЗначениеЗаполнено(Объект.Ссылка));
	
	Если ЭтоЮридическоеЛицо Тогда
		Элементы.ИНН.Маска = "9999999999";//10
		Элементы.КодПоОКПО.Маска = "99999999";//8
		Элементы.ОГРН.Маска = "9999999999999";//13
		Элементы.ОГРН.Заголовок = "ОГРН";
	Иначе
		Элементы.ИНН.Маска = "999999999999";//12
		Элементы.КодПоОКПО.Маска = "9999999999";//10
		Элементы.ОГРН.Маска = "999999999999999";//15
		Элементы.ОГРН.Заголовок = Нстр("ru='ОГРН ИП'");
	КонецЕсли;
	
	Элементы.ИНН.Маска = "999999999999";//12
	Элементы.КодПоОКПО.Маска = "9999999999";//10
	
	Элементы.ГруппаОбособленное.Видимость = ЭтоЮридическоеЛицо;
	Элементы.ГоловнаяОрганизация.Доступность = Объект.ОбособленноеПодразделение;
	
	УправлениеЭлементамиСНО();
			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОтветственныхЛиц()
	
	Руководитель = Справочники.ФизическиеЛица.ПустаяСсылка();
	Бухгалтер = Справочники.ФизическиеЛица.ПустаяСсылка();
	Кассир = Справочники.ФизическиеЛица.ПустаяСсылка();
	
	ОтветственныеЛица = Новый Массив;
	ОтветственныеЛица.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	ОтветственныеЛица.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	ОтветственныеЛица.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.Кассир);
	ОтветственныеЛица.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.Логист);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ОтветственныеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОтветственныеЛица.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|			&Период,
	|			СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				И ОтветственноеЛицо В (&ОтветственныеЛица)) КАК ОтветственныеЛица");
	
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.Ссылка);
	Запрос.УстановитьПараметр("ОтветственныеЛица", ОтветственныеЛица);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Руководитель Тогда
			
			Руководитель = Выборка.ФизическоеЛицо;
			
		ИначеЕсли Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер Тогда
			
			Бухгалтер = Выборка.ФизическоеЛицо;
			
		ИначеЕсли Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Кассир Тогда
			
			Кассир = Выборка.ФизическоеЛицо;
			
		ИначеЕсли Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Логист Тогда
			
			Логист = Выборка.ФизическоеЛицо;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.СтраницаОтветственныеЛица.ТолькоПросмотр = ЭтотОбъект.ТолькоПросмотр;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеОтветственныхЛиц(ТекущийОбъект);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Руководитель", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	СтруктураПараметров.Вставить("Бухгалтер", Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	СтруктураПараметров.Вставить("Кассир", Перечисления.ОтветственныеЛицаОрганизаций.Кассир);
	СтруктураПараметров.Вставить("Логист", Перечисления.ОтветственныеЛицаОрганизаций.Логист);
	
	Для каждого КлючИЗначение Из СтруктураПараметров Цикл
		СтруктураЗаписи = РегистрыСведений.ОтветственныеЛицаОрганизаций.ПолучитьПоследнее(
			ТекущаяДатаСеанса(),
			Новый Структура("СтруктурнаяЕдиница, ОтветственноеЛицо", ТекущийОбъект.Ссылка, КлючИЗначение.Значение)
			);
			
		Если СтруктураЗаписи.ФизическоеЛицо <> ЭтотОбъект[КлючИЗначение.Ключ] Тогда
			
			МенеджерЗаписи = РегистрыСведений.ОтветственныеЛицаОрганизаций.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = ТекущаяДатаСеанса();
			МенеджерЗаписи.СтруктурнаяЕдиница = ТекущийОбъект.Ссылка;
			МенеджерЗаписи.ОтветственноеЛицо = КлючИЗначение.Значение;
			МенеджерЗаписи.ФизическоеЛицо = ЭтотОбъект[КлючИЗначение.Ключ];
			МенеджерЗаписи.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФИО()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество,
	|	ФИОФизЛицСрезПоследних.Фамилия
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(&Период, ФизЛицо = &Ссылка) КАК ФИОФизЛицСрезПоследних");
	
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	Запрос.УстановитьПараметр("Период",ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			Фамилия = Выборка.Фамилия;
			Имя = Выборка.Имя;
			Отчество = Выборка.Отчество;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеФИО(ТекущийОбъект)
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Если ТекущийОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		СтруктураЗаписи = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(ТекущаяДатаСеанса, Новый Структура("ФизЛицо", ТекущийОбъект.Ссылка));
		Если СтруктураЗаписи.Фамилия <> Фамилия
			ИЛИ СтруктураЗаписи.Отчество <> Отчество
			ИЛИ СтруктураЗаписи.Имя <> Имя Тогда
			МенеджерЗаписи = РегистрыСведений.ФИОФизЛиц.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.ФизЛицо = ТекущийОбъект.Ссылка;
			МенеджерЗаписи.Период = ТекущаяДатаСеанса;
			МенеджерЗаписи.Фамилия = Фамилия;
			МенеджерЗаписи.Имя = Имя;
			МенеджерЗаписи.Отчество = Отчество;
			МенеджерЗаписи.Записать(Истина);
		КонецЕсли;
		
	Иначе
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ФИОФизЛицСрезПоследних.Имя,
		|	ФИОФизЛицСрезПоследних.Отчество,
		|	ФИОФизЛицСрезПоследних.Фамилия
		|ИЗ
		|	РегистрСведений.ФИОФизЛиц.СрезПоследних(&Период, ФизЛицо = &Ссылка) КАК ФИОФизЛицСрезПоследних");
		
		Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
		Запрос.УстановитьПараметр("Период",ТекущаяДатаСеанса);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Если Выборка.Фамилия <> Фамилия
				ИЛИ Выборка.Имя <> Имя
				ИЛИ Выборка.Отчество <> Отчество Тогда
				
				МенеджерЗаписи = РегистрыСведений.ФИОФизЛиц.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.ФизЛицо = ТекущийОбъект.Ссылка;
				МенеджерЗаписи.Период = ТекущаяДатаСеанса;
				МенеджерЗаписи.Фамилия = Фамилия;
				МенеджерЗаписи.Имя = Имя;
				МенеджерЗаписи.Отчество = Отчество;
				МенеджерЗаписи.Записать(Истина);
				
			КонецЕсли;
						
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСНО(ТекущийОбъект)
	
	СтруктураНалогообложения = СНООрганизации(ТекущийОбъект.Ссылка);
	
	Если НЕ ЗначениеЗаполнено(СтруктураНалогообложения.СНО) Тогда 
		
		СНООрганизации 				= Неопределено;
		
		Если СистемаНалогообложения = "УСН" Тогда
			Если УСНОбъектНалогообложения = "Доход" Тогда
				СНООрганизации = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход;
			Иначе
				СНООрганизации = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход;
			КонецЕсли;
			ОсвобожденОтНДС = Истина;
		ИначеЕсли СистемаНалогообложения = "ОСН" Тогда 
			СНООрганизации 	= Перечисления.ТипыСистемНалогообложенияККТ.ОСН;
			ОсвобожденОтНДС = Ложь;
		ИначеЕсли СистемаНалогообложения = "ЕНВД" Тогда 
			СНООрганизации 	= Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
			ОсвобожденОтНДС = Истина;
		ИначеЕсли СистемаНалогообложения = "Патент" Тогда 
			СНООрганизации 	= Перечисления.ТипыСистемНалогообложенияККТ.Патент;
			ОсвобожденОтНДС = Истина;
		ИначеЕсли СистемаНалогообложения = "ЕСН" Тогда 
			СНООрганизации 	= Перечисления.ТипыСистемНалогообложенияККТ.ЕСН;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений.ПрименениеСистемНалогообложения.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период 					= ТекущаяДатаСеанса();
		МенеджерЗаписи.Организация 				= ТекущийОбъект.Ссылка;
		МенеджерЗаписи.СистемаНалогообложения 	= СНООрганизации;
		МенеджерЗаписи.ОсвобожденОтНДС			= ОсвобожденОтНДС;
		МенеджерЗаписи.Записать();
		
	// Иначе из формы организации нет доступа к записи
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СНООрганизации(Организация)
	
	Результат = Новый Структура("СНО, ОбъектНалогообложения, ОсвобожденОтНДС");
		
	СНООрганизации = РегистрыСведений.ПрименениеСистемНалогообложения.СистемаНалогообложенияОрганизации(Организация);
	
	Если СНООрганизации <> Неопределено Тогда
		
		Если СНООрганизации.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход Тогда
			Результат.СНО 					= "УСН";
			Результат.ОбъектНалогообложения = "Доход";
		ИначеЕсли СНООрганизации.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход Тогда
			Результат.СНО 					= "УСН";
			Результат.ОбъектНалогообложения = Нстр("ru='Доход минус расход'");
		ИначеЕсли СНООрганизации.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
			Результат.СНО = "ОСН";
		ИначеЕсли СНООрганизации.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД Тогда
			Результат.СНО = "ЕНВД";
		ИначеЕсли СНООрганизации.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент Тогда
			Результат.СНО = "Патент";	
		ИначеЕсли СНООрганизации.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕСН Тогда
			Результат.СНО = "ЕСН";
		КонецЕсли;
		
		Результат.ОсвобожденОтНДС = СНООрганизации.ОсвобожденОтНДС;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УправлениеЭлементамиСНО()
	
	СтруктураНалогообложения = СНООрганизации(Объект.Ссылка);
	
	Если ЗначениеЗаполнено(СтруктураНалогообложения.СНО) Тогда
		СистемаНалогообложения = СтруктураНалогообложения.СНО;
		УСНОбъектНалогообложения = СтруктураНалогообложения.ОбъектНалогообложения;
		ОсвобожденОтНДС = СтруктураНалогообложения.ОсвобожденОтНДС;
		Элементы.СистемаНалогообложения.Доступность = Ложь;
		Элементы.УСНОбъектНалогообложения.Доступность = Ложь;
		Элементы.ДекорацияИзменитьСНО.Видимость = Истина;
		Элементы.ОсвобожденОтНДС.Доступность = Ложь;
	Иначе
		Элементы.СистемаНалогообложения.Доступность = Истина;
		Элементы.ДекорацияИзменитьСНО.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.УСНОбъектНалогообложения.Видимость = СистемаНалогообложения = "УСН";
	Элементы.ОсвобожденОтНДС.Видимость = СистемаНалогообложения = "ОСН" ИЛИ СистемаНалогообложения = "ЕСН";
	
	СписокСНО = Элементы.СистемаНалогообложения.СписокВыбора;
	СНОПатент = СписокСНО.НайтиПоЗначению("Патент");
	
	Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		
		Если СНОПатент = Неопределено Тогда
			СписокСНО.Добавить("Патент", НСтр("ru = 'Патентная система налогообложения'"));
		КонецЕсли;
	Иначе
		
		Если СНОПатент <> Неопределено Тогда
			СписокСНО.Удалить(СНОПатент);
		КонецЕсли;
		
		Если СистемаНалогообложения = "Патент" Тогда
			СистемаНалогообложения = "";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Элементы.ДекорацияАдресныйКлассификаторНеЗагружен.Видимость = АдресныйКлассификаторПуст();
	
	Текст = СтрШаблон(
		НСтр("ru = 'Для автоподбора и выбора адресных сведений необходимо <a href = %1 >загрузить классификатор</a>.'"),
		"ЗагрузитьАдресныйКлассификатор");
		ФорматированнаяСтрока = СтроковыеФункции.ФорматированнаяСтрока(Текст);
		Элементы.ДекорацияАдресныйКлассификаторНеЗагружен.Заголовок = ФорматированнаяСтрока;
		
	Элементы.ЗаполнитьПоИНН.Видимость = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	НастроитьСНО = НСтр("ru = 'Настроить использование нескольких систем налогообложения'");
	
	Элементы.СобственныйКонтрагент.Доступность = ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АдресныйКлассификаторПуст()
	Возврат НЕ АдресныйКлассификатор.АдресныйКлассификаторЗагружен();
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоИНН(Команда)
	
	Если ОтключитьЗаполнениеПоИНН <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ИНН) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Поле ""ИИН"" не заполнено'"));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Наименование) 
		ИЛИ ЗначениеЗаполнено(Объект.НаименованиеПолное) 
		ИЛИ ЗначениеЗаполнено(Объект.КПП) Тогда
		ТекстВопроса = НСтр("ru='Перезаполнить текущие реквизиты?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьРеквизитыПоИННЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьЗаполнениеРеквизитовПоИНН();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаполнениеРеквизитовПоИНН()
	
	ОписаниеОшибки = "";
	ЗаполнитьРеквизитыПоИНННаСервере(ОписаниеОшибки);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		// Обработка ошибок
		Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации" Тогда
			ТекстВопроса = НСтр("ru='Для автоматического заполнения реквизитов организации
				|необходимо подключиться к Интернет-поддержке пользователей.
				|Подключиться сейчас?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержку", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ПоказатьПредупреждение(, ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоИНННаСервере(ОписаниеОшибки = "")
	
	ЭтоЮридическоеЛицо = Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
	
	Если ЭтоЮридическоеЛицо Тогда
		
		РеквизитыКонтрагента = РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(Объект.ИНН);
		
		Если ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
			ОписаниеОшибки = РеквизитыКонтрагента.ОписаниеОшибки;
			Возврат;
		КонецЕсли;
		
		Если РеквизитыКонтрагента.ЕГРЮЛ = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, РеквизитыКонтрагента);
		ЗаполнитьЗначенияСвойств(Объект, РеквизитыКонтрагента.ЕГРЮЛ);
		
		ЗаполнитьЭлементКонтактнойИнформации(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации,
			РеквизитыКонтрагента.ЕГРЮЛ.ЮридическийАдрес);
			
		ЗаполнитьЭлементКонтактнойИнформации(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации,
			РеквизитыКонтрагента.ЕГРЮЛ.Телефон);
			
			
	Иначе
		
		РеквизитыКонтрагента = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(Объект.ИНН);
		
		Если ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
			ОписаниеОшибки = РеквизитыКонтрагента.ОписаниеОшибки;
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, РеквизитыКонтрагента);
		
		Объект.ОГРН = РеквизитыКонтрагента.РегистрационныйНомер;
		
		Фамилия  = РеквизитыКонтрагента.Фамилия;
		Имя      = РеквизитыКонтрагента.Имя;
		Отчество = РеквизитыКонтрагента.Отчество;
		
		Если РеквизитыКонтрагента.Свойство("СвидетельствоОРегистрации")
			И ЗначениеЗаполнено(РеквизитыКонтрагента.СвидетельствоОРегистрации) Тогда
			
			Объект.СвидетельствоСерияНомер = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 №%2'"), 
									РеквизитыКонтрагента.СвидетельствоОРегистрации.Серия, РеквизитыКонтрагента.СвидетельствоОРегистрации.Номер);
			Объект.СвидетельствоДатаВыдачи = РеквизитыКонтрагента.СвидетельствоОРегистрации.Дата;
			
		КонецЕсли;
		
	КонецЕсли;

	Модифицированность = Истина;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементКонтактнойИнформации(ВидКонтактнойИнформации, СтруктураДанных)
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Вид", ВидКонтактнойИнформации);
	Строки = ЭтотОбъект.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	ДанныеСтроки = ?(Строки.Количество() = 0, Неопределено, Строки[0]);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ДанныеСтроки.Представление = СтруктураДанных.Представление;
	ДанныеСтроки.Значение = СтруктураДанных.КонтактнаяИнформация;
	ЭтотОбъект[ДанныеСтроки.ИмяРеквизита] = СтруктураДанных.Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбособленноеПодразделениеПриИзменении(Элемент)
	
	ОбособленноеПодразделениеПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбособленноеПодразделениеПриИзмененииСервер()
	Если НЕ Объект.ОбособленноеПодразделение Тогда
		Объект.ГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	УправлениеЭлементамиФормыНаСервере();
КонецПроцедуры

// ИнтернетПоддержкаПользователей.СПАРКРиски
&НаКлиенте
Процедура Подключаемый_ОбновитьОтображениеИндексыСПАРК()
	ОбновитьОтображениеИндексыСПАРК();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеИндексыСПАРК()
	
	ПараметрыОтображения 	 = Новый Структура("ВариантОтображения", "Многострочный");
	ВидКонтрагентаСПАРКРиски = СПАРКРискиКлиентСерверРТ.ВидКонтрагентаСПАРКРиски(Объект.ЮрФизЛицо);
	
	СПАРКРискиКлиент.ОтобразитьИндексыСПАРК(
		ЭтотОбъект.ИндексыСПАРКРиски,
		Объект,
		Объект.ИНН, // Искать по ИНН
		ВидКонтрагентаСПАРКРиски,
		ЭтотОбъект,
		ПараметрыОтображения,
		Истина);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.СПАРКРиски

&НаКлиентеНаСервереБезКонтекста
Функция ПоказатьИнформациюСпарк(Форма)
	
	Объект = Форма.Объект;
	ПоказатьИнформацию = (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"));
	Возврат ПоказатьИнформацию;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуПрименениеСистемНалогообложения()
	
	Отбор 			= Новый Структура("Организация", Объект.Ссылка);
	ПараметрыФормы 	= Новый Структура("Отбор", Отбор);	
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		Истина, "РегистрСведений.ПрименениеСистемНалогообложения.ФормаСписка");
	
	ОткрытьФорму("РегистрСведений.ПрименениеСистемНалогообложения.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЮрФизЛицоПриИзмененииНаСервере()
	
	Если Объект.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		
		ОрганизацияИспользуетПатент = РегистрыСведений.ПрименениеСистемНалогообложения.ОрганизацияИспользуетПатент(
			Объект.Ссылка);
			
		Если ОрганизацияИспользуетПатент Тогда
			
			ТекстСообщения = НСтр("ru = 'Для организации ""%1"" настроено использование Патентной системы налогообложения. Юридические лица не могут использовать Патент.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Объект.Наименование),,"НастроитьСНО");
				
			Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыПодключенияНСПК()
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетТоваровФСС") Тогда
		ПараметрыПодключенияНСПК = Новый Структура("КлючОрганизацииНСПК");
		ЭлектронныеСертификатыНСПКРТ.ЗаполнитьПараметрыПодключенияОрганизацииНСПК(ПараметрыПодключенияНСПК, Объект.Ссылка);
		КлючОрганизацииНСПК = ПараметрыПодключенияНСПК.КлючОрганизацииНСПК;
		Элементы.ПараметрыНСПК.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	Иначе
		Элементы.ПараметрыНСПК.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПараметрыПодключенияНСПК()
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетТоваровФСС") Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если ЗначениеЗаполнено(КлючОрганизацииНСПК) Тогда
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Объект.Ссылка, КлючОрганизацииНСПК, "КлючОрганизацииНСПК");
		Иначе
			ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(Объект.Ссылка, "КлючОрганизацииНСПК");
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти