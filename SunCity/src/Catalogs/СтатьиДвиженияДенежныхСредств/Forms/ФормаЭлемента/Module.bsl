
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Подсистема запрета редактирования ключевых реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	Элементы.Наименование.ТолькоПросмотр = Объект.Предопределенный;
	Если Объект.Предопределенный Тогда
		Элементы.ХозяйственнаяОперация.СписокВыбора.Очистить();
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(Справочники.СтатьиДвиженияДенежныхСредств.ХозяйственнаяОперацияПредопределеннойСтатьиДДС(Объект.Ссылка));
	КонецЕсли;
	
	ЗаполнитьСписокВыбораКоррСчета();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Подсистема запрета редактирования ключевых реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХозяйственнаяОперацияПриИзменении(Элемент)
	
	Объект.КорреспондирующийСчет = "";
	ЗаполнитьСписокВыбораКоррСчета();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораКоррСчета()
	
	СписокСчетов = Элементы.КорреспондирующийСчет.СписокВыбора;
	СписокСчетов.Очистить();
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы Тогда
		СписокСчетов.Добавить("6290", НСтр("ru = 'Прочие доходы (6290)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы Тогда
		СписокСчетов.Добавить("8410", НСтр("ru = 'Накладные расходы (8410)'"));
		СписокСчетов.Добавить("7210", НСтр("ru = 'Административные расходы (7210)'"));
		СписокСчетов.Добавить("7110", НСтр("ru = 'Расходы по реализации продукции и оказанию услуг (7110)'"));
		СписокСчетов.Добавить("3350", НСтр("ru = 'Краткосрочная задолженность по оплате труда (3350)'"));
		СписокСчетов.Добавить("7480", НСтр("ru = 'Прочие расходы (7480)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику Тогда
		СписокСчетов.Добавить("3310", НСтр("ru = 'Краткосрочная задолженность поставщикам и подрядчикам (3310)'"));
		СписокСчетов.Добавить("3385", НСтр("ru = 'Задолженность по исполнительным листам (3385)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		СписокСчетов.Добавить("1210", НСтр("ru = 'Краткосрочная дебиторская задолженность покупателей и заказчиков (1210)'"));
		СписокСчетов.Добавить("1274", НСтр("ru = 'Прочая краткосрочная дебиторская задолженность (1274)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
		СписокСчетов.Добавить("3310", НСтр("ru = 'Краткосрочная задолженность поставщикам и подрядчикам (3310)'"));
		СписокСчетов.Добавить("3385", НСтр("ru = 'Задолженность по исполнительным листам (3385)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту Тогда
		СписокСчетов.Добавить("1210", НСтр("ru = 'Краткосрочная дебиторская задолженность покупателей и заказчиков (1210)'"));
		СписокСчетов.Добавить("1274", НСтр("ru = 'Прочая краткосрочная дебиторская задолженность (1274)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу Тогда
		СписокСчетов.Добавить("1010", НСтр("ru = 'Денежные средства в кассе (1010)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка Тогда
		СписокСчетов.Добавить("1030", НСтр("ru = 'Денежные средства на текущих банковских счетах (1030)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СдачаДенежныхСредствВБанк Тогда
		СписокСчетов.Добавить("1030", НСтр("ru = 'Денежные средства на текущих банковских счетах (1030)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюОрганизацию Тогда
		СписокСчетов.Добавить("3310", НСтр("ru = 'Краткосрочная задолженность поставщикам и подрядчикам (3310)'"));
		СписокСчетов.Добавить("1710", НСтр("ru = 'Краткосрочные авансы выданные (1710)'"));
		СписокСчетов.Добавить("3385", НСтр("ru = 'Задолженность по исполнительным листам (3385)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВКассуККМ Тогда
		СписокСчетов.Добавить("1010", НСтр("ru = 'Денежные средства в кассе (1010)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ Тогда
		СписокСчетов.Добавить("1010", НСтр("ru = 'Денежные средства в кассе (1010)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗаработнойПлатыПоВедомостям
		ИЛИ Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗаработнойПлатыРаботнику Тогда
		СписокСчетов.Добавить("3350", НСтр("ru = 'Краткосрочная задолженность по оплате труда (3350)'"));

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
