#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ИнтеграцияУРМКПереопределяемый.РазрешенРасширенныйРежимНастройкиОбменаУРМК() Тогда
		ТекстСообщения = НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ИнтеграцияУРМКПереопределяемый.УстановитьПараметрыВыбораКассыККМ(ЭтотОбъект, "КассыККМКассаККМ");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		Оповестить("ОбновлениеОтборовНаУзле");
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	КассыККМПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура КассыККМПриИзмененииНаСервере()
	
	ПустаяСсылкаКассаККМ = Метаданные.ОпределяемыеТипы.КассаККМУРМК.Тип.ПривестиЗначение();
	ПустаяСсылкаОрганизация = Метаданные.ОпределяемыеТипы.ОрганизацияУРМК.Тип.ПривестиЗначение();
	
	ОрганизацияТипРеквизитаСтрокой = ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипЗнч(ПустаяСсылкаОрганизация));
	ИмяРеквизитаОрганизация = ИнтеграцияУРМКСлужебный.ИменаРеквизитовПоТипу(ПустаяСсылкаКассаККМ, Тип(ОрганизацияТипРеквизитаСтрокой));
	
	Если ИмяРеквизитаОрганизация = "" Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДанных Из Объект.КассыККМ Цикл
		СтрокаДанных.Организация = СтрокаДанных.КассаККМ[ИмяРеквизитаОрганизация];
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КассыККМПриИзменении(Элемент)
	КассыККМПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти
