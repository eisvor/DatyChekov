
#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаРазблокированияРеквизитовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
	
		ИндивидуальныеУпаковки = (Объект.НаборУпаковок = ПредопределенноеЗначение("Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры")
		Или Не ЗначениеЗаполнено(Объект.НаборУпаковок));
		Если НЕ Элементы.ЕдиницаИзмерения.ТолькоПросмотр Тогда
			Элементы.ЕдиницаИзмерения.ТолькоПросмотр = Не ИндивидуальныеУпаковки;
		КонецЕсли;
		УправлениеЭлементамиФормыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ФОИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
		
		// подсистема запрета редактирования ключевых реквизитов объектов
		ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
		
		УправлениеЭлементамиФормыНаСервере();
	КонецЕсли; 
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	ИменаРеквизитовДляОткрытияФормыНастройкаСоставаРеквизитовСерии =
		Справочники.ВидыНоменклатуры.ИменаРеквизитовДляФормыНастройкаСоставаРеквизитовСерии("ОткрытиеФормыРедактирования");
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("РежимПредставленияОбъектаСервиса", "Гиперссылка");
	РаботаСНоменклатурой.ПриСозданииНаСервереФормаВидаНоменклатуры(ЭтотОбъект, Объект.Ссылка, Элементы.ГруппаНаименование, ДополнительныеПараметры);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	УстановитьПараметрыВыбораПравилаИменования(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ФОИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// подсистема запрета редактирования ключевых реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	УправлениеЭлементамиФормыНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Модифицированность Тогда
		Если Не ТекущийОбъект.ЭтоНовый() Тогда
			ОбъектИзменен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТекущийОбъект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
		ТекущийОбъект.ШаблонЦенника = "";
		ТекущийОбъект.ШаблонЭтикетки = "";
		ТекущийОбъект.НаборСвойствХарактеристик = "";
	КонецЕсли;
	
	Если НЕ ТекущийОбъект.ИспользоватьХарактеристики Тогда
		ТекущийОбъект.ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать;
	КонецЕсли;
	
	Если НЕ ТекущийОбъект.ИспользоватьСерии Тогда
		ТекущийОбъект.ИспользованиеСерий = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать;
	КонецЕсли;
	
	Если ТекущийОбъект.Наименование <> Объект.Ссылка.Наименование Тогда
		НаименованиеИзменилось = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// подсистема запрета редактирования ключевых реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	УстановитьПараметрыВыбораПравилаИменования(Ложь);
	
	ВидимостьПереходаКСериям();
	ВидимостьПереходаКХарактеристикам();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатурой.ПриЗаписиНаСервереФормаВидаНоменклатуры(ЭтотОбъект, ТекущийОбъект, Отказ);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если ИмяСобытия = РаботаСНоменклатуройКлиент.ОписаниеОповещенийПодсистемы().ЗагрузкаКатегорий Тогда
		Если Не Модифицированность Тогда
			ПерезаполнитьИдентификаторыКатегорий();
		КонецЕсли;
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если НаименованиеИзменилось Тогда
		ОбновитьИнтерфейс();
		Элементы.НаборСвойств.ОбновитьТекстРедактирования();
		Элементы.НаборСвойствХарактеристик.ОбновитьТекстРедактирования();
		НаименованиеИзменилось = Ложь;
	КонецЕсли;
	
	Если ОбъектИзменен Тогда
		
		ОбъектИзменен = Ложь;
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"ИзменениеНоменклатурыВопросЗавершение",
			ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru = 'Перезаполнить реквизиты номенклатуры, привязанной к данному виду номенклатуры?'");
		
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура УправлениеЭлементамиФормыНаСервере()
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНастройкиИспользованияХарактеристик", "Доступность", Объект.ИспользоватьХарактеристики И НЕ Элементы.ИспользованиеХарактеристик.ТолькоПросмотр);
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИспользованиеСерий"        , "Доступность", Объект.ИспользоватьСерии И НЕ Элементы.ИспользованиеСерий.ТолькоПросмотр);
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаАлкогольнаяПродукция", "Видимость", Объект.АлкогольнаяПродукция);
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаМеховыеИзделия", "Видимость", Объект.ПродукцияМаркируемаяДляГИСМ);
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСерии", "Видимость", Объект.ИспользоватьСерии);
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкуИспользованияСерий()
	
	Если Объект.ПродукцияМаркируемаяДляГИСМ
		Или Объект.КиЗГИСМ Тогда
		
		Если НЕ ИнтеграцияГИСМ.ПодсистемаНеИспользуется() Тогда
			Объект.ИспользоватьСерии = Истина;
			Элементы.ИспользоватьСерии.Доступность = Не (Объект.ПродукцияМаркируемаяДляГИСМ
				Или Объект.КиЗГИСМ);
		КонецЕсли;
		
		Объект.ИспользоватьНомерКИЗГИСМСерии = Объект.ИспользоватьСерии;
		Объект.ИспользоватьRFIDМеткиСерии    = Объект.ИспользоватьСерии;
	КонецЕсли;
	
	Если Объект.ИспользоватьСерии Тогда
		Объект.НастройкаИспользованияСерий   = Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара;
		Объект.ИспользоватьНомерКИЗГИСМСерии = Ложь;
		Объект.ИспользоватьНомерСерии        = НЕ Объект.КиЗГИСМ;
	Иначе
		Объект.НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Если Не Объект.АлкогольнаяПродукция Тогда
		Объект.АвтоматическиГенерироватьСерии      = Ложь;
		Объект.ИспользоватьДатуПроизводстваСерии   = Ложь;
		Объект.ИспользоватьПроизводителяЕГАИССерии = Ложь;
		Объект.ИспользоватьСправку2ЕГАИССерии      = Ложь;
	Иначе
		Объект.НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ПартияТоваров;
	КонецЕсли;
	
	Элементы.НастройкаИспользованияСерий.Доступность = Элементы.ИспользоватьСерии.Доступность;
КонецПроцедуры

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
			
	Если Не Объект.Ссылка.Пустая() Тогда
		ДополнительныеПараметры = Новый Структура;
		ОбработчикОповещения = Новый ОписаниеОповещения("ОбработкаРазблокированияРеквизитовЗавершение",
														ЭтотОбъект,
														ДополнительныеПараметры);
		ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект,
																							  ОбработчикОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьХарактеристикиПриИзменении(Элемент)
	
	Если Не Объект.ИспользоватьХарактеристики Тогда
		Объект.ИспользованиеХарактеристик = ПредопределенноеЗначение("Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать");
		Объект.ИспользоватьЦенуНоменклатуры = Ложь;
	Иначе
		Объект.ИспользованиеХарактеристик = ПредопределенноеЗначение("Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры");
	КонецЕсли;
	
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСерииПриИзменении(Элемент)
	
	Если Не Объект.ИспользоватьСерии Тогда
		Объект.ИспользованиеСерий = ПредопределенноеЗначение("Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать");
	Иначе
		Объект.ИспользованиеСерий = ПредопределенноеЗначение("Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры");
	КонецЕсли;
	
	ИспользоватьСерииПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИспользованияСерийПриИзменении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	НастройкаИспользованияСерийПриИзмененииСервер(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура НастройкаИспользованияСерийПриИзмененииСервер(Результат, ДополнительныеПараметры)
	
	ЗаполнитьЗначенияСвойств(Объект, Результат, Справочники.ВидыНоменклатуры.ИменаРеквизитовДляФормыНастройкаСоставаРеквизитовСерии("СохранениеРезультатов")); 
	
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилоИменованияСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("СозданиеПравилаИменованияЗавершение",ЭтаФорма);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбораВВидНоменклатуры",Истина);
	ПараметрыОткрытия.Вставить("ВидНоменклатуры",Объект.Ссылка);
	ПараметрыОткрытия.Вставить("Наименование",Объект.Наименование);
	ПараметрыОткрытия.Вставить("НаборыСвойств",Объект.НаборСвойств);
	
	ОткрытьФорму("Справочник.ПравилаИменованияНоменклатуры.Форма.ФормаЭлемента",ПараметрыОткрытия,ЭтаФорма,,,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЭтикеткиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.ХранилищеШаблонов.Форма.ФормаВыбора.Открытие");

	ОбработчикОповещения = Новый ОписаниеОповещения("ШаблонЭтикеткиНачалоВыбораЗавершение", ЭтаФорма);
	
	ПараметрыОткрытия = Новый Структура();
	
	ТипыШаблонов = Новый Массив;
	ТипыШаблонов.Добавить(ПредопределенноеЗначение("Перечисление.ТипыШаблонов.ЭтикеткаЦенник"));
	ТипыШаблонов.Добавить(ПредопределенноеЗначение("Перечисление.ТипыШаблонов.ЭтикеткаЦенникПринтераЭтикеток"));
	
	ПараметрыОткрытия.Вставить("Отбор", Новый Структура("ТипШаблона", ТипыШаблонов));
	
	ОткрытьФорму("Справочник.ХранилищеШаблонов.ФормаВыбора", ПараметрыОткрытия, ЭтаФорма,,,, ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЦенникаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.ХранилищеШаблонов.Форма.ФормаВыбора.Открытие");
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ШаблонЦенникаНачалоВыбораЗавершение", ЭтаФорма);
	
	ПараметрыОткрытия = Новый Структура();
	
	ТипыШаблонов = Новый Массив;
	ТипыШаблонов.Добавить(ПредопределенноеЗначение("Перечисление.ТипыШаблонов.ЭтикеткаЦенник"));
	ТипыШаблонов.Добавить(ПредопределенноеЗначение("Перечисление.ТипыШаблонов.ЭтикеткаЦенникПринтераЭтикеток"));
	
	ПараметрыОткрытия.Вставить("Отбор", Новый Структура("ТипШаблона", ТипыШаблонов));
	
	ОткрытьФорму("Справочник.ХранилищеШаблонов.ФормаВыбора", ПараметрыОткрытия, ЭтаФорма,,,, ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НаборУпаковокПриИзменении(Элемент)
	НаборУпаковокПриИзмененииСервер();
	ИндивидуальныеУпаковки = (Объект.НаборУпаковок = ПредопределенноеЗначение("Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры")
	Или Не ЗначениеЗаполнено(Объект.НаборУпаковок));
	
	Элементы.ЕдиницаИзмерения.ТолькоПросмотр = Не ИндивидуальныеУпаковки;
КонецПроцедуры

&НаКлиенте
Процедура ТипНоменклатурыПриИзмененииКлиент(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьЗначенияСвойств(Объект, Результат);
	ТипНоменклатурыПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТипНоменклатурыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ИзменитьТипНоменклатуры" Тогда
		ОписанияОповещения = Новый ОписаниеОповещения("ТипНоменклатурыПриИзмененииКлиент", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура("ТипНоменклатуры, ОсобенностьУчета, ПродаетсяВРозлив, ПризнакПредметаРасчета");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Объект);
		
		ОткрытьФорму("Перечисление.ТипыНоменклатуры.Форма.ВыборТипаНоменклатуры",
					ПараметрыФормы,
					ЭтотОбъект,
					,
					,
					,
					ОписанияОповещения,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИспользованияСерийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "НастройкаИспользованияСерийОбработкаНавигационнойСсылки" Тогда
			
		ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаИспользованияСерийПриИзменении", ЭтотОбъект);
		
		ТекущиеРеквизиты = Новый Структура(ИменаРеквизитовДляОткрытияФормыНастройкаСоставаРеквизитовСерии);
		
		ЗаполнитьЗначенияСвойств(ТекущиеРеквизиты, Объект);
        
        // &ЗамерПроизводительности
	    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		         Истина, "Справочник.ВидыНоменклатуры.Форма.НастройкаСоставаРеквизитовСерии.Открытие");
 
		ОткрытьФорму("Справочник.ВидыНоменклатуры.Форма.НастройкаСоставаРеквизитовСерии",
					ТекущиеРеквизиты,
					ЭтотОбъект,
					,
					,
					,
					ОписаниеОповещения,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеХарактеристикПриИзменении(Элемент)
	
	ВидимостьПереходаКХарактеристикам();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеСерийПриИзменении(Элемент)
	
	ВидимостьПереходаКСериям();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПолитикиУчетаСерий

&НаКлиенте
Процедура ПолитикиУчетаСерийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ Элемент.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ПолитикиУчетаСерийСклад" Тогда
		ПоказатьЗначение(,ТекущиеДанные.Магазин);
	ИначеЕсли Поле.Имя = "ПолитикиУчетаСерийПолитикаУчетаСерий" Тогда
		ПоказатьЗначение(,ТекущиеДанные.ПолитикаУчетаСерий);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолитикиУчетаСерийПолитикаУчетаСерийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.ПолитикиУчетаСерий.Форма.ФормаВыбора.Открытие");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЭлектронноеВзаимодействие.РаботаСНоменклатурой

&НаКлиенте
Процедура ОткрытьСписокСерий(Команда)
	
	ПараметрыФормы = Новый Структура("ВладелецСерии", Объект.Ссылка);
	ОткрытьФорму("Справочник.СерииНоменклатуры.ФормаСписка", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеФормы()
	
	Возврат РаботаСНоменклатурой.ПодготовитьДанныеДляИнтерактивногоЗаполнения(ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбработкаНавигационнойСсылкиРаботаСНоменклатурой(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеГиперссылки(ЭтотОбъект, Элемент, СтандартнаяОбработка,
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеРаботаСНоменклатурой(Элемент, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеГиперссылки(ЭтотОбъект, Элемент, СтандартнаяОбработка,
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеРежимОбновленияРаботаСНоменклатурой(Элемент)
	
	РаботаСНоменклатуройКлиент.НажатиеРежимОбновления(ЭтотОбъект, ПодготовитьДанныеФормы(),
		Новый ОписаниеОповещения("ЗакрытиеФормыЗаполненияОбъекта", ЭтотОбъект));
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(ПараметрыЗакрытияФормы)
	
	РаботаСНоменклатурой.ЗаполнитьРеквизитыФормы(ЭтотОбъект, ПараметрыЗакрытияФормы);
	
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры
// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой

&НаКлиенте
Процедура ВидАлкогольнойПродукцииЕГАИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
	     	 Истина, "Справочник.ВидыАлкогольнойПродукции.Форма.ФормаВыбора.Открытие");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИзменениеТиповНоменклатурыПоВиду(УникальныйИдентификатор)
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ВидНоменклатуры", Объект.Ссылка);
	ПараметрыПроцедуры.Вставить("ТипНоменклатуры", Объект.ОсобенностьУчета);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Изменение типа номенклатуры товарных позиций'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Справочники.Номенклатура.ИзменитьТипНоменклатурыПоВиду",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ИзменениеТиповНоменклатурыПоВидуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Оповестить("Запись_ВидНоменклатуры");
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если Не РезультатОперации Тогда
			ТекстСообщения = НСтр("ru = 'Обновление реквизитов не выполнено'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТипНоменклатурыПриИзмененииСервер()
	
	Если Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
		Объект.ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать;
		Объект.ИспользоватьХарактеристики = Ложь;
	КонецЕсли;
	
	Если НЕ Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		
		Если Объект.ИспользоватьСерии Тогда
			Объект.ИспользоватьСерии = Ложь;
			Объект.ИспользованиеСерий = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать;
		КонецЕсли;
		Если Объект.АлкогольнаяПродукция Тогда
			Объект.АлкогольнаяПродукция = Ложь;
		КонецЕсли;
		Если Объект.ПродаетсяВРозлив Тогда
			Объект.ПродаетсяВРозлив = Ложь;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.ВидАлкогольнойПродукцииЕГАИС) Тогда
			Объект.ВидАлкогольнойПродукцииЕГАИС = Справочники.ВидыАлкогольнойПродукции.ПустаяСсылка();
		КонецЕсли;
		Если Объект.ИмпортнаяАлкогольнаяПродукция Тогда
			Объект.ИмпортнаяАлкогольнаяПродукция = Ложь;
		КонецЕсли;
		Если Объект.ПродукцияМаркируемаяДляГИСМ Тогда
			Объект.ПродукцияМаркируемаяДляГИСМ = Ложь;
		КонецЕсли;
	Иначе
		
		Если Объект.АгентскиеУслуги Тогда
			Объект.АгентскиеУслуги = Ложь;
		КонецЕсли;
		
		//Отключенный функционал
		Объект.АлкогольнаяПродукция         = Ложь; //Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция
													//Или Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.Пиво;
		Объект.ПродукцияМаркируемаяДляГИСМ  = Ложь; //Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха;
		Объект.КиЗГИСМ                      = Ложь; //Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.КиЗГИСМ;
		Объект.ПодконтрольнаяПродукцияВЕТИС = Ложь; //Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС
													//Или Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС
													//Или Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС
													//Или Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС
													//Или Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС
													//Или Объект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС;
		// Конец Отключенный функционал
		УстановитьНастройкуИспользованияСерий();
		
		Если НЕ Объект.АлкогольнаяПродукция Тогда
			Объект.ИмпортнаяАлкогольнаяПродукция = Ложь;
			Объект.ВидАлкогольнойПродукцииЕГАИС = Справочники.ВидыАлкогольнойПродукции.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Объект.ИспользоватьСерии Тогда
		Объект.ПолитикиУчетаСерий.Очистить();
	КонецЕсли;
	
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	ДоступностьЭлементов = Объект.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.ПодарочныйСертификат;
	Элементы.ГруппаНастройкиИспользованияХарактеристик.Доступность = ДоступностьЭлементов И Объект.ИспользоватьХарактеристики И НЕ Элементы.ИспользованиеХарактеристик.ТолькоПросмотр;
	Элементы.ИспользованиеСерий.Доступность         = ДоступностьЭлементов И Объект.ИспользоватьСерии И НЕ Элементы.ИспользованиеСерий.ТолькоПросмотр;
	
	// Тип номенклатуры
	МассивТекстов = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.ТипНоменклатуры) Тогда
		МассивТекстов.Добавить(Новый ФорматированнаяСтрока(Строка(Объект.ТипНоменклатуры),
																Новый Шрифт(,,Истина)));
	Иначе
		МассивТекстов.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '<не указан>'"),
																Новый Шрифт(,,Истина),
																ЦветаСтиля.ЦветОсобогоТекста));
	КонецЕсли;
	
	Если Объект.ОсобенностьУчета <> Перечисления.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета Тогда
		МассивТекстов.Добавить(": ");
		МассивТекстов.Добавить(Строка(Объект.ОсобенностьУчета));
		Если Объект.ПродаетсяВРозлив Тогда
			МассивТекстов.Добавить(НСтр("ru = ' (Продается в розлив)'"));
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.ТипНоменклатуры.ТолькоПросмотр Тогда
		ИзменитьТипНоменклатуры = "";
	ИначеЕсли ЗначениеЗаполнено(Объект.ТипНоменклатуры) Тогда
		ИзменитьТипНоменклатуры = Новый ФорматированнаяСтрока(НСтр("ru = 'Изменить'"),,,,"ИзменитьТипНоменклатуры");
	Иначе
		ИзменитьТипНоменклатуры = Новый ФорматированнаяСтрока(НСтр("ru = 'Указать'"),,,,"ИзменитьТипНоменклатуры");
	КонецЕсли;
	
	ТипНоменклатурыСтрокой = Новый ФорматированнаяСтрока(МассивТекстов);
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(Перечисления.ТипыНоменклатуры.ПодсказкаПоТипуНоменклатуры(Объект.ТипНоменклатуры));
	МассивТекстов.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.ПодсказкаПоОсобенностиУчетаНоменклатуры(Объект.ОсобенностьУчета));
	
	Элементы.ИзменитьТипНоменклатуры.Подсказка = СтрСоединить(МассивТекстов, Символы.ПС);
	
	Если Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		Элементы.СтраницыНаборыИШаблоны.ТекущаяСтраница = Элементы.СтраницаНаборыИШаблоны;
		Элементы.ГруппаПравилаУчетаПечать.Видимость = Истина;
		Элементы.АгентскиеУслуги.Видимость = Ложь;
		Элементы.ГруппаНастройкиХарактеристик.Видимость = Истина;
		Элементы.ГруппаНастройкиСерий.Видимость = ФОИспользоватьСерииНоменклатуры;
		Элементы.ИспользоватьСерии.Видимость = Истина;
		Элементы.НастройкаИспользованияСерий.Видимость = Объект.ИспользоватьСерии;
		Элементы.ТипСрокаДействия.Видимость = Ложь;
	ИначеЕсли Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда 
		Элементы.СтраницыНаборыИШаблоны.ТекущаяСтраница = Элементы.СтраницаТолькоНаборы;
		Элементы.ГруппаПравилаУчетаПечать.Видимость = Ложь;
		Элементы.АгентскиеУслуги.Видимость = Ложь;
		Элементы.ГруппаНастройкиХарактеристик.Видимость = Ложь;
		Элементы.НастройкаИспользованияСерий.Видимость = Ложь;
		Элементы.ГруппаНастройкиСерий.Видимость = Ложь;
		Элементы.ИспользоватьСерии.Видимость = Ложь;
		Элементы.ТипСрокаДействия.Видимость = Истина;
	Иначе // Услуга
		Элементы.СтраницыНаборыИШаблоны.ТекущаяСтраница = Элементы.СтраницаНаборыИШаблоны;
		Элементы.ГруппаПравилаУчетаПечать.Видимость = Истина;
		Элементы.АгентскиеУслуги.Видимость = Истина;
		Элементы.ГруппаНастройкиХарактеристик.Видимость = Истина;
		Элементы.НастройкаИспользованияСерий.Видимость = Ложь;
		Элементы.ИспользоватьСерии.Видимость = Ложь;
		Элементы.ТипСрокаДействия.Видимость = Ложь;
		Элементы.ГруппаНастройкиСерий.Видимость = Ложь;
	КонецЕсли;
	
	ВидимостьПереходаКХарактеристикам();
	
	// Надпись использования серий
	#Область НадписьНастройкаИспользованияСерий
	
	Если Элементы.НастройкаИспользованияСерий.Видимость Тогда
		
		МассивТекстов = Новый Массив;
		
		МассивТекстов.Добавить(НСтр("ru = 'Серия идентифицирует'"));
		МассивТекстов.Добавить(" ");
		МассивТекстов.Добавить(Новый ФорматированнаяСтрока(Строка(Объект.НастройкаИспользованияСерий),
								Новый Шрифт(,,Истина)));
		
		МассивТекстов.Добавить(" (");
		
		МассивОписанияРеквизитов = Новый Массив;
		
		Если Объект.ИспользоватьНомерСерии Тогда
			МассивОписанияРеквизитов.Добавить(НСтр("ru = 'Номер'"));
		КонецЕсли;
		
		Если Объект.ИспользоватьСрокГодностиСерии Тогда
			
			Если Объект.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоДней Тогда
				МассивОписанияРеквизитов.Добавить(НСтр("ru = 'Срок годности с точностью до дней'"));
			Иначе
				МассивОписанияРеквизитов.Добавить(НСтр("ru = 'Срок годности с точностью до часов'"));
			КонецЕсли;
		КонецЕсли;
		
		Если Объект.ИспользоватьRFIDМеткиСерии Тогда
			МассивОписанияРеквизитов.Добавить(НСтр("ru = 'RFID-метка'"));
		ИначеЕсли Объект.НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара Тогда
			МассивОписанияРеквизитов.Добавить(НСтр("ru = 'без RFID-метки'"));
		КонецЕсли;
		
		Если Объект.ИспользоватьНомерКИЗГИСМСерии Тогда
			МассивОписанияРеквизитов.Добавить(НСтр("ru = 'КиЗ'"));
		КонецЕсли;
			
		МассивТекстов.Добавить(СтрСоединить(МассивОписанияРеквизитов, ", "));
		МассивТекстов.Добавить(") ");
		
		Если Не Элементы.ИспользоватьСерии.ТолькоПросмотр Тогда
			МассивТекстов.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'изменить'"),
																,
																,
																,
																"НастройкаИспользованияСерийОбработкаНавигационнойСсылки"));
		КонецЕсли;													
		
		Элементы.НастройкаИспользованияСерий.Заголовок = Новый ФорматированнаяСтрока(МассивТекстов);
		
	КонецЕсли;
	
	ВидимостьПереходаКСериям();
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораПравилаИменования(ВыполнятьПроверкуПравила)
	
	РаботаСПравиламиИменования.УстановитьПараметрыВыбораПравилаИменования(Объект.НаборСвойств,Элементы.ПравилоИменования,ВыполнятьПроверкуПравила);
		
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЭтикеткиНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат<>Неопределено Тогда
		Объект.ШаблонЭтикетки = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЦенникаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат<>Неопределено Тогда
		Объект.ШаблонЦенника = Результат;
	КонецЕсли;
	
КонецПроцедуры

// Процедура отрабатывает изменение поля "Набор упаковок".
//
&НаСервере
Процедура НаборУпаковокПриИзмененииСервер()
	
	Если ЗначениеЗаполнено(Объект.НаборУпаковок)
		И Объект.НаборУпаковок <> Справочники.НаборыУпаковок.ИндивидуальныйДляНоменклатуры Тогда
		Объект.ЕдиницаИзмерения = Объект.НаборУпаковок.ЕдиницаИзмерения;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИспользоватьСерииПриИзмененииСервер()
	
	УстановитьНастройкуИспользованияСерий();
	
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПравилаИменованияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.ПравилоИменования = Результат;
	КонецЕсли;
	УстановитьПараметрыВыбораПравилаИменования(Истина);
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.РаботаСНоменклатурой

&НаКлиенте
Процедура ЗакрытиеФормыВыбораОбъектаСервиса(ДанныеОбъекта, ДополнительныеПараметры) Экспорт 
	
	РаботаСНоменклатуройКлиент.ОбработкаОповещенияЗакрытиеФормыВыбора(
		ДанныеОбъекта, ДополнительныеПараметры, ПодготовитьДанныеФормы(),
			Новый ОписаниеОповещения("ЗакрытиеФормыЗаполненияОбъекта", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыЗаполненияОбъекта(ПараметрыЗакрытияФормы, ДополнительныеПараметры) Экспорт 
	
	ЗаполнитьРеквизитыФормы(ПараметрыЗакрытияФормы);
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьИдентификаторыКатегорий()
	
	РаботаСНоменклатурой.ПерезаполнитьВстроенныеРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой

&НаКлиенте
Процедура ИзменениеНоменклатурыВопросЗавершение(РезультатВопроса, Контекст) Экспорт 
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	РезультатВыполнения = ИзменениеТиповНоменклатурыПоВиду(ЭтотОбъект.УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
	"ИзменениеТиповНоменклатурыПоВидуЗавершение",
	ЭтотОбъект);
	
	Если РезультатВыполнения.Статус = "Выполнено" Тогда
		ИзменениеТиповНоменклатурыПоВидуЗавершение(РезультатВыполнения, Неопределено);
		Оповестить("Запись_ВидНоменклатуры");
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Процедура ВидимостьПереходаКСериям()
	
	Если НЕ Элементы.Найти("ФормаОбщаяКомандаОткрытьСписокСерий") = Неопределено Тогда
		ИзменениеЭлемента = НЕ ЭтотОбъект.ТолькоПросмотр;
		Если ЗначениеЗаполнено(Объект.Ссылка) И 
			Объект.ИспользоватьСерии И 
			Объект.ИспользованиеСерий = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры Тогда
			Элементы.ФормаОбщаяКомандаОткрытьСписокСерий.Видимость = Истина И ИзменениеЭлемента;
		Иначе
			Элементы.ФормаОбщаяКомандаОткрытьСписокСерий.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидимостьПереходаКХарактеристикам()
	
	Если НЕ Элементы.Найти("ФормаСправочникХарактеристикиНоменклатурыОткрытьПоЗначению") = Неопределено Тогда
		ИзменениеЭлемента = НЕ ЭтотОбъект.ТолькоПросмотр;
		Если ЗначениеЗаполнено(Объект.Ссылка) И 
			Объект.ИспользоватьХарактеристики И 
			Объект.ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры Тогда
			Элементы.ФормаСправочникХарактеристикиНоменклатурыОткрытьПоЗначению.Видимость = Истина И ИзменениеЭлемента;
		Иначе
			Элементы.ФормаСправочникХарактеристикиНоменклатурыОткрытьПоЗначению.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти