#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем ТекстСообщения;
	
	Если НЕ ЭтоГруппа И НЕ Сотрудник Тогда
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("ДатаРождения");
		МассивНепроверяемыхРеквизитов.Добавить("Пол");
		МассивНепроверяемыхРеквизитов.Добавить("Магазин");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
	Если НЕ ЭтоНовый() Тогда
		ЗначенияРеквизитовВИнформационнойБазе = ОбщегоНазначенияРТ.ЗначенияРеквизитовОбъекта(Ссылка, Новый Структура("ИНН"));
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИНН) Тогда 
		Если (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.ИНН <> ИНН)
			И НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь,ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				"ИНН",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ИспользоватьСкрытиеПерсональныхДанных = ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеПерсональныхДанныхСубъектов");
	ОчищатьИнформацию = ИспользоватьСкрытиеПерсональныхДанных
		И ЗащитаПерсональныхДанных.ЭтоОбъектСоСкрытымиПерсональнымиДанными(ЭтотОбъект);
		
	Если ОчищатьИнформацию Тогда
		ДополнительныеРеквизиты.Очистить();
	КонецЕсли;
	Для Каждого СтрокаКонтактнойИнформации Из КонтактнаяИнформация Цикл
		Если СтрокаКонтактнойИнформации.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			СтрокаКонтактнойИнформации.ОбратныйНомерТелефона
				= ОбщегоНазначенияРМК.ПолучитьОбратныйНомерТелефонаБезКодаСтраны(СтрокаКонтактнойИнформации.НомерТелефона);
		КонецЕсли;
		Если ОчищатьИнформацию Тогда
			СтрокаКонтактнойИнформации.Значение = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
