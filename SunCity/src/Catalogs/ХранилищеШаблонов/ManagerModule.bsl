#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имя поля из доступных полей компоновки данных.
//
// Параметры:
//  ИмяПоля - Строка - шаблон имени поля.
// 
// Возвращаемые параметры:
//  Строка - имя поля в шаблоне.
//
Функция ИмяПоляВШаблоне(Знач ИмяПоля) Экспорт
	
	ИмяПоля = СтрЗаменить(ИмяПоля, ".DeletionMark", ".ПометкаУдаления");
	ИмяПоля = СтрЗаменить(ИмяПоля, ".Owner", ".Владелец");
	ИмяПоля = СтрЗаменить(ИмяПоля, ".Code", ".Код");
	ИмяПоля = СтрЗаменить(ИмяПоля, ".Parent", ".Родитель");
	ИмяПоля = СтрЗаменить(ИмяПоля, ".Predefined", ".Предопределенный");
	ИмяПоля = СтрЗаменить(ИмяПоля, ".IsFolder", ".ЭтоГруппа");
	ИмяПоля = СтрЗаменить(ИмяПоля, ".Description", ".Наименование");
	Возврат ИмяПоля;
	
КонецФункции

// Получает массивы реквизитов зависимых от реквизита типа шаблона.
// Параметры:
//           Объект - СправочникСсылка.ХранилищеШаблонов - Элемент справочника для которого устанавливаем доступные
//                                                         реквизиты.
//           МассивВсехРеквизитов - Неопределено - Выходной параметр с типом Массив в который будут помещены имена всех
//                                                 реквизитов справочника.
//           МассивРеквизитов - Неопределено - Выходной параметр с типом Массив в который будут помещены имена
//                                                     реквизитов справочника.
//
Процедура ЗаполнитьИменаРеквизитовПоТипуШаблона(Объект, МассивВсехРеквизитов, МассивРеквизитов) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Наименование");
	МассивВсехРеквизитов.Добавить("ТипШаблона");
	МассивВсехРеквизитов.Добавить("ОбъектМетаданных");
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("Наименование");
	МассивРеквизитов.Добавить("ТипШаблона");
	
	Если Объект.ТипШаблона = Перечисления.ТипыШаблонов.ФискальныйЧек
		ИЛИ Объект.ТипШаблона = Перечисления.ТипыШаблонов.ПустаяСсылка() Тогда
		МассивРеквизитов.Добавить("ОбъектМетаданных");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает шаблон указанного типа, если он один в базе, иначе - пустую ссылку.
//
// Параметры:
//  Назначение - ПеречислениеСсылка.ТипыШаблонов - тип шаблона
// Возвращаемое значение:
//   СправочникСсылка.ХранилищеШаблонов - Шаблон по-умолчанию.
//
Функция ШаблонПоУмолчанию(ТипШаблона) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ШаблоныЭтикетокИЦенников.Ссылка
	|ИЗ
	|	Справочник.ХранилищеШаблонов КАК ШаблоныЭтикетокИЦенников
	|ГДЕ
	|	ШаблоныЭтикетокИЦенников.ТипШаблона = &ТипШаблона
	|	И НЕ ШаблоныЭтикетокИЦенников.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ТипШаблона", ТипШаблона);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.ХранилищеШаблонов.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяПараметраШтрихкод() Экспорт
	
	Возврат "Штрихкод";
	
КонецФункции

#КонецОбласти


#КонецЕсли
