#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СразуНаПринтер               = Параметры.СразуНаПринтер;
	КоличествоЭкземпляров = 1;
	ТипШаблона            = Перечисления.ТипыШаблонов.ЭтикеткаКодМаркировкиИСМП;
	ШаблонЭтикетки        = Параметры.ШаблонЭтикетки;
	
	Если Не ЗначениеЗаполнено(ШаблонЭтикетки) Тогда
		ШаблонЭтикетки = Справочники.ХранилищеШаблонов.ШаблонПоУмолчанию(ТипШаблона);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.АдресВХранилище) Тогда
		АдресВХранилище = ПоместитьВоВременноеХранилище(
			ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище),
			УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка"));
	
	ПараметрыПечати = Новый Структура("КоличествоЭкземпляров, СтруктураДанных, АдресВХранилище, КаждаяЭтикеткаНаНовомЛисте, РежимПечати",
		1,
		ДанныеПечатиСЗаполненнымШаблоном(),
		"",
		Истина,
		"ЭтикеткаКодМаркировкиИСМП");
	ПараметрыПечати.Вставить("СразуНаПринтер", СразуНаПринтер);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьЭтикетокИЦенников",
		"ЭтикеткаКодМаркировкиИСМП",
		ПараметрКоманды,
		ВладелецФормы,
		ПараметрыПечати);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеПечатиСЗаполненнымШаблоном()
	
	ДанныеПечати = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	Для Каждого СтрокаПечати Из ДанныеПечати.ОбъектыПечати Цикл
		СтрокаПечати.ШаблонЭтикетки = ШаблонЭтикетки;
	КонецЦикла;
	
	Возврат ДанныеПечати;
	
КонецФункции

#КонецОбласти