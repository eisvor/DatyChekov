#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьФормуДляПодбораВПалитруПлиточногоИнтерфейса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ИгнорироватьЗаполнениеБуфераПодбора
		И Не ПеренестиВПалитру
		И БуферПодбора.Количество() > 0 Тогда
		
		Отказ = Истина;
		
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		
		ОповещениеПередЗакрытием = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Товары из подбора не перенесены в документ. Выполнить перенос?'");
		КнопкиДиалога = РежимДиалогаВопрос.ДаНетОтмена;
		ПоказатьВопрос(ОповещениеПередЗакрытием, ТекстВопроса, КнопкиДиалога);
		
	КонецЕсли;
	
	ИгнорироватьЗаполнениеБуфераПодбора = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого ВыбраннаяПозиция Из ВыбраннаяСтрока Цикл
		
		ДанныеВыбора = Новый Структура;
		ДанныеВыбора.Вставить("Номенклатура", ВыбраннаяПозиция);
		ДобавитьДанныеДляПереноса(ДанныеВыбора);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВПалитруПлиточногоИнтерфейса(Команда)

	ПеренестиВПалитру = Истина;
	ДанныеПодбораВоВременномХранилище = РезультатПодбораВоВременномХранилище();
	Результат = Новый Структура();
	Результат.Вставить("ВыбранноеЗначение", ДанныеПодбораВоВременномХранилище);

	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьДанныеДляПереноса(ДанныеВыбора)
	
	ОтборДляПоиска = Новый Структура;
	ОтборДляПоиска.Вставить("Номенклатура", ДанныеВыбора.Номенклатура);
	
	РезультатПоиска = БуферПодбора.НайтиСтроки(ОтборДляПоиска);
	Если РезультатПоиска.Количество() = 0 Тогда
		
		ТекущаяСтрока = БуферПодбора.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ОтборДляПоиска);
		
		СостояниеПодбора = СтрШаблон( НСтр("ru = 'Подобрано позиций: %1.'"), БуферПодбора.Количество());
		Элементы.СтатусПодбора.Заголовок = СостояниеПодбора;
		Элементы.СтатусПодбора.Видимость = Истина;
		
	Иначе
		ТекущаяСтрока = РезультатПоиска[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПеренестиВПалитру = Истина;
		Закрыть();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		ИгнорироватьЗаполнениеБуфераПодбора = Истина;
		Закрыть();
	Иначе
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьДанныеПодбора()

	Если БуферПодбора.Количество() > 0 Тогда
		
		ПозицииПодбора = Новый Массив;
		Для Каждого ЭлементПодбора Из БуферПодбора Цикл
			
			Если ПозицииПодбора.Найти(ЭлементПодбора.Номенклатура) = Неопределено Тогда
				ПозицииПодбора.Добавить(ЭлементПодбора.Номенклатура);
			КонецЕсли;
				
		КонецЦикла;
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	Номенклатура.ОсобенностьУчета КАК ОсобенностьУчета
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	НЕ Номенклатура.ЭтоГруппа
		|	И НЕ Номенклатура.ПометкаУдаления
		|	И Номенклатура.Ссылка В(&ПозицииПодбора)");
		Запрос.УстановитьПараметр("ПозицииПодбора", ПозицииПодбора);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ОтборПоПозиции = Новый Структура();
			ОтборПоПозиции.Вставить("Номенклатура", Выборка.Номенклатура);
			
			Результат = БуферПодбора.НайтиСтроки(ОтборПоПозиции);
			Если Результат.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(Результат[0], Выборка);
			КонецЕсли;
			
		КонецЦикла
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуДляПодбораВПалитруПлиточногоИнтерфейса()
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
		
	КонецЕсли;
	
	ЭтотОбъект.КоманднаяПанель.Видимость = Ложь;
	Элементы.Список.МножественныйВыбор = Параметры.МножественныйВыбор;
	Элементы.Список.РежимВыделенияСтроки = РежимВыделенияСтрокиТаблицы.Строка;
	
КонецПроцедуры

&НаСервере
Функция РезультатПодбораВоВременномХранилище()
	
	ДополнитьДанныеПодбора();
	Позиции = БуферПодбора.Выгрузить();
	
	Адрес = ПоместитьВоВременноеХранилище(Позиции, УникальныйИдентификатор);

	Результат = Новый Структура;
	Результат.Вставить("АдресВыбранныхТоваровВХранилище", Адрес);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
