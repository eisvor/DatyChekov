
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Заголовок = Объект.Наименование;
	
	Превью = УправлениеМобильнымиПриложениями.ПревьюПриложения(Объект.Ссылка, УникальныйИдентификатор);
	
	СкачатьGooglePlay = УправлениеМобильнымиПриложениями.ПолучитьСсылкуНаСкачивание(
		Объект.Ссылка, УникальныйИдентификатор);
	Элементы.ГруппаСкачать.Видимость = Не ПустаяСтрока(СкачатьGooglePlay);
	
	Описание = УправлениеМобильнымиПриложениями.ПолучитьОписаниеПриложения(Объект.Ссылка);
	Элементы.Разработчик.Гиперссылка = Не ПустаяСтрока(Объект.СайтРазработчика);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьПерейтиКНастройкамНажатие(Элемент)
	
	
	ИмяФормыНастроек = "ПланОбмена.ОбменСМобильнымиПриложениями.Форма.ФормаСписка";
	НеобходимПереходПоСсылке = Ложь;
	УправлениеМобильнымиПриложениямиКлиентСерверПереопределяемый.ПриОпределенииФормыНастроекПриложения(
		Объект.ВариантНастройки, ИмяФормыНастроек, НеобходимПереходПоСсылке);
	
	Если НеобходимПереходПоСсылке Тогда
		ПерейтиПоНавигационнойСсылке(ИмяФормыНастроек);
	Иначе
		ОткрытьФорму(ИмяФормыНастроек);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РазработчикНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.СайтРазработчика) Тогда
		ПерейтиПоНавигационнойСсылке(Объект.СайтРазработчика);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
