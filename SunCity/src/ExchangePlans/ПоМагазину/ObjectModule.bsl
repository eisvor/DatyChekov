#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ОписаниеТиповОбъектовКОтправкеНазад;
Перем ОписаниеТиповОбъектовКФормированиюОстатков;
Перем ОписаниеТиповПеремещениеТоваров;
Перем ОписаниеТиповВводНачальныхОстатковУзла;
Перем ОписаниеТиповОтчетОРозничныхПродажах;
Перем ТребуетсяПровестиВводНачальныхОстатковУзла;
Перем МассивОбъектовКФормированиюОстатков;
Перем МассивОтчетовОРозничныхПродажах;
Перем ОписаниеТиповПеремещение;
Перем ЭтоГлавныйУзел;

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МодульОбменДанными = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп");

	ИмяУзла = МодульОбменДанными.ПолучитьИмяПланаОбмена(Ссылка);
	ПредопределенныйУзел = МодульОбменДанными.ПолучитьЭтотУзелПланаОбмена(ИмяУзла);
	
	Если ПредопределенныйУзел = Ссылка
		ИЛИ ПланыОбмена.ГлавныйУзел() = Ссылка Тогда
				
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("РежимВыгрузкиИнформативныхОстатков");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаНачалаВыгрузкиДокументов");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации()
		И ЭтоНовый()
		И ЭтотУзел = Ложь Тогда
		ТекстСообщения = НСтр(
		"ru = 'Ограничение базовой версии. Использование распределенных информационных баз в базовой версии не поддерживается.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

	МодульОбменДаннымиСервер = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер");
	Если МодульОбменДаннымиСервер.НадоВыполнитьОбработчикПослеЗагрузкиДанных(ЭтотОбъект, Ссылка) Тогда
		ПослеЗагрузкиДанных(Отказ);
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("ПланОбмена.ПоМагазину");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	Блокировка.Заблокировать();
	
	ПерваяНастройкаУзла = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПоМагазинуМагазины.Магазин КАК Магазин
	|ИЗ
	|	ПланОбмена.ПоМагазину.Магазины КАК ПоМагазинуМагазины
	|ГДЕ
	|	ПоМагазинуМагазины.Ссылка = &Ссылка
	|	И НЕ ПоМагазинуМагазины.Ссылка.ЭтотУзел";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить();
	ПерваяНастройкаУзла = Результат.Пустой();
	МодульОбменДаннымиПереопределяемый = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПереопределяемый");
	Если ПерваяНастройкаУзла Тогда
		Если ЭтоНовый() Тогда
			УстановитьСсылкуНового(ПланыОбмена.ПоМагазину.ПолучитьСсылку(Новый УникальныйИдентификатор));
		КонецЕсли;
		Если ЗначениеЗаполнено(ДатаНачалаВыгрузкиДокументов) 
			И ЗначениеЗаполнено(Код) Тогда
			МодульОбменДаннымиПереопределяемый.ЗаполнитьОстатки(Магазины.ВыгрузитьКолонку("Магазин"), ПолучитьСтруктуруЗаполненияВводаОстатковУзла(ЭтоНовый()), , Отказ);
		КонецЕсли;
	Иначе
		Если Документы.ВводНачальныхОстатковУзла.ВозможноВвестиВводОстатковДляУзла(Ссылка) Тогда
			ВводОстатков = Документы.ВводНачальныхОстатковУзла.НайтиПоРеквизиту("КодУзлаОбмена", Код);
			Если ЗначениеЗаполнено(ДатаНачалаВыгрузкиДокументов) Тогда
				Если ВводОстатков.Пустая() Тогда 
					МодульОбменДаннымиПереопределяемый.ЗаполнитьОстатки(, ПолучитьСтруктуруЗаполненияВводаОстатковУзла(ЭтоНовый()), ВводОстатков, Отказ);
				ИначеЕсли НачалоДня(ВводОстатков.Дата) <> ДатаНачалаВыгрузкиДокументов Тогда
					ВыборкаДокументов = Документы.ВводНачальныхОстатковУзла.Выбрать(,, Новый Структура("КодУзлаОбмена", Код), "Дата");
					ПервоеВхождение = Истина;
					Пока ВыборкаДокументов.Следующий() Цикл
						ДокументВыборки = ВыборкаДокументов.ПолучитьОбъект();
						Если ПервоеВхождение Тогда
							ВводОстатков = ДокументВыборки.Ссылка;
							ПервоеВхождение = Ложь;
							Продолжить;
						КонецЕсли; 
						ДокументВыборки.УстановитьПометкуУдаления(Истина);
					КонецЦикла;
					
					МодульОбменДаннымиПереопределяемый.ЗаполнитьОстатки(, ПолучитьСтруктуруЗаполненияВводаОстатковУзла(ЭтоНовый()), ВводОстатков, Отказ);
					
					Если Не Отказ Тогда
						ДополнительныеСвойства.Вставить("ПолучениеСообщенияОбмена");
						ПланыОбмена.ЗарегистрироватьИзменения(Ссылка);
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если Не ВводОстатков.Пустая() Тогда
					Попытка
						ОбъектВводОстатков = ВводОстатков.ПолучитьОбъект();
						ОбъектВводОстатков.Удалить();
					Исключение
						Инфо  = ИнформацияОбОшибке();
						Отказ = Истина;
						ЗаписьЖурналаРегистрации(НСтр("ru = 'Обмен данными.Удаление документа ""Ввод начальных остатков узла""'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,Инфо);
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;
		Иначе
			МодульОбменДанными = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп");

			ИмяУзла = МодульОбменДанными.ПолучитьИмяПланаОбмена(Ссылка);
			ПредопределенныйУзел = МодульОбменДанными.ПолучитьЭтотУзелПланаОбмена(ИмяУзла);

			ВыборкаДокументов = Документы.ВводНачальныхОстатковУзла.Выбрать(,, Новый Структура("КодУзлаОбмена", ПредопределенныйУзел.Код), "Дата");
			Пока ВыборкаДокументов.Следующий() Цикл
				ДокументВыборки = ВыборкаДокументов.ПолучитьОбъект();
				Если ДокументВыборки <> Неопределено
					И НЕ ДокументВыборки.ПометкаУдаления
					И НЕ ДокументВыборки.Проведен Тогда
					ДокументВыборки.Узел = Ссылка;
					Если НЕ МодульОбменДаннымиПереопределяемый.ЗаписатьВводОстатков(ДокументВыборки, РежимЗаписиДокумента.Проведение) Тогда
						Отказ = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьОбъект(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПослеЗагрузкиДанных(Отказ)
	
	МодульОбменДаннымиПереопределяемый = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПереопределяемый");

	Если ТребуетсяПровестиВводНачальныхОстатковУзла Тогда
		
		ВводОстатков = Документы.ВводНачальныхОстатковУзла.НайтиПоРеквизиту("КодУзлаОбмена", ПланыОбмена.ПоМагазину.ЭтотУзел().Код);
		Если НЕ ВводОстатков.Пустая() И НЕ ВводОстатков.ПометкаУдаления Тогда

			ОбъектВводОстатков = ВводОстатков.ПолучитьОбъект();
			ДатаСверткиПодчиненных = ВводОстатков.Дата;
			Если Не ЭтоНовый() И ДатаНачалаВыгрузкиДокументов <> ДатаСверткиПодчиненных И ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ
				|	ПоМагазину.Ссылка
				|ИЗ
				|	ПланОбмена.ПоМагазину КАК ПоМагазину
				|ГДЕ
				|	ПоМагазину.Ссылка <> &ТекущийУзел
				|	И ПоМагазину.Ссылка <> &ЭтотУзел";
				
				Запрос.УстановитьПараметр("ТекущийУзел", Ссылка);
				Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ПоМагазину.ЭтотУзел());
				
				РезультатЗапроса = Запрос.Выполнить();
				
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					УзелОбмена = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
					УзелОбмена.ДатаНачалаВыгрузкиДокументов = ДатаСверткиПодчиненных;
					УзелОбмена.ДополнительныеСвойства.Вставить("Загрузка", Истина);
					УзелОбмена.Записать();
				КонецЦикла;
				
			КонецЕсли;
			
			ВыборкаДокументов = Документы.ВводНачальныхОстатковУзла.Выбрать(,, Новый Структура("КодУзлаОбмена", ПланыОбмена.ПоМагазину.ЭтотУзел().Код), "Дата");
			Пока ВыборкаДокументов.Следующий() Цикл
				ДокументВыборки = ВыборкаДокументов.ПолучитьОбъект();
				Если ДокументВыборки <> Неопределено
					И НЕ ДокументВыборки.ПометкаУдаления
					И НЕ ДокументВыборки.Проведен Тогда

					Если НЕ МодульОбменДаннымиПереопределяемый.ЗаписатьВводОстатков(ДокументВыборки, РежимЗаписиДокумента.Проведение) Тогда
						Отказ = Истина;
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если МассивОбъектовКФормированиюОстатков.Количество() > 0 Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПеремещениеТоваров.Ссылка,
		|	ПеремещениеТоваров.Дата КАК Дата,
		|	ПеремещениеТоваров.МоментВремени КАК МоментВремени,
		|	ПеремещениеТоваров.Проведен
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
		|ГДЕ
		|	ПеремещениеТоваров.Ссылка В(&МассивОбъектовКФормированиюОстатков)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктОРасхожденияхПриПриемкеТоваров.Ссылка,
		|	АктОРасхожденияхПриПриемкеТоваров.Дата,
		|	АктОРасхожденияхПриПриемкеТоваров.МоментВремени,
		|	АктОРасхожденияхПриПриемкеТоваров.Проведен
		|ИЗ
		|	Документ.АктОРасхожденияхПриПриемкеТоваров КАК АктОРасхожденияхПриПриемкеТоваров
		|ГДЕ
		|	АктОРасхожденияхПриПриемкеТоваров.Ссылка В(&МассивОбъектовКФормированиюОстатков)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	МоментВремени");
		
		Запрос.УстановитьПараметр("МассивОбъектовКФормированиюОстатков", МассивОбъектовКФормированиюОстатков);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				ТоварыНаСкладах = РегистрыНакопления.ТоварыНаСкладах.СоздатьНаборЗаписей();
				ТоварыНаСкладах.Отбор.Регистратор.Установить(Выборка.Ссылка);
				
				Если Выборка.Проведен Тогда
					
					ДополнительныеСвойстваОбъекта = Новый Структура;
					ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Выборка.Ссылка, ДополнительныеСвойстваОбъекта);
					
					Документы[Выборка.Ссылка.Метаданные().Имя].ИнициализироватьДанныеДокумента(Выборка.Ссылка, ДополнительныеСвойстваОбъекта);
					
					ТаблицаДвижений = ДополнительныеСвойстваОбъекта.ТаблицыДляДвижений.ТаблицаТоварыНаСкладах;
					
					Если ТаблицаДвижений.Колонки.Найти("Регистратор") = Неопределено Тогда
						ТаблицаДвижений.Колонки.Добавить("Регистратор");
						ТаблицаДвижений.ЗаполнитьЗначения(Выборка.Ссылка, "Регистратор");
					КонецЕсли;
					
					ТоварыНаСкладах.Загрузить(ТаблицаДвижений);
				КонецЕсли;
				
				Попытка
				
					ТоварыНаСкладах.ОбменДанными.Отправитель = Ссылка;
					ТоварыНаСкладах.Записать();
					
				Исключение
					
					Инфо = ИнформацияОбОшибке();
					ТекстСообщенияОбОшибке = НСтр("ru = 'Ошибка при формировании остатков документа: %1. 
					|При загрузке данных из магазина: %2'");
					ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщенияОбОшибке, КраткоеПредставлениеОшибки(Инфо), Ссылка);
					ЗаписьЖурналаРегистрации(НСтр("ru = 'Обмен данными.Загрузка данных'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,Инфо);
					
				КонецПопытки;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоГлавныйУзел Тогда
		БонусныеБаллыСервер.ПровестиОтчетыОПродажахПриОбмене(МассивОтчетовОРозничныхПродажах, Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПолученииДанныхОтПодчиненного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	ТипЭлемента = ТипЗнч(ЭлементДанных);
	Если ОписаниеТиповОбъектовКФормированиюОстатков.СодержитТип(ТипЭлемента) Тогда
		Если ЗначениеЗаполнено(ЭлементДанных.Ссылка) Тогда
			МассивОбъектовКФормированиюОстатков.Добавить(ЭлементДанных.Ссылка);
		ИначеЕсли ЭлементДанных.Проведен Тогда
			ЭлементДанных.ОбменДанными.Отправитель = Ссылка;
			ЭлементДанных.ОбменДанными.Загрузка = Истина;
			ЭлементДанных.Записать(РежимЗаписиДокумента.Запись);
			МассивОбъектовКФормированиюОстатков.Добавить(ЭлементДанных.Ссылка);
		КонецЕсли;
	ИначеЕсли  ЭтоГлавныйУзел И ОписаниеТиповОтчетОРозничныхПродажах.СодержитТип(ТипЭлемента) Тогда
		Если ЭлементДанных.Проведен Тогда
			МассивОтчетовОРозничныхПродажах.Добавить(ЭлементДанных.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	Если ОписаниеТиповПеремещение.СодержитТип(ТипЭлемента) Тогда
		ОбновитьПеремещениеПоТТН(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПолученииДанныхОтГлавного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	ТипЭлемента = ТипЗнч(ЭлементДанных);
	Если ОписаниеТиповВводНачальныхОстатковУзла.СодержитТип(ТипЭлемента) Тогда
		ТребуетсяПровестиВводНачальныхОстатковУзла = Истина;
	КонецЕсли;
	
	Если ОписаниеТиповПеремещение.СодержитТип(ТипЭлемента) Тогда
		ОбновитьПеремещениеПоТТН(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	// ОбменДанными.Загрузка
	// Если настройка обмена удаляется (не важно каким способом), то удаляем все документы ВводНачальныхОстатковУзла для этого узла. 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Узел", Ссылка);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВводНачальныхОстатковУзла.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ВводНачальныхОстатковУзла КАК ВводНачальныхОстатковУзла
		|ГДЕ
		|	ВводНачальныхОстатковУзла.Узел = &Узел
		|";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		ВыбДокумент = Выборка.Ссылка;
		ДокументОбъект = ВыбДокумент.ПолучитьОбъект();
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов");
		ДокументОбъект.Удалить();
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОбъект(ДанныеЗаполнения)
	
	Если Не ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачалаВыгрузкиДокументов	= НачалоГода(ТекущаяДатаСеанса());

КонецПроцедуры

// Функция получает Структуру заполнения для документа Ввод остатков Узла.
//
Функция ПолучитьСтруктуруЗаполненияВводаОстатковУзла(НовыйУзел)
	
	СтруктураЗаполненияВводаОстатков = Новый Структура;
	СтруктураЗаполненияВводаОстатков.Вставить("Дата", ДатаНачалаВыгрузкиДокументов);
	Если НовыйУзел Тогда
		СтруктураЗаполненияВводаОстатков.Вставить("Узел", ПолучитьСсылкуНового());
	Иначе
		СтруктураЗаполненияВводаОстатков.Вставить("Узел", Ссылка);
	КонецЕсли;
	СтруктураЗаполненияВводаОстатков.Вставить("КодУзлаОбмена", Код);
	СтруктураЗаполненияВводаОстатков.Вставить("Комментарий");
	СтруктураЗаполненияВводаОстатков.Вставить("ПометкаУдаления");
	
	Возврат СтруктураЗаполненияВводаОстатков;
	
КонецФункции

Процедура ОбновитьПеремещениеПоТТН(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	Если НЕ ЗначениеЗаполнено(ЭлементДанных.ТТНВходящаяЕГАИС) Тогда
		Если ЭлементДанных.ЭтоНовый() Тогда
			ИдентификаторПеремещения = СокрЛП(ЭлементДанных.ПолучитьСсылкуНового().УникальныйИдентификатор());
		Иначе
			ИдентификаторПеремещения = СокрЛП(ЭлементДанных.Ссылка.УникальныйИдентификатор());
		КонецЕсли;
		ТТН = ИнтеграцияЕГАИСРТ.НайтиТТНПоИдентификаторуПеремещения(ИдентификаторПеремещения);
		Если ЗначениеЗаполнено(ТТН) Тогда
			ЭлементДанных.ТТНВходящаяЕГАИС = ТТН;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация
ОписаниеТиповОбъектовКОтправкеНазад = Новый ОписаниеТипов("РегистрНакопленияНаборЗаписей.ТоварыОрганизаций");
ОписаниеТиповОбъектовКФормированиюОстатков = Новый ОписаниеТипов("ДокументОбъект.ПеремещениеТоваров, ДокументОбъект.АктОРасхожденияхПриПриемкеТоваров");
ОписаниеТиповПеремещениеТоваров = Новый ОписаниеТипов("ДокументОбъект.ПеремещениеТоваров, ДокументСсылка.ПеремещениеТоваров");
ОписаниеТиповВводНачальныхОстатковУзла = Новый ОписаниеТипов("ДокументОбъект.ВводНачальныхОстатковУзла, ДокументСсылка.ВводНачальныхОстатковУзла");
ОписаниеТиповОтчетОРозничныхПродажах = Новый ОписаниеТипов("ДокументОбъект.ОтчетОРозничныхПродажах");
ОписаниеТиповПеремещение = Новый ОписаниеТипов("ДокументОбъект.ПеремещениеТоваров");

ТребуетсяПровестиВводНачальныхОстатковУзла = Ложь;
МассивОбъектовКФормированиюОстатков = Новый Массив;
МассивОтчетовОРозничныхПродажах = Новый Массив;

ЭтоГлавныйУзел = ПланыОбмена.ГлавныйУзел() = Неопределено;
#КонецОбласти

#КонецЕсли
