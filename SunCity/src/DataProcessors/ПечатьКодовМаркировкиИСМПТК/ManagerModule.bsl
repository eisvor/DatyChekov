#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция подготавливает структуру данных, необходимую для печати ценников и этикеток.
//
// Возвращаемое значение:
//  Стрруктура - данные, необходимые для печати этикеток и ценников.
//
Функция ПодготовитьСтруктуруДанных(СтруктураНастроек, Режим) Экспорт
	
	Если СтруктураНастроек.ИсходныеДанные <> Неопределено Тогда
		ИсходныеДанные = СтруктураНастроек.ИсходныеДанные.Скопировать();
	Иначе
		ИсходныеДанные = Неопределено;
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Таблица",                             Неопределено);
	СтруктураРезультата.Вставить("СоответствиеПолейСКДКолонкамТаблицы", Новый Соответствие);
	
	// Схема компоновки.
	СхемаКомпоновкиДанных = Обработки.ПечатьКодовМаркировкиИСМПТК.ПолучитьМакет(СтруктураНастроек.ИмяМакетаСхемыКомпоновкиДанных);
	ТекстЗапроса = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос;
	
	Если Режим = "ШтрихкодыУпаковок" Тогда
		// Добавляем поля в текст запроса СКД
		// Поля для GS1_128 и GS1_DataBarExpandedStacked одинаковы
		ИдентификаторыТипыКолонок = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСерверПовтИсп.ТипыКолонокПечатиПоТипуШтрихкода(Перечисления.ТипыШтрихкодовИСМПТК.GS1_128);
		
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ТекстРазрешенныхПолейИсходный =
		"{ВЫБРАТЬ
		|	ШтрихкодУпаковкиТовара.*,
		|	Штрихкод,
		|	Номенклатура.*,
		|	Характеристика.*,
		|	ЦифраРасширения,
		|	ПрефиксКомпанииGS1,
		|	СерийныйНомер,
		|	КонтрольноеЧисло,
		|	ИдентификаторОрганизации,
		|	ДатаМаркировки,
		|	НомерПоПорядку}";
		ТекстРазрешенныхПолейДобавляемый = "";
		
		Для Каждого ИдентификаторИТипКолонки Из ИдентификаторыТипыКолонок Цикл
			ИдентификаторПрименения = ИдентификаторИТипКолонки.Ключ;
			ИмяКолонки  = "ИдентификаторПрименения_" + ИдентификаторПрименения;
			ТипЗначения = ИдентификаторИТипКолонки.Значение;
			Если ТипЗначения.СодержитТип(Тип("Число")) Тогда
				ВыражениеПоля = "0";
			ИначеЕсли ТипЗначения.СодержитТип(Тип("Дата")) Тогда
				ВыражениеПоля = "ДАТАВРЕМЯ(1, 1, 1)";
			Иначе
				ВыражениеПоля = """""";
			КонецЕсли;
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Источники[0].Источник.Запрос.Операторы[0].ВыбираемыеПоля.Добавить(ВыражениеПоля);
			ИндексПоследнейКолонки = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Источники[0].Источник.Запрос.Колонки.Количество() - 1;
			ДобавленнаяКолонка = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Источники[0].Источник.Запрос.Колонки[ИндексПоследнейКолонки];
			ДобавленнаяКолонка.Псевдоним   = ИмяКолонки;
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Добавить("ИсходныеДанные." + ИмяКолонки);
			СхемаЗапроса.ПакетЗапросов[1].Операторы[0].ВыбираемыеПоля.Добавить("ИсходныеДанныеПоследнийЗапрос." + ИмяКолонки);
			
			ТекстРазрешенныхПолейДобавляемый = ТекстРазрешенныхПолейДобавляемый
			+ 	",
				|	" + ИмяКолонки;
			
			ПолеНабора = ИнтеграцияИСМПТКПереопределяемый.НовоеПолеНабора(
				СхемаКомпоновкиДанных.НаборыДанных.НаборДанных,
				ИмяКолонки,
				"ПоляGS1." + ИмяКолонки,
				ИмяКолонки,
				ТипЗначения);
		КонецЦикла;
		
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		ТекстРазрешенныхПолейДобавляемый = СтрЗаменить(ТекстРазрешенныхПолейИсходный, "}", ТекстРазрешенныхПолейДобавляемый + "}");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстРазрешенныхПолейИсходный, ТекстРазрешенныхПолейДобавляемый);
		
	КонецЕсли;
	
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос = ТекстЗапроса;
	
	// Подготовка компоновщика макета компоновки данных.
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	Компоновщик.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	Компоновщик.Настройки.Отбор.Элементы.Очистить();
	
	// Отбор компоновщика настроек.
	Если СтруктураНастроек.КомпоновщикНастроек <> Неопределено Тогда
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СкопироватьЭлементы(Компоновщик.Настройки.Отбор, СтруктураНастроек.КомпоновщикНастроек.Настройки.Отбор);
	КонецЕсли;
	
	// Выбранные поля компоновщика настроек.
	Для Каждого ОбязательноеПоле Из СтруктураНастроек.ОбязательныеПоля Цикл
		ПолеСКД = ОбщегоНазначенияИСМПТК.НайтиПолеСКДПоПолномуИмени(Компоновщик.Настройки.Выбор.ДоступныеПоляВыбора.Элементы, ОбязательноеПоле);
		Если ПолеСКД <> Неопределено Тогда
			ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Поле = ПолеСКД.Поле;
		КонецЕсли;
	КонецЦикла;
		
	// Компоновка макета компоновки данных.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Компоновщик.ПолучитьНастройки(),,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	Для каждого Поле Из МакетКомпоновкиДанных.НаборыДанных.НаборДанных.Поля Цикл
		СтруктураРезультата.СоответствиеПолейСКДКолонкамТаблицы.Вставить(Справочники.ХранилищеШаблоновИСМПТК.ИмяПоляВШаблоне(Поле.ПутьКДанным), Поле.Имя);
	КонецЦикла;
	
	Запрос = Новый Запрос(МакетКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос);
	
	// Заполнение параметров с полей отбора компоновщика настроек формы обработки.
	Для Каждого Параметр Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл
		Запрос.Параметры.Вставить(Параметр.Имя, Параметр.Значение);
	КонецЦикла;
	
	ПечатьКодовМаркировкиИСМПТК.ПриПодготовкеСтруктурыДанныхДляПечати(СтруктураНастроек, Режим, Запрос, ИсходныеДанные);

	СтруктураРезультата.Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Если ПечатьКодовМаркировкиИСМПТКПереопределяемый.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭтикеткаSSCC") Тогда
		
		ПечатнаяФорма = СформироватьПечатнуюФормуSSCC(МассивОбъектов, ПараметрыПечати, ПараметрыВывода.КодЯзыка);
		
		ПечатьКодовМаркировкиИСМПТКПереопределяемый.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЭтикеткаSSCC",
		НСтр("ru = 'Этикетка SSCC'"),
		ПечатнаяФорма);
		
		ОписаниеПечатнойФормы = КоллекцияПечатныхФорм.Найти(ВРег("ЭтикеткаSSCC"), "ИмяВРЕГ");
		Если ОписаниеПечатнойФормы <> Неопределено Тогда
			ОписаниеПечатнойФормы.ДоступенВыводНаДругихЯзыках = Истина;
		КонецЕсли;
		
	Иначе
		ПечатьКодовМаркировкиИСМПТК.ПриПечати(КоллекцияПечатныхФорм, ПараметрыПечати, ОбъектыПечати);
	КонецЕсли;
		
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуSSCC(МассивОбъектов, ПараметрыПечати, КодЯзыка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеПечатиМассив = ПараметрыПечати.ДанныеПечати;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Штрихкод_SSCC";
	
	Макет = ПечатьКодовМаркировкиИСМПТКПереопределяемый.МакетПечатнойФормы("Обработка.ПечатьКодовМаркировкиИСМПТК.SSCC", КодЯзыка);
		
	Для Каждого ДанныеПечати Из ДанныеПечатиМассив Цикл
	
		Область = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
		
		ПараметрыОбласти = Новый Структура;
		ПараметрыОбласти.Вставить("Организация", 		ДанныеПечати.Организация.НаименованиеПолное);
		ПараметрыОбласти.Вставить("ЦифраРасширения", 	ДанныеПечати.ЦифраРасширения);
		ПараметрыОбласти.Вставить("ПрефиксКомпанииGS1", ДанныеПечати.ПрефиксКомпанииGS1);
		ПараметрыОбласти.Вставить("СерийныйНомерSSCC",  ДанныеПечати.СерийныйНомерSSCC);
		
		ЗаполнитьЗначенияСвойств(Область.Параметры, ПараметрыОбласти);
		
		Эталон = Обработки.ПечатьКодовМаркировкиИСМПТК.ПолучитьМакет("Эталон");
		КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
		Рисунок = Область.Рисунки.ШтрихкодПечать;
		
		Если СтрДлина(ДанныеПечати.Штрихкод) = 18 Тогда
			ШтрихкодДляПечати = "(00)" + ДанныеПечати.Штрихкод;
		ИначеЕсли СтрДлина(ДанныеПечати.Штрихкод) = 20 Тогда
			ШтрихкодДляПечати = "(00)" + Прав(ДанныеПечати.Штрихкод, 18);
		Иначе
			ШтрихкодДляПечати = ДанныеПечати.Штрихкод;
		КонецЕсли;
		ЗначениеШтрихкодаДляКомпоненты = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.КодGS1ДляКомпонентыПечати(ШтрихкодДляПечати, 2);
		
		Область.Параметры.Штрихкод = ШтрихкодДляПечати;
		
		УровеньЧеткости = 1;
		Высота = Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе);
		Ширина = Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе);
		ПараметрыШтрихкода = Новый Структура;
		ПараметрыШтрихкода.Вставить("Ширина",          Ширина);
		ПараметрыШтрихкода.Вставить("Высота",          Высота);
		ПараметрыШтрихкода.Вставить("Штрихкод",        ЗначениеШтрихкодаДляКомпоненты);
		ПараметрыШтрихкода.Вставить("ТипКода",         2);
		ПараметрыШтрихкода.Вставить("ОтображатьТекст", Истина);
		ПараметрыШтрихкода.Вставить("НеGS1", 		   Ложь); //По умолчанию, в данном случае не нужен
		
		ПараметрыГенерацииШтрихкода = ПараметрыГенерацииШтрихкода();
		ЗаполнитьЗначенияСвойств(ПараметрыГенерацииШтрихкода, ПараметрыШтрихкода);
	
		РезультатГенерацииШтрихкода = ПечатьКодовМаркировкиИСМПТКВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыГенерацииШтрихкода);
		Если РезультатГенерацииШтрихкода <> Неопределено Тогда
			Рисунок.Картинка = РезультатГенерацииШтрихкода;
		Иначе
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(НСтр("ru = 'При генерации картинки штрихкода произошла ошибка.'"));
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Область);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция ПараметрыГенерацииШтрихкода() Экспорт
	
	ПараметрыШтрихкода = Новый Структура;
	ПараметрыШтрихкода.Вставить("Ширина"            , 100);
	ПараметрыШтрихкода.Вставить("Высота"            , 100);
	ПараметрыШтрихкода.Вставить("ТипКода"           , 99);
	ПараметрыШтрихкода.Вставить("ОтображатьТекст"   , Истина);
	ПараметрыШтрихкода.Вставить("РазмерШрифта"      , 12);
	ПараметрыШтрихкода.Вставить("УголПоворота"      , 0);
	ПараметрыШтрихкода.Вставить("Штрихкод"          , "");
	ПараметрыШтрихкода.Вставить("ПрозрачныйФон"     , Истина);
	ПараметрыШтрихкода.Вставить("УровеньКоррекцииQR", 1);
	ПараметрыШтрихкода.Вставить("Масштабировать"           , Ложь);
	ПараметрыШтрихкода.Вставить("СохранятьПропорции"       , Ложь);
	ПараметрыШтрихкода.Вставить("ВертикальноеВыравнивание" , 1); 
	ПараметрыШтрихкода.Вставить("GS1DatabarКоличествоСтрок", 2);
	ПараметрыШтрихкода.Вставить("ТипВходныхДанных", 0);
	ПараметрыШтрихкода.Вставить("УбратьЛишнийФон" , Ложь); 
	ПараметрыШтрихкода.Вставить("ЛоготипКартинка");
	ПараметрыШтрихкода.Вставить("ЛоготипРазмерПроцентОтШК");
	
	Возврат ПараметрыШтрихкода;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Функция возвращает пустую структуру настроек
// 
// Параметры: 
//  Нет
// 
// Возвращаемое значение: 
//  Структура - структура настроек.
Функция СтруктураНастроек() Экспорт
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ИсходныеДанные",                         Неопределено);
	СтруктураНастроек.Вставить("ОбязательныеПоля",                       Новый Массив);
	СтруктураНастроек.Вставить("СоответствиеШаблоновИСтруктурыШаблонов", Новый Соответствие);
	СтруктураНастроек.Вставить("ПараметрыДанных"    ,                    Новый Структура);
	СтруктураНастроек.Вставить("КомпоновщикНастроек",                    Неопределено);
	СтруктураНастроек.Вставить("ИмяМакетаСхемыКомпоновкиДанных",         Неопределено);
	
	Возврат СтруктураНастроек;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли