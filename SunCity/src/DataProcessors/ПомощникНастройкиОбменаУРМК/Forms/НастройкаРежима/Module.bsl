#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИнициализироватьСвойстваФормы();
	
	РазрешенРасширенныйРежимНастройки =
		ИнтеграцияУРМКПереопределяемый.РазрешенРасширенныйРежимНастройкиОбменаУРМК();
		
	Если Не РазрешенРасширенныйРежимНастройки Тогда
		ИспользоватьРасширенныйРежимНастройкиУРМК = Ложь;
		УпрощённыйРежимНастройкиУРМКПриИзмененииНаСервере();
		
	КонецЕсли;
	
	ИспользоватьРасширенныйРежимНастройкиУРМК =
		ИнтеграцияУРМКСлужебный.ИспользоватьРасширенныйРежимНастройкиУРМК();
		
	БлокировкаСнятияРасширенногоРежима = БлокировкаСнятияРасширенногоРежимаНаСервере();
		
	УстановитьВидимостьНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеОтборовНаУзле" Тогда
		БлокировкаСнятияРасширенногоРежима = БлокировкаСнятияРасширенногоРежимаНаСервере();
		УстановитьВидимостьНаСервере();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УпрощённыйРежимНастройкиУРМКПриИзменении(Элемент)
	
	УпрощённыйРежимНастройкиУРМКПриИзмененииНаСервере();
	
	УстановитьВидимостьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУРМКНажатие(Элемент)
	
	ОткрытьФорму("Обработка.ПомощникНастройкиОбменаУРМК.Форма.ПомощникНастройкиОбменаДанными");
	
КонецПроцедуры

&НаКлиенте
Процедура УРМКНажатие(Элемент)
	ОткрытьФорму("Справочник.УниверсальныеРабочиеМестаКассиров.ФормаСписка");
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияФормыПриСоздании

&НаСервере
Процедура ИнициализироватьСвойстваФормы()
	
	Заголовок = НСтр("ru = 'Режим настройки обмена с 1С:РМК'");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УпрощённыйРежимНастройкиУРМКПриИзмененииНаСервере()
	
	Константы.ИспользоватьРасширенныйРежимНастройкиУРМК.Установить(ИспользоватьРасширенныйРежимНастройкиУРМК);
	
КонецПроцедуры

&НаСервере
Функция БлокировкаСнятияРасширенногоРежимаНаСервере()
	Возврат ИнтеграцияУРМКСлужебный.БлокировкаСнятияРасширенногоРежима();
КонецФункции
	
#КонецОбласти

#Область ОбщиеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПолеФлажкаРасширенныйРежим",
		"Доступность",
		 Не БлокировкаСнятияРасширенногоРежима);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Прочее",
		"Видимость",
		ИспользоватьРасширенныйРежимНастройкиУРМК);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаДекорацияКомментарийДоступности",
		"Видимость",
		БлокировкаСнятияРасширенногоРежима);

	КомментарийДоступности =
		НСтр("ru = 'Отключение расширенного режима настройки недоступно, потому что:
		|- существуют Универсальные рабочие места с двумя подключенными кассами ККМ.'");

	Если БлокировкаСнятияРасширенногоРежима Тогда
		Элементы.ДекорацияКомментарийДоступности.Подсказка = КомментарийДоступности;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКомментарийДоступностиНажатие(Элемент)
	
	ТекстСообщения = Элемент.Подсказка;
	ПоказатьПредупреждение(, ТекстСообщения);

КонецПроцедуры

#КонецОбласти
