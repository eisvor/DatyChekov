
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеНастройки = Параметры.ИмяОткрытойНастройки;
	
	ЗаполнитьСписокНастроек(Параметры.АдресСохраненныхНастроек, Параметры.ТипПриемника);
	
	Если СписокНастроек.Количество() = 0 Тогда
		Элементы.СписокНастроекВыбрать.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы Тогда
		
		Если НЕ ИзмененСоставНастроек Тогда
			Возврат;
		КонецЕсли;
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Состав настроек изменен. Сохранить изменения?'");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПередЗакрытием", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		ОповеститьОВыборе(ПараметрЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокНастроекПриИзменении(Элемент)
	
	Если НЕ ИзмененСоставНастроек Тогда
		ИзмененСоставНастроек = Истина;
		Элементы.ГруппаСохраненныеНастройки.Заголовок = Элементы.ГруппаСохраненныеНастройки.Заголовок + "*";
	КонецЕсли;
	
	Если СписокНастроек.Количество() = 0 Тогда
		Элементы.СписокНастроекВыбрать.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Выбрать(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Выбрать(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОчиститьСообщения();
	
	ВыбраннаяНастройка = Элементы.СписокНастроек.ТекущиеДанные;
	
	Если ВыбраннаяНастройка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрЗакрытия = Новый Структура;
	
	ПараметрЗакрытия.Вставить("НаименованиеНастройки", ВыбраннаяНастройка.Значение);
	ПараметрЗакрытия.Вставить("СписокНастроек", СписокНастроек);
	ПараметрЗакрытия.Вставить("СохранитьСоставНастроек", Истина);
	ПараметрЗакрытия.Вставить("Восстановить", Истина);
	ПараметрЗакрытия.Вставить("ФормаВосстановленияНастройки");
	
	Закрыть(ПараметрЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	ПараметрЗакрытия = Новый Структура;
	ПараметрЗакрытия.Вставить("СохранитьСоставНастроек", СохранитьСоставНастроек);
	ПараметрЗакрытия.Вставить("СписокНастроек", СписокНастроек);
	ПараметрЗакрытия.Вставить("ФормаВосстановленияНастройки");
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСоставНастроек(Команда)
	
	СохранитьСоставНастроек = Истина;
	ИзмененСоставНастроек = Ложь;
	Элементы.ГруппаСохраненныеНастройки.Заголовок = НСтр("ru = 'Сохраненные настройки'");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокНастроек(АдресСохраненныхНастроек, ТипПриемника)

	Если ПустаяСтрока(АдресСохраненныхНастроек) Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныеНастройки = ПолучитьИзВременногоХранилища(АдресСохраненныхНастроек);
	
	Если СохраненныеНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПоТипуПриемника = СохраненныеНастройки.НайтиСтроки(Новый Структура("ТипПриемника", ТипПриемника));
	
	Для Каждого Настройка из НастройкиПоТипуПриемника Цикл
		НовСтрока               = СписокНастроек.Добавить();
		НовСтрока.Значение      = Настройка.ИмяНастройки;
		НовСтрока.Пометка       = Настройка.ПоУмолчанию;
		НовСтрока.Представление = Настройка.ИмяНастройки
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПередЗакрытием(ВозвратаДиалога, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	Если ВозвратаДиалога = КодВозвратаДиалога.Да Тогда
		СохранитьСоставНастроек = Истина;
		ИзмененСоставНастроек = Ложь;
	ИначеЕсли ВозвратаДиалога = КодВозвратаДиалога.Нет Тогда
		СохранитьСоставНастроек = Ложь;
		ИзмененСоставНастроек = Ложь;
	ИначеЕсли ВозвратаДиалога = КодВозвратаДиалога.Отмена Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ПараметрЗакрытия = Новый Структура;
		ПараметрЗакрытия.Вставить("СохранитьСоставНастроек", СохранитьСоставНастроек);
		ПараметрЗакрытия.Вставить("СписокНастроек", СписокНастроек);
		ПараметрЗакрытия.Вставить("ФормаВосстановленияНастройки");
		Закрыть(ПараметрЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
