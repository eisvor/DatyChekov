
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НаименованиеНастройки = Параметры.ИмяОткрытойНастройки;
	НеЗадаватьВопросСохраненияНастроек = Параметры.НеЗадаватьВопросСохраненияНастроек;
	
	ЗаполнитьСписокНастроек(Параметры.АдресСохраненныхНастроек, Параметры.ТипПриемника);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы Тогда
		
		Если ПараметрЗакрытия = Неопределено Тогда
			ПараметрЗакрытия = Новый Структура;
			ПараметрЗакрытия.Вставить("НеЗадаватьВопросСохраненияНастроек", НеЗадаватьВопросСохраненияНастроек);
			ПараметрЗакрытия.Вставить("СохранитьСоставНастроек", Ложь);
			ПараметрЗакрытия.Вставить("ФормаСохраненияНастройки");
		КонецЕсли;
		
		Если НЕ ИзмененСоставНастроек Тогда
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Состав настроек изменен. Сохранить изменения?'");
		
		ПараметрыОповещения = Новый Структура;
		Оповещение = Новый ОписаниеОповещения("ВызватьДиалогЗапросаОписаниеОповещения", ЭтотОбъект, ПараметрыОповещения);
		
		ДополнительныеПараметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ДополнительныеПараметры.Вставить("КнопкаПоУмолчанию", КодВозвратаДиалога.Да);
		ДополнительныеПараметры.Вставить("Заголовок", НСтр("ru = 'Загрузка данных'"));
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
			Оповещение,
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНетОтмена,
			ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		ОповеститьОВыборе(ПараметрЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокНастроекПриИзменении(Элемент)
	
	Если НЕ ИзмененСоставНастроек Тогда
		ИзмененСоставНастроек = Истина;
		Элементы.ГруппаСохраненныеНастройки.Заголовок = Элементы.ГруппаСохраненныеНастройки.Заголовок + "*";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Строка = СписокНастроек.НайтиПоИдентификатору(ВыбраннаяСтрока);
	НаименованиеНастройки = Строка.Значение;
	ИспользоватьПоУмочанию = Строка.Пометка;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокНастроек.НайтиПоЗначению(НаименованиеНастройки) = Неопределено Тогда
		СписокНастроек.Добавить(НаименованиеНастройки);
	КонецЕсли;
	ИзмененСоставНастроек = Ложь;
	
	ПараметрЗакрытия = Новый Структура;
	ПараметрЗакрытия.Вставить("НаименованиеНастройки", НаименованиеНастройки);
	ПараметрЗакрытия.Вставить("СписокНастроек", СписокНастроек);
	ПараметрЗакрытия.Вставить("ИспользоватьПоУмочанию", ИспользоватьПоУмочанию);
	ПараметрЗакрытия.Вставить("СохранитьСоставНастроек", Истина);
	ПараметрЗакрытия.Вставить("ФормаСохраненияНастройки");
	ПараметрЗакрытия.Вставить("Сохранить");
	ПараметрЗакрытия.Вставить("НеЗадаватьВопросСохраненияНастроек", НеЗадаватьВопросСохраненияНастроек);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	ПараметрЗакрытия = Новый Структура;
	ПараметрЗакрытия.Вставить("СохранитьСоставНастроек", СохранитьСоставНастроек);
	ПараметрЗакрытия.Вставить("СписокНастроек", СписокНастроек);
	ПараметрЗакрытия.Вставить("ФормаСохраненияНастройки");
	ПараметрЗакрытия.Вставить("НеЗадаватьВопросСохраненияНастроек", НеЗадаватьВопросСохраненияНастроек);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСоставНастроек(Команда)
	
	СохранитьСоставНастроек = Истина;
	ИзмененСоставНастроек = Ложь;
	Элементы.ГруппаСохраненныеНастройки.Заголовок = НСтр("ru = 'Сохраненные настройки'");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВызватьДиалогЗапросаОписаниеОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НеЗадаватьВопросСохраненияНастроек = Результат.БольшеНеЗадаватьЭтотВопрос;
	ОтветПользователя = Результат.Значение;
	
	Если ОтветПользователя = КодВозвратаДиалога.Да Тогда
		СохранитьСоставНастроек = Истина;
	ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Нет Тогда
		СохранитьСоставНастроек = Ложь;
	ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Отмена Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ПараметрЗакрытия.Вставить("НеЗадаватьВопросСохраненияНастроек", НеЗадаватьВопросСохраненияНастроек);
		ПараметрЗакрытия.Вставить("СохранитьСоставНастроек", СохранитьСоставНастроек);
		ПараметрЗакрытия.Вставить("СписокНастроек", СписокНастроек);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокНастроек(АдресСохраненныхНастроек, ТипПриемника)

	Если ПустаяСтрока(АдресСохраненныхНастроек) Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныеНастройки = ПолучитьИзВременногоХранилища(АдресСохраненныхНастроек);
	
	Если СохраненныеНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПоТипуПриемника = СохраненныеНастройки.НайтиСтроки(Новый Структура("ТипПриемника", ТипПриемника));
	
	Для Каждого Настройка из НастройкиПоТипуПриемника Цикл
		НовСтрока               = СписокНастроек.Добавить();
		НовСтрока.Значение      = Настройка.ИмяНастройки;
		НовСтрока.Пометка       = Настройка.ПоУмолчанию;
		НовСтрока.Представление = Настройка.ИмяНастройки;
	КонецЦикла;
	
	ОткрытаяНастройка = СписокНастроек.НайтиПоЗначению(НаименованиеНастройки);
	Если НЕ ОткрытаяНастройка = Неопределено Тогда
		ИспользоватьПоУмочанию = ОткрытаяНастройка.Пометка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти












