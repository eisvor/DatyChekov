#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает доступные пользователю магазины.
//
// Параметры:
//  УчитыватьТекущийМагазин - Булево - признак учета текущего магазина.
//  Пользователь - СправочникСсылка.Пользователи - текущий пользователь.
//
// Возвращаемое значение:
//  ТаблицаЗначений - список доступных магазинов.
//
Функция ДоступныеМагазины(УчитыватьТекущийМагазин = Ложь, Пользователь = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Магазины.Ссылка КАК Магазин
	|ИЗ
	|	Справочник.Магазины КАК Магазины");
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый ТаблицаЗначений;
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		Магазины = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Магазин");
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ОбменДаннымиРТ.СоздатьВТДоступныеМагазины(МенеджерВременныхТаблиц, Ложь);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СправочникМагазины.Ссылка КАК Магазин,
		|	СправочникМагазины.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА НЕ Пользователи.Подразделение ЕСТЬ NULL
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Пометка
		|ИЗ
		|	Справочник.Магазины КАК СправочникМагазины
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДоступныеМагазины КАК ВТДоступныеМагазины
		|		ПО СправочникМагазины.Ссылка = ВТДоступныеМагазины.Магазин
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО СправочникМагазины.Ссылка = Пользователи.Подразделение
		|			И (Пользователи.Ссылка = &Пользователь)
		|ГДЕ
		|	(&УчитыватьТекущийМагазин
		|			ИЛИ НЕ СправочникМагазины.Ссылка = &ТекущийМагазин)
		|	И НЕ СправочникМагазины.СкладУправляющейСистемы
		|	И СправочникМагазины.Ссылка В(&Магазины)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
		Запрос.УстановитьПараметр("Пользователь", ?(Пользователь = Неопределено, Пользователи.ТекущийПользователь(), Пользователь));
		Запрос.УстановитьПараметр("УчитыватьТекущийМагазин", УчитыватьТекущийМагазин);
		Запрос.УстановитьПараметр("ТекущийМагазин", ПараметрыСеанса.ТекущийМагазин);
		Запрос.УстановитьПараметр("Магазины", Магазины);
		
		Возврат Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
