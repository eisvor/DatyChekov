#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ЗначениеВыбора Из Параметры.СписокВыбора Цикл
		
		Если ТипЗнч(ЗначениеВыбора.Значение) = Тип("СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей") 
			ИЛИ ТипЗнч(ЗначениеВыбора.Значение) = Тип("СправочникСсылка.НастройкиОнлайнОплат") Тогда
			
			ИмяКнопки 		= "ПС_" + РаботаСПравиламиИменованияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениПараметра(XMLСтрока(ЗначениеВыбора.Значение), "");
			ЗаголовокКнопки = ЗначениеВыбора.Представление;

			Если ТипЗнч(ЗначениеВыбора.Значение) = Тип("СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей") Тогда
				
				ПараметрыИнтеграции = ИнтеграцияСПлатежнымиСистемамиРТСервер.ПараметрыИнтеграции(ЗначениеВыбора.Значение);
				ЗаголовокКнопки		= Строка(ПараметрыИнтеграции.ВидОплаты);
				
			КонецЕсли;
			
		Иначе
			ИмяКнопки 		= ЗначениеВыбора.Значение;
			ЗаголовокКнопки = ЗначениеВыбора.Представление;
		КонецЕсли;
		
		КомандаФормы  = Команды.Добавить(ИмяКнопки);
		КомандаФормы.Действие = "ОбработкаВыбораЗначения";
		
		Кнопка = Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), Элементы.ГруппаКнопок);
		Кнопка.Заголовок  = ЗаголовокКнопки;
		Кнопка.ИмяКоманды = ИмяКнопки;
		Кнопка.Высота     = 3;
		Кнопка.Ширина     = 20;
		
		СписокКнопок.Добавить(ЗначениеВыбора.Значение, ИмяКнопки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораЗначения(Команда)
	
	Для Каждого ЗначениеСписка ИЗ СписокКнопок Цикл
		
		Если ЗначениеСписка.Представление = Команда.Имя Тогда
			Интеграция = ЗначениеСписка.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	Закрыть(Интеграция);
	
КонецПроцедуры

#КонецОбласти