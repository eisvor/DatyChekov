
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("КассыККМ")
		ИЛИ НЕ Параметры.Свойство("Организация") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	ФИО = Параметры.ФИО;
	Email = Параметры.ИмяПользователя;
	ТаблицаКасс = ИнтеграцияWebKassaВызовСервера.МассивВТаблицуЗначений(Параметры.КассыККМ);
	ВыбратьДоступныеКассыККМ(ТаблицаКасс, Параметры.ДоступныеКассы);
	
	КассыККМ.Загрузить(ТаблицаКасс);
	ЗаполнитьСвязанныеКассыККМ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьПраваДоступаКассира(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ПроверитьЗаполнениеДанных() Тогда
		Возврат;
	КонецЕсли;
	
	Кассы = Новый Массив;
	Для Каждого СтрокаКассаККМ Из КассыККМ Цикл
		Если СтрокаКассаККМ.Выбрать = Истина Тогда
			Кассы.Добавить(СтрокаКассаККМ.СерийныйНомер);
		КонецЕсли;
	КонецЦикла;
	ВходныеПараметры = Новый Структура;
	ВходныеПараметры.Вставить("Организация",     Организация);
	ВходныеПараметры.Вставить("ИмяПользователя", Email);
	ВходныеПараметры.Вставить("ПереченьКасс",    Кассы);
	
	ПараметрыПодключения = Новый Структура;
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ВыполнитьИзмененияПравКассира_Завершение", ЭтотОбъект, ВходныеПараметры);
	ИнтеграцияWebKassaКлиент.НачатьИзменениеПравКассира(ОповещениеПриЗавершении, ВходныеПараметры, ПараметрыПодключения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеКассы(Команда)
	Для Каждого СтрокаКассаККМ Из КассыККМ Цикл
		СтрокаКассаККМ.Выбрать = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсеКассы(Команда)
	Для Каждого СтрокаКассаККМ Из КассыККМ Цикл
		СтрокаКассаККМ.Выбрать = Ложь;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСвязанныеКассыККМ()
	
	ТаблицаКасс = РеквизитФормыВЗначение("КассыККМ");
	МассивСерийныхНомеров = ТаблицаКасс.ВыгрузитьКолонку("СерийныйНомер");
	СоответствиеКассККМСерийнымНомерам = ИнтеграцияWebKassaВызовСервера.СоответствиеКассККМСерийнымНомерам(МассивСерийныхНомеров);
	
	Для Каждого СтрокаКассы Из ТаблицаКасс Цикл
		СписокКассККМ = СоответствиеКассККМСерийнымНомерам[СтрокаКассы.СерийныйНомер];
		Если СписокКассККМ <> Неопределено Тогда
			СтрокаКассы.СвязанныеКассыККМ.ЗагрузитьЗначения(СписокКассККМ);
			СтрокаКассы.ПредставлениеКассыККМ = СтрСоединить(СписокКассККМ, ",");
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаКасс, "КассыККМ");
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьДоступныеКассыККМ(ТаблицаКасс, ДоступныеКассы)
	
	Для Каждого СтрокаКасса Из ТаблицаКасс Цикл
		Если ДоступныеКассы.Найти(СтрокаКасса.СерийныйНомер)<>Неопределено Тогда
			СтрокаКасса.Выбрать = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеДанных()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КоличествоКасс = 0;
	Для Каждого СтрокаКассаККМ Из КассыККМ Цикл
		Если СтрокаКассаККМ.Выбрать = Истина Тогда
			КоличествоКасс = КоличествоКасс + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоКасс = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Необходимо выбрать хотя бы одну кассу ККМ!'");
		ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(ТекстОшибки, , "Ошибка заполнения");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьИзмененияПравКассира_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.РезультатВыполнения Тогда
		Кассы = Новый Массив;
		Для Каждого СтрокаКассаККМ Из КассыККМ Цикл
			Если СтрокаКассаККМ.Выбрать = Истина Тогда
				Кассы.Добавить(СтрокаКассаККМ.СерийныйНомер);
			КонецЕсли;
		КонецЦикла;
		СвязанныеКассы = СтрСоединить(Кассы, ",");
		РезультатВыбора = Новый Структура;
		РезультатВыбора.Вставить("РезультатВыполнения", Истина);
		РезультатВыбора.Вставить("СвязанныеКассы", СвязанныеКассы);
		РезультатВыбора.Вставить("ИмяПользователя", Email);
		Закрыть(РезультатВыбора);
	Иначе
		Если Результат.ВыходныеПараметры[0] = 999 Тогда
			ТекстСообщения = НСтр("ru = 'При изменении прав кассира произошла ошибка.
										|Дополнительное описание: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", Результат.ВыходныеПараметры[1]);
			ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьПользователяВНастройкуИнтеграции(Организация, Пользователь)
	Возврат Справочники.НастройкиИнтеграцииWebKassa.ДобавитьПользователяВНастройкуИнтеграции(Организация, Пользователь);
КонецФункции

&НаКлиенте
Процедура КассыККМВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "КассыККМПредставлениеКассыККМ" Тогда
		ТекущиеДанные = Элементы.КассыККМ.ТекущиеДанные;
		ОткрытьФорму("Обработка.Панель1СWebKassa.Форма.ПросмотрСпискаКасс",
			Новый Структура("СписокКасс", ТекущиеДанные.СвязанныеКассыККМ));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ф = Параметры;
	
КонецПроцедуры

#КонецОбласти
