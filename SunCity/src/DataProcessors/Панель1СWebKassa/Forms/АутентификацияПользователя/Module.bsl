
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПредлагатьЗапомнитьПароль = Истина;
	Если Параметры.Свойство("ПредлагатьЗапомнитьПароль", ПредлагатьЗапомнитьПароль)
		И ПредлагатьЗапомнитьПароль = Ложь Тогда
		Элементы.ЗапомнитьПароль.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
		СписокДоступныхПользователей.ЗагрузитьЗначения(
			Справочники.НастройкиИнтеграцииWebKassa.ПользователиОрганизации(Параметры.Организация));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаГотово(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) И СписокДоступныхПользователей.НайтиПоЗначению(Пользователь) = Неопределено Тогда
		ШаблонСообщения = НСтр("ru = 'Пользователь %1 не имеет доступа к организации %2.
									|Обратитесь к администратору.'");
		
		ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(
			ИнтеграцияWebKassaКлиентПереопределяемый.ПодставитьПараметрыВСтроку(ШаблонСообщения, Пользователь, Организация));
		
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	
	РезультатВыбора.Вставить("Пользователь",       Пользователь);
	РезультатВыбора.Вставить("ПарольПользователя", ПарольПользователя);
	РезультатВыбора.Вставить("ЗапомнитьПароль",    ЗапомнитьПарольНаВремяСеанса);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти
