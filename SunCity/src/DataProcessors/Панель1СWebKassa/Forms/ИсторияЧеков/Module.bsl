&НаКлиенте
Перем МассивЧеков;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Организация") 
		ИЛИ НЕ Параметры.Свойство("СерийныйНомер") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	СерийныйНомер = Параметры.СерийныйНомер;
	СведенияООрганизации = ИнтеграцияWebKassaВызовСервераПереопределяемый.ПолучитьСведенияОбОрганизации(Организация, ТекущаяДата());
	НаименованиеОрганизации = СведенияООрганизации.ПолноеНаименование;
	ИИН = СведенияООрганизации.ИНН;
	НомерНДС = Организация.НомерСвидетельстваПоНДС;
	СерияНДС = Организация.СерияСвидетельстваПоНДС;
	ШиринаЛенты = ШиринаЛентыПоУмолчанию(СерийныйНомер);
	
	УстановитьДоступностьКнопок(1, 1);
	КоличествоЧеков = 50;
	
	Элементы.ПринтерЧеков.Доступность = ИспользоватьПринтерЧеков;
	Элементы.ПечатьЧека.Доступность = ИспользоватьПринтерЧеков;
	РабочееМесто = ИнтеграцияWebKassaВызовСервераПереопределяемый.ПолучитьРабочееМестоКлиента();
	
	//принтер чеков
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипОборудования", Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков));
	Элементы.ПринтерЧеков.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьПринтерЧековПриИзменении(Элемент)
	
	Элементы.ПринтерЧеков.Доступность = ИспользоватьПринтерЧеков;
	Элементы.ПечатьЧека.Доступность = ИспользоватьПринтерЧеков;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЧеки()
	
	ВходныеПараметры = Новый Структура;
	ВходныеПараметры.Вставить("Организация",   Организация);
	ВходныеПараметры.Вставить("СерийныйНомер", СерийныйНомер);
	ВходныеПараметры.Вставить("НомерСмены",    НомерСмены);
	ВходныеПараметры.Вставить("Отступ",        Отступ);
	ВходныеПараметры.Вставить("Количество",    КоличествоЧеков);
	
	ПараметрыПодключения = Новый Структура;
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ПолучитьЧеки_Завершение", ЭтотОбъект);
	ИнтеграцияWebKassaКлиент.НачатьПолучениеИсторииЧеков(ОповещениеПриЗавершении, ВходныеПараметры, ПараметрыПодключения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПервуюСтраницу(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Отступ = 0;
	ПолучитьЧеки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущаяСтраница(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Отступ = Отступ - КоличествоЧеков;
	ПолучитьЧеки();
	
КонецПроцедуры

&НаКлиенте
Процедура СледующаяСтраница(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Отступ = Отступ + КоличествоЧеков;
	ПолучитьЧеки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЧек(Команда)
	
	ПоказатьЧек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧека(Команда)
	
	ПоказатьЧек(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьЧеки_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.РезультатВыполнения Тогда
		
		СписокЧеков.Очистить();
		Если Результат.ОбъектыJSON.Data.Total = 0 Тогда
			УстановитьДоступностьКнопок(1, 1);
			Элементы.НадписьСтраницы.Заголовок = нСтр("ru='Страница 0 из 0 (Всего элементов: 0)'", "ru");
			ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(нСтр("ru='В данной смене нет чеков.'", "ru"));
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Результат.ОбъектыJSON.Data.Items) И Результат.ОбъектыJSON.Data.Items.Количество() <> 0 Тогда
			Индекс = 0;
			Для Каждого Чек Из Результат.ОбъектыJSON.Data.Items Цикл
				Чек.CashboxIdentityNumber = Результат.ОбъектыJSON.Data.CashboxIdentityNumber;
				Чек.CashboxUniqueNumber = Результат.ОбъектыJSON.Data.CashboxUniqueNumber;
				Чек.CashboxRegistrationNumber = Результат.ОбъектыJSON.Data.CashboxRegistrationNumber;
				Если НЕ Чек.Свойство("Ofd") Тогда
					Чек.Вставить("Ofd", "");
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Чек.Ofd) Тогда
					Если Результат.ОбъектыJSON.Data.Свойство("Ofd") Тогда
						Чек.Ofd = Результат.ОбъектыJSON.Data.Ofd;
					КонецЕсли;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(СписокЧеков.Добавить(), Новый Структура("ФискальныйПризнак, ДатаЧека, СуммаЧека, ИндексЧека",
					Чек.Number,
					Чек.RegistratedOn,
					Чек.Total,
					Индекс
				));
				Индекс = Индекс + 1;
			КонецЦикла;
			МассивЧеков = ИнтеграцияWebKassaКлиентПереопределяемый.СкопироватьМассив(Результат.ОбъектыJSON.Data.Items);
		КонецЕсли;
		
		ВсегоЧеков = Результат.ОбъектыJSON.Data.Total;
		ВсегоСтраниц = ?((ВсегоЧеков - КоличествоЧеков * Цел(ВсегоЧеков / КоличествоЧеков)) > 0,
			Цел(ВсегоЧеков / КоличествоЧеков)+1, Цел(ВсегоЧеков / КоличествоЧеков));
		НомерСтраницы = Отступ/КоличествоЧеков + 1;
		Элементы.НадписьСтраницы.Заголовок = СтрШаблон(нСтр("ru='Страница %1 из %2 (Всего элементов: %3)'", "ru"),
			НомерСтраницы, ВсегоСтраниц, ВсегоЧеков);
		УстановитьДоступностьКнопок(НомерСтраницы, ВсегоСтраниц);
		
	Иначе
		Если Результат.ВыходныеПараметры[0] = 999 Тогда
			СписокЧеков.Очистить();
			УстановитьДоступностьКнопок(1, 1);
			Элементы.НадписьСтраницы.Заголовок = нСтр("ru='Страница 0 из 0 (Всего элементов: 0)'", "ru");
			ТекстСообщения = НСтр("ru = 'При получение произошла ошибка.
				|Дополнительное описание: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", Результат.ВыходныеПараметры[1]);
			ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКнопок(ТекущаяСтраница, ВсегоСтраниц)
	
	СледующаяСтраницаДоступность = Ложь;
	ПредыдущаяСтраницаДоступность = Ложь;
	
	КнопкаПредыдущая = Элементы.Найти("СтраницаПредыдущая");
	Если КнопкаПредыдущая <> Неопределено Тогда
		
		Если ТекущаяСтраница = 1 Тогда
			КнопкаПредыдущая.Доступность = Ложь;
		Иначе
			КнопкаПредыдущая.Доступность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	КнопкаСледующая = Элементы.Найти("СтраницаСледующая");
	Если КнопкаСледующая <> Неопределено Тогда
		
		Если ТекущаяСтраница = ВсегоСтраниц Тогда
			КнопкаСледующая.Доступность = Ложь;
		Иначе
			КнопкаСледующая.Доступность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьВыборЧека()
	
	Если Элементы.СписокЧеков.ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения операции необходимо выбрать чек!'");
		ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ШиринаЛентыПоУмолчанию(СерийныйНомер)
	
	ЗначениеПоУмолчанию = "24";
	МассивСерийныхНомеров = Новый Массив;
	МассивСерийныхНомеров.Добавить(СерийныйНомер);
	СоответствиеКассККМСерийнымНомерам = ИнтеграцияWebKassaВызовСервера.СоответствиеКассККМСерийнымНомерам(МассивСерийныхНомеров);
	Если СоответствиеКассККМСерийнымНомерам.Количество() = 0 Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	СписокКассККМ = СоответствиеКассККМСерийнымНомерам[СерийныйНомер];
	Для Каждого КассаККМ Из СписокКассККМ Цикл
		ОборудованиеКассы = КассаККМ;
		Если ЗначениеЗаполнено(ОборудованиеКассы) Тогда
			ПараметрыУстройства = Справочники.ПодключаемоеОборудование.ПолучитьПараметрыУстройства(ОборудованиеКассы);
			Если ПараметрыУстройства.Свойство("ШиринаЛенты") Тогда
				Если ПараметрыУстройства.Свойство("ЯзыкПоУмолчанию") Тогда
					ЯзыкПечати = ПараметрыУстройства.ЯзыкПоУмолчанию;
				КонецЕсли;
				ЗначениеПоУмолчанию = ПараметрыУстройства.ШиринаЛенты;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

&НаКлиенте
Процедура СписокЧековВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЧек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЧек(ПечататьЧек = Ложь)
	
	Если Не ПроверитьВыборЧека() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЧека = МассивЧеков[Элементы.СписокЧеков.ТекущиеДанные.ИндексЧека];
	ИнтеграцияWebKassaКлиентСервер.ДополнитьФорматДанныхЧекаИстории(ДанныеЧека);
	
	Для Каждого Позиция Из ДанныеЧека.Positions Цикл
		Позиция.Вставить("Discount", Позиция.DiscountTenge);
		Позиция.Вставить("TaxType",  Позиция.TaxPercent);
		Если Позиция.IsNds = Истина И Позиция.Tax <> 0 И Позиция.TaxPercent = 0 Тогда
			Позиция.TaxPercent = ДанныеЧека.TaxPercent;
			Позиция.TaxType = ДанныеЧека.TaxPercent;
		КонецЕсли;
		
		Если Позиция.Свойство("Mark")
			И ЗначениеЗаполнено(Позиция.Mark) Тогда 
				Позиция.Mark = ИнтеграцияWebKassaКлиентСервер.УдалитьНедопустимыеСимволыXML(Позиция.Mark);
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеЧека.Вставить("ИНК",                     ДанныеЧека.CashboxIdentityNumber);
	ДанныеЧека.Вставить("СерийныйНомерККМ",        ДанныеЧека.CashboxUniqueNumber);
	ДанныеЧека.Вставить("РегистрационныйНомерККМ", ДанныеЧека.CashboxRegistrationNumber);
	ДанныеЧека.Вставить("ФискальныйНомер",         ДанныеЧека.Number);
	ДанныеЧека.Вставить("АвтономныйРежимРаботы",   ДанныеЧека.IsOffline);
	ДанныеЧека.Вставить("НомерСмены",              ДанныеЧека.ShiftNumber);
	ДанныеЧека.Вставить("НомерВРамкахСмены",       ДанныеЧека.OrderNumber);
	ДанныеЧека.Вставить("ДатаЧека",                ДанныеЧека.RegistratedOn);
	ДанныеЧека.Вставить("АдресККМ",                ДанныеЧека.Address);
	ДанныеЧека.Вставить("СсылкаНаЧек",             ДанныеЧека.TicketUrl);
	
	ДопДанные = Новый Структура();
	ДопДанные.Вставить("Кассир",                  ДанныеЧека.EmployeeName);
	ДопДанные.Вставить("Организация",             НаименованиеОрганизации);
	ДопДанные.Вставить("ИИН",                     ИИН);
	ДопДанные.Вставить("НомерСвидетельстваПоНДС", НомерНДС);
	ДопДанные.Вставить("СерияСвидетельстваПоНДС", СерияНДС);
	ДопДанные.Вставить("ЭтоДубликат",             Истина);
	
	ПараметрыЛенты = Новый Структура;
	ПараметрыЛенты.Вставить("ШиринаЛенты", ШиринаЛенты);
	ПараметрыЛенты.Вставить("ЯзыкПоУмолчанию", ЯзыкПечати);
	
	Если ПечататьЧек Тогда
		ШиринаСтрокиПринтера = ПолучитьШиринуСтрокиПринтера(ПринтерЧеков);
		ПараметрыЛенты.ШиринаЛенты = ?(ШиринаСтрокиПринтера<>Неопределено, ШиринаСтрокиПринтера, 32);
		ТекстЧека = ПодключаемоеОборудованиеWebkassaФискальныеРегистраторыКлиент.СформироватьТекстФискальногоЧека(
			ДанныеЧека, ПараметрыЛенты, ДопДанные);
		ИнтеграцияWebKassaКлиентПереопределяемый.НачатьПечатьТекста(
			Новый ОписаниеОповещения("ПослеЗавершенияПечатиНаПринтереЧеков", ЭтотОбъект),
			Новый УникальныйИдентификатор(),
			ТекстЧека,
			ПринтерЧеков);
	Иначе
		ТабличныйДокумент = Новый ТабличныйДокумент;
		РезультатПечати = ИнтеграцияWebKassaВызовСервера.ПечатьФискальногоЧека(
		ДанныеЧека, 0, ПараметрыЛенты, ТабличныйДокумент, ДопДанные);
		Если ТабличныйДокумент = Неопределено Тогда
			ШаблонСообщения = НСтр("ru = 'Операция успешно проведена в системе Webkassa, но при печати произошла ошибка.
				|Дополнительное описание:
				|%1'");
			ТекстСообщения = ИнтеграцияWebKassaКлиентПереопределяемый.ПодставитьПараметрыВСтроку(ШаблонСообщения, РезультатПечати);
			ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Иначе
			ТабличныйДокумент.АвтоМасштаб = Истина;
			ТабличныйДокумент.Показать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияПечатиНаПринтереЧеков(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		//сообщить о проблемах с печатью
		ТекстСообщения = НСтр("ru = 'При печати произошла ошибка.
			|Дополнительное описание:
			|%ДополнительноеОписание%'");
		
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьШиринуСтрокиПринтера(Идентификатор)
	
	ПараметрыУстройства = ИнтеграцияWebKassaВызовСервераПереопределяемый.ПолучитьПараметрыУстройства(Идентификатор);
	ШиринаСтроки = 32;
	Если ПараметрыУстройства.Свойство("ШиринаСтроки") Тогда
		ШиринаСтроки = ПараметрыУстройства.ШиринаСтроки;
	ИначеЕсли ПараметрыУстройства.Свойство("P_LineLength") Тогда
		ШиринаСтроки = ПараметрыУстройства.P_LineLength;
	КонецЕсли;
	Возврат ШиринаСтроки;
	
КонецФункции

#КонецОбласти
