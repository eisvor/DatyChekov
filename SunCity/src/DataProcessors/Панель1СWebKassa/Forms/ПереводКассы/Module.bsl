
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Организация") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	
	ЗаполнитьСписокПартнеров(Параметры.Партнеры);
	
	Элементы.РежимДобавления.Видимость = ЗначениеФункциональнойОпции("ИспользоватьПодключаемоеОборудование");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда
		Элементы.РежимДобавления.Видимость = ЗначениеФункциональнойОпции("ИспользоватьПодключаемоеОборудование");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КассаККМПриИзменении(Элемент)
	
	СерийныйНомерЗаполнен = СерийныйНомерЗаполнен(КассаККМ);
	Если СерийныйНомерЗаполнен Тогда
		Элементы.ГруппаКассаСообщение.Видимость = Истина;
	Иначе
		Элементы.ГруппаКассаСообщение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПереводКассы(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ВходныеПараметры = Новый Структура;
	ВходныеПараметры.Вставить("Организация",      Организация);
	ВходныеПараметры.Вставить("НомерКассы",       НомерКассы);
	ВходныеПараметры.Вставить("НомерПартнера",    НомерПартнера);
	ВходныеПараметры.Вставить("НомерКарты",       НомерКарты);
	ВходныеПараметры.Вставить("КодПодтверждения", КодПодтверждения);
	ВходныеПараметры.Вставить("КодПартнера",      КодПартнера);
	ВходныеПараметры.Вставить("КодАктивации",     КодАктивации);
	
	ПараметрыПодключения = Новый Структура;
	
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ВыполнитьПереводКассы_Завершение", ЭтотОбъект);
	ИнтеграцияWebKassaКлиент.НачатьПереводКассы(ОповещениеПриЗавершении, ВходныеПараметры, ПараметрыПодключения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПереводКассы_Завершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат.РезультатВыполнения Тогда
		УстановитьЗначениеКонстантыПО();
		Если РежимДобавления=0 Тогда
			ИмяКассы = НаименованиеКассы;
			КассаККМ = СоздатьПодключаемоеОборудование(Организация, НаименованиеКассы, НомерКассы);
		Иначе
			ИмяКассы = Строка(КассаККМ);
			ИнтеграцияWebKassaВызовСервераПереопределяемый.ИзменитьСерийныйНомерОборудования(КассаККМ, НомерКассы);
		КонецЕсли;
		РезультатВыбора = Новый Структура;
		РезультатВыбора.Вставить("РезультатВыполнения", Истина);
		РезультатВыбора.Вставить("КассаККМ",            КассаККМ);
		РезультатВыбора.Вставить("СерийныйНомер",       НомерКассы);
		Закрыть(РезультатВыбора);
	Иначе
		Если Результат.ВыходныеПараметры[0] = 999 Тогда
			ТекстСообщения = НСтр("ru = 'При переводе кассы произошла ошибка.
				|Дополнительное описание: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", Результат.ВыходныеПараметры[1]);
			ИнтеграцияWebKassaКлиентПереопределяемый.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СерийныйНомерЗаполнен(КассаККМ)
	Возврат ИнтеграцияWebKassaВызовСервера.СерийныйНомерЗаполнен(КассаККМ);
КонецФункции

&НаКлиенте
Процедура РежимДобавленияПриИзменении(Элемент)
	
	ПанельКасса = Элементы.ГруупаКасса;
	Если РежимДобавления = 1 Тогда
		ПанельКасса.ТекущаяСтраница = ПанельКасса.ПодчиненныеЭлементы.ГруппаИспользовать;
	Иначе
		ПанельКасса.ТекущаяСтраница = ПанельКасса.ПодчиненныеЭлементы.ГруппаСоздать;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьПодключаемоеОборудование(Организация, ИмяКассы, СерийныйНомер)
	
	НовоеОборудование = ИнтеграцияWebKassaВызовСервераПереопределяемый.СоздатьПодключаемоеОборудованиеWebkassa(Организация, ИмяКассы, СерийныйНомер);
	Если НовоеОборудование=Ложь тогда
		Возврат Неопределено;
	Иначе
		Возврат НовоеОборудование;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если РежимДобавления = 1 Тогда
		НепроверяемыеРеквизиты.Добавить("НаименованиеКассы");
	Иначе
		НепроверяемыеРеквизиты.Добавить("КассаККМ");
	КонецЕсли;
	
	ИнтеграцияWebKassaВызовСервераПереопределяемый.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЗначениеКонстантыПО()
	Если Константы.ИспользоватьПодключаемоеОборудование.Получить() <> Истина Тогда
			Константы.ИспользоватьПодключаемоеОборудование.Установить(Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеФункциональнойОпции(ИмяОпции)
	
	Возврат Константы[ИмяОпции].Получить();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокПартнеров(МассивПартнеров)
	
	СписокВыбора = Новый СписокЗначений;
	
	Для Каждого ЭлементМассива Из МассивПартнеров Цикл
		Элементы.НомерПартнера.СписокВыбора.Добавить(ЭлементМассива.НомерПартнера, ЭлементМассива.Наименование);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
