#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сформировать печатные формы объектов.
//
// Параметры:
//  МассивОбъектов - Массив - список объектов, для которых была выполнена процедура Печать;
//  ПараметрыПечати - Структура - произвольные параметры, переданные при вызове команды печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - содержит сформированные табличные документы и дополнительную информацию;
//  ОбъектыПечати - СписокЗначений - соответствие между объектами и именами областей в табличных документах, где
//                                   значение - Объект, представление - имя области с объектом в табличных документах;
//  ПараметрыВывода - Структура - параметры, связанные с выводом табличных документов:
//   * ПараметрыОтправки - Структура - для заполнения письма при отправке печатной формы по электронной почте.
//                    см. РаботаСПочтовымиСообщениямиКлиент.РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПроверкаИспользуемыхКодов") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПроверкаИспользуемыхКодов", 
			НСтр("ru = 'Проверка используемых кодов'"), 
			ПечатьПроверкиИспользуемыхКодов(МассивОбъектов, ОбъектыПечати, ПараметрыПечати["СтруктураТаблиц"]));
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьПроверкиИспользуемыхКодов(МассивОбъектов, ОбъектыПечати, СтруктураТаблиц)
		
	ТабличныйДокумент = Новый ТабличныйДокумент;
		
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаполнениеДиапазонаСерийныхНомеров_ПроверкаИспользуемыхКодов";
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ЗаполнениеДиапазонаСерийныхНомеров.ПроверкаИспользуемыхКодов");
	
	ОбластьМакета 	= Макет.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета   = Макет.ПолучитьОбласть("Строка");
	ТаблицаКодов    = Неопределено;
	НомерСтроки     = 0;
		
	Если СтруктураТаблиц.Свойство("ТаблицаМагнитныхКодов", ТаблицаКодов) Тогда
		
		Для каждого СтрокаТаблицыКодов Из ТаблицаКодов Цикл
			
			НомерСтроки = НомерСтроки + 1;
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыКодов);
			ОбластьМакета.Параметры.Номер = НомерСтроки;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СтруктураТаблиц.Свойство("ТаблицаШтриховыхКодов", ТаблицаКодов) Тогда
		
		Для каждого СтрокаТаблицыКодов Из ТаблицаКодов Цикл
			НомерСтроки = НомерСтроки + 1;
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыКодов);
			ОбластьМакета.Параметры.Номер = НомерСтроки;
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
	КонецЕсли;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#КонецЕсли
