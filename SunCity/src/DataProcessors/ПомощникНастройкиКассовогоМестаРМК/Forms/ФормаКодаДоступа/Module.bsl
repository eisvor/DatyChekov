#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура КассирПриИзменении(Элемент)
	
	Если ТипЗнч(Кассир) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ЗаполнитьДанныеПоФизическомуЛицу();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоФизическомуЛицу()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ФизическиеЛица.ИНН КАК ИНН,
	|	ЕСТЬNULL(Штрихкоды.Штрихкод, """") КАК КодСотрудника
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК Штрихкоды
	|		ПО ФизическиеЛица.Ссылка = Штрихкоды.Владелец
	|ГДЕ
	|	ФизическиеЛица.Ссылка = &ФизическоеЛицо");
	Запрос.УстановитьПараметр("ФизическоеЛицо", Кассир);
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ИНН = Выборка.ИНН;
		КодСотрудника = Выборка.КодСотрудника;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ГруппаДоступа = Параметры.ГруппаДоступа;
	Пользователь = Параметры.Пользователь;
	Кассир = Параметры.Кассир;
	
	ОбщегоНазначенияРМКПереопределяемый.НайтиЗаписьРегистраПерсональныйКод(Пользователь, Кассир, КодСотрудника, ИНН);
	
	Элементы.ОткрытьФормуСозданияКодаДоступа.Доступность = НЕ ЗначениеЗаполнено(КодСотрудника);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИЗакрытьНаСервере(ТекстСообщения, РезультатДобавленияВГруппуДоступа)
	
	ОбщегоНазначенияРМКПереопределяемый.ДобавитьПользователяВГруппуДоступаПомощникРМК(Пользователь, ГруппаДоступа, ТекстСообщения, РезультатДобавленияВГруппуДоступа);
		
	ОбщегоНазначенияРМКПереопределяемый.ПрисвоитьПерсональныйКод(Пользователь, Кассир, КодСотрудника, ИНН);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Не ЗначениеЗаполнено(Кассир) Тогда
		ТекстСообщения = НСтр("ru = 'Кассир не заполнен'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = ''");
	РезультатДобавленияВГруппуДоступа = Ложь;
	ЗаписатьИЗакрытьНаСервере(ТекстСообщения, РезультатДобавленияВГруппуДоступа);
	Закрыть(ТекстСообщения);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ТекстСообщения = "";
	Если НЕ МенеджерОборудованияКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, ТекстСообщения)
		И ЗначениеЗаполнено(ИНН) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСозданияКодаДоступа(Команда)
	
	Если Не ЗначениеЗаполнено(Кассир) Тогда
		ТекстСообщения = НСтр("ru = 'Кассир не заполнен'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Кассир", Кассир);
	ПараметрыОткрытия.Вставить("НастроитьКассировДляВхода", Истина);
	ПараметрыОткрытия.Вставить("ГенерацияПерсональногоКода", Истина);
	
	ДополнительныйПараметрЗакрытияФормы = Новый Структура();
	ДополнительныйПараметрЗакрытияФормы.Вставить("КодДоступа", Неопределено);
	
	Оповещение = Новый ОписаниеОповещения("СозданиеКодаДоступаЗавершение", ЭтотОбъект,
		ДополнительныйПараметрЗакрытияФормы);
	
	ОткрытьФорму("Обработка.РабочееМестоКассира.Форма.ФормаУстановкиКодаДоступаКассира",
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор, , ,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура СозданиеКодаДоступаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодСотрудника = Результат;
	УстановитьДоступностьСозданияКода();
	
КонецПроцедуры

&НаКлиенте
Процедура КодСотрудникаПриИзменении(Элемент)
	УстановитьДоступностьСозданияКода();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьСозданияКода()
	Элементы.ОткрытьФормуСозданияКодаДоступа.Доступность = НЕ ЗначениеЗаполнено(КодСотрудника);
КонецПроцедуры


#КонецОбласти
