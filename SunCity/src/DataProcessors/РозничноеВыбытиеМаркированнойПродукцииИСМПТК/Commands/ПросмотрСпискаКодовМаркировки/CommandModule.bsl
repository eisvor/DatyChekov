
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеСпискаСтроковые = РозничноеВыбытиеИСМПТКПереопределяемый.ПолучитьПереопределяемыеДанныеСпискаЧековККМ(ПараметрыВыполненияКоманды.Источник.ИмяФормы); //Получение переопределенных наименований документа
	НаименованиеСписка 	  = ДанныеСпискаСтроковые.НаименованиеСписка;
	ДанныеЧека 			  = Неопределено;
	
	Если НаименованиеСписка = Неопределено Тогда
		//Для ЕРП и УТ команда отрабатывает иначе
		Ссылка = ОбщегоНазначенияИСМПТК.ЗначениеРеквизитаОбъекта(ПараметрыВыполненияКоманды.Источник.Объект.Ссылка, "Ссылка");
		ДанныеЧека = ПолучитьДанныеТекущегоЧека(Ссылка); 
	Иначе
		//Розница
		ТекущиеДанныеСтрокаЧека = ПараметрыВыполненияКоманды.Источник.Элементы[НаименованиеСписка].ТекущиеДанные;
		Если Не ТекущиеДанныеСтрокаЧека = Неопределено Тогда
			ДанныеЧека 	= ПолучитьДанныеТекущегоЧека(ТекущиеДанныеСтрокаЧека.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ДанныеЧека = Неопределено Тогда
		
		Организация  = ДанныеЧека.Организация;
		СписокКМ	 = ДанныеЧека.СписокКМ;
		СписокТовары = ДанныеЧека.СписокТовары;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ИмяРеквизитаФормыОбъект",       "Объект");
		ПараметрыОткрытия.Вставить("ИмяРеквизитаОрганизация",       "Организация");
		ПараметрыОткрытия.Вставить("РедактированиеФормыНедоступно", Истина);
		ПараметрыОткрытия.Вставить("ПроверятьМодифицированность",   Ложь);
		ПараметрыОткрытия.Вставить("СписокКМ",  					СписокКМ);
		ПараметрыОткрытия.Вставить("ТоварыЧека",  					СписокТовары);
		ПараметрыОткрытия.Вставить("Организация", 					Организация);
		ПараметрыОткрытия.Вставить("ВызовИзФормыСпискаЧеков", 		Истина);		
		ПараметрыОткрытия.Вставить("ОбъединятьСтрокиСОдинаковымиТоварами", Истина);		
		
		//Дополнительный параметр, присуствующий только при вызове из формы ЧекККМВозврат
		ПараметрыОткрытия.Вставить("ДанныеВозвращаемогоКода", Неопределено);
		
		ОткрытьФорму("Обработка.РозничноеВыбытиеМаркированнойПродукцииИСМПТК.Форма.ПроверкаКМ", ПараметрыОткрытия, ПараметрыВыполненияКоманды.Источник,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеТекущегоЧека(СсылкаЧек)
	
	Запрос = Новый Запрос();
	Запрос.Текст = РозничноеВыбытиеИСМПТКПереопределяемый.ТекстЗапросаЧекККМДанныеТекущегоЧекаИзСписка(СсылкаЧек);
	Запрос.УстановитьПараметр("Ссылка", СсылкаЧек);
	
	ДанныеЧека = Новый Структура();
	ДанныеЧека.Вставить("Организация");
	ДанныеЧека.Вставить("СписокКМ");
	ДанныеЧека.Вставить("СписокТовары");
	
	Организация  = Неопределено;
	СписокКМ 	 = Новый Массив();
	СписокТовары = Новый Массив();
	
	ВыборкаКодыМаркировки = Запрос.ВыполнитьПакет()[0].Выбрать();
	Пока ВыборкаКодыМаркировки.Следующий() Цикл
						
		ДанныеСтрокиКМ = Новый Структура();
		ДанныеСтрокиКМ.Вставить("КодМаркировки",    ВыборкаКодыМаркировки.КодМаркировки);
		ДанныеСтрокиКМ.Вставить("КлючСвязи",	    ВыборкаКодыМаркировки.КлючСвязи);
		ДанныеСтрокиКМ.Вставить("Номенклатура",     ВыборкаКодыМаркировки.Номенклатура);
		ДанныеСтрокиКМ.Вставить("Характеристика",   ВыборкаКодыМаркировки.Характеристика);
		ДанныеСтрокиКМ.Вставить("ЕдиницаИзмерения", ВыборкаКодыМаркировки.ЕдиницаИзмерения);
		СписокКМ.Добавить(ДанныеСтрокиКМ);
		Организация = ВыборкаКодыМаркировки.Организация;
		
	КонецЦикла;
	
	ВыборкаТовары = Запрос.ВыполнитьПакет()[1].Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
						
		ДанныеСтроки = Новый Структура();
		ДанныеСтроки.Вставить("НомерСтроки",      ВыборкаТовары.НомерСтроки);
		ДанныеСтроки.Вставить("КлючСвязи",	      ВыборкаТовары.КлючСвязи);
		ДанныеСтроки.Вставить("Номенклатура",     ВыборкаТовары.Номенклатура);
		ДанныеСтроки.Вставить("Характеристика",   ВыборкаТовары.Характеристика);
		ДанныеСтроки.Вставить("ЕдиницаИзмерения", ВыборкаТовары.Упаковка);
		СписокТовары.Добавить(ДанныеСтроки);
				
	КонецЦикла;
	
	ДанныеЧека.Организация  = Организация;
	ДанныеЧека.СписокКМ     = СписокКМ;
	ДанныеЧека.СписокТовары = СписокТовары;
	
	Возврат ДанныеЧека;
	
КонецФункции
