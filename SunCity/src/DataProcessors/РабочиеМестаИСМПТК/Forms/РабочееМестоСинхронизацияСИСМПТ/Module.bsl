////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РежимЗапуска = Параметры.РежимЗапуска;
	ВидыДокументов = "Акты"; //по умолчанию
	Если Параметры.Свойство("ВидыДокументов") И ЗначениеЗаполнено(Параметры.ВидыДокументов) Тогда
		ВидыДокументов = Параметры.ВидыДокументов;
	КонецЕсли;
	
	ОрганизацияОтбор = ?(Параметры.Свойство("ОрганизацияОтбор"), Параметры.ОрганизацияОтбор, Неопределено);
	ЗаполнитьТаблицуПрофилейДляСинхронизации(ОрганизацияОтбор);
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СобытияФормИСМПТККлиентПереопределяемый.ПриОткрытии(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСтруктурнаяЕдиницаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСтруктурнаяЕдиницаПриИзменении(Элемент)
	
	Строка = Элементы.Таблица.ТекущиеДанные;
	Строка.Пометка = Истина;
	Строка.ДатаОкончанияСинхронизацииИСМПТ = КонецДня(ПолучитьТекущуюДату());
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекущуюДату() Экспорт
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

&НаКлиенте
Процедура ТаблицаПрофильИСМПТНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПрофильИСМПТОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКомментарийОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
    
&НаКлиенте
Асинх Процедура Синхронизировать(Команда)
	
	// Создать массив профилей ИС МПТ из помеченных строк таблицы.
	МассивПрофилейИСМПТСДатойСинхронизации = Новый Массив; 
	СтруктурныеЕдиницы = Новый Соответствие;
	//
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		
		Если СтрокаТаблицы.Пометка Тогда
			
			ТокенАвторизации = Ждать ИнтеграцияИСМПТККлиент.ПолучитьТокенАвторизации(СтрокаТаблицы.СтруктурнаяЕдиница, ПолучитьТипДокумента(), Ложь);
			
			Если ТокенАвторизации = Неопределено Тогда
				
				ТекстСообщенияТокенНеПолучен = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОперацияПрерванаНеПолученыДанныеЭЦП();
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщенияТокенНеПолучен);
				Возврат;
				
			КонецЕсли;
			
			ДанныеСтрокиАвторизации  = Новый Структура();
			ДанныеСтрокиАвторизации.Вставить("СтруктурнаяЕдиница", СтрокаТаблицы.СтруктурнаяЕдиница);
			ДанныеСтрокиАвторизации.Вставить("ПрофильИСМПТ", 	   СтрокаТаблицы.ПрофильИСМПТ);
			ДанныеСтрокиАвторизации.Вставить("ДатаНачалаСинхронизацииВходящихДокументовИСМПТ", СтрокаТаблицы.ДатаНачалаСинхронизацииИСМПТ);
			ДанныеСтрокиАвторизации.Вставить("ДатаОкончанияСинхронизацииИСМПТ", 			   СтрокаТаблицы.ДатаОкончанияСинхронизацииИСМПТ);
			ДанныеСтрокиАвторизации.Вставить("ТокенАвторизации",    ТокенАвторизации);
			//виды получаемых документов
			//	"Акты" - акты приема-передачи и уведомления о расхождении
			//	"УведомленияОВвозе" - уведомления о ввозе из ЕАЭС
			//	"УведомленияОВвозеИмпорт" - уведомления о ввозе из третьих стран
			//  "УведомлениеОбОтгрузкеЕАЭС"
			ДанныеСтрокиАвторизации.Вставить("ВидыДокументов", ВидыДокументов);
			
			МассивПрофилейИСМПТСДатойСинхронизации.Добавить(ДанныеСтрокиАвторизации);
			СтруктурныеЕдиницы.Вставить(СтрокаТаблицы.СтруктурнаяЕдиница, "");
			
		КонецЕсли;
	КонецЦикла;
	
	//Проверка заполненности формы получения данных.
	Если МассивПрофилейИСМПТСДатойСинхронизации.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Выберите хотя бы одну настройку для синхронизации с ИС МПТ.'");
		Сообщение.Поле  = "Таблица";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЕстьПроблемные = Ложь;
	Для Каждого Настройка Из МассивПрофилейИСМПТСДатойСинхронизации Цикл
		Если Настройка.ДатаНачалаСинхронизацииВходящихДокументовИСМПТ = Дата("00010101") Тогда
			ТекстСообщения = НСтр("ru = 'Укажите начальную дату периода синхронизации с ИС МПТ для структурной единицы %1.'");
			ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, Настройка.СтруктурнаяЕдиница); 
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Поле  = "Таблица";
			Сообщение.Сообщить();
			ЕстьПроблемные = Истина;
		КонецЕсли;
	КонецЦикла;
	Если ЕстьПроблемные Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура("МассивПрофилейИСМПТСДатойСинхронизации", МассивПрофилейИСМПТСДатойСинхронизации);
		
	СинхронизироватьИСМПТ(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизироватьИСМПТ(ДополнительныеПараметры) Экспорт 
	
	ДлительнаяОперация = СинхронизироватьДанные(УникальныйИдентификатор, ДополнительныеПараметры);
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ПослеСинхронизацииДанных(ДлительнаяОперация, Неопределено);
		Возврат
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСинхронизацииДанных", ЭтотОбъект);
	ПараметрыОжидания     = ОбщегоНазначенияИСМПТККлиентПереопределяемый.ПараметрыОжидания(ЭтаФорма); 
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.ТекстСообщения 	= РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияЗаголовокПолучениеДанныхССервераМПТ();
	
	ОбщегоНазначенияИСМПТККлиентПереопределяемый.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция СинхронизироватьДанные(Знач УникальныйИдентификатор, Знач ДополнительныеПараметры)
	
	НаименованиеЗадания = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияЗаголовокПолучениеДанныхССервераМПТ();
	ИмяМетода           = "ИнтеграцияИСМПТК.ПолучитьНовыеДокументыИСМПТ";
	ПараметрыВыполнения = ОбщегоНазначенияИСМПТКПереопределяемый.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ОбщегоНазначенияИСМПТКПереопределяемый.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода, ДополнительныеПараметры);
	
КонецФункции

&НаСервере
Функция СообщенияФоновогоЗадания(УникальныйИдентификатор)
	Возврат ОбщегоНазначенияИСМПТКПереопределяемый.СообщенияПользователю(Истина, УникальныйИдентификатор);
КонецФункции 

&НаКлиенте
Процедура ПослеСинхронизацииДанных(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда // Пользователь отменил задание.
		Возврат;
	КонецЕсли;

	Если Результат.Статус = "Ошибка" Тогда
		ТекстОшибки = Результат.ПодробноеПредставлениеОшибки;
		ИмяСобытия  = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаВФункцииСПараметром();
		ИмяСобытия  = СтрЗаменить(ИмяСобытия, "%ИмяФункции%", "Обработки.РабочиеМестаИСМПТК.Формы.РабочееМестоСинхронизацияСИСМПТ.ПослеСинхронизацииДанных");
		ОбщегоНазначенияИСМПТКВызовСервера.СоздатьЗаписьЖурналаРегистрации(ИмяСобытия, "Ошибка",,, ТекстОшибки);
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки + РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияПодробностиВЖурналеРегистрации();
	КонецЕсли;
	
	Сообщения = СообщенияФоновогоЗадания(УникальныйИдентификатор);
	
	Если Не Сообщения.Количество() = 0 Тогда 
		СообщениеПользователю = "";
		
		Для Каждого Сообщение Из Сообщения Цикл 
			СообщениеПользователю = СообщениеПользователю + Символы.ПС + Символы.ПС + Сообщение.Текст;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(СообщениеПользователю) Тогда 
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(СообщениеПользователю);
		КонецЕсли; 
	КонецЕсли;

	//оповестить формы списка
	ИнтеграцияИСМПТККлиент.ОповеститьФормы(РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияЗаписьАктПриемаПередачи());
	ИнтеграцияИСМПТККлиент.ОповеститьФормы(РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияЗаписьУведомлениеОРасхождении());
	ИнтеграцияИСМПТККлиент.ОповеститьФормы(РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияЗаписьУведомлениеОВвозеИзЕАЭС());
	ИнтеграцияИСМПТККлиент.ОповеститьФормы(РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияЗаписьУведомлениеОВвозеИзТретьихСтран());
	ИнтеграцияИСМПТККлиент.ОповеститьФормы(РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияЗаписьУведомлениеОбОтгрузкеЕАЭС());
	ИнтеграцияИСМПТККлиент.ОповеститьФормы(РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияСинхронизацияИСМПТ());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		СтрокаТаблицы.Пометка = Истина;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		СтрокаТаблицы.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьТаблицуПрофилейДляСинхронизации(ОрганизацияОтбор = Неопределено)
	
	// Запрос организован таким образом, чтобы сработал RLS.
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК СтруктурнаяЕдиница,
	|	Организации.Наименование
	|ПОМЕСТИТЬ СтруктурныеЕдиницы
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	| [ЗапросПоСтруктурнымЕдиницам]
	|/////////////////////////////////////
	|;
	|
	|ВЫБРАТЬ
	|	СтруктурныеЕдиницы.СтруктурнаяЕдиница,
	|	ЕСТЬNULL(ПараметрыМетодовИСМПТК.ЗначениеПараметра, &ДатаПоУмолчанию) КАК ДатаНачалаСинхронизацииИСМПТ
	|ИЗ
	|	СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыМетодовИСМПТК КАК ПараметрыМетодовИСМПТК
	|		ПО (СтруктурныеЕдиницы.СтруктурнаяЕдиница = ПараметрыМетодовИСМПТК.Организация
	|				И ПараметрыМетодовИСМПТК.ИмяМетода = &ИмяМетода
	|				И ПараметрыМетодовИСМПТК.СкладЦЭДМ = ЗНАЧЕНИЕ(Справочник.СкладыИСЦЭДМ.ПустаяСсылка)
	//|				И ПараметрыМетодовИСМПТК.Направление = &Направление
	|				И ПараметрыМетодовИСМПТК.ИмяПараметра = &ИмяПараметра)
	|
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтруктурныеЕдиницы.СтруктурнаяЕдиница.Наименование";
	
	// Не все конфигурации поддерживают структурные единицы.
	Если ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ИспользуютсяСтруктурныеПодразделения() Тогда
		ЗапросПоСтруктурнымЕдиницам = 
		"ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка,
		|	ПодразделенияОрганизаций.Наименование
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.ЯвляетсяСтруктурнымПодразделением";		
	Иначе
		ЗапросПоСтруктурнымЕдиницам = "";
	КонецЕсли;	
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "[ЗапросПоСтруктурнымЕдиницам]", ЗапросПоСтруктурнымЕдиницам);
	ДатаНачалаПоУмолчанию = Дата(2020, 1, 1, 0, 0, 0);
	ДатаОкончания = ТекущаяДатаСеанса();
	Запрос.УстановитьПараметр("ИмяМетода",		 ИнтеграцияИСМПТК.ИмяМетодаДляВидаДокументов(ВидыДокументов));
	Запрос.УстановитьПараметр("ИмяПараметра", 	 Перечисления.ИменаПараметровИСМПТК.СоздаватьСДаты);
	//Запрос.УстановитьПараметр("Направление", 	 Перечисления.НаправленияДокументовИСМПТК.ПустаяСсылка());
	Запрос.УстановитьПараметр("ДатаПоУмолчанию", ДатаНачалаПоУмолчанию);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НайденныеСтроки = Таблица.НайтиСтроки(Новый Структура("СтруктурнаяЕдиница", Выборка.СтруктурнаяЕдиница));
		Если НайденныеСтроки.Количество() = 0 Тогда 
			СтрокаТаблицы = Таблица.Добавить();
		Иначе
			СтрокаТаблицы = НайденныеСтроки[0];
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		СтрокаТаблицы.ДатаОкончанияСинхронизацииИСМПТ = ДатаОкончания;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОрганизацияОтбор) Тогда
		Если ЭтотОбъект.Таблица.Количество() = 0 Тогда
			//Предзаполняем таблицу 
			НоваяСтрока = ЭтотОбъект.Таблица.Добавить();
			НоваяСтрока.Пометка = Истина;
			НоваяСтрока.СтруктурнаяЕдиница = ОрганизацияОтбор;
			НоваяСтрока.ДатаОкончанияСинхронизацииИСМПТ = ДатаОкончания;
		Иначе
			НайденныеСтроки = ЭтотОбъект.Таблица.НайтиСтроки(Новый Структура("СтруктурнаяЕдиница", ОрганизацияОтбор));
			Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
				СтрокаТаблицы.Пометка = Истина;
			КонецЦикла;
			Если НайденныеСтроки.Количество() = 0 Тогда
				НоваяСтрока = ЭтотОбъект.Таблица.Добавить();
				НоваяСтрока.Пометка = Истина;
				НоваяСтрока.СтруктурнаяЕдиница = ОрганизацияОтбор;
				НоваяСтрока.ДатаОкончанияСинхронизацииИСМПТ = ДатаОкончания;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЭтотОбъект.Таблица.Количество() = 1 Тогда
			ЭтотОбъект.Таблица[0].Пометка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуПрофилейДляСинхронизации()
	
	МассивПрофилейИСМПТ = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Если СтрокаТаблицы.Пометка Тогда
			МассивПрофилейИСМПТ.Добавить(СтрокаТаблицы.ПрофильИСМПТ);				
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьТаблицуПрофилейДляСинхронизации();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = РаботаСДокументамиИСМПТККлиентСервер.ИмяСобытияСинхронизацияИСМПТ() Тогда
		ОбновитьТаблицуПрофилейДляСинхронизации(); // Обновление даты синхронизации по результататм работы события
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	ТекстЗаголовка = НСтр("ru='Получение с сервера документов ""%НаименованиеСписка%"".'");
	
	Элементы.ДекорацияПояснение.Видимость = Истина;
	Если ВидыДокументов = "Акты" Тогда
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%НаименованиеСписка%", 
					   Метаданные.Документы.АктПриемаПередачиИСМПТК.ПредставлениеСписка + ", """ 
					   + Метаданные.Документы.УведомлениеОРасхожденииИСМПТК.ПредставлениеСписка + """");
	ИначеЕсли ВидыДокументов = "УведомленияОВвозе" Тогда
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%НаименованиеСписка%", Метаданные.Документы.УведомлениеОВвозеИзЕАЭСИСМПТК.ПредставлениеСписка);
	ИначеЕсли ВидыДокументов = "УведомленияОВвозеИмпорт" Тогда
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%НаименованиеСписка%", Метаданные.Документы.УведомлениеОВвозеИзТретьихСтранИСМПТК.ПредставлениеСписка);
	ИначеЕсли ВидыДокументов = "УведомленияОбОтгрузке" Тогда
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%НаименованиеСписка%", Метаданные.Документы.УведомлениеОбОтгрузкеЕАЭСИСМПТК.ПредставлениеСписка);
	Иначе
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%НаименованиеСписка%", РаботаСТекстамиИСМПТККлиентСервер.НаименованиеИСМПТ());
	КонецЕсли;

	Элементы.ДекорацияПояснение.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры	

&НаКлиенте
Асинх Функция СоздатьЭЦП_CMS_NCA_Layer(Знач ДанныеXML, ТипВходящихДанных, Знач ВключатьДанныеВПодпись) Экспорт
	
	Возврат Ждать ИнтеграцияИСМПТККлиент.СоздатьЭЦП_CMS_NCA_Layer(ДанныеXML, ТипВходящихДанных, ВключатьДанныеВПодпись);

КонецФункции

&НаКлиенте
Функция ПолучитьТипДокумента()
	Если ВидыДокументов = "Акты" Тогда
		Возврат ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСМПТК();
	ИначеЕсли ВидыДокументов = "УведомленияОВвозе" Тогда
		Возврат ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК();
	ИначеЕсли ВидыДокументов = "УведомленияОВвозеИмпорт" Тогда
		Возврат ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСМПТК();
	ИначеЕсли ВидыДокументов = "УведомленияОбОтгрузке" Тогда
		Возврат ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбОтгрузкеЕАЭСИСМПТК();
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции