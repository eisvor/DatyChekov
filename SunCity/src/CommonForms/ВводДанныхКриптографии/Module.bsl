#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПровайдерКриптографии = КриптографияВызовСервера.ПолучитьТипПровайдераКриптографии();
	
	ДанныеЭЦП = КриптографияКлиентСервер.ПолучитьПараметрыДанныхЭЦП();
	
	КлючИмя = НСтр("ru = 'Выбрать сертификат...'");
	
	ЗаполнитьДанныеЭЦП();
	
	УстановитьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КлючИмяНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'ЭЦП (*.p12)|*.p12'");
	ДиалогОткрытияФайла.Заголовок = "Выбор сертификата ЭЦП";
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		ОбработкаВыбораКлючаЗавершение(Истина, , ДиалогОткрытияФайла.ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораКлючаЗавершение(Результат, Адрес, ВыбранноеИмяФайла = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ИнформацияПоФайлу = ИнформацияПоФайлу(ВыбранноеИмяФайла);
	
	Если ВРег(ИнформацияПоФайлу.Расширение) <> ".P12" Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Файл должен иметь расширение ""*.p12"".'"));
		Возврат;
	КонецЕсли;
	
	ДанныеЭЦП.ИмяФайлаКлюча = ИнформацияПоФайлу.Имя;
	ДанныеЭЦП.ПолныйПутьКлюча = ИнформацияПоФайлу.ПолноеИмя;
	ДанныеЭЦП.ПутьКлюча = ИнформацияПоФайлу.Путь;
	
	КлючИмя = ИнформацияПоФайлу.Имя;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ЗначениеЗаполнено(ДанныеЭЦП.ИмяФайлаКлюча) Тогда
		КриптографияКлиентСерверПереопределяемый.СообщитьПользователю(НСтр("ru = 'Укажите путь к сертификату'"),,"КлючИмя");
		Возврат;
	КонецЕсли;
	
	Если ПровайдерКриптографии = ПредопределенноеЗначение("Перечисление.ТипПровайдераКриптографии.Kalkan")
		И ПустаяСтрока(Пароль) Тогда
		КриптографияКлиентСерверПереопределяемый.СообщитьПользователю(НСтр("ru = 'Укажите пароль сертификата'"),,"Пароль");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПарольЭЦП();
	
	Закрыть(ДанныеЭЦП);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПароль(Команда)
	
	ПереключитьРежимПароля("Пароль", "ПоказыватьПароль");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИнформацияПоФайлу(Знач ПолноеИмяФайла) Экспорт
	
	Файл = Новый Файл(ПолноеИмяФайла);
	
	Структура = Новый Структура;
	Структура.Вставить("Имя", Файл.Имя);
	Структура.Вставить("ИмяБезРасширения", Файл.ИмяБезРасширения);
	Структура.Вставить("ПолноеИмя", Файл.ПолноеИмя);
	Структура.Вставить("Путь", Файл.Путь);
	Структура.Вставить("Расширение", Файл.Расширение);
	
	Возврат Структура;
	
КонецФункции 

&НаСервере
Процедура ЗаполнитьДанныеЭЦП()
	Если ПровайдерКриптографии = Перечисления.ТипПровайдераКриптографии.Kalkan Тогда
		ДанныеЭЦП.ПарольКлюча      = Пароль;
		ДанныеЭЦП.ПровайдерСсылка  = Перечисления.ТипПровайдераКриптографии.Kalkan;
		ДанныеЭЦП.ПровайдерСтрокой = "KALKAN";
	ИначеЕсли ПровайдерКриптографии = Перечисления.ТипПровайдераКриптографии.NCALayer Тогда
		ДанныеЭЦП.ПровайдерСсылка  = Перечисления.ТипПровайдераКриптографии.NCALayer;
		ДанныеЭЦП.ПровайдерСтрокой = "NCALAYER";
	Иначе
		ДанныеЭЦП.ПровайдерСсылка  = Перечисления.ТипПровайдераКриптографии.ПустаяСсылка();
		ДанныеЭЦП.ПровайдерСтрокой = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПарольЭЦП()
	Если ПровайдерКриптографии = Перечисления.ТипПровайдераКриптографии.Kalkan Тогда
		ДанныеЭЦП.ПарольКлюча      = Пароль;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	Элементы.НадписьПарольНеИспользуется.Видимость              = ПровайдерКриптографии = Перечисления.ТипПровайдераКриптографии.NCALayer;
	Элементы.НадписьПровайдерКриптографииНеВыбран.Видимость     = ПровайдерКриптографии = Перечисления.ТипПровайдераКриптографии.ПустаяСсылка();
	Элементы.ДанныеКлюча.Доступность                            = НЕ ПровайдерКриптографии = Перечисления.ТипПровайдераКриптографии.ПустаяСсылка();
	Элементы.ОК.Доступность                                     = НЕ ПровайдерКриптографии = Перечисления.ТипПровайдераКриптографии.ПустаяСсылка();
	Элементы.ГруппаПароль.Видимость                             = НЕ Элементы.НадписьПарольНеИспользуется.Видимость;
КонецПроцедуры

&НаСервере
Процедура ПереключитьРежимПароля(ИмяЭлементаВвода, ИмяЭлементаПереключателя)
	
	Если Элементы.Найти(ИмяЭлементаВвода) <> Неопределено Тогда
		Элементы[ИмяЭлементаВвода].РежимПароля = НЕ Элементы[ИмяЭлементаВвода].РежимПароля;
		
		Если Элементы.Найти(ИмяЭлементаПереключателя) <> Неопределено Тогда
			
			Элементы[ИмяЭлементаПереключателя].Картинка = ?(Элементы[ИмяЭлементаВвода].РежимПароля,
				БиблиотекаКартинок.СкрытьПароль,
				БиблиотекаКартинок.ПоказатьПароль);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
