#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПоказыватьБонусы") Тогда
		ОтображатьБонусы = Параметры.ПоказыватьБонусы;
	КонецЕсли;
	
	Если Параметры.Свойство("Контрагент") Тогда
		ВладелецКарты = Параметры.Контрагент;
	КонецЕсли;
		
	Если Параметры.Свойство("ЭтоФормаЧека") Тогда
		ЭтоФормаЧека = Параметры.ЭтоФормаЧека;
	КонецЕсли;
	
	Если Параметры.Свойство("МассивДисконтныхКарт") Тогда
		Для Каждого СтрокаМассива Из Параметры.МассивДисконтныхКарт Цикл
			НоваяСтрока = ТаблицаДанных.Добавить();
			Если ТипЗнч(СтрокаМассива.ДисконтнаяКарта) = Тип("Строка") Тогда
				НоваяСтрока.ДисконтнаяКартаПредставление = СтрокаМассива.ДисконтнаяКарта;
			Иначе
				НоваяСтрока.ДисконтнаяКартаПредставление = Строка(СтрокаМассива.ДисконтнаяКарта);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМассива);
			ДополнитьДанныеКарты(НоваяСтрока, СтрокаМассива);
			НоваяСтрока.ОстатокБалловРуб = НоваяСтрока.ОстатокБонусныхБаллов * ?(НоваяСтрока.КурсБонусов = 0, 1, НоваяСтрока.КурсБонусов);
		КонецЦикла;
		
		ОбщийОстатокБаллов = ТаблицаДанных.Итог("ОстатокБонусныхБаллов");
		ОбщийОстатокБалловРуб = ТаблицаДанных.Итог("ОстатокБалловРуб");
		ТаблицаДанных.Сортировать("ОстатокБонусныхБаллов Убыв");
	КонецЕсли;
	
	Если Параметры.Свойство("НомерВыбраннойКарты") Тогда
		НомерВыбраннойКарты = Параметры.НомерВыбраннойКарты;
	КонецЕсли;
	
	Если Параметры.Свойство("СуммаДокумента") Тогда
		СуммаДокумента = Параметры.СуммаДокумента;
	КонецЕсли;
	
	Если Параметры.Свойство("ГорячиеКлавиши") Тогда
		ЗаполнитьГорячиеКлавиши(Параметры.ГорячиеКлавиши);
	КонецЕсли;
	ЗаполнитьПервоначальныеНастройки();
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьНастройкиФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы
&НаКлиенте
Процедура ТаблицаДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ОбработатьВыборСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКПоиску(Команда)
	МассивВыбранныхКарт = Новый Массив;
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("РеквизитыДисконтнойКарты", Неопределено);
	СтруктураВозврата.Вставить("ПерейтиКПоиску", Истина);
	СтруктураВозврата.Вставить("МассивВыбранныхКарт", Новый Массив);
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКарту(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаДанных.ТекущиеДанные;
	
	СтрокиТаблицы = ТаблицаДанных.НайтиСтроки(Новый Структура("Отметка", Истина));
	Если СтрокиТаблицы.Количество() > 0 Тогда
		СтрокаНачисления = СтрокиТаблицы[0];
	КонецЕсли;
	
	Если НЕ СтрокаНачисления = ТекущиеДанные
		И ОтображатьБонусы Тогда
		ОбработатьВыборСтроки(СтрокаНачисления, ТекущиеДанные);
	Иначе
		ОбработатьВыборСтроки(ТекущиеДанные);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОсновнуюКарту(Команда)
	ТекущаяСтрока = Элементы.ТаблицаДанных.ТекущиеДанные;
	Если ТекущаяСтрока.КартаНедействительна Тогда
		ТекстСообщения = НСтр("ru = 'Дисконтная карта %1 недействительна'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекущаяСтрока.ДисконтнаяКартаПредставление);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	Для Каждого СтрокаТовары Из ТаблицаДанных Цикл
		СтрокаТовары.Отметка = Ложь;
	КонецЦикла;
	ТекущаяСтрока.Отметка = Истина;
	ТаблицаДанных.Сортировать("Отметка Убыв");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	СтрокиТаблицы = ТаблицаДанных.НайтиСтроки(Новый Структура("Отметка", Истина));
	СписатьБаллыПоВладельцу = Истина;
	Если СтрокиТаблицы.Количество() > 0 Тогда
		ОбработатьВыборСтроки(СтрокиТаблицы[0]);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъединитьИВыбрать(Команда)
	СтрокиТаблицы = ТаблицаДанных.НайтиСтроки(Новый Структура("Отметка", Истина));
	ОбъединитьКарты = Истина;
	Если СтрокиТаблицы.Количество() > 0 Тогда
		ОбработатьВыборСтроки(СтрокиТаблицы[0]);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДополнитьДанныеКарты(СтрокаПриемник, СтрокаИсточник)
	
	ОбщегоНазначенияРМКПереопределяемый.ДополнитьДанныеОКарте(СтрокаПриемник, СтрокаИсточник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультат(РеквизитыДисконтнойКарты, СтрокаСписания = Неопределено)
	МассивВыбранныхКарт = Новый Массив;
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("РеквизитыДисконтнойКарты", РеквизитыДисконтнойКарты);
	СтруктураВозврата.Вставить("ПерейтиКПоиску", Ложь);
	
	ЗаполнитьМассив = СписатьБаллыПоВладельцу Или ОбъединитьКарты;
	
	Если ЗаполнитьМассив Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
			Если СтрокаТаблицы.ОстатокБонусныхБаллов > 0
				И НЕ СтрокаТаблицы.КартаНедействительна Тогда
				СтруктураСтрокиКарты = ЗаполнитьДанныеСтрокиМассива(СтрокаТаблицы);
				МассивВыбранныхКарт.Добавить(СтруктураСтрокиКарты);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не СтрокаСписания = Неопределено Тогда
		РеквизитыДисконтнойКарты.СписатьБаллыПоВладельцу = Истина;
		СтруктураСтрокиКарты = ЗаполнитьДанныеСтрокиМассива(СтрокаСписания);
		МассивВыбранныхКарт.Добавить(СтруктураСтрокиКарты);
	КонецЕсли;
	СтруктураВозврата.Вставить("МассивВыбранныхКарт", МассивВыбранныхКарт);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьДанныеСтрокиМассива(СтрокаТаблицы)
	СтруктураСтрокиКарты = Новый Структура;
	СтруктураСтрокиКарты.Вставить("ДисконтнаяКарта", СтрокаТаблицы.ДисконтнаяКарта);
	СтруктураСтрокиКарты.Вставить("ИдентификаторКарты", СтрокаТаблицы.ИдентификаторКарты);
	СтруктураСтрокиКарты.Вставить("КодКарты", СтрокаТаблицы.КодКарты);
	СтруктураСтрокиКарты.Вставить("ОстатокБонусныхБаллов", СтрокаТаблицы.ОстатокБонусныхБаллов);
	СтруктураСтрокиКарты.Вставить("КурсБонусов", СтрокаТаблицы.КурсБонусов);
	Возврат СтруктураСтрокиКарты;
КонецФункции

&НаКлиенте
Процедура УстановитьНастройкиФормы()
	
	ЗаголовокФормыТекст = НСтр("ru = 'Список дисконтных карт по контрагенту %1'");
	ЗаголовокФормыТекст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокФормыТекст, ВладелецКарты);
	ЭтаФорма.Заголовок = ЗаголовокФормыТекст;
	Элементы.ТаблицаДанныхОстатокБонусныхБаллов.Видимость = ОтображатьБонусы;
	Элементы.ТаблицаДанныхОстатокБалловРуб.Видимость = ОтображатьБонусы;
	Элементы.ГруппаКомандБонусы.Видимость = ОтображатьБонусы;
	Элементы.ТаблицаДанныхОтметка.Видимость = ОтображатьБонусы;
	Элементы.ФормаПерейтиКПоиску.Видимость = ЭтоФормаЧека;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеСписок = Элемент.Поля.Элементы.Добавить();
	ПолеСписок.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаДанных.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДанных.КартаНедействительна");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборСтроки(ДанныеСтроки, СтрокаСписания = Неопределено)
	
	Если ДанныеСтроки.КартаНедействительна Тогда
		ТекстСообщения = НСтр("ru = 'Дисконтная карта %1 недействительна'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДанныеСтроки.ДисконтнаяКартаПредставление);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	РеквизитыДисконтнойКарты = Новый Структура;
	РеквизитыДисконтнойКарты.Вставить("ПродажиОборот", ДанныеСтроки.ПродажиОборот);
	РеквизитыДисконтнойКарты.Вставить("КартаНедействительна", ДанныеСтроки.КартаНедействительна);
	РеквизитыДисконтнойКарты.Вставить("ДатаРождения", ДанныеСтроки.ДатаРождения);
	РеквизитыДисконтнойКарты.Вставить("КурсБонусов", ДанныеСтроки.КурсБонусов);
	РеквизитыДисконтнойКарты.Вставить("Карта", ДанныеСтроки.ДисконтнаяКарта);
	РеквизитыДисконтнойКарты.Вставить("СписатьБаллыПоВладельцу",СписатьБаллыПоВладельцу);
	РеквизитыДисконтнойКарты.Вставить("ОбъединитьКарты",ОбъединитьКарты);
	РеквизитыДисконтнойКарты.Вставить("НомерКарты", ДанныеСтроки.КодКарты);
	Если СтрокаСписания = Неопределено Тогда
		РеквизитыДисконтнойКарты.Вставить("БонуснаяПрограмма",ДанныеСтроки.БонуснаяПрограмма);
		РеквизитыДисконтнойКарты.Вставить("МаксимальныйПроцентОплаты",ДанныеСтроки.МаксимальныйПроцентОплаты);
	Иначе
		РеквизитыДисконтнойКарты.Вставить("БонуснаяПрограмма",СтрокаСписания.БонуснаяПрограмма);
		РеквизитыДисконтнойКарты.Вставить("МаксимальныйПроцентОплаты",СтрокаСписания.МаксимальныйПроцентОплаты);
	КонецЕсли;
	РеквизитыДисконтнойКарты.Вставить("ДополнительныеДанные",ДанныеСтроки.ДополнительныеДанные);
	РеквизитыДисконтнойКарты.Вставить("ИдентификаторКарты",ДанныеСтроки.ИдентификаторКарты);
	РеквизитыДисконтнойКарты.Вставить("ОстатокБонусныхБаллов",ДанныеСтроки.ОстатокБонусныхБаллов);
	ОбработатьРезультат(РеквизитыДисконтнойКарты, СтрокаСписания);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПервоначальныеНастройки()
	КартаНайдена = Ложь;
	Если ЗначениеЗаполнено(НомерВыбраннойКарты) Тогда
		Для Каждого СтрокаТовары Из ТаблицаДанных Цикл
			Если СтрокаТовары.КодКарты = НомерВыбраннойКарты
				И НЕ СтрокаТовары.КартаНедействительна Тогда
				КартаНайдена = Истина;
				СтрокаТовары.Отметка = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если Не КартаНайдена
		И ТаблицаДанных.Количество() > 0 Тогда
		ТаблицаДанных[0].Отметка = Истина;
	КонецЕсли;
	ТаблицаДанных.Сортировать("Отметка Убыв");
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГорячиеКлавиши(ГорячиеКлавиши)
	Для Каждого Команда Из ГорячиеКлавиши Цикл
		КомандаНаФорме = Элементы.Найти(Команда.Представление);
		Если НЕ КомандаНаФорме = Неопределено Тогда
			ТекстСочетания = ВернутьТекстСочетанияКлавиш(Команда.Значение);
			Если Не ПустаяСтрока(ТекстСочетания) Тогда
				ЗаголовокНовый = КомандаНаФорме.Заголовок + Символы.ПС + ТекстСочетания;
				КомандаНаФорме.Заголовок = ЗаголовокНовый;
				КомандаНаФорме.СочетаниеКлавиш = Команда.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ВернутьТекстСочетанияКлавиш(СочетаниеКлавиш)
	СочетаниеКлавишAlt = ?(СочетаниеКлавиш.Alt, "Alt + ", "");
	СочетаниеКлавишCtrl = ?(СочетаниеКлавиш.Ctrl, "Ctrl + ", "");
	СочетаниеКлавишShift = ?(СочетаниеКлавиш.Shift, "Shift + ", "");
	СочетаниеКлавишКлавиша = Строка(СочетаниеКлавиш.Клавиша);
	
	ТекстСочетания = СочетаниеКлавишAlt + СочетаниеКлавишCtrl + СочетаниеКлавишShift + СочетаниеКлавишКлавиша;
	
	Если ТекстСочетания = "Нет" Тогда
		ТекстСочетания = "";
	КонецЕсли;
	
	Возврат ТекстСочетания;
КонецФункции
#КонецОбласти
