
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("СсылкиНаДокументы") Тогда
		ТекстИсключения = НСтр("ru = 'Форма открывается из документов установки цен.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	СсылкиНаДокументы = Параметры.СсылкиНаДокументы;
	Если НЕ ТипЗнч(СсылкиНаДокументы[0]) = Тип("ДокументСсылка.ИзменениеАссортимента") Тогда
		Элементы.ФорматМагазинов.Видимость = Ложь;
	Иначе
		ИзменениеАссортимента = Истина;
		Если СсылкиНаДокументы.Количество() = 1 Тогда
			ФорматМагазинов = СсылкиНаДокументы[0].ОбъектПланирования;
		КонецЕсли;
		Элементы.Магазин.ТолькоПросмотр = НЕ ЗначениеЗаполнено(ФорматМагазинов);
	КонецЕсли;
	
	ИнициализироватьФорму();
	
	Элементы.Склад.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Магазин);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматМагазиновПриИзменении(Элемент)
	
	Элементы.Магазин.ТолькоПросмотр = НЕ ЗначениеЗаполнено(ФорматМагазинов);
	
КонецПроцедуры

&НаКлиенте
Процедура МагазинПриИзменении(Элемент)
	
	Элементы.Склад.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Магазин);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	ЕстьОшибки = Ложь;
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнено поле ""Организация""'"),,"Организация",,ЕстьОшибки);
	КонецЕсли;
	
	Если Не ЕстьОшибки Тогда
		
		ПараметрыВыбора = Новый Структура;
		ПараметрыВыбора.Вставить("Организация", Организация);
		ПараметрыВыбора.Вставить("Магазин", Магазин);
		ПараметрыВыбора.Вставить("Склад", Склад);
		ПараметрыВыбора.Вставить("ФорматМагазинов", ФорматМагазинов);
		
		Закрыть(ПараметрыВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьФорму()
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация   = ЗначениеНастроекПовтИсп.ОрганизацияПоУмолчанию(Организация, Ответственный);
	
	Если ИзменениеАссортимента Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Магазин.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
	
	Склад         = ЗначениеНастроекПовтИсп.СкладПоступленияПоУмолчанию(Магазин, ,Склад, Ответственный);
	Магазин       = ЗначениеНастроекПовтИсп.МагазинПоУмолчанию(Магазин);
	
	Если ЗначениеЗаполнено(Магазин) И НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = Магазин.СкладПоступления;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) И НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Склад.Организация;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
