
////////////////////////////////////////////////////////////////////////////////
// Форма предназначена для замены стандартной формы выбора объекта для случаев,
// когда эта форма не может быть использована по назначению.
// Параметры открытия формы:
//  МножественныйВыбор - Булево - Определяет возможность выбора нескольких значений
//  ЗаголовокФормы - Строка - 
//  ЗаголовокКолонкиТаблицы - Строка - 
//  ЗначениеВыбораДляЗапроса - Строка - Определяет объект для запроса данных, формирующих список выбора
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ЗначениеВыбораДляЗапроса") Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствуют обязательные параметры открытия формы! Невозможно открыть форму выбора значения.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;	
	
	ЭтаФорма.Заголовок = Параметры.ЗаголовокФормы;
	Элементы.ТаблицаЗначенийВыбораЗначениеСсылка.Заголовок = Параметры.ЗаголовокКолонкиТаблицы;
	МножественныйВыбор = Параметры.МножественныйВыбор;
	Элементы.ТаблицаЗначенийВыбора.РежимВыделения = ?(МножественныйВыбор, РежимВыделенияТаблицы.Множественный, РежимВыделенияТаблицы.Одиночный); 
	
	ИсточникДанных = Параметры.ЗначениеВыбораДляЗапроса;
	Если ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
	    Запрос = Новый Запрос();
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ОбъектЗапроса.Ссылка КАК ЗначениеСсылка
		|ИЗ
		|	%ИмяОбъектаВыбора% КАК ОбъектЗапроса
		|";
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяОбъектаВыбора%", ИсточникДанных);
		ТекстЗапроса = РаботаСДокументамиИСМПТКПереопределяемый.ДополнитьТекстЗапросаПереопределяемымиУсловиями(ТекстЗапроса);
		Запрос.Текст = ТекстЗапроса;
		ТаблицаПолученныхДанных = Запрос.Выполнить().Выгрузить();
		ТаблицаЗначенийВыбора.Загрузить(ТаблицаПолученныхДанных);
	Иначе
		//Склады контрагента ЦЭДМ
		ТаблицаПолученныхДанных = Новый ТаблицаЗначений();
		ТаблицаПолученныхДанных.Колонки.Добавить("ЗначениеСсылка", ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаСтрока(0), НСтр("ru = 'Склад ЦЭДМ (ТРП)'"));
		Для Каждого ЭлементМассива Из ИсточникДанных Цикл
			Если СтрДлина(ЭлементМассива.bin) > 12 Тогда
				НоваяСтрока = ТаблицаПолученныхДанных.Добавить();
				НоваяСтрока.ЗначениеСсылка = ЭлементМассива.bin + ", " + ?(ЗначениеЗаполнено(ЭлементМассива.nameRu), ЭлементМассива.nameRu, ЭлементМассива.nameKz);
			КонецЕсли;
		КонецЦикла;
		ТаблицаЗначенийВыбора.Загрузить(ТаблицаПолученныхДанных);
	КонецЕсли;

	СобытияФормИСМПТКПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ТаблицаЗначенийВыбораИспользование.Видимость = МножественныйВыбор;
	Элементы.ГруппаФлажки.Видимость = МножественныйВыбор;
	
	СобытияФормИСМПТККлиентПереопределяемый.ПриОткрытии(ЭтаФорма, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)

	ОбработатьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ТаблицаЗначенийВыбора Цикл
		СтрокаТЧ.Использование = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ТаблицаЗначенийВыбора Цикл
		СтрокаТЧ.Использование = ложь;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаЗначенийВыбораВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ОбработатьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбор()
	
	Если МножественныйВыбор Тогда
		ВыбранныеДанные = Новый Массив();
		Для Каждого СтрокаТЧ Из ТаблицаЗначенийВыбора Цикл
			Если СтрокаТЧ.Использование Тогда
				ВыбранныеДанные.Добавить(СтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
		Закрыть(ВыбранныеДанные);
	Иначе
		Если Не Элементы.ТаблицаЗначенийВыбора.ТекущиеДанные = Неопределено Тогда
			Закрыть(Элементы.ТаблицаЗначенийВыбора.ТекущиеДанные.ЗначениеСсылка);
		Иначе
			Закрыть(Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти