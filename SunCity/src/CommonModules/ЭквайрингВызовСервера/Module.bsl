#Область СлужебныйПрограммныйИнтерфейс

// Возвращает процент торговой уступки для переданных
// договора эквайринга и вида оплаты - платежная карта.
//
// Параметры:
//  ЭквайринговыйТерминал - СправочникСсылка.ПодключаемоеОборудование - эквайринговый терминал, по которому нужно получить % торговой уступки.
//  ВидОплаты - СправочникСсылка.ВидыОплаты - вид оплаты, для которого  нужно получить % торговой уступки.
//
// Возвращаемое значение:
//  Число - процент торговой уступки. 0 - если не найден.
//
Функция ПолучитьПроцентКомиссииДляПлатежнойКарты(ЭквайринговыйТерминал, ВидОплаты, ЭтоВозврат = Ложь, ЭтоВозвратДеньВДень = Ложь, ЭтоПолныйВозврат = Ложь) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Тарифы.ПроцентКомиссииПриПродаже КАК ПроцентКомиссииПриПродаже,
	|	Тарифы.ПроцентКомиссииПриВозврате КАК ПроцентКомиссииПриВозврате,
	|	Тарифы.ПроцентКомиссииПриОтмене КАК ПроцентКомиссииПриОтмене
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы.ТарифыЗаРасчетноеОбслуживание КАК Тарифы
	|ГДЕ
	|	Тарифы.Ссылка = &ЭквайринговыйТерминал
	|	И (Тарифы.ВидОплаты = &ВидОплаты
	|			ИЛИ Тарифы.ВидОплаты = ЗНАЧЕНИЕ(Справочник.ВидыОплатЧекаККМ.ПустаяСсылка))");
		
	Запрос.УстановитьПараметр("ЭквайринговыйТерминал", ЭквайринговыйТерминал);
	Запрос.УстановитьПараметр("ВидОплаты", ВидОплаты);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЭтоВозврат Тогда
			Если ЭтоВозвратДеньВДень И ЭтоПолныйВозврат Тогда
				ПроцентКомиссии = Выборка.ПроцентКомиссииПриОтмене;
			Иначе
				ПроцентКомиссии = Выборка.ПроцентКомиссииПриВозврате;
			КонецЕсли; 
		Иначе
			ПроцентКомиссии = Выборка.ПроцентКомиссииПриПродаже;
		КонецЕсли;
	Иначе
		ПроцентКомиссии = 0;
	КонецЕсли;
	
	Возврат ПроцентКомиссии;
	
КонецФункции

// По виду оплаты заполняет банка-кредитора и процент комиссии.
//
// Параметры:
//  ВидОплаты                 - СправочникСсылка.ВидыОплат - вид оплаты.
//  БанкКредитор              - СправочникСсылка.Контрагенты - банк кредитор.
//  ПроцентБанковскойКомиссии - Число - процент комиссии банка.
//
Процедура ЗаполнитьБанкиПроцентКомиссии(ВидОплаты, БанкКредитор, ПроцентБанковскойКомиссии) Экспорт

	БанкКредитор              = ВидОплаты.БанкКредитор;
	ПроцентБанковскойКомиссии = ВидОплаты.ПроцентБанковскойКомиссии;

КонецПроцедуры // ЗаполнитьБанкиПроцентКомиссии()

// Возвращает процент торговой уступки.
//
// Параметры:
//  ТерминалЭквайринга - СправочникСсылка.ПодключаемоеОборудование - эквайринговый терминал, по которому нужно получить % торговой уступки.
//  ВидОплаты - СправочникСсылка.ВидыОплат - вид оплаты, для которого  нужно получить % торговой уступки.
//  ЭтоВозврат - Булево - признак возврата.
//  ЭтоВозвратДеньВДень - Булево - признак возврата день в день.
//
// Возвращаемое значение:
//  Число - процент торговой уступки. 0 - если не найден.
//
Функция ПолучитьПроцентКомиссииПоТерминалу(ТерминалЭквайринга, ВидОплаты, ЭтоВозврат = Ложь, ЭтоВозвратДеньВДень = Ложь, ЭтоПолныйВозврат = Ложь) Экспорт
	
	Возврат ПолучитьПроцентКомиссии(ТерминалЭквайринга, ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
	
КонецФункции // ПолучитьПроцентТорговойНаценки()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает процент торговой уступки для переданных
// вида оплаты - банковский кредит.
//
// Параметры:
//  ВидОплаты - СправочникСсылка.ВидыОплат - вид оплаты, для которого  нужно получить % торговой уступки.
//
// Возвращаемое значение:
//  Число - процент торговой уступки. 0 - если не найден.
//
Функция ПолучитьПроцентКомиссииБанковскийКредит(ВидОплаты) Экспорт

	Возврат ВидОплаты.ПроцентБанковскойКомиссии;

КонецФункции // ПолучитьПроцентКомиссииБанковскийКредит()

// Функция возвращает процент торговой уступки.
//
// Параметры:
//  ТерминалЭквайринга - СправочникСсылка.ПодключаемоеОборудование - эквайринговый терминал, по которому нужно получить % торговой уступки.
//  ВидОплаты - СправочникСсылка.ВидыОплат - вид оплаты, для которого  нужно получить % торговой уступки.
//
// Возвращаемое значение:
//  Число - процент торговой уступки. 0 - если не найден.
//
Функция ПолучитьПроцентКомиссии(ТерминалЭквайринга, ВидОплаты, ЭтоВозврат = Ложь, ЭтоВозвратДеньВДень = Ложь, ЭтоПолныйВозврат = Ложь) Экспорт
	
	ТипОплаты = ВидОплаты.ТипОплаты;
	
	Если ТипОплаты = Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта Тогда
		
		Возврат ПолучитьПроцентКомиссииДляПлатежнойКарты(ТерминалЭквайринга, ВидОплаты, ЭтоВозврат, ЭтоВозвратДеньВДень, ЭтоПолныйВозврат);
		
	ИначеЕсли ТипОплаты = Перечисления.ТипыОплатЧекаККМ.БанковскийКредит Тогда
		
		Возврат ПолучитьПроцентКомиссииБанковскийКредит(ВидОплаты);
		
	Иначе
		
		Возврат 0;
		
	КонецЕсли;
	
КонецФункции // ПолучитьПроцентТорговойНаценки()

#КонецОбласти

