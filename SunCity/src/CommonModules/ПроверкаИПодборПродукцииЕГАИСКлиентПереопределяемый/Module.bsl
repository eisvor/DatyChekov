#Область СлужебныйПрограммныйИнтерфейс

// Заполняет специфичные параметры открытия формы проверки и подбора алкогольной продукции в зависимости от точки вызова
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма из которой происходит открытие формы проверки и подбора
//  Параметры - См. ИнтеграцияЕГАИСКлиент.ПараметрыПроверкиИПодбораАлкогольнойПродукции
Процедура  ПриУстановкеПараметровОткрытияФормыПроверкиИПодбора(Форма, Параметры) Экспорт
	
	Если Форма.ИмяФормы = "Обработка.РМКУправляемыйРежим.Форма.Форма"
		Или Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПеремещениеТоваров.Форма.ФормаДокумента" Тогда
		
		Если Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента" Тогда
			
			Параметры.ОрганизацияЕГАИС = ИнтеграцияЕГАИСРТВызовСервера.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
				Форма.Объект.Организация,
				Форма.Объект.Магазин);
				
			Параметры.ЧастичноеВыбытиеДоступно = Истина;
			
		ИначеЕсли Форма.ИмяФормы = "Документ.ПеремещениеТоваров.Форма.ФормаДокумента" Тогда
			
			Параметры.ОрганизацияЕГАИС = ИнтеграцияЕГАИСРТВызовСервера.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
				Форма.Объект.Организация,
				Форма.Объект.МагазинОтправитель);
		ИначеЕсли Форма.ИмяФормы = "Обработка.РМКУправляемыйРежим.Форма.Форма" Тогда
			
			Параметры.ОрганизацияЕГАИС = Форма.ОрганизацияЕГАИС;
			Параметры.ЧастичноеВыбытиеДоступно = Истина;
			
		КонецЕсли;
		
		Параметры.НачальныйСтатусПроверки =
			ПредопределенноеЗначение("Перечисление.СтатусыПроверкиНаличияПродукцииИС.ВНаличии");
	
		Параметры.ОписаниеОповещенияПриЗакрытии = Новый ОписаниеОповещения(
			"ПриЗакрытииФормыПроверкиИПодбора",
			СобытияФормЕГАИСРТКлиент,
			Новый Структура("Форма", Форма));
		
		Объект = Новый Структура;
		Объект.Вставить("ОрганизацияЕГАИС", Параметры.ОрганизацияЕГАИС);
		Объект.Вставить("АкцизныеМарки", Форма.Объект.АкцизныеМарки);
		Объект.Вставить("Товары", Форма.Объект.Товары);
		
		Параметры.АдресДанныхПроверяемойАлкогольнойПродукции =
			ПроверкаИПодборПродукцииЕГАИСРТВызовСервера.АдресДанныхПроверкиАлкогольнойПродукции(
				Неопределено,
				Объект,
				Форма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
