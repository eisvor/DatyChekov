////////////////////////////////////////////////////////////////////////////////
// ОбщегоНазначенияРТКлиентСервер содержит процедуры и функции 
// общего назначения для работы с конфигурацией.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииОбщегоНазначения

// Процедура добавления значений в структуру из другой структуры.
//
// Параметры:
//  Приемник - Структура - структура приемник.
//  Источник - Структура - структура источник.
//
Процедура ДобавитьВСтруктуру(Приемник, Источник) Экспорт

	Для Каждого КлючИЗначение Из Источник Цикл
		Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;

КонецПроцедуры

// Дополняет структуру приемник полями структуры источника.
//
// Параметры:
//  СтруктураПриемник - Структура - Структура, в которую необходимо скопировать поля.
//  СтруктураИсточник - Структура - Структура, из которой необходимо скопировать поля.
//  КопироватьЗначения - Булево - Истина, если надо копировать значения.
//
Процедура ДополнитьСтруктуру(СтруктураПриемник, СтруктураИсточник, КопироватьЗначения = Ложь) Экспорт
	
	Для Каждого Элемент Из СтруктураИсточник Цикл
		СтруктураПриемник.Вставить(Элемент.Ключ, ?(КопироватьЗначения, Элемент.Значение, Неопределено));
	КонецЦикла;
	
КонецПроцедуры // ДополнитьСтруктуру()

// Поиск в массиве структур по структуре поиска.
//
// Параметры:
//  МассивСтруктур - Массив - массив структур для поиска.
//  СтруктураПоиска - Структура - структура для поиска в массиве.
//
Функция НайтиВМассивеСтруктур(МассивСтруктур, СтруктураПоиска) Экспорт

	НайденнаяСтруктура = Неопределено;
	Для Каждого ТекСтруктура Из МассивСтруктур Цикл
		ВсеПоляСовпадают = Истина;
		Для Каждого КлючИЗначение Из СтруктураПоиска Цикл
			Если КлючИЗначение.Значение <> ТекСтруктура[КлючИЗначение.Ключ] Тогда
				ВсеПоляСовпадают = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если ВсеПоляСовпадают Тогда
			НайденнаяСтруктура = ТекСтруктура;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат НайденнаяСтруктура;

КонецФункции

// Сравнивает значения переданных структур по указанным свойствам.
//
// Параметры:
//  Структура1 - Структура - первая структура для сравнения.
//  Структура2 - Структура - вторая структура для сравнени.
//  Свойства - Массив, Строка - имена свойств, по которым нужно проводить сравнение,
//                              массив строк с именами свойств, или строка (имена перечисляются через запятую).
// Возвращаемое значение:
//  Булево - Истина, если структуры равны по значениям переданных свойств,
//			 Ложь в противном случае.
//
Функция СтруктурыРавны(Структура1, Структура2, Свойства) Экспорт
	Если ТипЗнч(Свойства) = Тип("Строка") Тогда
		МассивСвойств = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Свойства);
	Иначе
		МассивСвойств = Свойства
	КонецЕсли;
	
	Для Каждого СтрокаМассива Из МассивСвойств Цикл
		Попытка
			Если Структура1[СтрокаМассива]<> Структура2[СтрокаМассива] Тогда
				Возврат Ложь;
			КонецЕсли;
		Исключение
			Структура1 = Неопределено;
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

// Устанавливает значение свойства элементов формы, если находит элемент на форме.
//
// Параметры:
//  ЭлементыФормы       - ВсеЭлементыФормы - элементы формы, среди которых содержится искомый элемент.
//  МассивИменЭлементов - Массив - массив имен искомых элементов.
//  ИмяСвойства         - Строка - имя свойства, для которого будет устанавливаться значение.
//  Значение            - Произвольный - значение, которое будет установлено.
//
Процедура УстановитьСвойствоЭлементовФормы(ЭлементыФормы, МассивИменЭлементов, ИмяСвойства, Значение) Экспорт
	
	Для Каждого ИмяЭлемента Из МассивИменЭлементов Цикл
		УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяЭлемента, ИмяСвойства, Значение);
	КонецЦикла
	
КонецПроцедуры

// Устанавливает значение свойства элемента формы, если находит элемент на форме.
//
// Параметры:
//  ЭлементыФормы - ВсеЭлементыФормы - элементы формы, среди которых содержится искомый элемент.
//  ИмяЭлемента   - Строка - имя искомого элемента.
//  ИмяСвойства   - Строка - имя свойства, для которого будет устанавливаться значение.
//  Значение      - Произвольный - значение, которое будет установлено.
//
Процедура УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяЭлемента, ИмяСвойства, Значение) Экспорт
	
	Если ЭлементыФормы.Найти(ИмяЭлемента) <> Неопределено  Тогда
		Если ЭлементыФормы[ИмяЭлемента][ИмяСвойства] <> Значение Тогда
			ЭлементыФормы[ИмяЭлемента][ИмяСвойства] = Значение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановитьСвойствоЭлементаФорма()

// Обновляет состояние документа.
//
// Параметры:
//  Объект - ДокументОбъект - объект редактируемого документа.
//  СостояниеДокумента - Строка - состояние документа.
//  КартинкаСостоянияДокумента - Число - реквизит объекта.
//  РазрешеноПроведение - Булево - признак возможности проведения.
//
Процедура ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение) Экспорт
	
	Если Объект.Проведен Тогда
		СостояниеДокумента = НСтр("ru = 'Проведен'");
		КартинкаСостоянияДокумента = 1;
	ИначеЕсли РазрешеноПроведение И НЕ Объект.ПометкаУдаления Тогда
		СостояниеДокумента = НСтр("ru = 'Не проведен'"); 
		КартинкаСостоянияДокумента = 0;
	ИначеЕсли РазрешеноПроведение И Объект.ПометкаУдаления Тогда
		СостояниеДокумента = НСтр("ru = 'Не проведен'");
		КартинкаСостоянияДокумента = 2;
	ИначеЕсли Объект.ПометкаУдаления Тогда
		СостояниеДокумента = НСтр("ru = 'Записан'");
		КартинкаСостоянияДокумента = 5;
	Иначе
		СостояниеДокумента = НСтр("ru = 'Записан'");
		КартинкаСостоянияДокумента = 3;
	КонецЕсли;

КонецПроцедуры // ОбновитьСостояниеДокумента()

// Преобразует строку к множественному числу. Предназначена для формирования "правильной" подписи к числительным.
//
// Параметры: 
//  Слово1 - Строка - форма слова в ед числе      ("шкаф").
//  Слово2 - Строка - форма слова для числит 2-4  ("шкафа").
//  Слово3 - Строка - форма слова для числит 5-10 ("шкафов").
//  ЦелоеЧисло - Число - целое число.
//
// Возвращаемое значение:
//  Строка - одна из строк в зависимости от параметра ЦелоеЧисло.
//
Функция ФормаМножественногоЧисла(Слово1, Слово2, Слово3, Знач ЦелоеЧисло) Экспорт
	
	// Изменим знак целого числа, иначе отрицательные числа будут неправильно преобразовываться.
	Если ЦелоеЧисло < 0 Тогда
		ЦелоеЧисло = -1 * ЦелоеЧисло;
	КонецЕсли;
	
	Если ЦелоеЧисло <> Цел(ЦелоеЧисло) Тогда 
		// Для нецелых чисел - всегда вторая форма.
		Возврат Слово2;
	КонецЕсли;
	
	// остаток
	Остаток = ЦелоеЧисло%10;
	Если (ЦелоеЧисло >10) И (ЦелоеЧисло<20) Тогда
		// Для второго десятка - всегда третья форма.
		Возврат Слово3;
	ИначеЕсли Остаток=1 Тогда
		Возврат Слово1;
	ИначеЕсли (Остаток>1) И (Остаток<5) Тогда
		Возврат Слово2;
	Иначе
		Возврат Слово3;
	КонецЕсли;

КонецФункции

// Получает описания типов числа, заданной разрядности.
// 
// Параметры:
//  Разрядность 			- Число - разряд числа.
//  РазрядностьДробнойЧасти - Число - разряд дробной части.
//
// Возвращаемое значение:
//  ОписаниеТипов - для числа указанной разрядности.
//
Функция ОписаниеТиповЧисла(Разрядность, РазрядностьДробнойЧасти) Экспорт

	Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(Разрядность, РазрядностьДробнойЧасти));

КонецФункции

// Формирует описание типов Строка заданной длины.
// 
// Параметры:
//  ДлинаСтроки - Число - длинна строки.
// 
Функция ОписаниеТиповСтрока(ДлинаСтроки = 1000) Экспорт
	Возврат Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(ДлинаСтроки));
КонецФункции


// Получает описания типов даты.
// 
// Параметры:
//  ЧастиДаты - ЧастиДаты - системное перечисление ЧастиДаты.
// 
Функция ОписаниеТиповДаты(ЧастьДаты = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ЧастьДаты) Тогда
		ЧастьДаты = ЧастиДаты.Дата;
	КонецЕсли;
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Дата"));
	КвалификаторДаты = Новый КвалификаторыДаты(ЧастьДаты);
	Возврат Новый ОписаниеТипов(Массив, , , КвалификаторДаты);
	
КонецФункции

// Возвращает пользователя с учетом измененных прав
//
// Возвращаемое значение:
//  СправочниСсылка.Пользователи - пользователь с учетом измененных прав.
//
Функция ПользовательСУчетомИзмененныхПрав() Экспорт
	
	Пользователь = глЗначениеПеременной("ПользовательИзмененныхПрав");
	
	Если Пользователь = Неопределено 
		ИЛИ НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Возврат Пользователь
КонецФункции // ПользовательСУчетомИзмененныхПрав()

// Возвращает код валюты.
//
// Возвращаемое значение:
//  Число - код валюты.
//
Функция КодВалюты() Экспорт
	
	Возврат НСтр("ru = '643'");
	
КонецФункции

// Возвращает представление валюты.
//
// Возвращаемое значение:
//  Строка - представление валюты.
//
Функция Валюта() Экспорт
	
	Возврат НСтр("ru = 'тг.'");
	
КонецФункции

// Определяет отбор динамического списка в зависимости от наличия режима совместимости.
//
// Параметры:
//  Список - ДинамическийСписок - список, для которого надо определить отбор.
//
// Возвращаемое значение:
//   Отбор - требуемый отбор
//
Функция ПолучитьОтборДинамическогоСписка(Список) Экспорт
	
	Возврат Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеПроцедурыРаботыСОтчетами

// Получает параметр из компоновщика настроек.
//
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных, ПользовательскиеНастройкиКомпоновкиДанных - настройки компоновщика настроек.
//  Параметр - Произвольный - параметр для получения.
//
// Возвращаемое значение:
//  ЗначениеПараметраНастроекКомпоновкиДанных - значение параметра настроек.
//
Функция ПолучитьПараметр(Настройки, Параметр) Экспорт
	
	ЗначениеПараметра = Неопределено;
	ПолеПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Новый ПараметрКомпоновкиДанных(Параметр), Параметр);
	
	Если ТипЗнч(Настройки) = Тип("НастройкиКомпоновкиДанных") Тогда
		ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
	ИначеЕсли ТипЗнч(Настройки) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		Для каждого ЭлементНастройки Из Настройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И ЭлементНастройки.Параметр = ПолеПараметр Тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Настройки) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Для каждого ЭлементНастройки Из Настройки.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И ЭлементНастройки.Параметр = ПолеПараметр Тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеПараметра = Неопределено Тогда
			ЗначениеПараметра = Настройки.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
КонецФункции

// Копирует элементы из одной коллекции в другую.
//
// Параметры:
//  ПриемникЗначения - ВариантыПользовательскогоПоляВыборКомпоновкиДанных,
//					   УсловноеОформлениеКомпоновкиДанных,
//					   ОформляемыеПоляКомпоновкиДанных,
//					   ЗначенияПараметровДанныхКомпоновкиДанных - настройки компоновщика настроек.
//  ИсточникЗначения - ВариантыПользовательскогоПоляВыборКомпоновкиДанных,
//					   УсловноеОформлениеКомпоновкиДанных,
//					   ОформляемыеПоляКомпоновкиДанных,
//					   ЗначенияПараметровДанныхКомпоновкиДанных - параметр для получения.
//  ПроверятьДоступность - Булево - признак проверки доступности элементов.
//  ОчищатьПриемник - Булево - признак очищения приемника значений.
//
Процедура СкопироватьЭлементы(ПриемникЗначения, ИсточникЗначения, ПроверятьДоступность = Ложь, ОчищатьПриемник = Истина) Экспорт
	
	Если ТипЗнч(ИсточникЗначения) = Тип("УсловноеОформлениеКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ВариантыПользовательскогоПоляВыборКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ОформляемыеПоляКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		СоздаватьПоТипу = Ложь;
	Иначе
		СоздаватьПоТипу = Истина;
	КонецЕсли;
	ПриемникЭлементов = ПриемникЗначения.Элементы;
	ИсточникЭлементов = ИсточникЗначения.Элементы;
	Если ОчищатьПриемник Тогда
		ПриемникЭлементов.Очистить();
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из ИсточникЭлементов Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
			// Элементы порядка добавляем в начало.
			Индекс = ИсточникЭлементов.Индекс(ЭлементИсточник);
			ЭлементПриемник = ПриемникЭлементов.Вставить(Индекс, ТипЗнч(ЭлементИсточник));
		Иначе
			Если СоздаватьПоТипу Тогда
				ЭлементПриемник = ПриемникЭлементов.Добавить(ТипЗнч(ЭлементИсточник));
			Иначе
				ЭлементПриемник = ПриемникЭлементов.Добавить();
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		// В некоторых коллекциях необходимо заполнить другие коллекции.
		Если ТипЗнч(ИсточникЭлементов) = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Поля, ЭлементИсточник.Поля);
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
			ЗаполнитьЭлементы(ЭлементПриемник.Оформление, ЭлементИсточник.Оформление); 
		ИначеЕсли ТипЗнч(ИсточникЭлементов)	= Тип("КоллекцияВариантовПользовательскогоПоляВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
		КонецЕсли;
		
		// В некоторых элементах коллекции необходимо заполнить другие коллекции.
		Если ТипЗнч(ЭлементИсточник) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Варианты, ЭлементИсточник.Варианты);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
			ЭлементПриемник.УстановитьВыражениеДетальныхЗаписей (ЭлементИсточник.ПолучитьВыражениеДетальныхЗаписей());
			ЭлементПриемник.УстановитьВыражениеИтоговыхЗаписей(ЭлементИсточник.ПолучитьВыражениеИтоговыхЗаписей());
			ЭлементПриемник.УстановитьПредставлениеВыраженияДетальныхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияДетальныхЗаписей ());
			ЭлементПриемник.УстановитьПредставлениеВыраженияИтоговыхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияИтоговыхЗаписей ());
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет отбор в набор отборов компоновщика или группы отборов.
//
// Параметры:
//  ЭлементСтруктуры - НастройкиКомпоновкиДанных, 
//                     КомпоновщикНастроекКомпоновкиДанных, 
//                     ПользовательскиеНастройкиКомпоновкиДанных - элемент структуры отборов.
//  Поле - ПолеКомпоновкиДанных, Строка - поле компоновки данных.
//  Значение - Произвольный - значение поля отбора.
//  ВидСравнения - ВидСравнения - вид сравнения элементов.
//  Использование - Булево - признак использования элемента отбора.
//
// Возвращаемое значение:
//  ЭлементОтбора - элемент отбора.
//
Функция НовыйОтбор(ЭлементСтруктуры, Знач Поле, Значение, ВидСравнения = Неопределено, Использование = Истина) Экспорт
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Отбор = ЭлементСтруктуры.Настройки.Отбор;
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиКомпоновкиДанных") Тогда
		Отбор = ЭлементСтруктуры.Отбор;
	Иначе
		Отбор = ЭлементСтруктуры;
	КонецЕсли;
	
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	ЭлементОтбора = Неопределено;
	Для каждого Элемент Из Отбор.Элементы Цикл
		
		Если Элемент.ЛевоеЗначение = Поле Тогда
			ЭлементОтбора = Элемент;
		КонецЕсли;	
		
	КонецЦикла;
	
	Если ЭлементОтбора = Неопределено Тогда
		ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	КонецЕсли;
	ЭлементОтбора.Использование  = Использование;
	ЭлементОтбора.ЛевоеЗначение  = Поле;
	ЭлементОтбора.ВидСравнения   = ВидСравнения;
	ЭлементОтбора.ПравоеЗначение = Значение;
	Возврат ЭлементОтбора;
	
КонецФункции

// Получает структуру отбора.
//
Функция СтруктураОтбора() Экспорт

	Возврат Новый Структура("Поле, Значение, ВидСравнения, Использование", );

КонецФункции

// Рекурсивная процедура заполнения элементов.
//
// Параметры:
//  ПриемникЗначения - 
//  ИсточникЗначения - 
//  ПервыйУровень - 
//
Процедура ЗаполнитьЭлементы(ПриемникЗначения, ИсточникЗначения, ПервыйУровень = Неопределено) Экспорт
	
	Если ТипЗнч(ПриемникЗначения) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		КоллекцияЗначений = ИсточникЗначения;
	Иначе
		КоллекцияЗначений = ИсточникЗначения.Элементы;
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из КоллекцияЗначений Цикл
		Если ПервыйУровень = Неопределено Тогда
			ЭлементПриемник = ПриемникЗначения.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		Иначе
			ЭлементПриемник = ПервыйУровень.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		КонецЕсли;
		Если ЭлементПриемник = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		Если ТипЗнч(ЭлементИсточник) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
			Если ЭлементИсточник.ЗначенияВложенныхПараметров.Количество() <> 0 Тогда
				ЗаполнитьЭлементы(ЭлементПриемник.ЗначенияВложенныхПараметров, ЭлементИсточник.ЗначенияВложенныхПараметров, ПриемникЗначения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает параметр настроек компоновки данных.
//
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных,
//              ПользовательскиеНастройкиКомпоновкиДанных,
//              КомпоновщикНастроекКомпоновкиДанных - настройки СКД, для которых требуется установить параметры.
//  Параметр - Строка,ПараметрКомпоновкиДанных - параметр, который требуется установить.
//  Значение - Произвольный - значение, которое требуется установить.
//  Использование  - Булево - признак использования параметра СКД, значение по умолчанию Истина.
//
// Возвращаемое значение:
//  ЗначениеПараметраНастроекКомпоновкиДанных - установленный параметр настроек компоновки данных
//
Функция УстановитьПараметр(Настройки, Параметр, Значение, Использование = Истина) Экспорт
	
	ЗначениеПараметра = Неопределено;
	ПолеПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Новый ПараметрКомпоновкиДанных(Параметр), Параметр);
	
	Если ТипЗнч(Настройки) = Тип("НастройкиКомпоновкиДанных") Тогда
		ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
	ИначеЕсли ТипЗнч(Настройки) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		Для каждого ЭлементНастройки Из Настройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И ЭлементНастройки.Параметр = ПолеПараметр Тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Настройки) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Для каждого ЭлементНастройки Из Настройки.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И ЭлементНастройки.Параметр = ПолеПараметр Тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеПараметра = Неопределено Тогда
			ЗначениеПараметра = Настройки.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеПараметра.Значение = Значение;
	ЗначениеПараметра.Использование = Использование;
	
	Возврат ЗначениеПараметра;
КонецФункции

// Добавляет группировку в компоновщик настроек в самый нижний уровень структуры, если поле не указано - детальные поля.
//
// Параметры:
//  КомпоновщикНастроек - НастройкиКомпоновкиДанных,
//                        ПользовательскиеНастройкиКомпоновкиДанных,
//                        КомпоновщикНастроекКомпоновкиДанных - компоновщик в который добавляется группировка.
//  Поле - ПолеКомпоновкиДанных - поле компоновки данных в кото
//  Строки - Булево - признак того, что последний элемент является таблицей.
// 
// Возвращаемое значение:
//  Группировка - добавленная группировка компоновки данных.
//
Функция НоваяГруппировка(КомпоновщикНастроек, Знач Поле = Неопределено, Строки = Истина) Экспорт
	
	ЭлементСтруктуры = ПоследнийЭлементСтруктуры(КомпоновщикНастроек, Строки);
	Если ЭлементСтруктуры = Неопределено 
	 Или ЭлементСтруктурыДетальныеЗаписи(КомпоновщикНастроек) <> Неопределено 
	   И Поле = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных") 
	 Или ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		НоваяГруппировка = ЭлементСтруктуры.Структура.Добавить();
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных")
			ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") Тогда
		НоваяГруппировка = ЭлементСтруктуры.Добавить();
	Иначе
		НоваяГруппировка = ЭлементСтруктуры.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	КонецЕсли;
	
	НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	Если Поле <> Неопределено Тогда
		ПолеГруппировки = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Поле = Поле;
	КонецЕсли;
	Возврат НоваяГруппировка;
	
КонецФункции

// Возвращает последний элемент структуры - группировку.
//
// Параметры:
//  ЭлементСтруктурыНастроек - НастройкиКомпоновкиДанных, 
//                             КомпоновщикНастроекКомпоновкиДанных, 
//                             ПользовательскиеНастройкиКомпоновкиДанных - элемент структуры настроек.
//  Строки - Булево - признак того, что последний элемент является строками.
// 
// Возвращаемое значение:
//  НастройкиКомпоновкиДанных, 
//  КомпоновщикНастроекКомпоновкиДанных, 
//  ПользовательскиеНастройкиКомпоновкиДанных - элемент структуры отборов.
//
Функция ПоследнийЭлементСтруктуры(ЭлементСтруктурыНастроек, Строки = Истина) Экспорт
	
	Если ТипЗнч(ЭлементСтруктурыНастроек) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = ЭлементСтруктурыНастроек.Настройки;
	ИначеЕсли ТипЗнч(ЭлементСтруктурыНастроек) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = ЭлементСтруктурыНастроек;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Структура = Настройки.Структура;
	Если Структура.Количество() = 0 Тогда
		Возврат Настройки;
	КонецЕсли;
	
	Если Строки Тогда
		ИмяСтруктурыТаблицы = "Строки";
		ИмяСтруктурыДиаграммы = "Серии";
	Иначе
		ИмяСтруктурыТаблицы = "Колонки";
		ИмяСтруктурыДиаграммы = "Точки";
	КонецЕсли;
	
	Пока Истина Цикл
		ЭлементСтруктуры = Структура[0];
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыТаблицы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыДиаграммы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
			  Или ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
			  Или ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
			Если ЭлементСтруктуры.Структура.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры.Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыТаблицы];
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных")	Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
		Иначе
			Возврат ЭлементСтруктуры;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Структура[0];
	
КонецФункции

// Возвращает группировку - детальные записи компоновщика настроек.
//
// Параметры:
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик настроек по которому возвращается группировка.
//
// Возвращаемое значение:
//  ГруппировкаКомпоновкиДанных,
//  ГруппировкаТаблицыКомпоновкиДанных,
//  ГруппировкаДиаграммыКомпоновкиДанных - группировка-детальные записи.
//
Функция ЭлементСтруктурыДетальныеЗаписи(КомпоновщикНастроек) Экспорт
	
	ПоследнийЭлементСтруктуры = ПоследнийЭлементСтруктуры(КомпоновщикНастроек, Истина);
	Если ТипЗнч(ПоследнийЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
	 Или ТипЗнч(ПоследнийЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 Или ТипЗнч(ПоследнийЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		Если ПоследнийЭлементСтруктуры.ПоляГруппировки.Элементы.Количество() = 0 Тогда
			Возврат ПоследнийЭлементСтруктуры;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Устанавливает параметр вывода компоновщика настроек.
//
// Параметры:
//  КомпоновщикНастроекГруппировка - ГруппировкаКомпоновкиДанных - группировка для установки параметра вывода.
//  ИмяПараметра - Строка - имя параметра строкой.
//  Значение - Произвольный - произвольное значение параметра.
//
// Возвращаемое значение:
//  ПараметрКомпоновкиДанных - параметр компоновки данных.
//
Функция УстановитьПараметрВывода(КомпоновщикНастроекГруппировка, ИмяПараметра, Значение) Экспорт
	
	Если ТипЗнч(КомпоновщикНастроекГруппировка) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Иначе
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	КонецЕсли;
	Если ЗначениеПараметра = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		ЗначениеПараметра.Использование = Истина;
		ЗначениеПараметра.Значение = Значение;
		Возврат ЗначениеПараметра;
	КонецЕсли;
	
КонецФункции

// Возвращает признак используется ли поле в настройке СКД.
//
// Параметры:
//  ЭлементСтруктуры - НастройкиКомпоновкиДанных, 
//                     КомпоновщикНастроекКомпоновкиДанных, 
//                     ПользовательскиеНастройкиКомпоновкиДанных - настройки СКД для проверки используемости признака.
//  Поле - ПолеКомпоновкиДанных, Строка - поле компоновки данных.
//
// Возвращаемое значение:
//  Булево - признак использования поля в настройках СКД.
//
Функция ПолеИспользуется(ЭлементСтруктуры, Поле) Экспорт
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = ЭлементСтруктуры.ПолучитьНастройки();
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиКомпоновкиДанных") Тогда
	    Настройки = ЭлементСтруктуры;
	КонецЕсли;
	
	ЕстьГруппировка = Ложь;
	
	ЕстьГруппировка = НайтиПоле(Настройки.Структура, Поле);
	
	// Найти поле группировки в отборе.
	Для каждого ОтборПоле Из Настройки.Отбор.Элементы Цикл
		
		ПолеПериодРегистрации = Новый ПолеКомпоновкиДанных(Поле);
		
		Если ТипЗнч(ОтборПоле) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			ЕстьГруппировка = ИспользуетсяОтбор(ОтборПоле.Элементы, ПолеПериодРегистрации);
		Иначе
			Если ОтборПоле.Использование И (ОтборПоле.ЛевоеЗначение = ПолеПериодРегистрации Или ОтборПоле.ПравоеЗначение = ПолеПериодРегистрации) Тогда
				
				ЕстьГруппировка = Истина;
				
				Прервать;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ВыбранныеПоля = ПолучитьВыбранныеПоля(Настройки);
	
	Для каждого ПолеВыбора Из ВыбранныеПоля Цикл
		
		ПолеПериодРегистрации = Новый ПолеКомпоновкиДанных(Поле);
		
		Если ТипЗНЧ(ПолеВыбора) = Тип("ВыбранноеПолеКомпоновкиДанных") И ПолеВыбора.Использование И ПолеВыбора.Поле = ПолеПериодРегистрации Тогда
			
			ЕстьГруппировка = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	
	Возврат ЕстьГруппировка;
	
КонецФункции 

// Возвращает массив выбранных полей или групп выбранных полей.
//
// Параметр:
//  ЭлементСтруктуры - НастройкиКомпоновкиДанных, 
//                     КомпоновщикНастроекКомпоновкиДанных, 
//                     ПользовательскиеНастройкиКомпоновкиДанных - настройки СКД для отбора выбранных полей.
//  ТолькоГруппы     - Булево - признак, того что процедура будет возвращать только группы.
//
// Возвращаемое значение:
//  Массив - массив выбранных полей.
//
Функция ПолучитьВыбранныеПоля(ЭлементСтруктуры, ТолькоГруппы = Ложь) Экспорт
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") 
	 Или ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ВыбранныеПоля = ЭлементСтруктуры.Настройки.Выбор;
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиКомпоновкиДанных") Тогда
		ВыбранныеПоля = ЭлементСтруктуры.Выбор;
	КонецЕсли;
	
	МассивПолей = Новый Массив;
	ДобавитьВыбранныеПоляВМассив(ВыбранныеПоля.Элементы, МассивПолей, ТолькоГруппы);
	Возврат МассивПолей;
	
КонецФункции

// Добавляет выбранное поле в набор выбранных полей.
//
// Параметры:
//  ЭлементСтруктуры - НастройкиКомпоновкиДанных, 
//                     КомпоновщикНастроекКомпоновкиДанных, 
//                     ПользовательскиеНастройкиКомпоновкиДанных - настройки СКД для проверки используемости признака.
//  Поле - ПолеКомпоновкиДанных, Строка - поле компоновки данных.
//  Заголовок - Строка - заголовок выбранного поля.
//
// Возвращаемое значение:
//  ПолеКомпоновкиДанных - новое поле компоновки данны..
//
Функция ДобавитьВыбранноеПоле(ЭлементСтруктуры, Знач Поле, Заголовок = Неопределено) Экспорт
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоля = ЭлементСтруктуры.Настройки.Выбор;
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиКомпоновкиДанных") Тогда
		ВыбранныеПоля = ЭлементСтруктуры.Выбор;
	Иначе
		ВыбранныеПоля = ЭлементСтруктуры;
	КонецЕсли;
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	ВыбранноеПоле = ВыбранныеПоля.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Поле = Поле;
	Если Заголовок <> Неопределено Тогда
		ВыбранноеПоле.Заголовок = Заголовок;
	КонецЕсли;
	Возврат ВыбранноеПоле;
	
КонецФункции  

// Удаляет все элементы настройки компоновки данных из объекта.
//
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных, 
//              КомпоновщикНастроекКомпоновкиДанных, 
//              ПользовательскиеНастройкиКомпоновкиДанных - настройки для удаления и очистки.
//
Процедура ОчиститьНастройкиКомпоновкиДанных(Настройки) Экспорт
	
	Если Настройки = Неопределено Или ТипЗнч(Настройки) <> Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Параметр Из Настройки.ПараметрыДанных.Элементы Цикл
		Параметр.Значение = Неопределено;
		Параметр.Использование = ложь;
	КонецЦикла;
	
	Для каждого Параметр Из Настройки.ПараметрыВывода.Элементы Цикл
		Параметр.Использование = ложь;
	КонецЦикла;
	
	Настройки.ПользовательскиеПоля.Элементы.Очистить();
	Настройки.Отбор.Элементы.Очистить();
	Настройки.Порядок.Элементы.Очистить();
	Настройки.Выбор.Элементы.Очистить();
	Настройки.Структура.Очистить();
	
КонецПроцедуры

// Копирует настройки компоновки данных.
//
// Параметры:
//  НастройкиПриемник - НастройкиКомпоновкиДанных - настройки приемник.
//  НастройкиИсточник - НастройкиКомпоновкиДанных - настройки источник.
// 
Процедура СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник, НастройкиИсточник) Экспорт
	
	Если НастройкиИсточник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиПриемник) = Тип("НастройкиКомпоновкиДанных") Тогда
		Для каждого Параметр Из НастройкиИсточник.ПараметрыДанных.Элементы Цикл
			ЗначениеПараметра = НастройкиПриемник.ПараметрыДанных.НайтиЗначениеПараметра(Параметр.Параметр);
			Если ЗначениеПараметра <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ЗначениеПараметра, Параметр);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник.Настройки, НастройкиИсточник.Настройки);
		Возврат;
	КонецЕсли;
	
	// Копирование настроек
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных") Тогда
		
		ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыДанных, НастройкиИсточник.ПараметрыДанных);
		СкопироватьЭлементы(НастройкиПриемник.ПользовательскиеПоля, НастройкиИсточник.ПользовательскиеПоля);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,         НастройкиИсточник.Отбор);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,       НастройкиИсточник.Порядок);
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных")
	 Или ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 Или ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		СкопироватьЭлементы(НастройкиПриемник.ПоляГруппировки, НастройкиИсточник.ПоляГруппировки);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,           НастройкиИсточник.Отбор);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,         НастройкиИсточник.Порядок);
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		
	КонецЕсли;
	
	СкопироватьЭлементы(НастройкиПриемник.Выбор,              НастройкиИсточник.Выбор);
	СкопироватьЭлементы(НастройкиПриемник.УсловноеОформление, НастройкиИсточник.УсловноеОформление);
	ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыВывода,      НастройкиИсточник.ПараметрыВывода);
	
	// Копирование структуры
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных")
	 Или ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить(ТипЗнч(ЭлементСтруктурыИсточник));
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 Или ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Строки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Строки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Колонки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Колонки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Серии Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Серии.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Точки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Точки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСДатами

// Преобразовывает секунды в дату.
//
// Параметры:
//  Секунды - Число - количество секунд.
//
Функция ПреобразоватьСекундыВДату(Секунды) Экспорт
	
	Результат = Неопределено;
		
	Если Секунды < 0 Тогда
		
		Возврат Результат;
		
	КонецЕсли;
		
	Часов  = Цел(Секунды / 3600);
	Минут  = Цел((Секунды - (Часов * 3600)) / 60);
	Секунд = Секунды - ((Минут * 60) + (Часов * 3600));
	
	Если Не (Часов > 23
		Или Минут > 59
		Или Секунд > 59) Тогда
		Результат = Дата(1,1,1, Часов, Минут, Секунд);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сложение даты и времени.
//
// Параметры:
//  ДатаКалендаря - Дата - дата календаря для сложения.
//  Время - Время - время для сложения.
//
// Возвращаемое значение:
//  ДатаВремя - дата и время после сложения.
//
Функция СложитьДатуИВремя(ДатаКалендаря, Время) Экспорт
	
	Результат = '00010101';
	
	Год   = Год(ДатаКалендаря);
	Месяц = Месяц(ДатаКалендаря);
	Число = День(ДатаКалендаря);
	
	Час     = Час(Время);
	Минута  = Минута(Время);
	Секунда = Секунда(Время);
	
	Возврат Дата(Год, Месяц, Число, Час, Минута, Секунда);
	
КонецФункции

#КонецОбласти

#Область ПроцедурыФункцииРаботыСПроверкойЗаполнения

// Заполняет массив непроверяемых реквизитов значениями, не используемыми при выбранной хозяйственной
// операции документа.
//
// Параметры:
//  МассивВсехРеквизитов - Массив - массив всех реквизитов документа.
//  МассивРеквизитовОперации - Массив - массив реквизитов документа, которые относятся к хозяйственной операции
//                                      документа.
//  МассивНепроверяемыхРеквизитов - массив реквизитов объекта, не требующих проверки.
//
Процедура ЗаполнитьМассивНепроверяемыхРеквизитов(МассивВсехРеквизитов, МассивРеквизитовОперации, МассивНепроверяемыхРеквизитов) Экспорт
	
	Для Каждого ЭлементМассива Из МассивВсехРеквизитов Цикл
		
		// Если реквизит не используется для хозяйственной операции, исключаем данный реквизит из проверки.
		Если МассивРеквизитовОперации.Найти(ЭлементМассива) = Неопределено Тогда
			МассивНепроверяемыхРеквизитов.Добавить(ЭлементМассива);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьМассивНепроверяемыхРеквизитов()

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСВалютами

// Возвращает представление валюты учета.
//
// Возвращаемое значение:
//  Структура - структура представления валюты учета.
//
Функция ПредставлениеВалютыУчета() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("НаименованиеСокращенное", НСтр("ru = 'тг.'"));
	Структура.Вставить("КодСимвольный", НСтр("ru = 'KZT'"));
		
	Возврат Структура;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииРМК

// Получает структуру выводимой информации в РМК.
//
// Возвращаемое значение:
//  Структура - структура выводимой информации в РМК.
//
Функция ВыводимаяВРМКИнформация() Экспорт
	
	ПараметрыИнформации = Новый Структура;
	ПараметрыИнформации.Вставить("ЗаголовокИнформации", "");
	ПараметрыИнформации.Вставить("ТекстИнформации", "");
	ПараметрыИнформации.Вставить("ПоказатьПодсказку", Ложь);
	ПараметрыИнформации.Вставить("Подсказка", "");
	ПараметрыИнформации.Вставить("ТипОтвета", "");
	ПараметрыИнформации.Вставить("ОтветПоУмолчанию", "");
	ПараметрыИнформации.Вставить("Кнопки", Неопределено);
	
	Возврат ПараметрыИнформации;
	
КонецФункции

// Получение структуры для создания элемента справочника.
// Штрихкоды упаковок товаров.
Функция СтруктураШтрихкодУпаковки() Экспорт 
	
	СтруктураШтрихкодаУпаковки = Новый Структура;
	СтруктураШтрихкодаУпаковки.Вставить("Организация");
	СтруктураШтрихкодаУпаковки.Вставить("Магазин");
	СтруктураШтрихкодаУпаковки.Вставить("Штрихкод");
	СтруктураШтрихкодаУпаковки.Вставить("КлючСвязи");
	СтруктураШтрихкодаУпаковки.Вставить("Номенклатура");
	СтруктураШтрихкодаУпаковки.Вставить("Характеристика");
	
	Возврат СтруктураШтрихкодаУпаковки;
	
КонецФункции // СтруктураШтрихкодУпаковки()

// Получение структуры для заполнения табличной части Акцизные марки.
//
Функция СтруктураДляЗаполненияАкцизныхМарок() Экспорт 
	
	СтруктураДляЗаполнения = Новый Структура;
	СтруктураДляЗаполнения.Вставить("Адрес");
	СтруктураДляЗаполнения.Вставить("ДополнительныеПараметры");
	СтруктураДляЗаполнения.Вставить("Организация");
	СтруктураДляЗаполнения.Вставить("Номенклатура");
	СтруктураДляЗаполнения.Вставить("Характеристика");
	СтруктураДляЗаполнения.Вставить("Объект");
	
	Возврат СтруктураДляЗаполнения;
	
КонецФункции

#КонецОбласти

// Изменяет элемент отбора динамического списка.
//
// Параметры:
//  Список - ДинамическийСписок - обрабатываемый динамический список.
//  ИмяПоля - Строка - наименование поля компоновки данных.
//  ПравоеЗначение - ПравоеЗначение - значение, которое нужно установить.
//  Установить - Булево - признак необходимости установить отбор.
//  ВидСравнения - ВидСравнения - вид сравнения отбора, по умолчанию - Равно.
//  РежимОтображения - РежимОтображения - режим отображения динамического списка.
//
Процедура ИзменитьЭлементОтбораСписка(
			Список,
			ИмяПоля,
			ПравоеЗначение = Неопределено,
			Установить = Истина,
			ВидСравнения = Неопределено,
			РежимОтображения = Неопределено) Экспорт
	
	УдалитьЭлементОтбораСписка(Список, ИмяПоля);
	
	КоллекцияЭлементов = Список.Отбор.Элементы;
	ЭлементОтбора = КоллекцияЭлементов.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ЭлементОтбора.Использование  = Установить;
	ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
	
	Если ВидСравнения = Неопределено Тогда
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Иначе
		ЭлементОтбора.ВидСравнения = ВидСравнения;
	КонецЕсли;
	
	Если РежимОтображения = Неопределено Тогда
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто;
	Иначе
		ЭлементОтбора.РежимОтображения = РежимОтображения;
	КонецЕсли;
	
КонецПроцедуры

// Удаляет элемент отбора динамического списка.
//
// Параметры:
// Список - ДинамическийСписок - обрабатываемый динамический список,
// ИмяПоля - Строка - имя поля компоновки, отбор по которому нужно удалить,
//
Процедура УдалитьЭлементОтбораСписка(Список, ИмяПоля) Экспорт
	
	КоллекцияЭлементов = Список.Отбор.Элементы;
	ПолеКомпоновки = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			И ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда
			КоллекцияЭлементов.Удалить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Создает группу отбора динамического списка.
//
// Параметры:
// КоллекцияЭлементов - ГруппаЭлементовОтбораКомпоновкиДанных - коллекция элементов для создания отбора.
// Представление - Представление - представление отбора.
// ТипГруппы - ТипГруппы - тип группы отбора.
//
Функция СоздатьГруппуЭлементовОтбора(КоллекцияЭлементов, Представление, ТипГруппы) Экспорт
	
	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;
	
	ГруппаЭлементовОтбора.Представление    = Представление;
	ГруппаЭлементовОтбора.Применение       = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто;
	ГруппаЭлементовОтбора.ТипГруппы        = ТипГруппы;
	ГруппаЭлементовОтбора.Использование    = Истина;
	
	Возврат ГруппаЭлементовОтбора;
	
КонецФункции

// Изменяет элемент отбора динамического списка.
//
// Параметры:
// ГруппаЭлементов  ГруппаЭлементовОтбораКомпоновкиДанных - коллекция элементов для изменения отбора.
// ИмяПоля - Строка - имя поля компоновки.
// ПравоеЗначение - ПравоеЗначение - значение, которое необходимо установить.
//
Процедура ДобавитьЭлементОтбораВГруппу(ГруппаЭлементов, ИмяПоля, ПравоеЗначение) Экспорт
	
	НовыйЭлемент = ГруппаЭлементов.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.Использование = Истина;
	НовыйЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	НовыйЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НовыйЭлемент.ПравоеЗначение = ПравоеЗначение;
	НовыйЭлемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто;
	
КонецПроцедуры

#Область МетодыРаботыСРеквизитамиОбъектов

// Проверяет наличие реквизита объекта.
//
// Параметры:
//  Объект - ЛюбаяСсылка - 
//  ИмяРеквизита - Строка - наименование реквизита.
//
// Возвращаемое значение:
//  Структура - структура реквизита объекта.
//
Функция ЕстьРеквизитОбъекта(Объект, ИмяРеквизита) Экспорт
	
	КлючУникальности   = Новый УникальныйИдентификатор;
	СтруктураРеквизита = Новый Структура(ИмяРеквизита, КлючУникальности);

	ЗаполнитьЗначенияСвойств(СтруктураРеквизита, Объект);
	
	Возврат СтруктураРеквизита[ИмяРеквизита] <> КлючУникальности;
	
КонецФункции

// Устанавливает картинку для комментария.
//
// Параметры:
//  ГруппаДополнительно - ЭлементФормы - группа для установки картинки.
//  Комментарий - Строка - комментарий.
//
Процедура УстановитьКартинкуДляКомментария(ГруппаДополнительно, Комментарий) Экспорт
	
	Если ЗначениеЗаполнено(Комментарий) Тогда
		ГруппаДополнительно.Картинка = БиблиотекаКартинок.Комментарий;
	Иначе
		ГруппаДополнительно.Картинка = Новый Картинка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСЭлементамиФормы

// Создает элемент формы "ГруппаФормы" с вертикальной группировкой элементов группы.
//
// Параметры:
//  Форма - Форма, для которой надо создать элемент;
//  Имя - Строка - имя группы;
//  Заголовок - Строка - заголовок группы;
//  РодительскаяГруппа - Элемент Формы - элемент формы, который будет являться родителем для создаваемой группы;
//  СледующийЭлемент - Элемент Формы - элемент формы, перед которым необходимо разместить создаваемую группу.
//
// Возвращаемое значение:
//  ГруппаФормы.
//
Функция ВертикальнаяГруппа(Форма, Имя, Заголовок = "", РодительскаяГруппа = Неопределено, СледующийЭлемент = Неопределено) Экспорт
	
	Возврат ГруппаФормы(ГруппировкаПодчиненныхЭлементовФормы.Вертикальная,
		Форма, Имя, Заголовок, РодительскаяГруппа, СледующийЭлемент);
КонецФункции

// Создает элемент формы "ГруппаФормы" с горизонтальной группировкой элементов группы.
//
// Параметры:
//  Форма - Форма, для которой надо создать элемент;
//  Имя - Строка - имя группы;
//  Заголовок - Строка - заголовок группы;
//  РодительскаяГруппа - Элемент Формы - элемент формы, который будет являться родителем для создаваемой группы;
//  СледующийЭлемент - Элемент Формы - элемент формы, перед которым необходимо разместить создаваемую группу.
//
// Возвращаемое значение:
//  ГруппаФормы.
//
Функция ГоризонтальнаяГруппа(Форма, Имя, Заголовок = "", РодительскаяГруппа = Неопределено, СледующийЭлемент = Неопределено) Экспорт
	
	Возврат ГруппаФормы(ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная,
		Форма, Имя, Заголовок, РодительскаяГруппа, СледующийЭлемент);
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НайтиПоле(Структура, Поле)
	
	ЕстьПоле = ложь;
	
	Если ТипЗнч(Структура) <> Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") 
	 И ТипЗнч(Структура) <> Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") 
	 И ТипЗнч(Структура) <> Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") 
	 Тогда
		
		Возврат ЕстьПоле;
		
	КонецЕсли;
	
	ПолеПериодРегистрации = Новый ПолеКомпоновкиДанных(Поле);
	
	Для каждого ЭлементСтруктуры Из Структура Цикл
		
		Если Тип(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных") 
			Или  Тип(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных") 
			Или  Тип(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
			Для каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				Если ПолеГруппировки.Использование И ПолеГруппировки.Поле = ПолеПериодРегистрации Тогда
					ЕстьПоле = истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЕстьПоле Тогда
			Прервать;
		КонецЕсли;
		Если Тип(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			ЕстьПоле = НайтиПоле(ЭлементСтруктуры.Строки, Поле) Или НайтиПоле(ЭлементСтруктуры.Колонки, Поле);
		ИначеЕсли Тип(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных") 
			Или  Тип(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных") 
			Или  Тип(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
			ЕстьПоле = НайтиПоле(ЭлементСтруктуры.Структура, Поле);
		ИначеЕсли Тип(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			Если ЭлементСтруктуры.Точки.Количество() <> 0 Тогда
				ЕстьПоле = НайтиПоле(ЭлементСтруктуры.Точки, Поле) Или НайтиПоле(ЭлементСтруктуры.Серии, Поле);
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;
	
	Возврат ЕстьПоле;
	
КонецФункции // НайтиПоле()

Функция ИспользуетсяОтбор(Элементы, ПолеПериодРегистрации)
	
	ЕстьГруппировка = ложь;
	
	Для каждого ОтборПоле Из Элементы Цикл
		
		Если ТипЗнч(ОтборПоле) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			ЕстьГруппировка = ИспользуетсяОтбор(ОтборПоле.Элементы, ПолеПериодРегистрации);
		Иначе
			Если ОтборПоле.Использование И (ОтборПоле.ЛевоеЗначение = ПолеПериодРегистрации Или ОтборПоле.ПравоеЗначение = ПолеПериодРегистрации) Тогда
				
				ЕстьГруппировка = истина;
				
				Прервать;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЕстьГруппировка;
	
КонецФункции

Процедура ДобавитьВыбранныеПоляВМассив(ЭлементСтруктуры, МассивПолей, ТолькоГруппы = Ложь)
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоля = ЭлементСтруктуры.Настройки.Выбор;
	Иначе
		ВыбранныеПоля = ЭлементСтруктуры;
	КонецЕсли;
	
	Для каждого Элемент Из ЭлементСтруктуры Цикл
		Если ТипЗнч(Элемент) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			Продолжить;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Если ТолькоГруппы Тогда
				МассивПолей.Добавить(Элемент);
			КонецЕсли;
			ДобавитьВыбранныеПоляВМассив(Элемент.Элементы, МассивПолей, ТолькоГруппы);
		Иначе
			Если Не ТолькоГруппы Тогда
				МассивПолей.Добавить(Элемент);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление)
	
	ВозвращаемоеЗначение = Неопределено;
	
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ГруппаФормы(Группировка, Форма, Имя, Заголовок, РодительскаяГруппа, СледующийЭлемент)
	
	Если СледующийЭлемент = Неопределено Тогда
		ГруппаСтрокаЭлементов = Форма.Элементы.Добавить(Имя, Тип("ГруппаФормы"), РодительскаяГруппа);
	Иначе
		ГруппаСтрокаЭлементов = Форма.Элементы.Вставить(Имя, Тип("ГруппаФормы"), РодительскаяГруппа, СледующийЭлемент);
	КонецЕсли;
	ГруппаСтрокаЭлементов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСтрокаЭлементов.Группировка = Группировка;
	ГруппаСтрокаЭлементов.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСтрокаЭлементов.Заголовок = Заголовок;
	ГруппаСтрокаЭлементов.ОтображатьЗаголовок = Ложь;
	ГруппаСтрокаЭлементов.РазрешитьИзменениеСостава = Ложь;
	
	Возврат ГруппаСтрокаЭлементов;
КонецФункции

// Функция возвращает представление доверенности
Функция ПолучитьТекстДоверенности(ДанныеДоверенности) Экспорт
	
	ТекстДоверенности = "";
	
	Если ЗначениеЗаполнено(ДанныеДоверенности.ДоверенностьНомер) ИЛИ ЗначениеЗаполнено(ДанныеДоверенности.ДоверенностьДата) Тогда			
		ТекстДоверенности = НСтр("ru = '№%1 от %2 года'");
		
		ТекстДоверенности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДоверенности, СокрЛП(ДанныеДоверенности.ДоверенностьНомер), 
					Формат(ДанныеДоверенности.ДоверенностьДата,"ДФ=dd.MM.yyyy"));
	Иначе
		ТекстДоверенности = СокрЛП(ДанныеДоверенности.УдалитьДоверенность);			
	КонецЕсли; 
	
	Возврат ТекстДоверенности;
	
КонецФункции

#КонецОбласти
