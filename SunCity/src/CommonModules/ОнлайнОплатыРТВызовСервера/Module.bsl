
Функция ДанныеЭквайераПоУмолчанию() Экспорт
	
	ДатаСреза   = ТекущаяДатаСеанса();
	ЭквайерыXML = ОнлайнОплатыРТ.ПолучитьМакетЭквайеры();
	
	ЭквайерыXMLТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ЭквайерыXML).Данные;
	ЭквайерыXMLТаблица.Колонки.Добавить("ДатаСреза", Новый ОписаниеТипов("Дата",,,
													 Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	Для Каждого СтрокаТаблицы Из ЭквайерыXMLТаблица Цикл 
		СтрокаТаблицы.ДатаСреза = ?(ПустаяСтрока(СтрокаТаблицы.DateFrom), Дата('00010101'), Дата(СтрокаТаблицы.DateFrom));
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЭквайерыXMLТаблица"	, ЭквайерыXMLТаблица);
	Запрос.УстановитьПараметр("ДатаСреза"			, ДатаСреза);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭквайерыXMLТаблица.FullName КАК FullName,
	|	ЭквайерыXMLТаблица.Name КАК Name,
	|	ЭквайерыXMLТаблица.INN КАК INN,
	|	ЭквайерыXMLТаблица.KPP КАК KPP,
	|	ЭквайерыXMLТаблица.OGRN КАК OGRN,
	|	ЭквайерыXMLТаблица.OKVED КАК OKVED,
	|	ЭквайерыXMLТаблица.OKPO КАК OKPO,
	|	ЭквайерыXMLТаблица.ДатаСреза КАК ДатаСреза
	|ПОМЕСТИТЬ ЭквайерыXMLТаблица
	|ИЗ
	|	&ЭквайерыXMLТаблица КАК ЭквайерыXMLТаблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЭквайерыXMLТаблица.ДатаСреза) КАК ДатаСреза
	|ПОМЕСТИТЬ ПолучениеПоДатеСреза
	|ИЗ
	|	ЭквайерыXMLТаблица КАК ЭквайерыXMLТаблица
	|ГДЕ
	|	ЭквайерыXMLТаблица.ДатаСреза <= &ДатаСреза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭквайерыXMLТаблица.FullName КАК НаименованиеПолное,
	|	ЭквайерыXMLТаблица.Name КАК Наименование,
	|	ЭквайерыXMLТаблица.INN КАК ИНН,
	|	ЭквайерыXMLТаблица.KPP КАК КПП,
	|	ЭквайерыXMLТаблица.OGRN КАК ОГРН,
	|	ЭквайерыXMLТаблица.OKVED КАК ОКВЭД,
	|	ЭквайерыXMLТаблица.OKPO КАК КодПоОКПО
	|ИЗ
	|	ЭквайерыXMLТаблица КАК ЭквайерыXMLТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПолучениеПоДатеСреза КАК ПолучениеПоДатеСреза
	|		ПО ЭквайерыXMLТаблица.ДатаСреза = ПолучениеПоДатеСреза.ДатаСреза";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ДанныеЭквайера = Новый Структура("НаименованиеПолное, Наименование, ИНН, КПП, ОГРН, ОКВЭД, КодПоОКПО");
	ЗаполнитьЗначенияСвойств(ДанныеЭквайера, Выборка);
	
	Возврат ДанныеЭквайера;
		
КонецФункции

Функция ЗагрузитьОперацииОнлайнОплатыВФоне(Знач ПараметрыЗагрузки) Экспорт
	
	ДлительнаяОперация = ОнлайнОплатыРТ.ЗагрузитьОперацииОнлайнОплатыВФоне(ПараметрыЗагрузки);
	
	Возврат ДлительнаяОперация;
	
КонецФункции
