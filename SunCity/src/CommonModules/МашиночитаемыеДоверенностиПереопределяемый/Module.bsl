

#Область ПрограммныйИнтерфейс

// Заполняет при изменении данные физического лица по ссылке.
//
// Параметры:
//  ФизЛицо - СправочникСсылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. МашиночитаемыеДоверенности.НовыеДанныеФизЛица
//
Процедура ПриИзмененииДанныеФизЛица(ФизЛицо, Сведения) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Ссылка,
	|	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
	|	ФизическиеЛица.ИНН КАК ИНН,
	|	ФизическиеЛица.Пол КАК Пол,
	|	ФизическиеЛица.Наименование КАК Наименование
	|ПОМЕСТИТЬ ВТФизЛицо
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.Ссылка = &ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТФизЛицо.Ссылка КАК Ссылка,
	|	ВТФизЛицо.ДатаРождения КАК ДатаРождения,
	|	ВТФизЛицо.ИНН КАК ИНН,
	|	ВТФизЛицо.Пол КАК Пол,
	|	ВТФизЛицо.Наименование КАК Наименование,
	|	ФИОФизЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизЛицСрезПоследних.Отчество КАК Отчество,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан,
	|	ДокументыФизическихЛицСрезПоследних.КодПодразделения КАК КодПодразделения
	|ИЗ
	|	ВТФизЛицо КАК ВТФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ТекущаяДатаСеанса, Физлицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
	|		ПО ВТФизЛицо.Ссылка = ФИОФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(&ТекущаяДатаСеанса, Физлицо = &ФизЛицо) КАК ДокументыФизическихЛицСрезПоследних
	|		ПО ВТФизЛицо.Ссылка = ДокументыФизическихЛицСрезПоследних.Физлицо
	|			И (ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("ТекущаяДатаСеанса", ТекущаяДатаСеанса());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
	
		СведенияОФизЛице = МашиночитаемыеДоверенности.НовыеДанныеФизЛица();

		СведенияОФизЛице.ДатаРождения      = Выборка.ДатаРождения;
		СведенияОФизЛице.ИНН               = Выборка.ИНН;
		СведенияОФизЛице.Пол               = Строка(Выборка.Пол);
		СведенияОФизЛице.ФИО               = Выборка.Наименование;
		
		СведенияОФизЛице.Фамилия           = Выборка.Фамилия;
		СведенияОФизЛице.Имя               = Выборка.Имя;
		СведенияОФизЛице.Отчество          = Выборка.Отчество;
		
		СведенияОФизЛице.Серия			   = Выборка.Серия;
		СведенияОФизЛице.Номер			   = Выборка.Номер;
		СведенияОФизЛице.ДатаВыдачи		   = Выборка.ДатаВыдачи;
		СведенияОФизЛице.КемВыдан		   = Выборка.КемВыдан;
		СведенияОФизЛице.КодПодразделения  = Выборка.КодПодразделения;
		
		ЗаполнитьЗначенияСвойств(Сведения, СведенияОФизЛице);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
