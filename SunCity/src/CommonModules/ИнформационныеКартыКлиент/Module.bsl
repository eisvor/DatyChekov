////////////////////////////////////////////////////////////////////////////////
// ИнформационныеКартыКлиент содержит процедуры и функции 
// для интерактивной работы с информационными картами.
//  
////////////////////////////////////////////////////////////////////////////////


#Область ПрограммныйИнтерфейс

// Вызывает вопрос о подтверждении создания информационной карты.
//
// Параметры:
//  ОписаниОповещения - ОписаниеОповещения - оповещение вызываемое после ответа на вопрос.
//  ТекстВопросаНовойКарты - Строка - текст вопроса пользователю.
//
// Возвращаемое значение:
//  Булево - Истина, если пользователь подтвердил создание карты, Ложь - иначе.
//
Процедура ПолучитьРазрешениеПользователяНаСозданиеКарты(ОписаниеОповещения, ТекстВопросаНовойКарты) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТекстВопросаНовойКарты) Тогда
		ТекстВопросаНовойКарты = НСтр("ru = 'Создать новую информационную карту?'")
	КонецЕсли;
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопросаНовойКарты, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры 

// Подготавливает текст оповещения о создании дисконтной карты
// и вызывает окно оповещения пользователя.
//
// Параметры:
//  ИнформационнаяКарта - СправочникСсылка.ИнформационныеКарты - ссылка на созданную карту.
//
Процедура ОповеститьОСозданииНовойКарты(ИнформационнаяКарта) Экспорт
	
	Если ЗначениеЗаполнено(ИнформационнаяКарта) Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создана информационная карта'"),
			,
			СтрЗаменить(НСтр("ru = 'Создана информационная карта ""%Карта%""'"), "%Карта%", ИнформационнаяКарта),
			БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
		
КонецПроцедуры

// Подготавливает параметры открытия формы
// и открывает форму опроса владельца дисконтной карты
// если на указанную дату нужно проводить опрос.
//
// Параметры:
//		Карта - СправочникСсылка.ИнформационныеКарты - ссылка на проверяемую карту.
//		ДатаСобытия - Дата - дата проверки.
//
Процедура ПровестиОпросВладельца(Карта, ДатаСобытия) Экспорт
	
	НуженОпрос = ИнформационныеКартыВызовСервера.НеобходимостьОпросаВладельца(Карта, ДатаСобытия);
	
	Если НуженОпрос Тогда
		// &ЗамерПроизводительности
		ОценкаПроизводительностиРТКлиент.НачатьЗамер(
			Истина, "Справочник.ИнформационныеКарты.Форма.ФормаОпросаПоДисконтнойКарте.Открытие");
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Карта", Карта);
		
		ОткрытьФорму("Справочник.ИнформационныеКарты.Форма.ФормаОпросаПоДисконтнойКарте", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры // ПровестиОпросВладельца()

// Обработка оповещения выбора шаблона для создания дисконтной карты.
//
// Параметры:
//  Результат - Структура - структура результата выполнения процедуры оповещения.
//  ДополнительныеПараметры - Структура - дополнительные параметры выполнения процедуры.
//
Процедура ВыборШаблонаСозданияДисконтнойКарты(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		// &ЗамерПроизводительности
		ОценкаПроизводительностиРТКлиент.НачатьЗамер(
			Истина, "РегистрСведений.ШаблоныРегистрацииНовыхКарт.Форма.ШаблоныНовыхКарт.Открытие");
		ОбработчикОповещения = Новый ОписаниеОповещения("СоздатьДисконтнуюКарту", ЭтотОбъект, ДополнительныеПараметры);
		РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("АдресШаблоновВХранилище", ДополнительныеПараметры.СтруктураПараметровКлиента.РегистрацияНовойКартыВыборШаблона);
		ОткрытьФорму("РегистрСведений.ШаблоныРегистрацииНовыхКарт.Форма.ШаблоныНовыхКарт", ПараметрыОткрытия, , , , , ОбработчикОповещения, РежимОткрытия);
		
	КонецЕсли;
	
КонецПроцедуры

// Создает дисконтную карту по шаблону.
// После чего вызывает обработку данных по изменению карты в форме.
//
// Параметры:
//  Результат - Структура - структура результата выполнения процедуры оповещения.
//  ДополнительныеПараметры - Структура - дополнительные параметры выполнения процедуры.
//
Процедура СоздатьДисконтнуюКарту(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Форма = ДополнительныеПараметры.Форма;
		СтруктураПараметровКлиента = ДополнительныеПараметры.СтруктураПараметровКлиента;
		
		СтруктураКарты = Неопределено;
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			
			СтруктураКарты = ИнформационныеКартыВызовСервера.НоваяДисконтнаяКарта(СтруктураПараметровКлиента.РегистрацияНовойКарты);
			
		ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
			Если СтруктураПараметровКлиента.Свойство("РегистрацияНовойКартыВыборШаблона")
				И Результат.Свойство("КодКарты")
				И Результат.Свойство("НаименованиеШаблона") Тогда
				МассивШаблонов = ПолучитьИзВременногоХранилища(СтруктураПараметровКлиента.РегистрацияНовойКартыВыборШаблона);
				Для Каждого СтрокаШаблона Из МассивШаблонов Цикл
					Если СтрокаШаблона.НаименованиеШаблона = Результат.НаименованиеШаблона Тогда
						СтруктураКарты = ИнформационныеКартыВызовСервера.НоваяДисконтнаяКарта(СтрокаШаблона);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураКарты <> Неопределено
			И ЗначениеЗаполнено(СтруктураКарты.Карта) Тогда
			
			СтруктураПараметров = ПодключаемоеОборудованиеРТКлиент.СтруктураДанныхПоиска();
			СтруктураПараметров.ЗначенияПоиска.Добавить(СтруктураКарты);
			Если ДополнительныеПараметры.Свойство("ЛогироватьСозданиеКарты") Тогда
				СтруктураПараметров.Вставить("ЛогироватьСозданиеКарты", ДополнительныеПараметры.ЛогироватьСозданиеКарты);
			КонецЕсли;
			
			СтруктураПараметров.НеизвестныеДанныеПО = Ложь;
			Если Форма.ИмяФормы = "Справочник.ИнформационныеКарты.Форма.ФормаСписка"
				ИЛИ Форма.ИмяФормы = "Справочник.ИнформационныеКарты.Форма.ФормаВыбора"
				ИЛИ Форма.ИмяФормы = "Документ.ЗаменаДисконтныхКарт.Форма.ФормаДокумента"
				ИЛИ Форма.ИмяФормы = "Документ.ИнформационнаяРассылка.Форма.ФормаДокумента"
				ИЛИ Форма.ИмяФормы = "Документ.НачислениеИСписаниеБонусныхБаллов.Форма.ФормаДокумента"
				ИЛИ Форма.ИмяФормы = "Обработка.РМКУправляемыйРежим.Форма.ФормаВыбораДисконтнойКарты" 
				ИЛИ Форма.ИмяФормы = "Обработка.РМКУправляемыйРежим.Форма.ФормаДокументаЗаменаОднойКарты" 
				ИЛИ Форма.ИмяФормы = "Обработка.РМКУправляемыйРежим.Форма.ФормаПросмотраНакоплений" 
				Тогда
				Форма.ОбработатьДанныеПоКодуСервер(СтруктураПараметров);
			Иначе
				Форма.ПродолжитьОбработатьДанныеПоКодуКлиент(СтруктураПараметров);
			КонецЕсли;
			
			Форма.ОбработатьДанныеПоКодуКлиент(СтруктураПараметров);
			ОповеститьОСозданииНовойКарты(СтруктураКарты.Карта);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
