////////////////////////////////////////////////////////////////////////////////
// РаботаСФормуламиКлиентСервер содержит процедуры и функции для работы 
// с формулами и обработки действий пользователя с формулами.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Преобразует операнд в строку для вставки в формулу.
//
// Параметры:
//  Операнд - Произвольный - операнд для вставки в строку.
//
// Возвращаемое значение:
//  Строка - текст операнда для вставки.
//
Функция ТекстОперандаДляВставки(Операнд) Экспорт
	
	Возврат "[" + Операнд + "]";
	
КонецФункции // ПолучитьТекстОперандаДляВставки()

// Выполняет проверку формулы.
//
// Параметры:
//  Формула - Строка - формула для проверки строкой.
//  Операнды - Массив - массив операндов для вставки в формулу.
//  СообщениеОбОшибке - Строка - сообщение об ошибке выполнения операции.
// 
// Возвращаемое значение:
//  Булево - результат выполнения операции.
//
Функция ПроверитьФормулу(Формула, Операнды, Знач Поле = "", Знач СообщениеОбОшибке = "") Экспорт
	
	Результат = Истина;
	
	ТекстРасчета = Формула;
	Если ЗначениеЗаполнено(ТекстРасчета) Тогда
		
		Для Каждого Операнд Из Операнды Цикл
			ТекстРасчета = СтрЗаменить(ТекстРасчета, ТекстОперандаДляВставки(Операнд), " 1 ");
		КонецЦикла;
		
		Попытка
			РезультатРасчета = РаботаСФормуламиВызовСервера.ВычислитьФормулу(ТекстРасчета);
		Исключение
		
			Результат = Ложь;
			
			ТекстСообщения = НСтр("ru='В формуле обнаружены ошибки'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Формула");
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПроверитьФормулу()

#КонецОбласти
