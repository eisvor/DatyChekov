////////////////////////////////////////////////////////////////////////////////
// Модуль "УправлениеПользователями", содержит процедуры и функции для 
// управления пользователями.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроцедурыУправленияДополнительнымиПравамиПользователей

// Возвращает список значений права, установленных для пользователя.
// Если количество значений меньше количество доступных ролей, то возвращается значение по умолчанию.
//
// Параметры:
//  Право               - ПланВидовХарактеристикСсылка.ПраваПользователей - право, для которого определяются значения.
//  ЗначениеПоУмолчанию - Произвольный - значение по умолчанию для передаваемого права (возвращается в случае
//                                       отсутствия значений в регистре сведений).
//  ИспользоватьКэш     - Булево - признак использования кэша.
//
// Возвращаемое значение:
//  СписокЗначений - список всех значений, установленных наборам прав (ролям), доступных пользователю.
//
Функция ЗначениеПраваДляТекущегоПользователя(Право, ЗначениеПоУмолчанию = Неопределено, ИспользоватьКэш = Истина) Экспорт
	
	Если ИспользоватьКэш Тогда
		КэшДополнительныхПрав = глЗначениеПеременной("ЗначенияДополнительныхПравПользователя");
		ЗначениеПрава = КэшДополнительныхПрав[Право];
		Если ЗначениеПрава = Неопределено Тогда
			ЗначениеПрава = ПрочитатьЗначениеПраваДляТекущегоПользователя(Право, ЗначениеПоУмолчанию);
			КэшДополнительныхПрав[Право] = ЗначениеПрава;
			#Если Сервер Тогда
				глЗначениеПеременнойУстановить("ЗначенияДополнительныхПравПользователя", КэшДополнительныхПрав, Истина);
			#КонецЕсли
		КонецЕсли;
	Иначе
		ЗначениеПрава = ПрочитатьЗначениеПраваДляТекущегоПользователя(Право, ЗначениеПоУмолчанию);
	КонецЕсли;
	Возврат ЗначениеПрава;
	
КонецФункции // ЗначениеПраваДляТекущегоПользователя()

// Читает право текущего пользователя.
//
// Параметры:
//  Право               - ПланВидовХарактеристикСсылка.ПраваПользователей - право, для которого определяются значения.
//  ЗначениеПоУмолчанию - Произвольный - значение по умолчанию для передаваемого права (возвращается в случае
//                                       отсутствия значений в регистре сведений).
//
// Возвращаемое значение:
//  СписокЗначений - список всех значений, установленных наборам прав (ролям), доступных пользователю.
//
Функция ПрочитатьЗначениеПраваДляТекущегоПользователя(Право, ЗначениеПоУмолчанию)

	ВозвращаемыеЗначения = Новый СписокЗначений;
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("Пользователь"     , Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("ПравоПользователя", Право);

	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	РегистрЗначениеПрав.Значение
	               |ИЗ
	               |	РегистрСведений.ЗначенияДополнительныхПравПользователя КАК РегистрЗначениеПрав
	               |ГДЕ
	               |	РегистрЗначениеПрав.Право = &ПравоПользователя
	               |	И РегистрЗначениеПрав.Пользователь В
	               |			(ВЫБРАТЬ
	               |				ПользователиГруппы.Ссылка КАК Ссылка
	               |			ИЗ
	               |				Справочник.ГруппыПользователей.Состав КАК ПользователиГруппы
	               |			ГДЕ
	               |				ПользователиГруппы.Пользователь = &Пользователь
	               |		
	               |			ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |			ВЫБРАТЬ
	               |				ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	               |		
	               |			ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |			ВЫБРАТЬ
	               |				&Пользователь)";
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Количество() = 0 Тогда
		ВозвращаемыеЗначения.Добавить(ЗначениеПоУмолчанию);
	Иначе
		Пока Выборка.Следующий() Цикл
			ВозвращаемыеЗначения.Добавить(Выборка.Значение);
		КонецЦикла;
	КонецЕсли;

	Возврат ВозвращаемыеЗначения;

КонецФункции // ПрочитатьЗначениеПраваДляТекущегоПользователя()

// Возвращает булево значение права текущего пользователя.
//
// Параметры:
//  Право               - ПланВидовХарактеристикСсылка.ПраваПользователей - право, для которого определяются значения.
//  ЗначениеПоУмолчанию - Произвольный - значение по умолчанию для передаваемого права (возвращается в случае
//                                       отсутствия значений в регистре сведений).
//  Пользователь        - СправочникСсылка.Пользователи - пользователь информационной базы.
//
// Возвращаемое значение:
//  Булево - булево значения права текущего пользователя.
//
Функция ПолучитьБулевоЗначениеПраваПользователя(Право, ЗначениеПоУмолчанию = Ложь, Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено
	 ИЛИ Пользователь = Пользователи.ТекущийПользователь() Тогда
		Список = УправлениеПользователями.ЗначениеПраваДляТекущегоПользователя(Право, ЗначениеПоУмолчанию, Ложь);
	Иначе
		Список = УправлениеПользователями.ПолучитьЗначениеПраваДляПользователя(Право, ЗначениеПоУмолчанию, Пользователь);
	КонецЕсли;
	
	Если (Список.Количество() = 0) ИЛИ (Список.Количество() > 1) Тогда
		Возврат Истина;
	Иначе
		Возврат Список[0].Значение;
	КонецЕсли;
	
КонецФункции 

// Возвращает булево значение права текущего пользователя с учетом изменения прав.
// При каждом вызове функции определяется пользователь, поэтому если есть необходимость получения нескольких прав,
// то лучше заранее получить пользователя и использовать функцию ПолучитьБулевоЗначениеПраваПользователя.
//
// Параметры:
//  Право               - ПланВидовХарактеристикСсылка.ПраваПользователей - право, для которого определяются значения.
//  ЗначениеПоУмолчанию - Произвольный - значение по умолчанию для передаваемого права (возвращается в случае
//                                       отсутствия значений в регистре сведений).
//
// Возвращаемое значение:
//  Булево - булево значения права текущего пользователя.
//
Функция ПолучитьБулевоЗначениеПраваПользователяСУчетомИзмененныхПрав(Право, ЗначениеПоУмолчанию = Ложь) Экспорт
	
	Пользователь = ОбщегоНазначенияРТКлиентСервер.ПользовательСУчетомИзмененныхПрав();
	
	Если Пользователь = Неопределено
	 ИЛИ Пользователь = Пользователи.ТекущийПользователь() Тогда
		Список = УправлениеПользователями.ЗначениеПраваДляТекущегоПользователя(Право, ЗначениеПоУмолчанию, Ложь);
	Иначе
		Список = УправлениеПользователями.ПолучитьЗначениеПраваДляПользователя(Право, ЗначениеПоУмолчанию, Пользователь);
	КонецЕсли;
	
	Если (Список.Количество() = 0) ИЛИ (Список.Количество() > 1) Тогда
		Возврат Истина;
	Иначе
		Возврат Список[0].Значение;
	КонецЕсли;
	
КонецФункции 

// Возвращает список значений права, установленных для пользователя.
// Если количество значений меньше количество доступных ролей, то возвращается значение по умолчанию.
//
// Параметры:
//  Право               - ПланВидовХарактеристикСсылка.ПраваПользователей - право, для которого определяются значения.
//  ЗначениеПоУмолчанию - Произвольный - значение по умолчанию для передаваемого права (возвращается в случае
//                                       отсутствия значений в регистре сведений).
//
// Возвращаемое значение:
//  СписокЗначений - список всех значений, установленных наборам прав (ролям), доступных пользователю.
//
Функция ПолучитьЗначениеПраваДляПользователя(Право, ЗначениеПоУмолчанию = Неопределено, Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВозвращаемыеЗначения = Новый СписокЗначений;
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("Пользователь"     , Пользователь);
	Запрос.УстановитьПараметр("ПравоПользователя", Право);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РегистрЗначениеПрав.Значение
	|ИЗ
	|	РегистрСведений.ЗначенияДополнительныхПравПользователя КАК РегистрЗначениеПрав
	|ГДЕ
	|	РегистрЗначениеПрав.Право = &ПравоПользователя
	|	И РегистрЗначениеПрав.Пользователь = &Пользователь";
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Количество() = 0 Тогда
		ВозвращаемыеЗначения.Добавить(ЗначениеПоУмолчанию);
	Иначе
		Пока Выборка.Следующий() Цикл
			ВозвращаемыеЗначения.Добавить(Выборка.Значение);
		КонецЦикла;
	КонецЕсли;

	Возврат ВозвращаемыеЗначения;

КонецФункции // ЗначениеПраваДляТекущегоПользователя()

#КонецОбласти

#Область ПроцедурыДополнительныхПрав

// Устанавливает доступность.
//
// Параметры:
//  Доступность - Булево - доступность элемента.
//  Право       - ПланВидовХарактеристикСсылка.ПраваПользователей - право, для которого определяются значения.
//
Процедура УстановитьДоступностьДляРеквизитовТабличнойЧасти(Доступность, Право) Экспорт
	
	Доступность = Доступность И УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(Право, Ложь);
	
КонецПроцедуры // УстановитьДоступностьДляРеквизитовТабличнойЧасти()

// Устанавливает доступность.
//
// Параметры:
//  ТолькоПросмотр - Булево - ТолькоПросмотр элемента.
//  Право          - ПланВидовХарактеристикСсылка.ПраваПользователей - право, для которого определяются значения.
//
Процедура УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти(ТолькоПросмотр, Право) Экспорт
	
	ТолькоПросмотр = ТолькоПросмотр ИЛИ НЕ УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(Право, Ложь);
	
КонецПроцедуры // УстановитьТолькоПросмотрДляРеквизитовТабличнойЧасти()

#КонецОбласти

#КонецОбласти