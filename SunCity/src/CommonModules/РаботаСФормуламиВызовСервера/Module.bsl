////////////////////////////////////////////////////////////////////////////////
// РаботаСФормуламиВызовСервера содержит процедуры и функции для работы 
// с формулами и обработки действий пользователя с формулами.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует пустое дерево операторов.
// 
// Возвращаемое значение:
//  ДеревоЗначений - пустое дерево операторов.
//
Функция ПолучитьПустоеДеревоОператоров() Экспорт
	
	Дерево = Новый ДеревоЗначений();
	Дерево.Колонки.Добавить("Наименование");
	Дерево.Колонки.Добавить("Оператор");
	Дерево.Колонки.Добавить("Сдвиг", Новый ОписаниеТипов("Число"));
	
	Возврат Дерево;
	
КонецФункции

// Добавляет группу операторов в строки дерева.
//
// Параметры:
//  Дерево - ДеревоЗначений - дерево в которое необходимо добавить группу операторов.
//  Наименование - Строка - наименование группы операторов.
//
// Возвращаемое значение:
//  СтрокаДереваЗначений - добавленная строка дерева значений.
//
Функция ДобавитьГруппуОператоров(Дерево, Наименование) Экспорт
	
	НоваяГруппа = Дерево.Строки.Добавить();
	НоваяГруппа.Наименование = Наименование;
	
	Возврат НоваяГруппа;
	
КонецФункции

// Добавляет оператор в строки дерева
// или в строки строки дерева.
//
// Параметры:
//  Дерево - ДеревоЗначений - дерево в которое необходимо добавить группу операторов.
//  Наименование - Строка - наименование группы операторов.
//
// Возвращаемое значение:
//  СтрокаДереваЗначений - добавленная строка дерева значений.
//
Функция ДобавитьОператор(Дерево, Родитель, Наименование, Оператор = Неопределено, Сдвиг = 0) Экспорт
	
	НоваяСтрока = ?(Родитель <> Неопределено, Родитель.Строки.Добавить(), Дерево.Строки.Добавить());
	НоваяСтрока.Наименование = Наименование;
	НоваяСтрока.Оператор = ?(ЗначениеЗаполнено(Оператор), Оператор, Наименование);
	НоваяСтрока.Сдвиг = Сдвиг;
	
	Возврат НоваяСтрока;
	
КонецФункции

// Формирует стандартное дерево операторов.
// 
// Возвращаемое значение:
//  ДеревоЗначение - стандартное дерево операторов.
//
Функция ПолучитьСтандартноеДеревоОператоров() Экспорт
	
	Дерево = ПолучитьПустоеДеревоОператоров();
	ГруппаОператоров = ДобавитьГруппуОператоров(Дерево, "Операторы");
	ДобавитьОператор(Дерево, ГруппаОператоров, "+", " + ");
	ДобавитьОператор(Дерево, ГруппаОператоров, "-", " - ");
	ДобавитьОператор(Дерево, ГруппаОператоров, "*", " * ");
	ДобавитьОператор(Дерево, ГруппаОператоров, "/", " / ");
	
	Возврат Дерево;
	
КонецФункции

// Получает результат вычисления
// по заданному тексту формулы.
//
// Параметры:
//  ТекстРасчета - Строка - формула для вычисления.
//
// ВозвращаемоеЗначение:
//  Число - результат вычисления по формуле.
//
Функция ВычислитьФормулу(ТекстРасчета) Экспорт
	
	Возврат ОбщегоНазначения.ВычислитьВБезопасномРежиме(ТекстРасчета);
	
КонецФункции

#КонецОбласти
