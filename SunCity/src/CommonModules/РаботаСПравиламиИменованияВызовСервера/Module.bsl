////////////////////////////////////////////////////////////////////////////////
// РаботаСПравиламиИменованияВызовСервера содержит процедуры и функции для работы 
// с правилами именования и обработки действий пользователя с именованиями.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет все возможные наборы дополнительных реквизитов и сведений, которые могут участвовать в правилах именования номенклатуры.
//
// Параметры:
//	НаборыСвойств - СписокЗначений - список для заполнения.
//
Процедура ЗаполнитьНаборыСвойств(НаборыСвойств) Экспорт
	
	РаботаСПравиламиИменования.ЗаполнитьНаборыСвойств(НаборыСвойств);
			
КонецПроцедуры

// Возвращает значение функциональной опции "Использовать правила именования номенклатуры".
//
// Возвращаемое значение:
//  Булево - признак использования правил именования.
//
Функция ИспользоватьПравилаИменования() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьПравилаИменованияНоменклатуры");
	
КонецФункции

// Возвращает значения параметров именования номенклатуры.
//
// Параметры:
//	ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - Вид номенклатуры для выборки данных.
//	ПараметрыПравилаИменования - ДанныеФормыКоллекция - содержит таблицу параметров именования. 
//	Номенклатура - СправочникСсылка.Номенклатура - Номенклатура по которой выбираются данные. Если Неопределено, то данные выбираются по всему виду номенклатуры.
//
// Возвращаемое значение:
//  Массив - массив значений параметров именования номенклатуры.
//
Функция ЗначенияПараметровНоменклатуры(ВидНоменклатуры, Знач ПараметрыПравилаИменования, Номенклатура = Неопределено) Экспорт
	
	Результат = РаботаСПравиламиИменования.ЗначенияПараметровНоменклатуры(ВидНоменклатуры,ПараметрыПравилаИменования,Номенклатура);
	
	КлючиСтруктуры = "";
	Для Каждого Колонка Из Результат.Колонки Цикл
		Если Колонка.Имя = "Номенклатура"
			Или Колонка.Имя = "НоменклатураНаименование" Тогда
			Продолжить;
		КонецЕсли;
		КлючиСтруктуры = ?(КлючиСтруктуры = "","",КлючиСтруктуры + ",") + Колонка.Имя;
	КонецЦикла;
	
	Выборка = Результат.Выбрать();
	МассивВозврат = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		СтруктураДанных = Новый Структура(КлючиСтруктуры);
		ЗаполнитьЗначенияСвойств(СтруктураДанных,Выборка);
		МассивВозврат.Добавить(СтруктураДанных);
	КонецЦикла;
	
	Возврат МассивВозврат;
	
КонецФункции

// Выполняет проверку заполнения структуры правила именования номенклатуры.
//
// Параметры:
//	СтруктураПравила - Структура - см. РаботаСПравиламиИменованияКлиентСервер.ПолучитьПустуюСтруктуруПравилаИменования
//
Процедура ПроверитьСтруктуруПравилаИменованияСервер(СтруктураПравила) Экспорт
	
	РаботаСПравиламиИменования.ПроверитьСтруктуруПравилаИменованияСервер(СтруктураПравила);
	
КонецПроцедуры

#КонецОбласти