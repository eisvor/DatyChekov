
#Область ПрограммныйИнтерфейс

Функция АдресДанныхПроверкиАлкогольнойПродукции(Знач ПараметрыСканирования, Знач Объект, УникальныйИдентификатор) Экспорт
	
	ДанныеХранилища = Новый Структура("ДеревоУпаковок, ТаблицаАлкогольнойПродукцииКОпределениюСправок2", Неопределено, Новый ТаблицаЗначений);
	ДанныеХранилища.ДеревоУпаковок = АкцизныеМаркиЕГАИСРТ.ШтрихкодыУпаковок(Объект, Объект.ОрганизацияЕГАИС, "НоменклатураЕГАИС", Истина).ДеревоУпаковок;
	
	Если ТипЗнч(Объект) = Тип("Структура") Тогда
		ДанныеХранилища.ТаблицаАлкогольнойПродукцииКОпределениюСправок2 = Документы.ЧекККМ.ТаблицаАлкогольнойПродукцииКОпределениюСправок2(Объект.Товары);
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		ДанныеХранилища.ТаблицаАлкогольнойПродукцииКОпределениюСправок2 = Документы.ЧекККМ.ТаблицаАлкогольнойПродукцииКОпределениюСправок2(Объект.Ссылка);
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеХранилища, УникальныйИдентификатор);
	
КонецФункции

Процедура ПриЗакрытииФормыПроверкиИПодбораВФормеРМК(Форма, Результат) Экспорт
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Результат);
	ДеревоМаркируемойПродукции = РезультатПроверки.ДеревоМаркируемойПродукции;
	
	ДанныеПроверкиИПодбора = ПолучитьИзВременногоХранилища(Результат);
	
	ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(Форма);
	
	ПараметрыШтрихкодаДляОбработки = ШтрихкодированиеИСРТ.ПараметрыШтрихкодаДляОбработки(Форма, ПараметрыСканирования);
	ПараметрыШтрихкодаДляОбработки.Вставить("ВидПродукцииИС", Перечисления.ВидыПродукцииИС.Алкогольная);
	
	ИнтеграцияИСРТ.ЗафиксироватьРезультатПроверкиИПодбора(
		ДеревоМаркируемойПродукции,
		ПараметрыШтрихкодаДляОбработки,
		Форма);
	
КонецПроцедуры

#КонецОбласти
