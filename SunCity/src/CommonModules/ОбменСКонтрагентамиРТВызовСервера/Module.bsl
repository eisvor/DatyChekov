
////////////////////////////////////////////////////////////////////////////////
// Общий модуль ОбменСКонтрагентамиВызовСервераРТ
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// См. ОбменСКонтрагентамиРТ.СоздатьНоменклатуруПоДаннымКонтрагента.
Функция СоздатьНоменклатуруПоДаннымКонтрагента(Знач НаборНоменклатурыКонтрагентов, Знач ЗначенияПоУмолчанию) Экспорт
	
	СозданнаяНоменклатура = ОбменСКонтрагентамиРТ.СоздатьНоменклатуруПоДаннымКонтрагента(
		НаборНоменклатурыКонтрагентов, ЗначенияПоУмолчанию);
	
	Возврат СозданнаяНоменклатура;
	
КонецФункции

// Возвращает входящий электронный документ на основании которого была создана счет-фактура полученная.
// Используется в случае если источником был документ ПеремещениеТоваров.
//
// Параметры:
//  СчетФактураПолученныйСсылка - ДокументСсылка.СчетФактураПолученный	 - Счет фактура полученный.
// 
// Возвращаемое значение:
//  СсылкаЭД - ссылка на найденный электронный документ.
//
Функция СсылкаЭДСчетФактураПолученныйПоПеремещению(СчетФактураПолученныйСсылка) Экспорт
	
	СсылкаЭД = Неопределено;
	
	ЕстьПеремещениеВОсновании = Ложь;
	
	ДокументыОснования = СчетФактураПолученныйСсылка.ДокументыОснования;
	Для Каждого СтрокаОснования Из ДокументыОснования Цикл
		Если ТипЗнч(СтрокаОснования.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			ЕстьПеремещениеВОсновании = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПеремещениеВОсновании Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭлектронныйДокументВходящийДокументыОснования.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЭлектронныйДокументВходящий.ДокументыОснования КАК ЭлектронныйДокументВходящийДокументыОснования
		|ГДЕ
		|	ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование = &ДокументОснование";
		
		Запрос.УстановитьПараметр("ДокументОснование", СчетФактураПолученныйСсылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СсылкаЭД = Выборка.Ссылка;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат СсылкаЭД;
	
КонецФункции

#КонецОбласти