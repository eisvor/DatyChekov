////////////////////////////////////////////////////////////////////////////////
// Подсистема "Коммерческие предложения документы".
// ОбщийМодуль.КоммерческиеПредложенияДокументыРТВызовСервера.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Получает реквизиты номенклатуры и значение признака "ХарактеристикиИспользуются".
//
// Параметры:
//  СсылкаНаОбъект - СправочникСсылка.Номенклатура - объект, значения реквизитов которого необходимо получить.
//  Реквизиты - Строка - имена реквизитов, перечисленные через запятую.
//
// Возвращаемое значение:
//  Структура - содержит имена (ключи) и значения затребованных реквизитов.
//     Если используются характеристики, то дополнительно содержит
//     значение признака "ХарактеристикиИспользуются".
//
Функция РеквизитыНоменклатуры(Знач СсылкаНаОбъект, Знач Реквизиты) Экспорт
	
	Возврат КоммерческиеПредложенияДокументыРТ.РеквизитыНоменклатурыПриИзменении(СсылкаНаОбъект, Реквизиты);
	
КонецФункции

// Получает ставки НДС для массива номенклатуры
//
// Параметры:
//  МассивНоменклатуры  - Массив - массив номенклатуры, для которой получаются ставки НДС
//
// Возвращаемое значение:
//   Соответствие:
//    * Ключ     - номенклатура
//    * Значение - ставка НДС
Функция СтавкиНДСДляМассиваНоменклатуры(МассивНоменклатуры) Экспорт
	
	ДанныеПоСтавкамНДС = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	Номенклатура.Ссылка    КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&МассивНоменклатуры)";
	
	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
	
	Выборка = Запрос.Выполнить().Выбрать();;
	
	Пока Выборка.Следующий() Цикл
	
		ДанныеПоСтавкамНДС.Вставить(Выборка.Номенклатура, Выборка.СтавкаНДС);
	
	КонецЦикла;
	
	Возврат ДанныеПоСтавкамНДС;
	
КонецФункции

#КонецОбласти