
#Область ЗаполнениеДокументовИСМПТ

// Возвращает номер документа на печать.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.<Имя> - Ссылка на документ, для которого необходимо получить номер на печать.
//
// Возвращаемое значение:
//  Строка - Номер документа, который может использоваться для печати.
//
Функция ПолучитьНомерНаПечать(ДокументСсылка) Экспорт
	
	Если ДокументСсылка.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		Если СтрНайти(ДокументСсылка.Номер,"-И-") > 0 Тогда
			Возврат ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрЗаменить(ДокументСсылка.Номер,"-И-",""), Истина, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

//Вызов из первичных документов (РТУ, ПТУ и т.д.), проверяет наличие в ТЧ Товары маркируемой продукции (по реквизитам Номенклатура.ТабачнаяПродукция, Номенклатура.ОбувнаяПродукция и т.д.)
//В зависимости от наличия такой продукции устанавливается видимость группы создания документов ИС МПТ в документах-основаниях
Функция ПроверитьНаличиеМаркированногоТовараДляВводаДокументаИСМПТ(ПервичныйДокумент, ИмяКоманды = Неопределено) Экспорт
	
	ТаблицаТовары = ПервичныйДокумент.Товары;
	
	Если ТаблицаТовары.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
			
			Если ТипЗнч(ПервичныйДокумент.Ссылка) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
								
				//Ввод Заказа КМ - входят товарные группы контуров ИС МПТ и ИС ЦЭДМ
				Если СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция")
					ИЛИ СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС") 
					ИЛИ СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты")
					ИЛИ СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция") Тогда
					ЕстьМарки = Истина;
					Прервать;
				Иначе
					ЕстьМарки = Ложь;
				КонецЕсли;
				
			ИначеЕсли Не ИмяКоманды = Неопределено
				И ИмяКоманды = "СоздатьЭкспортЕАЭС" Тогда 
				// Увед.Экспорт ЕАЭС - только для обувной продукции по схеме взаимного призанния
				Если СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция") Тогда
					ЕстьМарки = Истина;
					Прервать;
				Иначе
					ЕстьМарки = Ложь;
				КонецЕсли;
				
			Иначе
				// Ввод прочих документов ИС МПТ - исключается фарм.продукция контура ЦЭДМ
				Если СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция") 
					ИЛИ СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция") 
					ИЛИ СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС")
					ИЛИ СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция")
					ИЛИ СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность") Тогда
					ЕстьМарки = Истина;
					Прервать;
				Иначе
					ЕстьМарки = Ложь;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат ЕстьМарки;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКодСтраныДляУведомленияОввозе(СтранаОтправления) Экспорт
	
	Возврат СтранаОтправления.КодАльфа2;
	
КонецФункции

// Возвращает ссылку на элемент справочника страны мира по коду.
// Если элемент справочника не найден, то он будет создан на основании данных заполнения.
//
// Параметры:
//  КодСтраны - Строка    - Код страны, код альфа2, код альфа3.
// 
// Возвращаемое значение:
//  СправочникСсылка.СтраныМира - Ссылка на элемент справочника СтраныМиры.
//                                Если найдено несколько значений, то будет возвращено первое.
//                                Если ничего не найдено, данные заполнения не указаны, то возвращается пустая ссылка.
//
Функция ПолучитьСтрануПоКоду(КодСтраны) Экспорт
	
	// Проверка наличия страны в справочнике Страны мира и неявное добавление ее в случае отсутствия.
	СтранаМира = УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(КодСтраны);
	Возврат СтранаМира;
	
КонецФункции

Функция ПолучитьКодТНВЭДПоНоменклатуре(Номенклатура) Экспорт
	
	Возврат Строка(Номенклатура.КодТНВЭД);
	
КонецФункции

Функция ПолучитьЗначениеОрганизацииПоТипу(Организация) Экспорт
	
	Возврат Организация;
	
КонецФункции

Функция ПолучитьВидОперацииАПППооснованию(ВидОперации) Экспорт
	
	Возврат ВидОперации;	
	
КонецФункции

Функция ЗаполнитьСтрануПоУмолчаниюПризнаниеКодовЕАЭС() Экспорт
	
	//Значение по умолчанию для документа Экспорта ЕАЭС
	Возврат Справочники.СтраныМира.Россия;
		
Конецфункции

Функция ЭтоФормированиеУведомленияОВвозеИзТретьихСтран(ДокументОснование, ТипДокументаИСМПТ = Неопределено) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров") 
		И ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСМПТК") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

//Универсальная функция для перезаполнения некоторых сведений в документе ИС МПТ по выбранному в нем документу-основанию
//
Функция ПолучитьДанныеДокументаОснованияДляПерезаполненияИСМПТ_Общий(ДокументОснование, ДокументИСМПТ) Экспорт
	
	ИмяДокументаОснования = ПолучитьИмяДокументаДляПроверкиПоТипу(ДокументОснование);
	ТекстЗапроса = ТекстЗапросаОбщиеДанныеОснования("ИСМПТ", ИмяДокументаОснования);
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументов", ДокументОснование);
	
	РезультатЗапроса  = Запрос.ВыполнитьПакет();
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	ТаблицаРеквизиты = РезультатЗапроса[КоличествоПакетов - 3].Выгрузить();
	Реквизиты = Новый Структура();
	
	Для Каждого Строка Из ТаблицаРеквизиты Цикл
		
		Реквизиты.Вставить("Организация", Строка.Организация);
		Если Не ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК()
			И Не ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСМПТК() Тогда
			
			Реквизиты.Вставить("Поставщик", Строка.Поставщик);
			Реквизиты.Вставить("ПоставщикИдентификационныйНомер", Строка.ПоставщикИдентификационныйНомер);
			Реквизиты.Вставить("ПоставщикНаименование", Строка.ПоставщикНаименование);
		Иначе
			//У ввоза заполнение наоборот
			Реквизиты.Вставить("Получатель", Строка.Поставщик);
			Реквизиты.Вставить("ПолучательИдентификационныйНомер", Строка.ПоставщикИдентификационныйНомер);
			Реквизиты.Вставить("ПолучательНаименование", Строка.ПоставщикНаименование);
			Реквизиты.Вставить("Контрагент", Строка.Контрагент);
			Реквизиты.Вставить("Поставщик",  Строка.Получатель);
			Реквизиты.Вставить("ПоставщикИдентификационныйНомер", Строка.ПолучательИдентификационныйНомер);
			Реквизиты.Вставить("ПоставщикНаименование", Строка.ПолучательНаименование);
		КонецЕсли;
		
		Если Не ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВыводеИзОборотаИСМПТК()
			И Не ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВводеВОборотИСМПТК() 
			И Не ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК()
			И Не ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСМПТК() Тогда
			Реквизиты.Вставить("Контрагент", Строка.Контрагент);
			Реквизиты.Вставить("Получатель", Строка.Получатель);
			Реквизиты.Вставить("ПолучательИдентификационныйНомер", Строка.ПолучательИдентификационныйНомер);
			Реквизиты.Вставить("ПолучательНаименование", Строка.ПолучательНаименование);
		КонецЕсли;
		
		Если ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК()
			ИЛИ ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСМПТК() Тогда
			Реквизиты.Вставить("СтранаОтправления", ПолучитьСтрануОтправленияВвозИзЕАЭС(Строка.Контрагент));
		ИначеЕсли ДокументИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбЭкспортеЕАЭСИСМПТК() Тогда
			Реквизиты.Вставить("СтранаПолучатель",  ПолучитьСтрануОтправленияВвозИзЕАЭС(Строка.Контрагент));
		КонецЕсли;
		
		Реквизиты.Вставить("ДокументОснование", Строка.ДокументОснование);
		
	КонецЦикла;
	
	ТаблицаТовары = РезультатЗапроса[КоличествоПакетов - 2].Выгрузить(); 
	Товары = Новый Массив();
	
	Для Каждого Строка Из ТаблицаТовары Цикл
		
		СтрокаТовары = Новый Структура();
		СтрокаТовары.Вставить("Номенклатура", 	    Строка.Номенклатура);
		СтрокаТовары.Вставить("КоличествоИсточник", Строка.КоличествоИсточник);
		СтрокаТовары.Вставить("ЕдиницаИзмерения",   Строка.ЕдиницаИзмеренияБазовая);
		СтрокаТовары.Вставить("ВидПродукции",	    Строка.ВидПродукции);
		Товары.Добавить(СтрокаТовары);
		
	КонецЦикла;
			
	ДанныеДокументаОснования = Новый Структура();
	ДанныеДокументаОснования.Вставить("Реквизиты", Реквизиты);
	ДанныеДокументаОснования.Вставить("Товары",	   Товары);
		
	Возврат ДанныеДокументаОснования;
	
КонецФункции

#КонецОбласти

#Область ДокументыОснования

Процедура ОчиститьЗначенияРеквизитовИСМПТВПервичномДокументе(Форма) Экспорт
	
	Если ЗначениеЗаполнено(Форма.СписокДокументовИСМПТ) Тогда
		Для Каждого ДокументИСМПТ Из Форма.СписокДокументовИСМПТ Цикл
			ОтсоединитьДокументИСМПТ(ДокументИСМПТ);
		КонецЦикла;
	ИначеЕсли ЗначениеЗаполнено(Форма.ДокументИСМПТ) Тогда
		ОтсоединитьДокументИСМПТ(Форма.ДокументИСМПТ);
	КонецЕсли;		
	
	Форма.ДокументИСМПТ = Неопределено;
	Форма.СписокДокументовИСМПТ = Неопределено;
	Форма.НадписьДокументИСМПТ = "";
	
КонецПроцедуры

Процедура ОтсоединитьДокументИСМПТ(ДокументИСМПТ)
	
	ДокументОбъект = ДокументИСМПТ.ПолучитьОбъект();
	ДокументОбъект.ДокументОснование = Неопределено;
	ДокументОбъект.Записать();
	
КонецПроцедуры

Функция ИмяГруппыДляВключенияИСМПТ(ДанныеФормы) Экспорт
	
	Если ДанныеФормы.Элементы.Найти("ГруппаПодвалЛевая") <> Неопределено Тогда
		Возврат "ГруппаПодвалЛевая";
	ИначеЕсли ДанныеФормы.Элементы.Найти("Подвал") <> Неопределено Тогда 
		Возврат "Подвал";
	ИначеЕсли ДанныеФормы.Элементы.Найти("ГруппаКомментарийАвтор") <> Неопределено Тогда 
		Возврат "ГруппаКомментарийАвтор";
	Иначе
		Возврат "ГруппаИнтеграция";
	КонецЕсли;
	
КонецФункции

Функция ИмяПослеГруппыИСМПТ(Форма, ГруппаДляВключенияМеню) Экспорт
	
	Возврат "";
	
КонецФункции

Функция ПолучитьСтрануОтправленияВвозИзЕАЭС(Контрагент) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	Контрагенты.СтранаРегистрации КАК СтранаРегистрации
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.СтранаРегистрации;
	КонецЦикла;
	
	Возврат Справочники.СтраныМира.ПустаяСсылка();
	
КонецФункции

//С помощью данной функции можно скрывать в формах документов ИС МПТ и ИС ЦЭДМ группу Отражение в учете,
//если конфигурация не поддерживает ввод на основании для данного документа.
Функция ПроверитьВозможностьОтраженияДокументаВУчете(ИмяДокумента) Экспорт
	
	Если ИмяДокумента = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАгрегацияВнеПроизводстваИСЦЭДМ() 
		Или ИмяДокумента = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАгрегацияКодовМаркировкиСУЗИСМПТК()
		Или ИмяДокумента = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаОтчетОПередачеКИОтНерезидентаРКИСЦЭДМ() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

#Область ПроверкаОснований

Функция ПроверитьДокументыОснованияПриСозданииИСМПТ(МассивДокументов, ТипДокумента, ЭтоВыборАПП = Ложь, ИмяКоманды = "") Экспорт
	
	Если ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Если ЭтоВыборАПП Тогда
			//При установке связи Вход.АПП + ПТУ
			Возврат ПроверитьОснованияДокументаИСМПТОбщая("АктПриемаПередачи_ПТУ", МассивДокументов);
		Иначе
			Возврат ПроверитьОснованияДокументаИСМПТОбщая("ВвозТоваров", МассивДокументов);
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		И ЭтоВыборАПП Тогда
		
		//При установке связи Вход.АПП + Возврат от клиента
		Возврат ПроверитьОснованияДокументаИСМПТОбщая("АктПриемаПередачи_Возврат", МассивДокументов);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		//Из РТУ могут быть созданы 2 документа маркировки: АПП и Увед.о выводе из оборота (интернет-торговля).
		//Т.к. ограничения по виду опрерации РТУ у них одинаковые, проверяем общей проверкой.
		
		Возврат ПроверитьОснованияДокументаИСМПТОбщая("АктПриемаПередачиИсх_РТУ", МассивДокументов);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		//При создании АПП исход по Возврату поставщику
		Возврат ПроверитьОснованияДокументаИСМПТОбщая("АктПриемаПередачиИсх_Возврат", МассивДокументов);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				
		Возврат ПроверитьОснованияЗаказКМ(МассивДокументов);
		
	Иначе
		
		//Для Оприходования и Списания проверки не выполняем, т.к. там нет вида операции
		Возврат Новый Массив();
		
	КонецЕсли;
   	
КонецФункции

Функция ПроверитьОснованияЗаказКМ(МассивДокументов)
	
	МассивДокументовПроверки = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказПоставщику.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Ссылка В(&Основание)
	|	И НЕ(ЗаказПоставщику.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеТоваров)
	|				ИЛИ ЗаказПоставщику.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту))";
	
	Запрос.УстановитьПараметр("Основание", МассивДокументов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МассивДокументовПроверки.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат МассивДокументовПроверки;
		
КонецФункции

Функция ПроверитьОснованияАППИсходРеализация(МассивДокументов)
	
	МассивДокументовПроверки = Новый Массив();
	Возврат МассивДокументовПроверки;
	
КонецФункции

Функция ПроверитьОснованияАППИсходВозврат(МассивДокументов)
	
	МассивДокументовПроверки = Новый Массив();
	Возврат МассивДокументовПроверки;
	
КонецФункции

Функция ПроверитьОснованияВвозТоваров(МассивДокументов)
	
	МассивДокументовПроверки = Новый Массив();
	Возврат МассивДокументовПроверки;
	
КонецФункции

Функция ПроверитьОснованияАППВходПоПТУ(МассивДокументов)
	
	Возврат ПроверитьОснованияДокументаИСМПТОбщая("АктПриемаПередачи_ПТУ", МассивДокументов);
	
КонецФункции

Функция ПроверитьОснованияАППВходПоВозврату(МассивДокументов)
	
	Возврат ПроверитьОснованияДокументаИСМПТОбщая("АктПриемаПередачи_Возврат", МассивДокументов);
	
КонецФункции

Функция ПроверитьОснованияДокументаИСМПТОбщая(ИмяДокументаИСМПТ, МассивДокументов)
	
	МассивДокументовПроверки = Новый Массив();
	Возврат МассивДокументовПроверки;
	
КонецФункции

Функция ПроверитьВидВвозаИзОснованияПТУ(ПТУ) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СсылкаНаИСМПТ

Процедура ОбновитьРеквизитыИСМПТ(ДанныеФормы, РеквизитНадписьИСМПТ = "НадписьДокументИСМПТ", ТребуетсяИСМПТ = Истина, ГруппаДляВключенияМеню = Неопределено, ПослеГруппы = "") Экспорт
	
	Если ПолучитьДанныеИСМПТ(ДанныеФормы, ГруппаДляВключенияМеню, ПослеГруппы) Тогда 
		ЗаполнитьТекстПроИСМПТ(ДанныеФормы, РеквизитНадписьИСМПТ, ТребуетсяИСМПТ);
		УправлениеГруппойИСМПТ(ДанныеФормы, ТребуетсяИСМПТ);
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьДанныеИСМПТ(ДанныеФормы, ГруппаДляВключенияМеню, ПослеГруппы = "") Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемойПродукцииИСМПТК") Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	//1.Создаем элементы маркировки на форме документа-основания
	Если ДанныеФормы.Элементы.Найти("НадписьДокументИСМПТ") = Неопределено Тогда
		Если ГруппаДляВключенияМеню = Неопределено 
			И НЕ ДанныеФормы.Элементы.Найти("ГруппаПрочиеЭлектронныеДокументы") = Неопределено Тогда
			//Используем группу формы по умолчанию
			СоздатьЭлементыФормыДляВводаИСМПТ(ДанныеФормы, "ГруппаПрочиеЭлектронныеДокументы", ПослеГруппы);
		ИначеЕсли Не ГруппаДляВключенияМеню = Неопределено Тогда
			//Размещаем элементы в группе типовой формы
			СоздатьЭлементыФормыДляВводаИСМПТ(ДанныеФормы, ГруппаДляВключенияМеню, ПослеГруппы);
		Иначе
			ТекстОшибки = НСтр("ru='Невозможно разместить элементы подсистемы ИС МПТ на форме документа, т.к. не назначена группа формы для включения!'");
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	//2. Заполняем созданные реквизиты
	ДокументОснование = ДанныеФормы.Объект.Ссылка; 
	
	//Находим ссылки на связанные документы маркировки
	Если ЗначениеЗаполнено(ДанныеФормы.ДокументИСМПТСсылка) Тогда
		ДокументИСМПТСсылка = ДанныеФормы.ДокументИСМПТСсылка;
	Иначе
		СвязанныеДокументыМаркировки = ПроверитьНаличиеСвязанногоДокументаИСМПТ(ДанныеФормы.Объект.Ссылка, Истина);
		
		Если ТипЗнч(СвязанныеДокументыМаркировки) <> Тип("СписокЗначений") И СвязанныеДокументыМаркировки <> Неопределено Тогда //Связанный документ прямая ссылка на документ
			
			ДокументИСМПТСсылка = СвязанныеДокументыМаркировки.Ссылка;
			
		ИначеЕсли ТипЗнч(СвязанныеДокументыМаркировки) = Тип("СписокЗначений") И СвязанныеДокументыМаркировки.Количество() = 1 Тогда
			
			ДокументИСМПТСсылка = СвязанныеДокументыМаркировки[0].Значение;
			
		ИначеЕсли ТипЗнч(СвязанныеДокументыМаркировки) = Тип("СписокЗначений") И СвязанныеДокументыМаркировки.Количество()> 1 Тогда
			
			Если ДанныеФормы.СписокДокументыИСМПТСсылка.Количество() = 0 Тогда
				Для Каждого СтрокаТаблицыИсточник Из СвязанныеДокументыМаркировки Цикл
					ЗаполнитьЗначенияСвойств(ДанныеФормы.СписокДокументыИСМПТСсылка.Добавить(), СтрокаТаблицыИсточник);
				КонецЦикла;
			КонецЕсли;
			
		Иначе 
			
			//Если не нашлось связанных документов
			ДокументИСМПТСсылка = Неопределено;
			ДанныеФормы.СписокДокументыИСМПТСсылка.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеДокументаИСМПТ = Неопределено;
	
	Если ЗначениеЗаполнено(ДокументИСМПТСсылка) Тогда
		
		РеквизитыИСМПТ = Новый Структура("Ссылка, Дата, ПометкаУдаления", ДокументИСМПТСсылка, '00010101', Ложь);
		
		ЗначенияРеквизитов = ОбщегоНазначенияИСМПТК.ЗначенияРеквизитовОбъекта(ДокументИСМПТСсылка, "Дата, ПометкаУдаления");
		ЗаполнитьЗначенияСвойств(РеквизитыИСМПТ, ЗначенияРеквизитов);
		
		ТекстИСМПТ  = ПолучитьТекстИСМПТ(РеквизитыИСМПТ);
		
		ДанныеДокументаИСМПТ = Новый Структура("ИСМПТ, ТекстИСМПТ, ПометкаУдаления", ДокументИСМПТСсылка, ТекстИСМПТ, РеквизитыИСМПТ.ПометкаУдаления);
		ДанныеФормы.ДокументИСМПТСсылка  = ДокументИСМПТСсылка;
		ДанныеФормы.ДанныеДокументаИСМПТ = ДанныеДокументаИСМПТ;
		
	ИначеЕсли ДанныеФормы.СписокДокументыИСМПТСсылка.Количество() <> 0 Тогда
		
		СписокСсылокИСМПТ = Новый СписокЗначений;
		
		Для каждого СсылкаИСМПТ Из ДанныеФормы.СписокДокументыИСМПТСсылка Цикл
			
			РеквизитыИСМПТ 	   = Новый Структура("Ссылка, Дата, ПометкаУдаления", СсылкаИСМПТ.Значение, '00010101', Ложь);
			ЗначенияРеквизитов = ОбщегоНазначенияИСМПТК.ЗначенияРеквизитовОбъекта(СсылкаИСМПТ.Значение, "Дата, ПометкаУдаления");
			ЗаполнитьЗначенияСвойств(РеквизитыИСМПТ, ЗначенияРеквизитов);
			
			СписокСсылокИСМПТ.Добавить(РеквизитыИСМПТ);
			
		КонецЦикла; 
		
		ТекстИСМПТ = ПолучитьТекстИСМПТ(СписокСсылокИСМПТ);
		
		Если СписокСсылокИСМПТ.Количество() <> 0 Тогда
			
			СписокДокументыИСМПТСсылка = Новый СписокЗначений;
			
			Для каждого Ссылки Из СписокСсылокИСМПТ Цикл
				СсылкаИСМПТ = Ссылки.Значение.Ссылка;
				СписокДокументыИСМПТСсылка.Добавить(СсылкаИСМПТ);
			КонецЦикла; 
			
			ДанныеДокументаИСМПТ = Новый Структура("ИСМПТ, ТекстИСМПТ, ПометкаУдаления", СписокДокументыИСМПТСсылка, ТекстИСМПТ, РеквизитыИСМПТ.ПометкаУдаления);
			ДанныеФормы.СписокДокументыИСМПТСсылка = СписокДокументыИСМПТСсылка;
			ДанныеФормы.ДанныеДокументаИСМПТ = ДанныеДокументаИСМПТ;
			
		КонецЕсли;
	Иначе
		ДанныеФормы.ДокументИСМПТСсылка  = ДокументИСМПТСсылка;
		ДанныеФормы.ДанныеДокументаИСМПТ = ДанныеДокументаИСМПТ;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьТекстИСМПТ(ДанныеДокументовИСМПТ) Экспорт
	
	Если ТипЗнч(ДанныеДокументовИСМПТ) = Тип("СписокЗначений") Тогда
		
		НадписьКоличествоИСМПТ = НСтр("ru='Всего документов ИС МПТ: (%КоличествоДокументов%)'");
		Текст = СтрЗаменить(НадписьКоличествоИСМПТ, "%КоличествоДокументов%", ДанныеДокументовИСМПТ.Количество());
		
	Иначе
		
		ДокументИСМПТ = ?(ДанныеДокументовИСМПТ.Свойство("Ссылка"), ДанныеДокументовИСМПТ.Ссылка, Неопределено);
		Если Не ДокументИСМПТ = Неопределено Тогда
			ПредставлениеДокумента = Строка(ДокументИСМПТ);
		Иначе
			ПредставлениеДокумента = "";
		КонецЕсли;
		ДополнительныйТекст = "";
		
		Если ДанныеДокументовИСМПТ.ПометкаУдаления Тогда
			ДополнительныйТекст = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияПомеченНаУдаление();
		КонецЕсли;
		
		Текст = ПредставлениеДокумента + ДополнительныйТекст;
		
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции 

Процедура ЗаполнитьТекстПроИСМПТ(Форма, РеквизитНадписьИСМПТ = "НадписьДокументИСМПТ", ТребуетсяИСМПТ = Истина) Экспорт
	
	ДанныеДокументаИСМПТ = Форма.ДанныеДокументаИСМПТ;
	
	Если ТребуетсяИСМПТ И ДанныеДокументаИСМПТ = Неопределено Тогда
		Форма[РеквизитНадписьИСМПТ] = "";
		Возврат;
	КонецЕсли;
	
	Если НЕ ТребуетсяИСМПТ Тогда
		Форма[РеквизитНадписьИСМПТ] = НСтр("ru='Не требуется'");
		Форма.Элементы[РеквизитНадписьИСМПТ].ЦветТекста = Новый Цвет(51, 51, 51); // текст формы
	Иначе
		Форма[РеквизитНадписьИСМПТ] = ДанныеДокументаИСМПТ.ТекстИСМПТ;
		
		Если ТребуетсяИСМПТ И (ДанныеДокументаИСМПТ.ПометкаУдаления) Тогда
			Форма.Элементы[РеквизитНадписьИСМПТ].ЦветТекста = Новый Цвет(255, 0, 0);  // красный
			
		ИначеЕсли ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("СписокЗначений") Тогда
			//Если связанных документов несколько, их состояние может быть разным. 
			//Поэтому выводим общую ссылку в нейтральном цвете.
			Форма.Элементы[РеквизитНадписьИСМПТ].ЦветТекста = Новый Цвет(51, 51, 51); // текст формы
			
		ИначеЕсли (Не ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК")
			И Не ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.НанесениеКодовМаркировкиСУЗИСМПТК")
			И Не ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.АгрегацияКодовМаркировкиСУЗИСМПТК")
			И Не ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.УведомлениеОбОтгрузкеЕАЭСИСМПТК")
			И Не ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.УведомлениеОПриемкеЕАЭСИСМПТК"))
			И Не ЗначениеЗаполнено(ДанныеДокументаИСМПТ.ИСМПТ.НомерИСМПТ) Тогда
			
			//Документ не отправлен на сервер ИС МПТ
			Форма.Элементы[РеквизитНадписьИСМПТ].ЦветТекста = Новый Цвет(51, 51, 51); // текст формы
			
		ИначеЕсли (ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК")
			Или ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.АгрегацияКодовМаркировкиСУЗИСМПТК")
			Или ТипЗнч(ДанныеДокументаИСМПТ.ИСМПТ) = Тип("ДокументСсылка.НанесениеКодовМаркировкиСУЗИСМПТК"))
			И Не ЗначениеЗаполнено(ДанныеДокументаИСМПТ.ИСМПТ.OrderID) Тогда
			Форма.Элементы[РеквизитНадписьИСМПТ].ЦветТекста = Новый Цвет(51, 51, 51); // текст формы
		Иначе
			Форма.Элементы[РеквизитНадписьИСМПТ].ЦветТекста = Новый Цвет(0, 100, 0); // зеленый
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеГруппойИСМПТ(Форма, ТребуетсяИСМПТ = Истина) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если ТребуетсяИСМПТ И (ЗначениеЗаполнено(Форма.СписокДокументыИСМПТСсылка) ИЛИ ЗначениеЗаполнено(Форма.ДокументИСМПТСсылка)) Тогда
		Элементы.ГруппаИСМПТ.ТекущаяСтраница = Элементы.ГруппаСсылкаИСМПТ;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЭлементыФормыДляВводаИСМПТ(Форма, ГруппаДляВключенияМеню, ПослеГруппы = "") Экспорт
	
	ДокументИсточник = Форма.Объект.Ссылка;
	СвойстваДокументИСМПТ = СформироватьСвойстваДокументаИСМПТ(ДокументИсточник);
	Если СвойстваДокументИСМПТ.ОписаниеТиповДокументИСМПТ = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	//Подготовим описание необходимых реквизитов маркировки
	ДобавляемыеРеквизиты = Новый Массив;
	
	//Реквизит-представление ссылки документа маркировки на форме
	НовыйРеквизитНадписиДокументаИСМПТ = Новый РеквизитФормы("НадписьДокументИСМПТ", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(500))); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитНадписиДокументаИСМПТ);
	
	//Реквизит для хранения данных документа маркировки
	НовыйРеквизитДанныеДокументаИСМПТ = Новый РеквизитФормы("ДанныеДокументаИСМПТ", Новый ОписаниеТипов());
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитДанныеДокументаИСМПТ);
	
	//Реквизит, хранящий ссылку на связанный документ маркировки
	НовыйРеквизитДокументМаркировка = Новый РеквизитФормы("ДокументИСМПТСсылка", СвойстваДокументИСМПТ.ОписаниеТиповДокументИСМПТ);
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитДокументМаркировка);
	
	//Реквизит, хранящий список ссылок, если связанных документов маркировки несколько
	НовыйРеквизитСписокДокументовМаркировки = Новый РеквизитФормы("СписокДокументыИСМПТСсылка", Новый ОписаниеТипов("СписокЗначений"));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитСписокДокументовМаркировки);
	
	//Создаем реквизиты по описанию на типовой форме
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Если ЗначениеЗаполнено(ПослеГруппы) Тогда
		//Создадим группу страниц для работы с ИСМПТ
		ГруппаИСМПТ = Форма.Элементы.Вставить("ГруппаИСМПТ", Тип("ГруппаФормы"),, Форма.Элементы[ПослеГруппы]);
		ГруппаИСМПТ.Вид = ВидГруппыФормы.Страницы;
		ГруппаИСМПТ.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе 
		//Получим группу в котороую встроим элементы
		ПодменюРазмещения = Форма.Элементы[ГруппаДляВключенияМеню];
		//Создадим группу страниц для работы с ИСМПТ
		ГруппаИСМПТ = Форма.Элементы.Добавить("ГруппаИСМПТ", Тип("ГруппаФормы"), ПодменюРазмещения);
		ГруппаИСМПТ.Вид = ВидГруппыФормы.Страницы;
		ГруппаИСМПТ.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	//Создадим страницу ссылки ИСМПТ
	ГруппаСсылкаИСМПТ = Форма.Элементы.Добавить("ГруппаСсылкаИСМПТ", Тип("ГруппаФормы"), ГруппаИСМПТ);
	ГруппаСсылкаИСМПТ.Вид = ВидГруппыФормы.Страница;
	
	ПолеНадписиИСМПТ = Форма.Элементы.Добавить("НадписьДокументИСМПТ", Тип("ПолеФормы"), ГруппаСсылкаИСМПТ);
	ПолеНадписиИСМПТ.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеНадписиИСМПТ.Гиперссылка = Истина;
	ПолеНадписиИСМПТ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеНадписиИСМПТ.ПутьКДанным = "НадписьДокументИСМПТ";
	
	ПолеНадписиИСМПТ.УстановитьДействие("Нажатие", "НадписьИСМПТНажатие");
	
КонецПроцедуры

#КонецОбласти

#Область ВводНаОснованииИСМПТК

Функция ПолучитьИмяТЧТовары(Документ) Экспорт
	
	Возврат "Товары";
	
КонецФункции

Функция ВключатьТаблицуМарок(Основание) Экспорт
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.УведомлениеОРасхожденииИСМПТК") 
		ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.АктПриемаПередачиИСМПТК")
		ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.УведомлениеОбОтгрузкеЕАЭСИСМПТК") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьМассивПервичныхДокументов_Общая(ЭлементМассива, СписокПервичныхДокументов, Отказ, ФормироватьСписокДокументов, ПроводитьСозданныеДокументы, ТипСоздаваемогоДокумента, ЗаписьПрошлаУспешно) Экспорт
	
	ДокументИС = ЭлементМассива.Реквизиты[0].ДокументОснование;
	Если ТипСоздаваемогоДокумента = "ПоступлениеТоваров" Тогда
		СозданныйДокумент = Документы.ПоступлениеТоваров.СоздатьДокумент();
		
	ИначеЕсли ТипСоздаваемогоДокумента = "ВозвратТоваровОтПокупателя" Тогда
		СозданныйДокумент = Документы.ВозвратТоваровОтПокупателя.СоздатьДокумент();
		СозданныйДокумент.АналитикаХозяйственнойОперации = Справочники.АналитикаХозяйственныхОпераций.ПолучитьОперациюПоУмолчанию(Перечисления.ХозяйственныеОперации.ВозвратОтПокупателя);
				
	ИначеЕсли ТипСоздаваемогоДокумента = "РеализацияТоваров" Тогда
		СозданныйДокумент = Документы.РеализацияТоваров.СоздатьДокумент();
		
	ИначеЕсли ТипСоздаваемогоДокумента = "ВозвратТоваровПоставщику" Тогда
		СозданныйДокумент = Документы.ВозвратТоваровПоставщику.СоздатьДокумент();
		
	ИначеЕсли ТипСоздаваемогоДокумента = "ОприходованиеТоваров" Тогда
		СозданныйДокумент = Документы.ОприходованиеТоваров.СоздатьДокумент();
				
	ИначеЕсли ТипСоздаваемогоДокумента = "СписаниеТоваров" Тогда
		СозданныйДокумент = Документы.СписаниеТоваров.СоздатьДокумент();
		
	ИначеЕсли ТипСоздаваемогоДокумента = "ПеремещениеТоваров" Тогда
		СозданныйДокумент = Документы.ПеремещениеТоваров.СоздатьДокумент();
		
	ИначеЕсли ТипСоздаваемогоДокумента = "ОрдерНаПеремещениеТоваров" Тогда
		СозданныйДокумент = Документы.ОрдерНаПеремещениеТоваров.СоздатьДокумент();
		
	Иначе
		ТекстСообщения = НСтр("ru = 'При создании документа произошла ошибка: тип документа не определен.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СозданныйДокумент, ЭлементМассива.Реквизиты[0]);
	
	УчитыватьНДС = ПолучитьПризнакУчетаНДС(ТекущаяДатаСеанса(), СозданныйДокумент.Организация);
	Если Не ТипСоздаваемогоДокумента = "ПеремещениеТоваров"
		И Не ТипСоздаваемогоДокумента = "ОрдерНаПеремещениеТоваров"
		И Не ТипСоздаваемогоДокумента = "СписаниеТоваров"
		И Не ТипСоздаваемогоДокумента = "ОприходованиеТоваров" Тогда
		СозданныйДокумент.УчитыватьНДС 	  = УчитыватьНДС;	
		СозданныйДокумент.ЦенаВключаетНДС = УчитыватьНДС;
	КонецЕсли;
	
	Если Не ЭлементМассива.Реквизиты.Колонки.Найти("Автор") = Неопределено Тогда
		СозданныйДокумент.Ответственный = ЭлементМассива.Реквизиты[0].Автор;
	Иначе
		СозданныйДокумент.Ответственный = ЭлементМассива.Реквизиты[0].Ответственный;
	КонецЕсли;
	
	Если ТипСоздаваемогоДокумента = "ВозвратТоваровОтПокупателя" Тогда
		СозданныйДокумент.АналитикаХозяйственнойОперации = Справочники.АналитикаХозяйственныхОпераций.ПолучитьОперациюПоУмолчанию(Перечисления.ХозяйственныеОперации.ВозвратОтПокупателя);
 	КонецЕсли;
	
	//Заполняем табличную часть "Товары"
	Для Каждого СтрокаТЧ Из ЭлементМассива.Товары Цикл
		
		НоваяСтрокаТЧ = СозданныйДокумент.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаТЧ);
				
	КонецЦикла;	
	
	Попытка 
		СозданныйДокумент.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаПриЗаписиДокументаСПараметром()
					   + Символы.ПС
				 	   + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СозданныйДокумент);
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		ЗаписьПрошлаУспешно = Ложь;
		Возврат;
	КонецПопытки;
	
	Если ПроводитьСозданныеДокументы Тогда  
		
		Попытка 
			СозданныйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Отказ = Истина;
		КонецПопытки;
		
		Если Отказ Тогда
			ТекстСообщения = НСтр("ru = 'При проведении документа ""%1"" произошла ошибка. Выполнение ввода документов прервано.'");
			ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СозданныйДокумент);
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
			ЗаписьПрошлаУспешно = Ложь;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		Если ЗначениеЗаполнено(ДокументИС) Тогда
			
			ДокументИСМПТОбъект = ДокументИС.ПолучитьОбъект();
			ДокументИСМПТОбъект.ДокументОснование = СозданныйДокумент.Ссылка;
			ДокументИСМПТОбъект.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЕсли;
	Исключение
		
		ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(
						НСтр("ru='Ошибка при записи документа %1: %2.'"),
						ЭлементМассива.ДокументОснование,
						ИнформацияОбОшибке().Описание);
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		ЗаписьПрошлаУспешно = Ложь;
		
	КонецПопытки;
	
	Если ФормироватьСписокДокументов И ЗаписьПрошлаУспешно Тогда
		СписокПервичныхДокументов.Добавить(СозданныйДокумент.Ссылка);
	КонецЕсли;		
		
КонецПроцедуры

Функция ПолучитьМассивТиповДокументовМаркировки();
	
	МассивДокументовМаркировки = Новый Массив();
	//ИС МПТ
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.АктПриемаПередачиИСМПТК"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОВводеВОборотИСМПТК"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОВыводеИзОборотаИСМПТК"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСМПТК"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСМПТК"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОбЭкспортеЕАЭСИСМПТК"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОПриемкеЕАЭСИСМПТК")); 
	//СУЗ
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК"));
	
	Возврат МассивДокументовМаркировки;
	
КонецФункции

Функция СформироватьСвойстваДокументаИСМПТ(ДокументИсточник, ВидОперацииВФорме = Неопределено) Экспорт
	
	МассивДокументовМаркировки = ПолучитьМассивТиповДокументовМаркировки();	
	СвойстваДокументИСМПТ = Новый Структура();
	СвойстваДокументИСМПТ.Вставить("ОписаниеТиповДокументИСМПТ", Новый ОписаниеТипов(МассивДокументовМаркировки));
	СвойстваДокументИСМПТ.Вставить("ПредставлениеСоздатьДокументИСМПТ", "Ввести новый документ маркировки (ИС МПТ)");
	
	Возврат СвойстваДокументИСМПТ;
	
КонецФункции

Функция ПолучитьПустуюСсылкуДокументИСМПТПоОснованию(ДокументОснование, ИмяДокумента = Неопределено) Экспорт
	
	МассивДокументовМаркировки = ПолучитьМассивТиповДокументовМаркировки();	
	Возврат Новый ОписаниеТипов(МассивДокументовМаркировки);
	
КонецФункции

Функция ПроверитьИмяДокументаПриСозданииЭлементовФормыДляВводаДокументаИСМПТ(ИмяФормы) Экспорт
	
	Возврат ?(СтрНайти(ИмяФормы, "ПоступлениеТоваров") <> 0, Истина, Ложь);
	
КонецФункции

Функция ПроверитьНаличиеМаркируемогоТовара(ВыбранноеЗначение) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.РеализацияТоваров")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОприходованиеТоваров")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СписаниеТоваров")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ПоступлениеТоваров") 
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		//ИС МПТ
		ВДокументеЕстьМаркируемаяПродукция = ПроверитьТоварыОбщийЗапросИСМПТ(ВыбранноеЗначение);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		//СУЗ
		ВДокументеЕстьМаркируемаяПродукция = ПроверитьТоварыОбщийЗапросСУЗ(ВыбранноеЗначение);
		
	Иначе
		ВДокументеЕстьМаркируемаяПродукция = Ложь;
	КонецЕсли;	
	
	Возврат ВДокументеЕстьМаркируемаяПродукция;
	
КонецФункции

Функция ПроверитьТоварыОбщийЗапросИСМПТ(ДокументДляПроверки)
	
	//Для ИС МПТ - все ТГ, кроме Фармы
	Запрос = Новый Запрос();
	Текст = "ВЫБРАТЬ
	|	ДокументПроверки.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ." + ДокументДляПроверки.Ссылка.Метаданные().Имя + ".Товары КАК ДокументПроверки
	|ГДЕ
	|	ДокументПроверки.Ссылка = &Ссылка
	|	И (ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)  
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция)
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность))";
	
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("Ссылка", ДокументДляПроверки);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат ?(Результат.Количество() = 0, Ложь, Истина);
	
КонецФункции

Функция ПроверитьТоварыОбщийЗапросСУЗ(ДокументДляПроверки)
	
	//Для СУЗ - все доступные ТГ
	Запрос = Новый Запрос();
	Текст = "ВЫБРАТЬ
	|	ДокументПроверки.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ." + ДокументДляПроверки.Ссылка.Метаданные().Имя + ".Товары КАК ДокументПроверки
	|ГДЕ
	|	ДокументПроверки.Ссылка = &Ссылка
	|	И (ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)  
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция)
	|	ИЛИ ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность))";
	
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("Ссылка", ДокументДляПроверки);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат ?(Результат.Количество() = 0, Ложь, Истина);
	
КонецФункции

Функция ПодготовитьТекстЗапросаДляЗаполненияПервичныхДокументов(ПараметрыЗаполнения) Экспорт
	
    ДокументИСМПТК = Неопределено;
	Если ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВводеВОборотИСМПТК") Тогда
		ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВводеВОборотИСМПТК();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВыводеИзОборотаИСМПТК") Тогда
		ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВыводеИзОборотаИСМПТК();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.АктПриемаПередачиИСМПТК") Тогда
		ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСМПТК();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСМПТК") Тогда
		ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСМПТК") Тогда
		ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСМПТК();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОбЭкспортеЕАЭСИСМПТК") Тогда
		ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбЭкспортеЕАЭСИСМПТК();
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТЧ.Ссылка КАК Ссылка,
	|	СУММА(ДокументТЧ.Количество) КАК КоличествоУпаковок,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка,
	|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура% КАК Номенклатура,
	|	ДокументТЧ.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ДокументТЧ.Номенклатура.КодТНВЭД КАК КодТНВЭД
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.%ИмяДокумента%.%ИмяТаблицыДанных% КАК ДокументТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|		ПО ДокументТЧ.EAN = ШтрихкодыНоменклатуры.Штрихкод
	|ГДЕ
	|	ДокументТЧ.Ссылка В (&МассивИСМПТДляСозданияПервичныхДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ШтрихкодыНоменклатуры.Упаковка,
	|	ШтрихкодыНоменклатуры.Характеристика,
	|	ДокументТЧ.Ссылка,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%,
	|	ДокументТЧ.Номенклатура.КодТНВЭД,
	|	ДокументТЧ.Номенклатура.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Ссылка КАК ДокументОснование,
	|	ВТ_Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВТ_Товары.КоличествоУпаковок КАК Количество,
	|	ВТ_Товары.Упаковка КАК Упаковка,
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВТ_Товары.Характеристика КАК Характеристика,
	|	ВТ_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_Товары.КодТНВЭД КАК КодТНВЭД,
	|	ДокументИСМПТК.Дата КАК Дата,
	|	ДокументИСМПТК.Организация КАК Организация,
	|	%Контрагент%
	|	ДокументИСМПТК.Автор КАК Автор,
	|	"""" КАК ВидОперации,
	|	"""" КАК Партнер	
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%ИмяДокумента% КАК ДокументИСМПТК
	|		ПО ВТ_Товары.Ссылка = ДокументИСМПТК.Ссылка
	|ГДЕ
	|	ДокументИСМПТК.Ссылка В (&МассивИСМПТДляСозданияПервичныхДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	ВТ_ДанныеДокумента.Организация КАК Организация,
	|	ВТ_ДанныеДокумента.Контрагент КАК Контрагент,
	|	ВТ_ДанныеДокумента.Партнер КАК Партнер,
	|	ВТ_ДанныеДокумента.ВидОперации КАК ВидОперации,
	|	ВТ_ДанныеДокумента.Автор КАК Автор,
	|	ВТ_ДанныеДокумента.Дата КАК Дата,
	|	ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДокумента.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДокумента.Упаковка КАК Упаковка,
	|	ВТ_ДанныеДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеДокумента.КодТНВЭД КАК КодТНВЭД,
	|	ВТ_ДанныеДокумента.Количество КАК Количество,
	|	ВТ_ДанныеДокумента.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|ИЗ
	|	вт_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Контрагент КАК Контрагент,
	|	вт_ДанныеДляЗаполнения.Партнер КАК Партнер,
	|	вт_ДанныеДляЗаполнения.ВидОперации КАК ВидОперации,
	|	вт_ДанныеДляЗаполнения.Автор КАК Автор,
	|	вт_ДанныеДляЗаполнения.Дата КАК Дата
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|	УПОРЯДОЧИТЬ ПО
	|		ДокументОснование
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.Характеристика КАК Характеристика,
	|	вт_ДанныеДляЗаполнения.Упаковка КАК Упаковка,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	вт_ДанныеДляЗаполнения.КодТНВЭД КАК КодТНВЭД,
	|	вт_ДанныеДляЗаполнения.Количество КАК Количество,
	|	вт_ДанныеДляЗаполнения.КоличествоУпаковок КАК КоличествоУпаковок
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|	УПОРЯДОЧИТЬ ПО
	|		ДокументОснование";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", 	   ДокументИСМПТК);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяТаблицыДанных%", "Марки");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",   ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры());

	Если ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСМПТК()
		Или ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК()
		Или ДокументИСМПТК = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбЭкспортеЕАЭСИСМПТК() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Контрагент%", "ДокументИСМПТК.Контрагент КАК Контрагент,");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Контрагент%", "Неопределено КАК Контрагент,");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьПустуюСтруктуруДокументаОснованияДляИСМПТ(Параметры = Неопределено) Экспорт
	
	Если Не Параметры = Неопределено Тогда
		ТипДокумента = Параметры.ТипСоздаваемогоДокумента;
	Иначе
		ТипДокумента = Параметры;
	КонецЕсли;
	
	СтруктураДокумента = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = "Организация," 
	+ "Контрагент,"
	+ "Партнер,"
	+ "ДокументОснование," 
	+ "ВидОперации,"
	+ "Дата,"
	+ "Автор,"
	;
	
	СтруктураДокумента.Вставить("Реквизиты", ИнтеграцияИСМПТКВызовСервера.ПолучитьТаблицуПараметров(СписокОбязательныхКолонок));
	
	СписокОбязательныхКолонок = 
	"Организация,"
	+ "Номенклатура,"
	+ "Цена,"
	+ "КодТНВЭД,"
	+ "Характеристика,"
	+ "Количество,"
	+ "Упаковка,"
	+ "КоличествоУпаковок,"
	+ "ЕдиницаИзмерения,"
	+ "ДокументОснование"
	;
	
	СтруктураДокумента.Вставить("Товары", ИнтеграцияИСМПТКВызовСервера.ПолучитьТаблицуПараметров(СписокОбязательныхКолонок));
	
	Возврат СтруктураДокумента;
	
КонецФункции

//Универсальный запрос к первичным документам для получения данных для заполнения создаваемого документа ИС ЦЭДМ
//
Функция ПодготовитьТекстЗапросаДляЗаполненияДокументаИСМПТ_Общий(ПараметрыЗаполнения) Экспорт
	
	ДокументОснования = ?(ТипЗнч(ПараметрыЗаполнения.МассивДокументов) = Тип("Массив"), ПараметрыЗаполнения.МассивДокументов[0], ПараметрыЗаполнения.МассивДокументов);
	
	Если ТипЗнч(ДокументОснования) = Тип("ДокументСсылка.УведомлениеОРасхожденииИСМПТК") Тогда
		//Нужно получить дополнительную информацию о кодах маркировки для ввода АПП корректировочного
		ТекстЗапроса = ТекстЗапросаДляСхемыКорректировкиАПП(ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОРасхожденииИСМПТК());
		Возврат ТекстЗапроса;		
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснования) = Тип("ДокументСсылка.АктПриемаПередачиИСМПТК") Тогда
		//Нужно получить дополнительную информацию о кодах маркировки для ввода УОР или Вывода из оборота
		Если Не ДокументОснования.Расхождения.Количество() = 0 Тогда
			ТекстЗапроса = ТекстЗапросаДляСхемыКорректировкиАПП(ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСМПТК());
		Иначе
			ТекстЗапроса = ТекстЗапросаДляВыводаИзОборотаПоАПП(ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСМПТК());
		КонецЕсли;
		Возврат ТекстЗапроса;		
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснования) = Тип("ДокументСсылка.УведомлениеОбОтгрузкеЕАЭСИСМПТК") Тогда
		//Нужно получить дополнительную информацию о кодах маркировки для ввода Уведомления о приемке
		ТекстЗапроса = ТекстЗапросаУведомлениеОбОтгрузке(ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбОтгрузкеЕАЭСИСМПТК());
		Возврат ТекстЗапроса;		
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснования) = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСМПТК") Тогда
		ТекстЗапроса = ТекстЗапросаУведомлениеОВвозеЕАЭС(ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК());
		Возврат ТекстЗапроса;		
	КонецЕсли;
	
	ИмяДокументаОснования = ПолучитьИмяДокументаДляПроверкиПоТипу(ДокументОснования);
	ТекстЗапроса = ТекстЗапросаОбщиеДанныеОснования(,ИмяДокументаОснования); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДляСхемыКорректировкиАПП(ИмяДокумента)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументИСМПТ.Ссылка КАК ДокументОснование,
	|	ДокументИСМПТ.Организация КАК Организация,
	|	ДокументИСМПТ.Контрагент КАК Контрагент,
	|	ДокументИСМПТ.Поставщик КАК Поставщик,
	|	ДокументИСМПТ.ПоставщикНаименование КАК ПоставщикНаименование,
	|	ДокументИСМПТ.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	ДокументИСМПТ.Получатель КАК Получатель,
	|	ДокументИСМПТ.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	ДокументИСМПТ.ПолучательНаименование КАК ПолучательНаименование,
	|	ДокументИСМПТ.ВидОперации КАК ВидОперации
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	Документ.%ИмяДокумента% КАК ДокументИСМПТ
	|ГДЕ
	|	ДокументИСМПТ.Ссылка В(&МассивДокументов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Если ИмяДокумента = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОРасхожденииИСМПТК() Тогда
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаМарки.КодМаркировки КАК КодМаркировки,
		|	ТаблицаМарки.ВидУпаковки КАК ВидУпаковки,
		|	ТаблицаМарки.Количество КАК Количество,
		|	ТаблицаМарки.GTIN КАК GTIN,
		|	ТаблицаМарки.Номенклатура КАК Номенклатура,
		|	ТаблицаМарки.КоличествоИсточник КАК КоличествоИсточник,
		|	ТаблицаМарки.EAN КАК EAN,
		|	ТаблицаМарки.КодИдентификации КАК КодИдентификации,
		|	Неопределено КАК СтатусСверкиКода,
		|	ТаблицаМарки.ВидПродукции КАК ВидПродукции,
		|	ТаблицаМарки.GTINВерхнегоУровня КАК GTINВерхнегоУровня
		|ПОМЕСТИТЬ вт_Марки
		|ИЗ
		|	Документ.УведомлениеОРасхожденииИСМПТК.Марки КАК ТаблицаМарки
		|ГДЕ
		|	ТаблицаМарки.Ссылка В(&МассивДокументов)
		|	И (ТаблицаМарки.СтатусСверкиКода = ЗНАЧЕНИЕ(Перечисление.СтатусыСверкиКодаИСМПТК.Найден)
		|			ИЛИ ТаблицаМарки.СтатусСверкиКода = ЗНАЧЕНИЕ(Перечисление.СтатусыСверкиКодаИСМПТК.Добавлен))
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
	Иначе
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаМарки.КодМаркировки КАК КодМаркировки,
		|	ТаблицаМарки.ВидУпаковки КАК ВидУпаковки,
		|	ТаблицаМарки.Количество КАК Количество,
		|	ТаблицаМарки.GTIN КАК GTIN,
		|	ТаблицаМарки.Номенклатура КАК Номенклатура,
		|	Неопределено КАК КоличествоИсточник,
		|	ТаблицаМарки.EAN КАК EAN,
		|	ТаблицаМарки.КодИдентификации КАК КодИдентификации,
		|	ТаблицаМарки.СтатусСверкиКода КАК СтатусСверкиКода,
		|	ТаблицаМарки.ВидПродукции КАК ВидПродукции,
		|	ТаблицаМарки.GTINВерхнегоУровня КАК GTINВерхнегоУровня
		|ПОМЕСТИТЬ вт_Марки
		|ИЗ
		|	Документ.АктПриемаПередачиИСМПТК.Расхождения КАК ТаблицаМарки
		|ГДЕ
		|	ТаблицаМарки.Ссылка В(&МассивДокументов)
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
	КонецЕсли;
		
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	//Реквизиты
	|	вт_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента.Организация КАК Организация,
	|	вт_ДанныеДокумента.Контрагент КАК Контрагент,
	|	вт_ДанныеДокумента.Поставщик КАК Поставщик,
	|	вт_ДанныеДокумента.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДокумента.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДокумента.Получатель КАК Получатель,
	|	вт_ДанныеДокумента.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДокумента.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДокумента.ВидОперации КАК ВидОперации
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
    //Товары
	|ВЫБРАТЬ
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_Марки.Количество КАК Количество
	|ИЗ
	|	вт_Марки КАК вт_Марки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//Марки
	|ВЫБРАТЬ
	|	вт_Марки.КодМаркировки КАК КодМаркировки,
	|	вт_Марки.ВидУпаковки КАК ВидУпаковки,
	|	вт_Марки.Количество КАК Количество,
	|	вт_Марки.GTIN КАК GTIN,
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_Марки.EAN КАК EAN,
	|	вт_Марки.КодИдентификации КАК КодИдентификации,
	|	вт_Марки.СтатусСверкиКода КАК СтатусСверкиКода,
	|	вт_Марки.ВидПродукции КАК ВидПродукции,
	|	вт_Марки.GTINВерхнегоУровня КАК GTINВерхнегоУровня
	|ИЗ
	|	вт_Марки КАК вт_Марки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаДляВыводаИзОборотаПоАПП(ИмяДокумента)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументИСМПТ.Ссылка КАК ДокументОснование,
	|	ДокументИСМПТ.Организация КАК Организация,
	|	ДокументИСМПТ.Контрагент КАК Контрагент,
	|	ДокументИСМПТ.Поставщик КАК Поставщик,
	|	ДокументИСМПТ.ПоставщикНаименование КАК ПоставщикНаименование,
	|	ДокументИСМПТ.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	ДокументИСМПТ.Получатель КАК Получатель,
	|	ДокументИСМПТ.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	ДокументИСМПТ.ПолучательНаименование КАК ПолучательНаименование,
	|	ДокументИСМПТ.ВидОперации КАК ВидОперации
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	Документ.АктПриемаПередачиИСМПТК КАК ДокументИСМПТ
	|ГДЕ
	|	ДокументИСМПТ.Ссылка В(&МассивДокументов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаМарки.КодМаркировки КАК КодМаркировки,
	|	ТаблицаМарки.ВидУпаковки КАК ВидУпаковки,
	|	ТаблицаМарки.Количество КАК Количество,
	|	ТаблицаМарки.GTIN КАК GTIN,
	|	ТаблицаМарки.Номенклатура КАК Номенклатура,
	|	Неопределено КАК КоличествоИсточник,
	|	ТаблицаМарки.EAN КАК EAN,
	|	ТаблицаМарки.КодИдентификации КАК КодИдентификации,
	|	ТаблицаМарки.ВидПродукции КАК ВидПродукции,
	|	ТаблицаМарки.GTINВерхнегоУровня КАК GTINВерхнегоУровня
	|ПОМЕСТИТЬ вт_Марки
	|ИЗ
	|	Документ.АктПриемаПередачиИСМПТК.Марки КАК ТаблицаМарки
	|ГДЕ
	|	ТаблицаМарки.Ссылка В(&МассивДокументов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//Реквизиты
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента.Организация КАК Организация,
	|	вт_ДанныеДокумента.Контрагент КАК Контрагент,
	|	вт_ДанныеДокумента.Поставщик КАК Поставщик,
	|	вт_ДанныеДокумента.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДокумента.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДокумента.Получатель КАК Получатель,
	|	вт_ДанныеДокумента.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДокумента.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДокумента.ВидОперации КАК ВидОперации
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//Товары
	|ВЫБРАТЬ
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_Марки.Количество КАК Количество
	|ИЗ
	|	вт_Марки КАК вт_Марки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//Марки
	|ВЫБРАТЬ
	|	вт_Марки.КодМаркировки КАК КодМаркировки,
	|	вт_Марки.ВидУпаковки КАК ВидУпаковки,
	|	вт_Марки.Количество КАК Количество,
	|	вт_Марки.GTIN КАК GTIN,
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_Марки.EAN КАК EAN,
	|	вт_Марки.КодИдентификации КАК КодИдентификации,
	|	вт_Марки.ВидПродукции КАК ВидПродукции,
	|	вт_Марки.GTINВерхнегоУровня КАК GTINВерхнегоУровня
	|ИЗ
	|	вт_Марки КАК вт_Марки";
		
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаУведомлениеОбОтгрузке(ИмяДокумента)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументИСМПТ.Ссылка КАК ДокументОснование,
	|	ДокументИСМПТ.Организация КАК Организация,
	|	ДокументИСМПТ.Контрагент КАК Контрагент,
	|	ДокументИСМПТ.Поставщик КАК Поставщик,
	|	ДокументИСМПТ.ПоставщикНаименование КАК ПоставщикНаименование,
	|	ДокументИСМПТ.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	ДокументИСМПТ.Получатель КАК Получатель,
	|	ДокументИСМПТ.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	ДокументИСМПТ.ПолучательНаименование КАК ПолучательНаименование,
	|	ДокументИСМПТ.СтранаОтправления КАК СтранаОтправления,
	|	ДокументИСМПТ.Идентификатор КАК Идентификатор,
	|	ДокументИСМПТ.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
	|	ДокументИСМПТ.ДатаПервичногоДокумента КАК ДатаПервичногоДокумента
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	Документ.УведомлениеОбОтгрузкеЕАЭСИСМПТК КАК ДокументИСМПТ
	|ГДЕ
	|	ДокументИСМПТ.Ссылка В(&МассивДокументов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаМарки.КодИдентификации КАК КодИдентификации,
	|	ТаблицаМарки.ВидУпаковки КАК ВидУпаковки,
	|	ТаблицаМарки.GTIN КАК GTIN,
	|	ТаблицаМарки.EAN КАК EAN,
	|	ТаблицаМарки.Номенклатура КАК Номенклатура,
	|	ТаблицаМарки.ВидПродукции КАК ВидПродукции,
	|	ТаблицаМарки.GTINВерхнегоУровня КАК GTINВерхнегоУровня,
	|	ТаблицаМарки.Количество КАК Количество,
	|	ТаблицаМарки.ЦенаЗаЕдиницу КАК ЦенаЗаЕдиницу,
	|	ТаблицаМарки.СуммаНДС КАК СуммаНДС	
	|ПОМЕСТИТЬ вт_Марки
	|ИЗ
	|	Документ.УведомлениеОбОтгрузкеЕАЭСИСМПТК.Марки КАК ТаблицаМарки
	|ГДЕ
	|	ТаблицаМарки.Ссылка В(&МассивДокументов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//Реквизиты
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента.Организация КАК Организация,
	|	вт_ДанныеДокумента.Контрагент КАК Контрагент,
	|	вт_ДанныеДокумента.Поставщик КАК Поставщик,
	|	вт_ДанныеДокумента.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДокумента.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДокумента.Получатель КАК Получатель,
	|	вт_ДанныеДокумента.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДокумента.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДокумента.СтранаОтправления КАК СтранаОтправления,
	|	вт_ДанныеДокумента.Идентификатор КАК ИдентификаторДокументаОтгрузки,
	|	вт_ДанныеДокумента.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
	|	вт_ДанныеДокумента.ДатаПервичногоДокумента КАК ДатаПервичногоДокумента
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//Товары
	|ВЫБРАТЬ
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.Количество КАК Количество
	|ИЗ
	|	вт_Марки КАК вт_Марки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//Марки
	|ВЫБРАТЬ
	|	вт_Марки.КодИдентификации КАК КодИдентификации,
	|	вт_Марки.ВидУпаковки КАК ВидУпаковки,
	|	вт_Марки.GTIN КАК GTIN,
	|	вт_Марки.EAN КАК EAN,
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.ВидПродукции КАК ВидПродукции,
	|	вт_Марки.GTINВерхнегоУровня КАК GTINВерхнегоУровня,
	|	вт_Марки.Количество КАК Количество,
	|	вт_Марки.ЦенаЗаЕдиницу КАК ЦенаЗаЕдиницу,
	|	вт_Марки.СуммаНДС КАК СуммаНДС
	|ИЗ
	|	вт_Марки КАК вт_Марки";
			
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаУведомлениеОВвозеЕАЭС(ИмяДокумента)
	
	ТекстЗапроса =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументИСМПТ.Ссылка КАК Ссылка,
	|	ДокументИСМПТ.ДокументОснование КАК ДокументОснование,
	|	ДокументИСМПТ.Идентификатор КАК Идентификатор,
	|	ДокументИСМПТ.НомерИСМПТ КАК НомерИСМПТ,
	|	ДокументИСМПТ.Организация КАК Организация,
	|	ДокументИСМПТ.Контрагент КАК Контрагент,
	|	ДокументИСМПТ.Поставщик КАК Поставщик,
	|	ДокументИСМПТ.ПоставщикНаименование КАК ПоставщикНаименование,
	|	ДокументИСМПТ.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	ДокументИСМПТ.Получатель КАК Получатель,
	|	ДокументИСМПТ.ПолучательНаименование КАК ПолучательНаименование,
	|	ДокументИСМПТ.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	ДокументИСМПТ.СтранаОтправления КАК СтранаОтправления,
	|	ДокументИСМПТ.ДатаУплатыАкциза КАК ДатаУплатыАкциза,
	|	ДокументИСМПТ.НомерКвитанцииАкциза КАК НомерКвитанцииАкциза,
	|	ДокументИСМПТ.СуммаАкциза КАК СуммаАкциза
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	Документ.УведомлениеОВвозеИзЕАЭСИСМПТК КАК ДокументИСМПТ
	|ГДЕ
	|	ДокументИСМПТ.Ссылка В(&МассивДокументов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаМарки.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ТаблицаМарки.Ссылка.Организация КАК Организация,
	|	ТаблицаМарки.КодМаркировки КАК КодМаркировки,
	|	ТаблицаМарки.КодИдентификации КАК КодИдентификации,
	|	ТаблицаМарки.ВидУпаковки КАК ВидУпаковки,
	|	ТаблицаМарки.GTIN КАК GTIN,
	|	ТаблицаМарки.EAN КАК EAN,
	|	ТаблицаМарки.Номенклатура КАК Номенклатура,
	|	ТаблицаМарки.ВидПродукции КАК ВидПродукции,
	|	ТаблицаМарки.GTINВерхнегоУровня КАК GTINВерхнегоУровня,
	|	ТаблицаМарки.КоличествоИсточник КАК КоличествоИсточник,
	|	ТаблицаМарки.Количество КАК Количество
	|ПОМЕСТИТЬ вт_Марки
	|ИЗ
	|	Документ.УведомлениеОВвозеИзЕАЭСИСМПТК.Марки КАК ТаблицаМарки
	|ГДЕ
	|	ТаблицаМарки.Ссылка В(&МассивДокументов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//Реквизиты
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента.Идентификатор КАК Идентификатор,
	|	вт_ДанныеДокумента.НомерИСМПТ КАК НомерИСМПТ,
	|	вт_ДанныеДокумента.Организация КАК Организация,
	|	вт_ДанныеДокумента.Контрагент КАК Контрагент,
	|	вт_ДанныеДокумента.Поставщик КАК Поставщик,
	|	вт_ДанныеДокумента.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДокумента.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДокумента.Получатель КАК Получатель,
	|	вт_ДанныеДокумента.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДокумента.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДокумента.СтранаОтправления КАК СтранаОтправления,
	|	вт_ДанныеДокумента.ДатаУплатыАкциза КАК ДатаУплатыАкциза,
	|	вт_ДанныеДокумента.НомерКвитанцииАкциза КАК НомерКвитанцииАкциза,
	|	вт_ДанныеДокумента.СуммаАкциза КАК СуммаАкциза
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//Товары
	|ВЫБРАТЬ
	|	вт_Марки.ДокументОснование КАК ДокументОснование,
	|	вт_Марки.Организация КАК Организация,
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.Количество КАК Количество
	|ИЗ
	|	вт_Марки КАК вт_Марки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//Марки
	|ВЫБРАТЬ
	|	вт_Марки.ДокументОснование КАК ДокументОснование,
	|	вт_Марки.Организация КАК Организация,
	|	вт_Марки.КодИдентификации КАК КодИдентификации,
	|	вт_Марки.КодМаркировки КАК КодМаркировки,
	|	вт_Марки.ВидУпаковки КАК ВидУпаковки,
	|	вт_Марки.GTIN КАК GTIN,
	|	вт_Марки.EAN КАК EAN,
	|	вт_Марки.Номенклатура КАК Номенклатура,
	|	вт_Марки.ВидПродукции КАК ВидПродукции,
	|	вт_Марки.GTINВерхнегоУровня КАК GTINВерхнегоУровня,
	|	вт_Марки.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_Марки.Количество КАК Количество
	|ИЗ
	|	вт_Марки КАК вт_Марки";
		
	Возврат ТекстЗапроса;
	
КонецФункции	

#КонецОбласти

Функция ПолучитьКусокТекстаЗапросаПоВидуПродукции(ИнфСистема)
	
	Если ИнфСистема = "ИСМПТ" Тогда
		Возврат "И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
		|ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
		|ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
		|ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция)
		|ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность))";
	ИначеЕсли ИнфСистема = "ИСЦЭДМ" Тогда
		Возврат "И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляПечатиКМ(КоличествоКПечатиПоКаждомуШК = 0) Экспорт
	
	ТекстЗапросаШаблон = "ВЫБРАТЬ 
	|	ПулКодовМаркировкиСУЗИСМПТК.GTIN КАК GTIN,
	|	ПулКодовМаркировкиСУЗИСМПТК.ВидПродукции КАК ВидПродукции,
	|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию КАК ЗаказНаЭмиссию,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодИдентификации КАК КодИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.Номенклатура КАК Номенклатура,
	|	ПулКодовМаркировкиСУЗИСМПТК.Организация КАК Организация,
	|	ПулКодовМаркировкиСУЗИСМПТК.СпособВводаВОборот КАК СпособВводаВОборот,
	|	ПулКодовМаркировкиСУЗИСМПТК.Характеристика КАК Характеристика,
	|	ПулКодовМаркировкиСУЗИСМПТК.ХешСуммаКодаИдентификации КАК ХешСуммаКодаИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.Шаблон КАК Шаблон,
	|	ПулКодовМаркировкиСУЗИСМПТК.ТипШтрихкода КАК ТипШтрихкода,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки КАК Штрихкод
	|	ПОМЕСТИТЬ ВТКодыПромежуточная
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ПулКодовМаркировкиСУЗИСМПТК
	|ГДЕ
	//Отбор только не распечатанных по дате
	|	ПулКодовМаркировкиСУЗИСМПТК.ДатаПечати = ДАТАВРЕМЯ(1, 1, 1)  
	//Отбор только готовых к печати по статусу
	|	И Не ПулКодовМаркировкиСУЗИСМПТК.Статус В (&НедоступныеСтатусы)
	|	И (";
	
	ТекстЗапросаДопУсловиеШаблон = "%ИЛИ (ПулКодовМаркировкиСУЗИСМПТК.GTIN В(&GTIN)
	|	И ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию = &ЗаказНаЭмиссию)%ЗакрывющаяСкобка
	|";
	
	ТекстЗаказаОбщееУсловие = "";
	СчетчикЦикла = 0;
	
	Пока Не СчетчикЦикла = КоличествоКПечатиПоКаждомуШК Цикл
		
		ИмяПараметрGTIN = "&GTIN" + Строка(СчетчикЦикла + 1);
		ИмяПараметрЗаказНаЭмиссию = "&ЗаказНаЭмиссию" + Строка(СчетчикЦикла + 1);
		
		ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаДопУсловиеШаблон, "&GTIN", 	  	  ИмяПараметрGTIN);
		ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаЭтойСвязки, 	   "&ЗаказНаЭмиссию", ИмяПараметрЗаказНаЭмиссию);
		
		Если СчетчикЦикла = 0 Тогда 
			ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаЭтойСвязки, "%ИЛИ", "");
		Иначе
			ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаЭтойСвязки, "%ИЛИ", "ИЛИ");
		КонецЕсли;
		
		Если СчетчикЦикла = КоличествоКПечатиПоКаждомуШК - 1 Тогда
			ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаЭтойСвязки, "%ЗакрывющаяСкобка", ")");
		Иначе
			ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаЭтойСвязки, "%ЗакрывющаяСкобка", "");
		КонецЕсли;
		
		ТекстЗаказаОбщееУсловие = ТекстЗаказаОбщееУсловие + ТекстЗапросаЭтойСвязки;
		СчетчикЦикла = СчетчикЦикла + 1;
		
	КонецЦикла;
	
	ТекстЗаказаОбщий = ТекстЗапросаШаблон + ТекстЗаказаОбщееУсловие;
	
	ТекстЗапросаСвязкаПакета = ";
	|	///////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапросаСвязка = "
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	
	ТекстЗапросаКоличествоШаблон = "ВЫБРАТЬ ПЕРВЫЕ %КоличествоКПечати
	|	ВТКодыПромежуточная.GTIN КАК GTIN,
	|	ВТКодыПромежуточная.ВидПродукции КАК ВидПродукции,
	|	ВТКодыПромежуточная.ЗаказНаЭмиссию КАК ЗаказНаЭмиссию,
	|	ВТКодыПромежуточная.КодИдентификации КАК КодИдентификации,
	|	ВТКодыПромежуточная.Номенклатура КАК Номенклатура,
	|	ВТКодыПромежуточная.Организация КАК Организация,
	|	ВТКодыПромежуточная.СпособВводаВОборот КАК СпособВводаВОборот,
	|	ВТКодыПромежуточная.Характеристика КАК Характеристика,
	|	ВТКодыПромежуточная.ХешСуммаКодаИдентификации КАК ХешСуммаКодаИдентификации,
	|	ВТКодыПромежуточная.Шаблон КАК Шаблон,
	|	ВТКодыПромежуточная.ТипШтрихкода КАК ТипШтрихкода,
	|	ВТКодыПромежуточная.Штрихкод КАК Штрихкод,
	|	ИСТИНА КАК ДляПечати
	|ИЗ
	|	ВТКодыПромежуточная КАК ВТКодыПромежуточная
	|ГДЕ
	|	ВТКодыПромежуточная.GTIN = &GTIN
	|	И ВТКодыПромежуточная.ЗаказНаЭмиссию = &ЗаказНаЭмиссию";
	
	ТекстЗаказаКоличествоОбщий = "";
	СчетчикЦикла = 0;
	
	Пока Не СчетчикЦикла = КоличествоКПечатиПоКаждомуШК Цикл
		
		ИмяПараметраКоличество 	  = "КоличествоКПечати" + Строка(СчетчикЦикла + 1);
		ИмяПараметрGTIN 	      = "&GTIN" 		    + Строка(СчетчикЦикла + 1);
		ИмяПараметрЗаказНаЭмиссию = "&ЗаказНаЭмиссию"   + Строка(СчетчикЦикла + 1);
		
		ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаКоличествоШаблон, "КоличествоКПечати", ИмяПараметраКоличество);
		ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаЭтойСвязки, 	   "&GTIN", 			ИмяПараметрGTIN);
		ТекстЗапросаЭтойСвязки = СтрЗаменить(ТекстЗапросаЭтойСвязки, 	   "&ЗаказНаЭмиссию",   ИмяПараметрЗаказНаЭмиссию);
		ТекстЗаказаКоличествоОбщий = ТекстЗаказаКоличествоОбщий + ТекстЗапросаЭтойСвязки;
		
		Если Не СчетчикЦикла = КоличествоКПечатиПоКаждомуШК - 1 Тогда
			ТекстЗаказаКоличествоОбщий = ТекстЗаказаКоличествоОбщий + ТекстЗапросаСвязка;
		КонецЕсли;
		
		СчетчикЦикла = СчетчикЦикла + 1;
		
	КонецЦикла;
	
	ТекстЗаказаОбщий = ТекстЗаказаОбщий + ТекстЗапросаСвязкаПакета + ТекстЗаказаКоличествоОбщий;
	
	Возврат ТекстЗаказаОбщий;
	
КонецФункции

#Область ПроверкаЗаполненияДокументовИСМПТ

Функция ТабличныеЧастиДокументаИСМПТ(ДокументИСМПТ)
	
	ТабличныеЧастиВыбора = Новый СписокЗначений;	
	
	Если ТипЗнч(ДокументИСМПТ.Ссылка) = Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСМПТК") Тогда
		ТабличныеЧастиВыбора.Добавить("Марки", 	"Марки");
		ТабличныеЧастиВыбора.Добавить("ДокументыСоответствия", 	"Документы соответствия");
	КонецЕсли;
	
	Возврат ТабличныеЧастиВыбора;	
	
КонецФункции

Процедура ДокументАПП_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументЭкспортЕАЭС_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументПриемкаЕАЭС_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументУОР_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	ЕстьТовары = Ложь;
	ПроверяемыеТабличныеЧасти = ТабличныеЧастиДокументаИСМПТ(ДокументИСМПТ);
	Для Каждого ТабличнаяЧасть Из ПроверяемыеТабличныеЧасти Цикл
		Если ДокументИСМПТ[ТабличнаяЧасть.Значение] <> Неопределено Тогда
			ДокументИСМПТ_ПроверитьЗаполнениеТабличнойЧастиПострочно(ДокументИСМПТ, ДокументИСМПТ[ТабличнаяЧасть.Значение], ТабличнаяЧасть.Значение, ТабличнаяЧасть.Представление, Отказ, ЕстьТовары);
		КонецЕсли;
	КонецЦикла; 
		
КонецПроцедуры

Процедура ДокументВВО_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	ПроверяемыеРеквизиты.Добавить("ПричинаВводаВОборот");
	
КонецПроцедуры

Процедура ДокументВИО_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура УведомлениеОВвозеЕАЭС_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура УведомлениеОВвозеТретьиСтраны_ОбработкаПроверкиЗаполнения(ДокументИСМПТ, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументИСМПТ_ПроверитьЗаполнениеТабличнойЧастиПострочно(ОбъектДокумент, ПроверяемаяТабличнаяЧасть, ИмяТабличнойЧасти, ПредставлениеИмяТабличнойЧасти, Отказ, ЕстьТовары = Ложь)
	
	Если ПроверяемаяТабличнаяЧасть.Количество() = 0 Тогда
		ТекстСообщения = ИмяТабличнойЧасти + НСтр("ru = '. Данные не заполнены.'"); 
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТабличнойЧасти Из ПроверяемаяТабличнаяЧасть Цикл
		
		ЕстьТовары = Истина;
		ТекстСообщенияШаблон = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""%3""'");              
		
		Если ИмяТабличнойЧасти = "Товары" Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "Номенклатура", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].Номенклатура";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;	
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ТНВЭД) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "ТНВЭД", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].ТНВЭД";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.КоличествоИсточник) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "Количество", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].Количество";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтранаПроизводства) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "СтранаПроизводства", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].СтранаПроизводства";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.НомерПозицииВДекларацииИлиЗаявлении) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "НомерПозицииВДекларацииИлиЗаявлении", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].НомерПозицииВДекларацииИлиЗаявлении";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
		ИначеЕсли ИмяТабличнойЧасти = "Марки" Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.КодИдентификации) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "Код идентификации", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].Код";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Количество) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "Количество подобрано", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].Количество";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.GTIN) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "GTIN", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].GTIN";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
		ИначеЕсли ИмяТабличнойЧасти = "ДокументыСоответствия" Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ТипДокумента) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "Тип документа", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].ТипДокумента";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДатаДокумента) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "Дата документа", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].ДатаДокумента";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.НомерДокумента) Тогда
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияШаблон, "Номер документа", Формат(СтрокаТабличнойЧасти.НомерСтроки, "ЧН=0; ЧГ="), ПредставлениеИмяТабличнойЧасти);
				Поле = ИмяТабличнойЧасти + "[" + Формат(СтрокаТабличнойЧасти.НомерСтроки-1, "ЧН=0; ЧГ=") + "].НомерДокумента";
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, ОбъектДокумент, Поле, "Объект", Отказ);
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьСоответсвиеИменРеквизитовПолейЗапросов(СоответсвиеИменРеквизитовПолейЗапросов) Экспорт
	
	Для Каждого ЭлементСоответсвия Из СоответсвиеИменРеквизитовПолейЗапросов Цикл
		Значение = "";
		
		Если ЭлементСоответсвия.Ключ = "%ДанныеДокументаЕдиницаИзмерения" Тогда	
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%СтруктурнаяЕдиницаИдентификационныйНомер"	Тогда	
			Значение = "ИНН";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%КонтрагентИНН"	Тогда	
			Значение = "ИНН";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%ОрганизацияИНН"	Тогда	
			Значение = "ИНН";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%БуквенныйКодВалюты"	Тогда	
			Значение = "Наименование";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%ИмяРеквизитаВладелецДоговора"	Тогда	
			Значение = "Контрагент";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%ИмяРеквизитаОрганизацияДоговора"	Тогда	
			Значение = "Организация";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%ДоговорНомер"	Тогда	
			Значение = "Номер";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%ДоговорДата"	Тогда	
			Значение = "Дата";
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%ЕдиницаИзмерения"	Тогда
			Значение = "ЕдиницаИзмерения";			
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%КодТНВЭД"	Тогда
			Значение = "ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).КодТНВЭД.Код"
			
		ИначеЕсли ЭлементСоответсвия.Ключ = "%КлассификаторСтранМира" Тогда
			Значение = "Справочник.СтраныМира"
		КонецЕсли;
		
		СоответсвиеИменРеквизитовПолейЗапросов.Вставить(ЭлементСоответсвия.Ключ, Значение);
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьНаличиеСвязанногоДокументаИСМПТ(Основание, ЭтоПроверкаИзОснования = Ложь, ИмяКоманды = Неопределено) Экспорт
	
	Возврат РаботаСДокументамиИСМПТК.ПроверитьНаличиеСвязанногоДокументаМаркировки_Общая(Основание, ЭтоПроверкаИзОснования, ИмяКоманды);
	
КонецФункции

Процедура ЗаписатьШтрихкодНоменклатуры(Номенклатура, ШтрихкодEAN, ЕИ = Неопределено, Характеристика) Экспорт
	
	ОбщегоНазначенияИСМПТКПереопределяемый.ЗаписатьШтрихкодНоменклатуры(Номенклатура, ШтрихкодEAN, ЕИ, Характеристика);
	
КонецПроцедуры

Функция ПолучитьЕдиницуИзмерения(Номенклатура) Экспорт 
	
	ЕдиницаИзмерения = ОбщегоНазначенияИСМПТК.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
	
	Возврат ЕдиницаИзмерения;
	
КонецФункции

Функция ЭтоДокументПТУ(Ссылка) Экспорт
	
	Возврат ?(ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваров"), Истина, Ложь);
	
КонецФункции

Функция ДокументНеПоддерживаетСозданиеНовогоДляИСМПТ(Документ) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ОприходованиеТоваров")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.СписаниеТоваров") 
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваров") 
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказПоставщику") 
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ПеремещениеТоваров")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваров") 
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ВДокументеНужноЗаполнятьМаркиИзОснованияИСМПТ(ИмяДокументаИСМПТ, ДокументОснованиеДляЗаполнения) Экспорт
	
	Возврат (ИмяДокументаИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВыводеИзОборотаИСМПТК()
			И ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("ДокументСсылка.АктПриемаПередачиИСМПТК"))
		ИЛИ (ИмяДокументаИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСМПТК()
			И ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("ДокументСсылка.УведомлениеОРасхожденииИСМПТК"))
		ИЛИ (ИмяДокументаИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОРасхожденииИСМПТК()
			И ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("ДокументСсылка.АктПриемаПередачиИСМПТК"))
		ИЛИ (ИмяДокументаИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОПриемкеЕАЭСИСМПТК()
			И ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("ДокументСсылка.УведомлениеОбОтгрузкеЕАЭСИСМПТК"))
		ИЛИ (ИмяДокументаИСМПТ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК()
			И ТипЗнч(ДокументОснованиеДляЗаполнения) = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСМПТК"));
			
КонецФункции

#Область ЗаполнениеЗначенийПоУмолчаниюПриСозданииИСМПТ

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияАктаПриемаПередачиИСМПТ(ДокументыОснования) Экспорт

	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("ВидОперации",    ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСМПТК.Реализация"));
	СтруктураРеквизитов.Вставить("ТипАкта", 	   ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исходный"));
	СтруктураРеквизитов.Вставить("Направление",    ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	СтруктураРеквизитов.Вставить("Автор",  		   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка"));
	СтруктураРеквизитов.Вставить("ЭтапОформления", ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе"));
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВводеИСМПТ(ДокументыОснования) Экспорт

	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("ПричинаВводаВОборот", ПредопределенноеЗначение("Перечисление.ПричиныПовторногоВводаКМВОборотИСМПТК.Излишки"));
	СтруктураРеквизитов.Вставить("Автор",  		   		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа",		ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка"));
	СтруктураРеквизитов.Вставить("ЭтапОформления", 		ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе"));
	СтруктураРеквизитов.Вставить("Направление",    		ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	
	ДанныеДляРазбора = ДокументыОснования[0]; 
	СтруктураРеквизитов.Вставить("ДокументОснованиеНаименование", Строка(ТипЗнч(ДанныеДляРазбора)));
	СтруктураРеквизитов.Вставить("ДокументОснованиеДата",  ДанныеДляРазбора.Дата);
	СтруктураРеквизитов.Вставить("ДокументОснованиеНомер", ДанныеДляРазбора.Номер);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВыводеИСМПТ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("ПричинаВыводаИзОборота", ПредопределенноеЗначение("Перечисление.ПричиныВыводаКМИзОборотаИСМПТК.Другое"));
	СтруктураРеквизитов.Вставить("Автор",  		   		   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", 		   ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка"));
	СтруктураРеквизитов.Вставить("ЭтапОформления", 		   ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе"));
	СтруктураРеквизитов.Вставить("Направление",    		   ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	
	ДанныеДляРазбора = ДокументыОснования[0]; 
	СтруктураРеквизитов.Вставить("ДокументОснованиеНаименование", Строка(ТипЗнч(ДанныеДляРазбора)));
	СтруктураРеквизитов.Вставить("ДокументОснованиеДата",  ДанныеДляРазбора.Дата);
	СтруктураРеквизитов.Вставить("ДокументОснованиеНомер", ДанныеДляРазбора.Номер);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВвозеЕАЭСИСМПТ(ДокументыОснования) Экспорт

	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Автор", 		   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка"));
	СтруктураРеквизитов.Вставить("Направление",    ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	
	ДанныеДляРазбора = ДокументыОснования[0];
	СтруктураРеквизитов.Вставить("СтранаОтправления", РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьСтрануОтправленияВвозИзЕАЭС(ДанныеДляРазбора.Контрагент));
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВвозеИмпортИСМПТ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Автор", 		   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка"));
	СтруктураРеквизитов.Вставить("Направление",    ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	
	ДанныеДляРазбора = ДокументыОснования[0];
	СтруктураРеквизитов.Вставить("СтранаОтправления", РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьСтрануОтправленияВвозИзЕАЭС(ДанныеДляРазбора.Контрагент));
		
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОбЭкспортеИСМПТ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Автор", 			 ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("Направление", 	 ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", 	 ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная"));
	СтруктураРеквизитов.Вставить("СтранаПолучатель", Справочники.СтраныМира.Россия);
		
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОПриемкеИСМПТ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Автор", 			   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("Направление", 	   ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", 	   ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная"));
	СтруктураРеквизитов.Вставить("ОтклонитьВсеТовары", Ложь);
	СтруктураРеквизитов.Вставить("ДатаПриемки", 	   ТекущаяДатаСеанса());
			
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС МПТ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС МПТ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОРасхожденияхИСМПТ(ДокументыОснования) Экспорт

	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Направление",  ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	СтруктураРеквизитов.Вставить("Автор",  		 ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	
	ДанныеДляРазбора = ДокументыОснования[0];
	СтруктураРеквизитов.Вставить("ВидОперации",  ДанныеДляРазбора.ВидОперации);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

#КонецОбласти

#Область ЗаказКодовВСУЗ

Функция ПолучитьДанныеДокументаОснованияЗаказаКМНаСервере(ДокументОснование) Экспорт
	
	ТекстЗапроса = "";
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ТекстЗапроса = ТекстЗапросаЗаказПоставщику(ТекстЗапроса = "");
	КонецЕсли;
		
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	ТекстЗапроса = ДополнитьТекстЗапросЗаполненияЗаказКодов(ТекстЗапроса);
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументов", ДокументОснование);
	
	РезультатЗапроса  = Запрос.ВыполнитьПакет();
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	ТаблицаРеквизиты = РезультатЗапроса[КоличествоПакетов - 2].Выгрузить();
	Реквизиты = Новый Структура();
	
	Для Каждого Строка Из ТаблицаРеквизиты Цикл
		
		Реквизиты.Вставить("Организация", 		 Строка.Организация);
		Реквизиты.Вставить("ВидПродукции", 		 Строка.ВидПродукции);
		Реквизиты.Вставить("Ответственный", 	 Строка.Ответственный);
		Реквизиты.Вставить("ДокументОснование",  Строка.ДокументОснование);
		Реквизиты.Вставить("СпособВводаВОборот", Строка.СпособВводаВОборот);
		
	КонецЦикла;
	
	ТаблицаТовары = РезультатЗапроса[КоличествоПакетов - 1].Выгрузить(); 
	Товары = Новый Массив();
	
	Для Каждого Строка Из ТаблицаТовары Цикл
		
		//Из запроса получаем EAN, а для заполнения в документе нужно указывать GTIN 
		GTIN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.GTINПоШтрихкодуEAN(Строка.GTIN);
		
		СтрокаТовары = Новый Структура();
		СтрокаТовары.Вставить("Номенклатура", 	  	  	 Строка.Номенклатура);
		СтрокаТовары.Вставить("Характеристика",   		 Строка.Характеристика);
		СтрокаТовары.Вставить("Упаковка", 	 			 Строка.Упаковка);
		СтрокаТовары.Вставить("КоличествоУпаковок", 	 Строка.КоличествоУпаковок);
		СтрокаТовары.Вставить("Количество", 	  		 Строка.Количество);
		СтрокаТовары.Вставить("ЕдиницаИзмеренияБазовая", Строка.ЕдиницаИзмеренияБазовая);
		СтрокаТовары.Вставить("GTIN", 					 GTIN);
		
		Товары.Добавить(СтрокаТовары);
		
	КонецЦикла;
	
	ДанныеДокументаОснования = Новый Структура();
	ДанныеДокументаОснования.Вставить("Реквизиты", Реквизиты);
	ДанныеДокументаОснования.Вставить("Товары",	   Товары);
	
	Возврат ДанныеДокументаОснования;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляМассиваЗаказКодов(МассивВидовДокумента)
	
	ТекстЗапроса = "";
	Для Каждого ЭлементМассива Из МассивВидовДокумента Цикл
		
		ТекстЗапросаДокумента = "";
		Если ЭлементМассива = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ТекстЗапросаДокумента = ТекстЗапросаЗаказПоставщику(ТекстЗапроса = "");
		КонецЕсли;
		
		Если ТекстЗапроса <> "" И ТекстЗапросаДокумента <> "" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|"  
		КонецЕсли;	
		
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаДокумента;
		
	КонецЦикла;	
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПодготовитьТекстЗапросаДляЗаполненияЗаказКодов(ПараметрыЗаполнения) Экспорт
	
	ДокументыОснования 	 = ПараметрыЗаполнения.МассивДокументов;
	МассивВидовДокумента = ПараметрыЗаполнения.МассивВидовДокумента;
	
	Если ТипЗнч(ДокументыОснования) = Тип("Массив") Тогда
		ТекстЗапроса = ПолучитьТекстЗапросаДляМассиваЗаказКодов(МассивВидовДокумента);
		Если ТекстЗапроса = "" Тогда
			Возврат Неопределено;
		КонецЕсли;	   

	ИначеЕсли ТипЗнч(ДокументыОснования) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ТекстЗапроса = ТекстЗапросаЗаказПоставщику();
	
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаказПоставщику(ПервыйЗапрос = Истина)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПоставщику.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Обувная)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция)
	|				ИНАЧЕ ВЫБОР
	|						КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты)
	|						ИНАЧЕ ВЫБОР
	|								КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Табачная)
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК ВидПродукции,
	|	ЗаказПоставщику.Ответственный КАК Ответственный,
	|	ТаблицаТовары.Ссылка КАК ДокументОснование,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияБазовая,
	|	ЗаказПоставщику.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК GTIN
	|ПОМЕСТИТЬ вт_ДанныеДокумента_Предварительная
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|		ПО ТаблицаТовары.Ссылка = ЗаказПоставщику.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|		ПО ТаблицаТовары.Номенклатура = ШтрихкодыНоменклатуры.%ИмяНоменклатура%
	|			И ТаблицаТовары.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|			И ТаблицаТовары.Упаковка = ШтрихкодыНоменклатуры.Упаковка
	|ГДЕ
	|	ЗаказПоставщику.Ссылка В(&МассивДокументов)
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|			ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|			ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|			ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента_Предварительная.Организация КАК Организация,
	|	вт_ДанныеДокумента_Предварительная.ВидПродукции КАК ВидПродукции,
	|	вт_ДанныеДокумента_Предварительная.Ответственный КАК Ответственный,
	|	вт_ДанныеДокумента_Предварительная.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента_Предварительная.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДокумента_Предварительная.Характеристика КАК Характеристика,
	|	вт_ДанныеДокумента_Предварительная.Упаковка КАК Упаковка,
	|	СУММА(вт_ДанныеДокумента_Предварительная.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(вт_ДанныеДокумента_Предварительная.Количество) КАК Количество,
	|	вт_ДанныеДокумента_Предварительная.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	ВЫБОР
	|		КОГДА вт_ДанныеДокумента_Предварительная.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеТоваров)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗИСМПТК.Производство)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА вт_ДанныеДокумента_Предварительная.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|						ИЛИ вт_ДанныеДокумента_Предварительная.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗИСМПТК.Импорт)
	|			КОНЕЦ
	|	КОНЕЦ КАК СпособВводаВОборот,
	|	вт_ДанныеДокумента_Предварительная.GTIN КАК GTIN
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	вт_ДанныеДокумента_Предварительная КАК вт_ДанныеДокумента_Предварительная
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ДанныеДокумента_Предварительная.ВидПродукции,
	|	вт_ДанныеДокумента_Предварительная.Организация,
	|	вт_ДанныеДокумента_Предварительная.Номенклатура,
	|	вт_ДанныеДокумента_Предварительная.Ответственный,
	|	вт_ДанныеДокумента_Предварительная.Упаковка,
	|	вт_ДанныеДокумента_Предварительная.ДокументОснование,
	|	вт_ДанныеДокумента_Предварительная.Характеристика,
	|	вт_ДанныеДокумента_Предварительная.ЕдиницаИзмеренияБазовая,
	|	ВЫБОР
	|		КОГДА вт_ДанныеДокумента_Предварительная.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеТоваров)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗИСМПТК.Производство)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА вт_ДанныеДокумента_Предварительная.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|						ИЛИ вт_ДанныеДокумента_Предварительная.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗИСМПТК.Импорт)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	вт_ДанныеДокумента_Предварительная.GTIN";    
	
	Если Не ПервыйЗапрос Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ вт_ДанныеДокумента", "");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры()); 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%", ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочееОприходованиеТоваров(ПервыйЗапрос = Истина)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПрочееОприходованиеТоваров.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Обувная)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция)
	|				ИНАЧЕ ВЫБОР
	|						КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты)
	|						ИНАЧЕ ВЫБОР
	|								КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Табачная)
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК ВидПродукции,
	|	ПрочееОприходованиеТоваров.Ответственный КАК Ответственный,
	|	ТаблицаТовары.Ссылка КАК ДокументОснование,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияБазовая,
	|	ПрочееОприходованиеТоваров.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК GTIN
	|ПОМЕСТИТЬ вт_ДанныеДокумента_Предварительная
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров
	|		ПО ТаблицаТовары.Ссылка = ПрочееОприходованиеТоваров.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|		ПО ТаблицаТовары.Номенклатура = ШтрихкодыНоменклатуры.%ИмяНоменклатура%
	|			И ТаблицаТовары.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|			И ТаблицаТовары.Упаковка = ШтрихкодыНоменклатуры.Упаковка
	|ГДЕ
	|	ПрочееОприходованиеТоваров.Ссылка В(&МассивДокументов)
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|			ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|			ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|			ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента_Предварительная.Организация КАК Организация,
	|	вт_ДанныеДокумента_Предварительная.ВидПродукции КАК ВидПродукции,
	|	вт_ДанныеДокумента_Предварительная.Ответственный КАК Ответственный,
	|	вт_ДанныеДокумента_Предварительная.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента_Предварительная.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДокумента_Предварительная.Характеристика КАК Характеристика,
	|	вт_ДанныеДокумента_Предварительная.Упаковка КАК Упаковка,
	|	СУММА(вт_ДанныеДокумента_Предварительная.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(вт_ДанныеДокумента_Предварительная.Количество) КАК Количество,
	|	вт_ДанныеДокумента_Предварительная.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗИСМПТК.Производство) КАК СпособВводаВОборот,
	|	вт_ДанныеДокумента_Предварительная.GTIN КАК GTIN
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	вт_ДанныеДокумента_Предварительная КАК вт_ДанныеДокумента_Предварительная
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ДанныеДокумента_Предварительная.ВидПродукции,
	|	вт_ДанныеДокумента_Предварительная.Организация,
	|	вт_ДанныеДокумента_Предварительная.Номенклатура,
	|	вт_ДанныеДокумента_Предварительная.Ответственный,
	|	вт_ДанныеДокумента_Предварительная.Упаковка,
	|	вт_ДанныеДокумента_Предварительная.ДокументОснование,
	|	вт_ДанныеДокумента_Предварительная.Характеристика,
	|	вт_ДанныеДокумента_Предварительная.ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДокумента_Предварительная.GTIN";     
	
	Если Не ПервыйЗапрос Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ вт_ДанныеДокумента", "");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры()); 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%", ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды()); 
			
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ДополнитьТекстЗапросЗаполненияЗаказКодов(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.Организация,
	|	вт_ДанныеДокумента.ВидПродукции,
	|	вт_ДанныеДокумента.Ответственный,
	|	вт_ДанныеДокумента.ДокументОснование,
	|	вт_ДанныеДокумента.Номенклатура,
	|	вт_ДанныеДокумента.Характеристика,
	|	вт_ДанныеДокумента.Упаковка,
	|	вт_ДанныеДокумента.КоличествоУпаковок,
	|	вт_ДанныеДокумента.Количество,
	|	вт_ДанныеДокумента.ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДокумента.СпособВводаВОборот,
	|	вт_ДанныеДокумента.GTIN
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|ГДЕ
	|	вт_ДанныеДокумента.Количество > 0
	|";
	
	Возврат ТекстЗапроса + РаботаСДокументамиИСМПТК.ПолучитьТекстПакетовЗапросаДляЗаполненияЗаказКодов();
	
КонецФункции

// Используется в документе ЗаказНаЭмиссиюКодовМаркировкиСУЗ при заполнении ТЧ. 
// Функция возвращает значение GTIN из РС ШтрихкодыНоменклатуры по совпадению полей Номенклатура, Характеристика (если используются), Упаковка (если заполнена),
// если штрихкод не найден, если найден, но не EAN13 или если  
// 
Функция ПодобратьGTINДляЗаказа(Номенклатура, Упаковка, Характеристика, ХарактеристикиИспользуются, ЭтоТабак = Ложь) Экспорт
	
	Возврат РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйПереопределяемый.ПодобратьGTINДляЗаказа(Номенклатура, Упаковка, Характеристика, ХарактеристикиИспользуются, ЭтоТабак);
	
КонецФункции

Функция ПолучитьСписокВсехGTINНоменклатуры(Номенклатура) Экспорт
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура% КАК Номенклатура,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%.Представление КАК ПредставлениеНоменклатура,
	|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	|	ШтрихкодыНоменклатуры.Характеристика.Представление КАК ПредставлениеХарактеристика,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка,
	|	ШтрихкодыНоменклатуры.Упаковка.Представление КАК ПредставлениеУпаковка
	|ИЗ
	|	РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура% = &Номенклатура";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры()); 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%", ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды()); 
		
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СпискоЗначенийШтрихКодов = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		GTIN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.GTINПоШтрихкодуEAN(Выборка.Штрихкод);
		ПредставлениеКода = "" + GTIN + " / " + Выборка.ПредставлениеНоменклатура;
		
		Если ЗначениеЗаполнено(Выборка.ПредставлениеХарактеристика) Тогда 
			ПредставлениеКода = ПредставлениеКода + " / " + Выборка.ПредставлениеХарактеристика;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ПредставлениеУпаковка) Тогда
			ПредставлениеКода = ПредставлениеКода + " / " + Выборка.ПредставлениеУпаковка;
		КонецЕсли;
		
		СпискоЗначенийШтрихКодов.Добавить(GTIN, ПредставлениеКода);
	КонецЦикла;
	
	Возврат СпискоЗначенийШтрихКодов;
	
КонецФункции

Функция ПодобратьДанныеПоGTINДляЗаказа(МассивGTIN) Экспорт
	
	Возврат ОбщегоНазначенияИСМПТКПереопределяемый.ПодобратьДанныеПоGTINДляЗаказа(МассивGTIN);
	
КонецФункции

Функция ПолучитьДанныеОНоменклатуреПоGTIN(ТаблицаШК) Экспорт
	
	Возврат ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьДанныеОНоменклатуреПоGTIN(ТаблицаШК);
	
КонецФункции

Функция МассивGTINМаркированногоТовара(Номенклатура, Характеристика, Упаковка) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод
	|ИЗ
	|	РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура% = &Номенклатура
	|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика
	|	<Упаковка>";
	
	Если ЗначениеЗаполнено(Упаковка) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<Упаковка>", "И ШтрихкодыНоменклатуры.Упаковка = &Упаковка");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<Упаковка>", "И ШтрихкодыНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%", ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды());
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Номенклатура",   Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Если ЗначениеЗаполнено(Упаковка) Тогда
		Запрос.УстановитьПараметр("Упаковка",   Упаковка);
	КонецЕсли;
	
	МассивШтрихкодов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Штрихкод");
	СписокGTIN =  Новый Массив;
	Для Каждого Штрихкод Из МассивШтрихкодов Цикл
		
		Если РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ПроверитьКорректностьGTIN(Штрихкод) Тогда
			СписокGTIN.Добавить(Штрихкод);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокGTIN;
	
КонецФункции

Функция ЗаполнитьЗаказИзПодбора(ТаблицаТоваров, ВидПродукции, АдресТоваровВХранилище) Экспорт
	
	ТаблицаПодбора = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	ПроверятьВидПродукции = Не ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка");
	
	Для Каждого СтрокаТоваров Из ТаблицаПодбора Цикл
		
		ВидПродукцииНоменклатуры = ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьВидПродукцииПоНоменклатуре(СтрокаТоваров.Номенклатура);
		
		Если Не ВидПродукцииНоменклатуры = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка") 
			И (Не ПроверятьВидПродукции Или ВидПродукции = ВидПродукцииНоменклатуры)Тогда
			
			Характеристика 			   = СтрокаТоваров.Характеристика;
			ХарактеристикиИспользуются = СтрокаТоваров.ХарактеристикиИспользуются;
			
			УпаковкаНеШтучная = ОбщегоНазначенияИСМПТКПереопределяемый.УпаковкаНеШтучная(СтрокаТоваров.Номенклатура, СтрокаТоваров.Упаковка);
			
			Если ВидПродукцииНоменклатуры = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Табачная") Тогда 
				//Для табака важна конкретная упаковка в регистре, т.к. от этого зависит шаблон кода: на базовую ЕИ выставляется пачка сигарет, на упаковку - блок.
				ДанныеШтрихкодов = ПодобратьGTINДляЗаказа(СтрокаТоваров.Номенклатура, СтрокаТоваров.Упаковка, Характеристика, ХарактеристикиИспользуются, Истина);
			Иначе
				//Для остальных ТГ не принципиально значение упаковки, т.к. групповых не поддерживается или они имеют такой же шаблон кода, как потребительские.
				ДанныеШтрихкодов = ПодобратьGTINДляЗаказа(СтрокаТоваров.Номенклатура, СтрокаТоваров.Упаковка, Характеристика, ХарактеристикиИспользуются);
			КонецЕсли;
			
			Если ДанныеШтрихкодов.Количество() = 0 Тогда
				ТекстСообщения = НСтр("ru='Для номенклатуры %1 не зарегистрирован штрихкод! Добавление невозможно.'");
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТоваров.Номенклатура);
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
				Продолжить;
			ИначеЕсли ДанныеШтрихкодов.Количество() = 1 Тогда
				GTIN = ДанныеШтрихкодов[0];
			Иначе
				GTIN = "";
			КонецЕсли;
			
			Если ВидПродукцииНоменклатуры = Перечисления.ВидыПродукцииИСМПТК.Обувная Тогда
				Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗИСМПТК.Обувь;
				
			ИначеЕсли ВидПродукцииНоменклатуры = Перечисления.ВидыПродукцииИСМПТК.МолочнаяПродукция Тогда
				Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗИСМПТК.МолочнаяПродукция;
				
			ИначеЕсли ВидПродукцииНоменклатуры = Перечисления.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты Тогда
				Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗИСМПТК.ЛекарственныеПрепараты;
				
			ИначеЕсли ВидПродукцииНоменклатуры = Перечисления.ВидыПродукцииИСМПТК.ЛегкаяПромышленность Тогда
				Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗИСМПТК.ЛегкаяПромышленность;
				
			ИначеЕсли ВидПродукцииНоменклатуры = Перечисления.ВидыПродукцииИСМПТК.Табачная Тогда
				Если ЗначениеЗаполнено(СтрокаТоваров.Упаковка) 
					И УпаковкаНеШтучная Тогда
					//При регистрации ШК указана упаковка и при этом она не равна 1 единице товара
					Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗИСМПТК.БлокТабачныхПачек;
				Иначе
					Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗИСМПТК.ТабачнаяПачка;
				КонецЕсли;
			Иначе
				//Значение по умолчанию
				Шаблон = ИнтеграцияИСМПТКВызовСервера.ПолучитьШаблонДляНоменклатурыЗаказа(СтрокаТоваров.Номенклатура);
			КонецЕсли;
			
			НоваяСтрока = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров,,"Количество, КоличествоУпаковок");
			
			Если Не ЗначениеЗаполнено(НоваяСтрока.Упаковка)
				Или НЕ УпаковкаНеШтучная Тогда
				НоваяСтрока.Упаковка = РозничноеВыбытиеИСМПТКПереопределяемый.ПолучитьЗначениеБазовойЕИНоменклатуры(НоваяСтрока.Номенклатура);
				//Базовая ЕИ, значит количество = количеству упаковок
				НоваяСтрока.Количество 		   = СтрокаТоваров.КоличествоУпаковок;
				НоваяСтрока.КоличествоУпаковок = СтрокаТоваров.КоличествоУпаковок;
			Иначе
				Если Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗИСМПТК.БлокТабачныхПачек Тогда
					НоваяСтрока.Количество 		   = СтрокаТоваров.КоличествоУпаковок;
					НоваяСтрока.КоличествоУпаковок = СтрокаТоваров.КоличествоУпаковок;	
				Иначе
					НоваяСтрока.Количество 		   = СтрокаТоваров.Количество;
					НоваяСтрока.КоличествоУпаковок = СтрокаТоваров.КоличествоУпаковок;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока.GTIN = GTIN;
			Если GTIN = "" Тогда
				ТекстСообщения = НСтр("ru='В строке %1 требуется уточнение GTIN.'");
				ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, НоваяСтрока.НомерСтроки);
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
			КонецЕсли;
			
			НоваяСтрока.Шаблон = Шаблон;
			НоваяСтрока.СпособФормированияСерийногоНомера = ПредопределенноеЗначение("Перечисление.СпособыФормированияСерийногоНомераСУЗИСМПТК.Автоматически");
			
		Иначе
			ТекстСообщения = НСтр("ru='Особенности учета номенклатуры %1 не соответствуют указанному в документе виду продукции! Добавление невозможно.'");
			ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТоваров.Номенклатура);
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

#КонецОбласти

#Область НанесениеКодов

Функция ПодготовитьТекстЗапросаДляЗаполненияНанесенияКодов(ПараметрыЗаполнения) Экспорт
	
	ДокументыОснования 	 = ПараметрыЗаполнения.МассивДокументов;
	МассивВидовДокумента = ПараметрыЗаполнения.МассивВидовДокумента;
	
	Если ТипЗнч(ДокументыОснования) = Тип("Массив") Тогда
		ТекстЗапроса = ПолучитьТекстЗапросаДляМассиваНанесениеКодов(МассивВидовДокумента);
		Если ТекстЗапроса = "" Тогда
			Возврат Неопределено;
		КонецЕсли;	   
	ИначеЕсли ТипЗнч(ДокументыОснования) = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК") Тогда
		ТекстЗапроса = ТекстЗапросаЗаказКМДляНанесения();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляМассиваНанесениеКодов(МассивВидовДокумента)
	
	ТекстЗапроса = "";
	Для Каждого ЭлементМассива Из МассивВидовДокумента Цикл
		ТекстЗапросаДокумента = "";
		Если ЭлементМассива = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК") Тогда
			ТекстЗапросаДокумента = ТекстЗапросаЗаказКМДляНанесения(ТекстЗапроса = "");
		КонецЕсли;
		
		Если ТекстЗапроса <> "" И ТекстЗапросаДокумента <> "" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|"  
		КонецЕсли;	
		
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаДокумента;
		
	КонецЦикла;	
	
	Возврат ТекстЗапроса;
	
КонецФункции

//Документ НанесениеКодовМаркировкиСУЗИСМПТК вводится на основании ЗаказКодовМаркировкиСУЗИСМПТК 
//для ТГ Молочная продукция, Лекарственные препараты (т.к. для Обуви нанесение происходит автоматически при получении КМ)
//
Функция ТекстЗапросаЗаказКМДляНанесения(ПервыйЗапрос = Истина)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Ссылка КАК ДокументОснование,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Организация КАК Организация,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.ВидПродукции КАК ВидПродукции,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Ответственный КАК Ответственный,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.ИдентификаторПроизводственнойЛинии КАК ИдентификаторПроизводственнойЛинии,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодИдентификации КАК КодИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.Номенклатура КАК Номенклатура,
	|	ПулКодовМаркировкиСУЗИСМПТК.Характеристика КАК Характеристика,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки КАК КодМаркировки,
	|	ПулКодовМаркировкиСУЗИСМПТК.GTIN КАК GTIN
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	Документ.ЗаказКодовМаркировкиСУЗИСМПТК КАК ЗаказКодовМаркировкиСУЗИСМПТК
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ПулКодовМаркировкиСУЗИСМПТК
	|		ПО ЗаказКодовМаркировкиСУЗИСМПТК.Ссылка = ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию
	|ГДЕ
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Ссылка В(&МассивДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Ссылка,
	|	ПулКодовМаркировкиСУЗИСМПТК.Номенклатура,
	|	ПулКодовМаркировкиСУЗИСМПТК.Характеристика,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.GTIN,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Организация,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.ВидПродукции,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.ИдентификаторПроизводственнойЛинии,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Ответственный";    
	
	Если Не ПервыйЗапрос Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ вт_ДанныеДокумента", "");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ДополнитьТекстЗапросЗаполненияНанесениеКодов(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.ДокументОснование,
	|	вт_ДанныеДокумента.Организация,
	|	вт_ДанныеДокумента.ВидПродукции,
	|	вт_ДанныеДокумента.Ответственный,
	|	вт_ДанныеДокумента.ИдентификаторПроизводственнойЛинии,
	|	вт_ДанныеДокумента.КодИдентификации,
	|	вт_ДанныеДокумента.Номенклатура,
	|	вт_ДанныеДокумента.Характеристика,
	|	вт_ДанныеДокумента.КодМаркировки,
	|	вт_ДанныеДокумента.GTIN
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|";
	
	Возврат ТекстЗапроса + РаботаСДокументамиИСМПТК.ПолучитьТекстПакетовЗапросаДляЗаполненияНанесенияКодов();
	
КонецФункции

Функция ПолучитьДанныеДокументаОснованияНанесениеКМНаСервере(ДокументОснование) Экспорт
	
	ТекстЗапроса = "";
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК") Тогда
		ТекстЗапроса = ТекстЗапросаЗаказКМДляНанесения(ТекстЗапроса = "");
		ЗаполнятьТоварыПолностью = Ложь;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	ТекстЗапроса = ДополнитьТекстЗапросЗаполненияНанесениеКодов(ТекстЗапроса);
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументов", ДокументОснование);
	
	РезультатЗапроса  = Запрос.ВыполнитьПакет();
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	ТаблицаРеквизиты = РезультатЗапроса[КоличествоПакетов - 2].Выгрузить();
	Реквизиты = Новый Структура();
	
	Для Каждого Строка Из ТаблицаРеквизиты Цикл
		
		Реквизиты.Вставить("Организация", 		Строка.Организация);
		Реквизиты.Вставить("ДокументОснование", Строка.ДокументОснование);
		Реквизиты.Вставить("ВидПродукции", 		Строка.ВидПродукции);
		Реквизиты.Вставить("Ответственный",  	Строка.Ответственный);
		
	КонецЦикла;
	
	ТаблицаТовары = РезультатЗапроса[КоличествоПакетов - 1].Выгрузить(); 
	Товары = Новый Массив();
	
	Для Каждого Строка Из ТаблицаТовары Цикл
		
		СтрокаТовары = Новый Структура();
		СтрокаТовары.Вставить("Номенклатура", 	  Строка.Номенклатура);
		СтрокаТовары.Вставить("Характеристика",   Строка.Характеристика);
		СтрокаТовары.Вставить("GTIN", 			  Строка.GTIN);
		СтрокаТовары.Вставить("КодИдентификации", Строка.КодИдентификации);
		СтрокаТовары.Вставить("КодМаркировки", 	  Строка.КодМаркировки);
		
		Товары.Добавить(СтрокаТовары);
		
	КонецЦикла;
	
	ДанныеДокументаОснования = Новый Структура();
	ДанныеДокументаОснования.Вставить("Реквизиты", Реквизиты);
	ДанныеДокументаОснования.Вставить("Товары",	   Товары);
	
	Возврат ДанныеДокументаОснования;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбменИСЦЭДМ

#Область СсылкаНаИСЦЭДМ

Процедура ОбновитьРеквизитыИСЦЭДМ(ДанныеФормы, РеквизитНадписьИСЦЭДМ = "НадписьДокументИСЦЭДМ", ТребуетсяИСЦЭДМ = Истина, ГруппаДляВключенияМеню = Неопределено, ПослеГруппы = "") Экспорт
	
	Если ПолучитьДанныеИСЦЭДМ(ДанныеФормы, ГруппаДляВключенияМеню, ПослеГруппы) Тогда 
		ЗаполнитьТекстПроИСЦЭДМ(ДанныеФормы, РеквизитНадписьИСЦЭДМ, ТребуетсяИСЦЭДМ);
		УправлениеГруппойИСЦЭДМ(ДанныеФормы, ТребуетсяИСЦЭДМ);
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьДанныеИСЦЭДМ(ДанныеФормы, ГруппаДляВключенияМеню, ПослеГруппы = "") Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемыхЛекарствИСМПТК") Тогда //ИСЦЭДМ Работает только с фармой
		Возврат Ложь;
	КонецЕсли;
	
	//1.Создаем элементы маркировки на форме документа-основания
	Если ДанныеФормы.Элементы.Найти("НадписьДокументИСЦЭДМ") = Неопределено Тогда
		Если ГруппаДляВключенияМеню = Неопределено 
			И НЕ ДанныеФормы.Элементы.Найти("ГруппаПрочиеЭлектронныеДокументы") = Неопределено Тогда
			//Используем группу формы по умолчанию
			СоздатьЭлементыФормыДляВводаИСЦЭДМ(ДанныеФормы, "ГруппаПрочиеЭлектронныеДокументы", ПослеГруппы);
		ИначеЕсли Не ГруппаДляВключенияМеню = Неопределено Тогда
			//Размещаем элементы в группе типовой формы
			СоздатьЭлементыФормыДляВводаИСЦЭДМ(ДанныеФормы, ГруппаДляВключенияМеню, ПослеГруппы);
		Иначе
			ТекстОшибки = НСтр("ru='Невозможно разместить элементы подсистемы ИС ЦЭДМ на форме документа: не назначена группа формы для включения!'");
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	//2. Заполняем созданные реквизиты
	ДокументОснование = ДанныеФормы.Объект.Ссылка; 
	
	//Находим ссылки на связанные документы маркировки
	Если ЗначениеЗаполнено(ДанныеФормы.ДокументИСЦЭДМСсылка) Тогда
		ДокументИСЦЭДМСсылка = ДанныеФормы.ДокументИСЦЭДМСсылка;
	Иначе
		СвязанныеДокументыМаркировки = ПроверитьНаличиеСвязанногоДокументаИСЦЭДМ(ДанныеФормы.Объект.Ссылка, Истина);
		
		Если ТипЗнч(СвязанныеДокументыМаркировки) <> Тип("СписокЗначений") И СвязанныеДокументыМаркировки <> Неопределено Тогда //Связанный документ прямая ссылка на документ
			
			ДокументИСЦЭДМСсылка = СвязанныеДокументыМаркировки.Ссылка;
			
		ИначеЕсли ТипЗнч(СвязанныеДокументыМаркировки) = Тип("СписокЗначений") И СвязанныеДокументыМаркировки.Количество() = 1 Тогда
			
			ДокументИСЦЭДМСсылка = СвязанныеДокументыМаркировки[0].Значение;
			
		ИначеЕсли ТипЗнч(СвязанныеДокументыМаркировки) = Тип("СписокЗначений") И СвязанныеДокументыМаркировки.Количество()> 1 Тогда
			
			Если ДанныеФормы.СписокДокументыИСЦЭДМСсылка.Количество() = 0 Тогда
				Для Каждого СтрокаТаблицыИсточник Из СвязанныеДокументыМаркировки Цикл
					ЗаполнитьЗначенияСвойств(ДанныеФормы.СписокДокументыИСЦЭДМСсылка.Добавить(), СтрокаТаблицыИсточник);
				КонецЦикла;
			КонецЕсли;
			
		Иначе 
			
			//Если не нашлось связанных документов
			ДокументИСЦЭДМСсылка = Неопределено;
			ДанныеФормы.СписокДокументыИСЦЭДМСсылка.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеДокументаИСЦЭДМ = Неопределено;
	
	Если ЗначениеЗаполнено(ДокументИСЦЭДМСсылка) Тогда
		
		РеквизитыИСЦЭДМ = Новый Структура("Ссылка, Дата, ПометкаУдаления", ДокументИСЦЭДМСсылка, '00010101', Ложь);
		
		ЗначенияРеквизитов = ОбщегоНазначенияИСМПТК.ЗначенияРеквизитовОбъекта(ДокументИСЦЭДМСсылка, "Дата, ПометкаУдаления");
		ЗаполнитьЗначенияСвойств(РеквизитыИСЦЭДМ, ЗначенияРеквизитов);
		
		ТекстИСЦЭДМ  = ПолучитьТекстИСЦЭДМ(РеквизитыИСЦЭДМ);
		
		ДанныеДокументаИСЦЭДМ = Новый Структура("ИСЦЭДМ, ТекстИСЦЭДМ, ПометкаУдаления", ДокументИСЦЭДМСсылка, ТекстИСЦЭДМ, РеквизитыИСЦЭДМ.ПометкаУдаления);
		ДанныеФормы.ДокументИСЦЭДМСсылка  = ДокументИСЦЭДМСсылка;
		ДанныеФормы.ДанныеДокументаИСЦЭДМ = ДанныеДокументаИСЦЭДМ;
		
	ИначеЕсли ДанныеФормы.СписокДокументыИСЦЭДМСсылка.Количество() <> 0 Тогда
		
		СписокСсылокИСЦЭДМ = Новый СписокЗначений;
		
		Для каждого СсылкаИСЦЭДМ Из ДанныеФормы.СписокДокументыИСЦЭДМСсылка Цикл
			
			РеквизитыИСЦЭДМ    = Новый Структура("Ссылка, Дата, ПометкаУдаления", СсылкаИСЦЭДМ.Значение, '00010101', Ложь);
			ЗначенияРеквизитов = ОбщегоНазначенияИСМПТК.ЗначенияРеквизитовОбъекта(СсылкаИСЦЭДМ.Значение, "Дата, ПометкаУдаления");
			ЗаполнитьЗначенияСвойств(РеквизитыИСЦЭДМ, ЗначенияРеквизитов);
			
			СписокСсылокИСЦЭДМ.Добавить(РеквизитыИСЦЭДМ);
			
		КонецЦикла; 
		
		ТекстИСЦЭДМ = ПолучитьТекстИСЦЭДМ(СписокСсылокИСЦЭДМ);
		
		Если СписокСсылокИСЦЭДМ.Количество() <> 0 Тогда
			
			СписокДокументыИСЦЭДМСсылка = Новый СписокЗначений;
			
			Для каждого Ссылки Из СписокСсылокИСЦЭДМ Цикл
				СсылкаИСЦЭДМ = Ссылки.Значение.Ссылка;
				СписокДокументыИСЦЭДМСсылка.Добавить(СсылкаИСЦЭДМ);
			КонецЦикла; 
			
			ДанныеДокументаИСЦЭДМ = Новый Структура("ИСЦЭДМ, ТекстИСЦЭДМ, ПометкаУдаления", СписокДокументыИСЦЭДМСсылка, ТекстИСЦЭДМ, РеквизитыИСЦЭДМ.ПометкаУдаления);
			ДанныеФормы.СписокДокументыИСЦЭДМСсылка = СписокДокументыИСЦЭДМСсылка;
			ДанныеФормы.ДанныеДокументаИСЦЭДМ = ДанныеДокументаИСЦЭДМ;
			
		КонецЕсли;
	Иначе
		ДанныеФормы.ДокументИСЦЭДМСсылка  = ДокументИСЦЭДМСсылка;
		ДанныеФормы.ДанныеДокументаИСЦЭДМ = ДанныеДокументаИСЦЭДМ;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьТекстИСЦЭДМ(ДанныеДокументовИСЦЭДМ) Экспорт
	
	Если ТипЗнч(ДанныеДокументовИСЦЭДМ) = Тип("СписокЗначений") Тогда
		
		НадписьКоличествоИСЦЭДМ = НСтр("ru='Всего документов ИС ЦЭДМ: (%КоличествоДокументов%)'");
		Текст = СтрЗаменить(НадписьКоличествоИСЦЭДМ, "%КоличествоДокументов%", ДанныеДокументовИСЦЭДМ.Количество());
		
	Иначе
		
		ДокументИСЦЭДМ = ?(ДанныеДокументовИСЦЭДМ.Свойство("Ссылка"), ДанныеДокументовИСЦЭДМ.Ссылка, Неопределено);
		Если Не ДокументИСЦЭДМ = Неопределено Тогда
			ПредставлениеДокумента = Строка(ДокументИСЦЭДМ);
		Иначе
			ПредставлениеДокумента = "";
		КонецЕсли;
		ДополнительныйТекст = "";
		
		Если ДанныеДокументовИСЦЭДМ.ПометкаУдаления Тогда
			ДополнительныйТекст = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияПомеченНаУдаление();
		КонецЕсли;
		
		Текст = ПредставлениеДокумента + ДополнительныйТекст;
		
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции 

Процедура ЗаполнитьТекстПроИСЦЭДМ(Форма, РеквизитНадписьИСЦЭДМ = "НадписьДокументИСЦЭДМ", ТребуетсяИСЦЭДМ = Истина) Экспорт
	
	ДанныеДокументаИСЦЭДМ = Форма.ДанныеДокументаИСЦЭДМ;
	
	Если ТребуетсяИСЦЭДМ И ДанныеДокументаИСЦЭДМ = Неопределено Тогда
		Форма[РеквизитНадписьИСЦЭДМ] = "";
		Возврат;
	КонецЕсли;
	
	Если НЕ ТребуетсяИСЦЭДМ Тогда
		Форма[РеквизитНадписьИСЦЭДМ] = НСтр("ru='Не требуется'");
		Форма.Элементы[РеквизитНадписьИСЦЭДМ].ЦветТекста = Новый Цвет(51, 51, 51); // текст формы
	Иначе
		Форма[РеквизитНадписьИСЦЭДМ] = ДанныеДокументаИСЦЭДМ.ТекстИСЦЭДМ;
		
		Если ТребуетсяИСЦЭДМ И (ДанныеДокументаИСЦЭДМ.ПометкаУдаления) Тогда
			Форма.Элементы[РеквизитНадписьИСЦЭДМ].ЦветТекста = Новый Цвет(255, 0, 0);  // красный
			
		ИначеЕсли ТипЗнч(ДанныеДокументаИСЦЭДМ.ИСЦЭДМ) = Тип("СписокЗначений") Тогда
			//Если связанных документов несколько, их состояние может быть разным. 
			//Поэтому выводим общую ссылку в нейтральном цвете.
			Форма.Элементы[РеквизитНадписьИСЦЭДМ].ЦветТекста = Новый Цвет(51, 51, 51); // текст формы
			
		ИначеЕсли Не ЗначениеЗаполнено(ДанныеДокументаИСЦЭДМ.ИСЦЭДМ.НомерИСЦЭДМ) Тогда
			
			//Документ не отправлен на сервер ИС МПТ
			Форма.Элементы[РеквизитНадписьИСЦЭДМ].ЦветТекста = Новый Цвет(51, 51, 51); // текст формы
		Иначе
			Форма.Элементы[РеквизитНадписьИСЦЭДМ].ЦветТекста = Новый Цвет(0, 100, 0); // зеленый
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеГруппойИСЦЭДМ(Форма, ТребуетсяИСЦЭДМ = Истина) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если ТребуетсяИСЦЭДМ И (ЗначениеЗаполнено(Форма.СписокДокументыИСЦЭДМСсылка) ИЛИ ЗначениеЗаполнено(Форма.ДокументИСЦЭДМСсылка)) Тогда
		Элементы.ГруппаИСЦЭДМ.ТекущаяСтраница = Элементы.ГруппаСсылкаИСЦЭДМ;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЭлементыФормыДляВводаИСЦЭДМ(Форма, ГруппаДляВключенияМеню, ПослеГруппы = "") Экспорт
	
	ДокументИсточник = Форма.Объект.Ссылка;
	СвойстваДокументИСЦЭДМ = СформироватьСвойстваДокументаИСЦЭДМ(ДокументИсточник);
	Если СвойстваДокументИСЦЭДМ.ОписаниеТиповДокументИСЦЭДМ = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	//Подготовим описание необходимых реквизитов маркировки
	ДобавляемыеРеквизиты = Новый Массив;
	
	//Реквизит-представление ссылки документа маркировки на форме
	НовыйРеквизитНадписиДокументаИСЦЭДМ = Новый РеквизитФормы("НадписьДокументИСЦЭДМ", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(500))); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитНадписиДокументаИСЦЭДМ);
	
	//Реквизит для хранения данных документа маркировки
	НовыйРеквизитДанныеДокументаИСЦЭДМ = Новый РеквизитФормы("ДанныеДокументаИСЦЭДМ", Новый ОписаниеТипов());
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитДанныеДокументаИСЦЭДМ);
	
	//Реквизит, хранящий ссылку на связанный документ маркировки
	НовыйРеквизитДокументМаркировка = Новый РеквизитФормы("ДокументИСЦЭДМСсылка", СвойстваДокументИСЦЭДМ.ОписаниеТиповДокументИСЦЭДМ);
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитДокументМаркировка);
	
	//Реквизит, хранящий список ссылок, если связанных документов маркировки несколько
	НовыйРеквизитСписокДокументовМаркировки = Новый РеквизитФормы("СписокДокументыИСЦЭДМСсылка", Новый ОписаниеТипов("СписокЗначений"));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизитСписокДокументовМаркировки);
	
	//Создаем реквизиты по описанию на типовой форме
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Если ЗначениеЗаполнено(ПослеГруппы) Тогда
		//Создадим группу страниц для работы с ИСЦЭДМ
		ГруппаИСЦЭДМ = Форма.Элементы.Вставить("ГруппаИСЦЭДМ", Тип("ГруппаФормы"),, Форма.Элементы[ПослеГруппы]);
		ГруппаИСЦЭДМ.Вид = ВидГруппыФормы.Страницы;
		ГруппаИСЦЭДМ.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе 
		//Получим группу в котороую встроим элементы
		ПодменюРазмещения = Форма.Элементы[ГруппаДляВключенияМеню];
		//Создадим группу страниц для работы с ИСЦЭДМ
		ГруппаИСЦЭДМ = Форма.Элементы.Добавить("ГруппаИСЦЭДМ", Тип("ГруппаФормы"), ПодменюРазмещения);
		ГруппаИСЦЭДМ.Вид = ВидГруппыФормы.Страницы;
		ГруппаИСЦЭДМ.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	//Создадим страницу ссылки ИСЦЭДМ
	ГруппаСсылкаИСЦЭДМ = Форма.Элементы.Добавить("ГруппаСсылкаИСЦЭДМ", Тип("ГруппаФормы"), ГруппаИСЦЭДМ);
	ГруппаСсылкаИСЦЭДМ.Вид = ВидГруппыФормы.Страница;
	
	ПолеНадписиИСЦЭДМ = Форма.Элементы.Добавить("НадписьДокументИСЦЭДМ", Тип("ПолеФормы"), ГруппаСсылкаИСЦЭДМ);
	ПолеНадписиИСЦЭДМ.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеНадписиИСЦЭДМ.Гиперссылка = Истина;
	ПолеНадписиИСЦЭДМ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеНадписиИСЦЭДМ.ПутьКДанным = "НадписьДокументИСЦЭДМ";
	
	ПолеНадписиИСЦЭДМ.УстановитьДействие("Нажатие", "НадписьИСЦЭДМНажатие");
	
КонецПроцедуры

Функция СформироватьСвойстваДокументаИСЦЭДМ(ДокументИсточник, ВидОперацииВФорме = Неопределено) Экспорт
	
	МассивДокументовМаркировки = ПолучитьМассивТиповДокументовМаркировкиИСЦЭДМ();	
	СвойстваДокументИС = Новый Структура();
	СвойстваДокументИС.Вставить("ОписаниеТиповДокументИСЦЭДМ", Новый ОписаниеТипов(МассивДокументовМаркировки));
	СвойстваДокументИС.Вставить("ПредставлениеСоздатьДокументИСЦЭДМ", "Ввести новый документ маркировки (ИС ЦЭДМ)");
	
	Возврат СвойстваДокументИС;
	
КонецФункции

Функция ПолучитьМассивТиповДокументовМаркировкиИСЦЭДМ();
	
	МассивДокументовМаркировки = Новый Массив();
	//ИС ЦЭДМ
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.АктПриемаПередачиИСЦЭДМ"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОВыводеИзОборотаИСЦЭДМ"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.УведомлениеОВводеВОборотИСЦЭДМ"));
	МассивДокументовМаркировки.Добавить(Тип("ДокументСсылка.АктВнутреннегоПеремещенияИСЦЭДМ"));
	
	Возврат МассивДокументовМаркировки;
	
КонецФункции

#КонецОбласти

#Область ПроверкаЗаполненияДокументовИСЦЭДМ

Процедура ДокументАПП_ОбработкаПроверкиЗаполненияИСЦЭДМ(АПП, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументВВО_ОбработкаПроверкиЗаполненияИСЦЭДМ(ВВО, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	ПроверяемыеРеквизиты.Добавить("ПричинаВводаВОборот");
	
КонецПроцедуры

Процедура ДокументВИО_ОбработкаПроверкиЗаполненияИСЦЭДМ(ВИО, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	ПроверяемыеРеквизиты.Добавить("ПричинаВыводаИзОборота");
	
КонецПроцедуры

Процедура ДокументАгрегацияВнеПроизводства_ОбработкаПроверкиЗаполненияИСЦЭДМ(АПП, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументАктВнутреннегоПеремещения_ОбработкаПроверкиЗаполненияИСЦЭДМ(АПП, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументВвозЕАЭС_ОбработкаПроверкиЗаполненияИСЦЭДМ(АПП, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДокументввозИмпорт_ОбработкаПроверкиЗаполненияИСЦЭДМ(АПП, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкиПриВводеНаОсновании

Функция ПроверитьДокументыОснованияПриСозданииИСЦЭДМ(МассивДокументов, ТипДокумента, ЭтоВыборАПП = Ложь, ИмяКоманды = "") Экспорт
	
	Если ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Если ЭтоВыборАПП Тогда
			//При установке связи Вход.АПП + ПТУ
			Возврат ПроверитьОснованияДокументаИСЦЭДМОбщая("АктПриемаПередачи_ПТУ", МассивДокументов);
		Иначе
			Если ИмяКоманды = "СоздатьВвозИмпорт_ЦЭДМ" Тогда
				Возврат ПроверитьОснованияДокументаИСЦЭДМОбщая("ВвозТоваровИмпорт", МассивДокументов);
			Иначе
				Возврат ПроверитьОснованияДокументаИСЦЭДМОбщая("ВвозТоваровЕАЭС",   МассивДокументов);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		И ЭтоВыборАПП Тогда
		
		//При установке связи Вход.АПП + Возврат от клиента
		Возврат ПроверитьОснованияДокументаИСЦЭДМОбщая("АктПриемаПередачи_Возврат", МассивДокументов);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		//Из РТУ могут быть созданы 2 документа маркировки: АПП и Увед.о выводе из оборота (интернет-торговля).
		//Т.к. ограничения по виду опрерации РТУ у них одинаковые, проверяем общей проверкой.
		
		Возврат ПроверитьОснованияДокументаИСЦЭДМОбщая("АктПриемаПередачиИсх_РТУ", МассивДокументов);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		//При создании АПП исход по Возврату поставщику
		Возврат ПроверитьОснованияДокументаИСЦЭДМОбщая("АктПриемаПередачиИсх_Возврат", МассивДокументов);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПеремещениеТоваров") 
		Или ТипДокумента = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров") Тогда
		
		//При создании АПП исход по Возврату поставщику
		Возврат ПроверитьОснованияДокументаИСЦЭДМОбщая("АктВнутреннегоПеремещения", МассивДокументов);
		
	Иначе
		
		//Для Оприходования и Списания проверки не выполняем, т.к. там нет вида операции
		Возврат Новый Массив();
		
	КонецЕсли;
	
КонецФункции

Функция ПроверитьОснованияДокументаИСЦЭДМОбщая(ИмяДокументаИСЦЭДМ, МассивДокументов)
	
	МассивДокументовПроверки = Новый Массив();
	Возврат МассивДокументовПроверки;
	
КонецФункции

Функция ПроверитьНаличиеСвязанногоДокументаИСЦЭДМ(Основание, ЭтоПроверкаИзОснования = Ложь, ИмяДокумента = Неопределено) Экспорт
	
	Возврат РаботаСДокументамиИСМПТК.ПроверитьНаличиеСвязанногоДокументаМаркировки_ОбщаяИСЦЭДМ(Основание, ЭтоПроверкаИзОснования);
	
КонецФункции

Функция ПроверитьНаличиеМаркируемогоТовараИСЦЭДМ(ВыбранноеЗначение) Экспорт
	
	//Для ИС ЦЭДМ важно наличие в исходном документе товара с признаком Лекарственная продукция.
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.РеализацияТоваров") 
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОприходованиеТоваров")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СписаниеТоваров")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ПоступлениеТоваров")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ПеремещениеТоваров")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров") Тогда
		
		ВДокументеЕстьМаркируемаяПродукция = ПроверитьНаличиеМаркируемойФармы(ВыбранноеЗначение);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		//СУЗ
		ВДокументеЕстьМаркируемаяПродукция = ПроверитьТоварыОбщийЗапросСУЗ(ВыбранноеЗначение);	
	Иначе
		ВДокументеЕстьМаркируемаяПродукция = Ложь;
	КонецЕсли;
	
	Возврат ВДокументеЕстьМаркируемаяПродукция;
	
КонецФункции

Функция ПроверитьНаличиеМаркируемойФармы(ДокументДляПроверки)
	
	//Для ИС ЦЭДМ - только фарма
	Запрос = Новый Запрос();
	Текст = "ВЫБРАТЬ
	|	ДокументПроверки.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ." + ДокументДляПроверки.Ссылка.Метаданные().Имя + ".Товары КАК ДокументПроверки
	|ГДЕ
	|	ДокументПроверки.Ссылка = &Ссылка
	|	И ДокументПроверки.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)";
	
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("Ссылка", ДокументДляПроверки);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат ?(Результат.Количество() = 0, Ложь, Истина);
	
КонецФункции

//Вызов из первичных документов (РТУ, ПТУ и т.д.), проверяет наличие в ТЧ Товары маркируемой продукции (по реквизитам Номенклатура.ТабачнаяПродукция, Номенклатура.ОбувнаяПродукция и т.д.)
//В зависимости от наличия такой продукции устанавливается видимость группы создания документов ИС ЦЭДМ в документах-основаниях
Функция ПроверитьНаличиеМаркированногоТовараДляВводаДокументаИСЦЭДМ(ПервичныйДокумент) Экспорт
	
	ИмяТЧТовары   = ПолучитьИмяТЧТовары(ПервичныйДокумент);
	ТаблицаТовары = ПервичныйДокумент[ИмяТЧТовары];
	
	Если ТаблицаТовары.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
			
			Если СтрокаТовар.Номенклатура.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты") Тогда
				ЕстьМарки = Истина;
				Прервать;
			Иначе
				ЕстьМарки = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат ЕстьМарки;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

//Универсальный запрос к документам ИС ЦЭДМ для получения данных для заполнения создаваемого документа-основания
//
Функция ПодготовитьТекстЗапросаДляЗаполненияПервичныхДокументовИСЦЭДМ_Общий(ПараметрыЗаполнения, ИмяТаблицыДанных = "Марки") Экспорт
	
	//Полная поддержка процедуры на стороне РТ! Не переносить из библиотеки!
	
	ДокументИСЦЭДМ = Неопределено;
	Если ПараметрыЗаполнения.ТипДокументаИСМПТ  = Тип("ДокументСсылка.УведомлениеОВводеВОборотИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВводеВОборотИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ  = Тип("ДокументСсылка.УведомлениеОВыводеИзОборотаИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВыводеИзОборотаИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ  = Тип("ДокументСсылка.АктПриемаПередачиИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ  = Тип("ДокументСсылка.АгрегацияВнеПроизводстваИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАгрегацияВнеПроизводстваИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ  = Тип("ДокументСсылка.АктВнутреннегоПеремещенияИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктВнутреннегоПеремещенияИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ  = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ  = Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСЦЭДМ();
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТЧ.Ссылка КАК Ссылка,
	|	ДокументТЧ.EAN КАК EAN,
	|	ДокументТЧ.Номенклатура КАК Номенклатура,
	|";
	Если ИмяТаблицыДанных = "Марки" Тогда
		ТекстЗапроса = ТекстЗапроса +
					"	СУММА(ДокументТЧ.КоличествоИсточник) КАК КоличествоИсточник,
					|";
	Иначе
		ТекстЗапроса = ТекстЗапроса +
					"	СУММА(ДокументТЧ.Количество) КАК КоличествоИсточник,
					|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"	СУММА(ДокументТЧ.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ДокументМарки
	|ИЗ
	|	Документ.%ИмяДокумента%.%ИмяТаблицыДанных% КАК ДокументТЧ
	|ГДЕ
	|	ДокументТЧ.Ссылка В(&МассивИСЦЭДМДляСозданияПервичныхДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументТЧ.Ссылка,
	|	ДокументТЧ.EAN,
	|	ДокументТЧ.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТЧ.Ссылка КАК Ссылка,
	|	ДокументТЧ.EAN КАК EAN,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Штрихкоды.%ИмяНоменклатура%) КАК КоличествоНомРазлич,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Штрихкоды.Характеристика) КАК КоличествоХарРазлич
	|ПОМЕСТИТЬ ВТ_ШтрихкодыДокумента
	|ИЗ
	|	ВТ_ДокументМарки КАК ДокументТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРСШтрихкоды% КАК Штрихкоды
	|		ПО ДокументТЧ.EAN = Штрихкоды.Штрихкод
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументТЧ.Ссылка,
	|	ДокументТЧ.EAN
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК EAN,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура% КАК Номенклатура,
	|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка,
	|	ВТ_ШтрихкодыДокумента.Ссылка КАК Ссылка,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%.КодТНВЭД КАК КодТНВЭД
	|ПОМЕСТИТЬ ВТ_ДанныеШК
	|ИЗ
	|	РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ШтрихкодыДокумента КАК ВТ_ШтрихкодыДокумента
	|		ПО (ВТ_ШтрихкодыДокумента.EAN = ШтрихкодыНоменклатуры.Штрихкод)
	|ГДЕ
	|	ВТ_ШтрихкодыДокумента.КоличествоНомРазлич = 1
	|	И ВТ_ШтрихкодыДокумента.КоличествоХарРазлич = 1
	|
	|СГРУППИРОВАТЬ ПО
	|	ШтрихкодыНоменклатуры.Штрихкод,
	|	ВТ_ШтрихкодыДокумента.Ссылка,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%,
	|	ШтрихкодыНоменклатуры.Характеристика,
	|	ШтрихкодыНоменклатуры.Упаковка,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%.ЕдиницаИзмерения,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%.КодТНВЭД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТЧ.Ссылка КАК Ссылка,
	|	СУММА(ДокументТЧ.Количество) КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ВТ_ДанныеШК.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ВТ_ДанныеШК.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.БазовыеЕдиницыИзмерения.ПустаяСсылка)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ВТ_ДанныеШК.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ВТ_ДанныеШК.Номенклатура.КодТНВЭД
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодТНВЭД,
	|	ДокументТЧ.EAN КАК EAN,
	|	ЕСТЬNULL(ВТ_ДанныеШК.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(ВТ_ДанныеШК.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
	|	ЕСТЬNULL(ВТ_ДанныеШК.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)) КАК Упаковка,
	|	ЕСТЬNULL(ДокументТЧ.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК НоменклатураДокумент
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	ВТ_ДокументМарки КАК ДокументТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеШК КАК ВТ_ДанныеШК
	|		ПО (ВТ_ДанныеШК.Ссылка = ДокументТЧ.Ссылка)
	|			И (ВТ_ДанныеШК.EAN = ДокументТЧ.EAN)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументТЧ.Ссылка,
	|	ДокументТЧ.EAN,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ВТ_ДанныеШК.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ВТ_ДанныеШК.Номенклатура.КодТНВЭД
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ВТ_ДанныеШК.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ВТ_ДанныеШК.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.БазовыеЕдиницыИзмерения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЕСТЬNULL(ВТ_ДанныеШК.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
	|	ЕСТЬNULL(ВТ_ДанныеШК.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
	|	ЕСТЬNULL(ВТ_ДанныеШК.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)),
	|	ЕСТЬNULL(ДокументТЧ.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументИСЦЭДМ.Дата КАК Дата,
	|	ДокументИСЦЭДМ.Организация КАК Организация,
	|	ДокументИСЦЭДМ.Склад КАК Склад,
	|	%Контрагент%
	|	ДокументИСЦЭДМ.Автор КАК Автор,
	|	"""" КАК ВидОперации,
	|	"""" КАК Партнер,
	|	ВТ_Товары.Ссылка КАК ДокументОснование,
	|	ВТ_Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВТ_Товары.КоличествоУпаковок КАК Количество,
	|	ВТ_Товары.Упаковка КАК Упаковка,
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВТ_Товары.Характеристика КАК Характеристика,
	|	ВТ_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_Товары.КодТНВЭД КАК КодТНВЭД,
	|	ВТ_Товары.НоменклатураДокумент КАК НоменклатураДокумент
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%ИмяДокумента% КАК ДокументИСЦЭДМ
	|		ПО ВТ_Товары.Ссылка = ДокументИСЦЭДМ.Ссылка
	|ГДЕ
	|	ДокументИСЦЭДМ.Ссылка В(&МассивИСЦЭДМДляСозданияПервичныхДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	ВТ_ДанныеДокумента.Организация КАК Организация,
	|	ВТ_ДанныеДокумента.Склад КАК Склад,
	|	ВТ_ДанныеДокумента.Контрагент КАК Контрагент,
	|	ВТ_ДанныеДокумента.Партнер КАК Партнер,
	|	ВТ_ДанныеДокумента.ВидОперации КАК ВидОперации,
	|	ВТ_ДанныеДокумента.Автор КАК Автор,
	|	ВТ_ДанныеДокумента.Дата КАК Дата,
	|	ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДокумента.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДокумента.Упаковка КАК Упаковка,
	|	ВТ_ДанныеДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеДокумента.КодТНВЭД КАК КодТНВЭД,
	|	СУММА(ВТ_ДанныеДокумента.Количество) КАК Количество,
	|	СУММА(ВТ_ДанныеДокумента.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	ВТ_ДанныеДокумента.НоменклатураДокумент КАК НоменклатураДокумент
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.ДокументОснование,
	|	ВТ_ДанныеДокумента.Организация,
	|	ВТ_ДанныеДокумента.Склад,
	|	ВТ_ДанныеДокумента.Контрагент,
	|	ВТ_ДанныеДокумента.Партнер,
	|	ВТ_ДанныеДокумента.ВидОперации,
	|	ВТ_ДанныеДокумента.Автор,
	|	ВТ_ДанныеДокумента.Дата,
	|	ВТ_ДанныеДокумента.Номенклатура,
	|	ВТ_ДанныеДокумента.Характеристика,
	|	ВТ_ДанныеДокумента.Упаковка,
	|	ВТ_ДанныеДокумента.ЕдиницаИзмерения,
	|	ВТ_ДанныеДокумента.КодТНВЭД,
	|	ВТ_ДанныеДокумента.НоменклатураДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Склад КАК Склад,
	|	вт_ДанныеДляЗаполнения.Контрагент КАК Контрагент,
	|	вт_ДанныеДляЗаполнения.Партнер КАК Партнер,
	|	вт_ДанныеДляЗаполнения.ВидОперации КАК ВидОперации,
	|	вт_ДанныеДляЗаполнения.Автор КАК Автор,
	|	вт_ДанныеДляЗаполнения.Дата КАК Дата
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА вт_ДанныеДляЗаполнения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА вт_ДанныеДляЗаполнения.НоменклатураДокумент
	|		ИНАЧЕ вт_ДанныеДляЗаполнения.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.Характеристика КАК Характеристика,
	|	вт_ДанныеДляЗаполнения.Упаковка КАК Упаковка,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	вт_ДанныеДляЗаполнения.КодТНВЭД КАК КодТНВЭД,
	|	вт_ДанныеДляЗаполнения.Количество КАК Количество,
	|	вт_ДанныеДляЗаполнения.КоличествоУпаковок КАК КоличествоУпаковок
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", 	   ДокументИСЦЭДМ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяТаблицыДанных%", ИмяТаблицыДанных);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",   ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры());

	Если ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСЦЭДМ()
		Или ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСЦЭДМ() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Контрагент%", "ДокументИСЦЭДМ.Контрагент КАК Контрагент,");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Контрагент%", "Неопределено КАК Контрагент,");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПодготовитьТекстЗапросаДляЗаполненияПервичныхДокументовИСЦЭДМ_Перемещение(ПараметрыЗаполнения, ИмяТаблицыДанных = "Марки") Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТЧ.Ссылка КАК Ссылка,
	|	СУММА(ДокументТЧ.Количество) КАК КоличествоУпаковок,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка,
	|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура% КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.АктВнутреннегоПеремещенияИСЦЭДМ.%ИмяТаблицыДанных% КАК ДокументТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|		ПО ДокументТЧ.EAN = ШтрихкодыНоменклатуры.Штрихкод
	|ГДЕ
	|	ДокументТЧ.Ссылка В (&МассивИСЦЭДМДляСозданияПервичныхДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ШтрихкодыНоменклатуры.Упаковка,
	|	ШтрихкодыНоменклатуры.Характеристика,
	|	ДокументТЧ.Ссылка,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Ссылка КАК ДокументОснование,
	|	ВТ_Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВТ_Товары.КоличествоУпаковок КАК Количество,
	|	ВТ_Товары.Упаковка КАК Упаковка,
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВТ_Товары.Характеристика КАК Характеристика,
	|	ДокументИСЦЭДМ.Дата КАК Дата,
	|	ДокументИСЦЭДМ.Организация КАК Организация,
	|	ДокументИСЦЭДМ.СкладПоставщик КАК СкладОтправитель,
	|	ДокументИСЦЭДМ.СкладПолучатель КАК СкладПолучатель,
	|	ДокументИСЦЭДМ.Автор КАК Ответственный,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров) КАК ВидОперации	
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктВнутреннегоПеремещенияИСЦЭДМ КАК ДокументИСЦЭДМ
	|		ПО ВТ_Товары.Ссылка = ДокументИСЦЭДМ.Ссылка
	|ГДЕ
	|	ДокументИСЦЭДМ.Ссылка В (&МассивИСЦЭДМДляСозданияПервичныхДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	ВТ_ДанныеДокумента.Организация КАК Организация,
	|	ВТ_ДанныеДокумента.СкладОтправитель КАК СкладОтправитель,
	|	ВТ_ДанныеДокумента.СкладПолучатель КАК СкладПолучатель,
	|	ВТ_ДанныеДокумента.Ответственный КАК Ответственный,
	|	ВТ_ДанныеДокумента.ВидОперации КАК ВидОперации,
	|	ВТ_ДанныеДокумента.Дата КАК Дата,
	|	ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДокумента.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДокумента.Упаковка КАК Упаковка,
	|	ВТ_ДанныеДокумента.Количество КАК Количество,
	|	ВТ_ДанныеДокумента.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|ИЗ
	|	вт_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.СкладОтправитель КАК СкладОтправитель,
	|	вт_ДанныеДляЗаполнения.СкладПолучатель КАК СкладПолучатель,
	|	вт_ДанныеДляЗаполнения.ВидОперации КАК ВидОперации,
	|	вт_ДанныеДляЗаполнения.Ответственный КАК Ответственный,
	|	вт_ДанныеДляЗаполнения.Дата КАК Дата
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|	УПОРЯДОЧИТЬ ПО
	|		ДокументОснование
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.Характеристика КАК Характеристика,
	|	вт_ДанныеДляЗаполнения.Упаковка КАК Упаковка,
	|	вт_ДанныеДляЗаполнения.Количество КАК Количество,
	|	вт_ДанныеДляЗаполнения.КоличествоУпаковок КАК КоличествоУпаковок
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|	УПОРЯДОЧИТЬ ПО
	|		ДокументОснование";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяТаблицыДанных%", ИмяТаблицыДанных);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",   ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры());
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПодготовитьТекстЗапросаПоИтогамДокументаЦЭДМ(ПараметрыЗаполнения) Экспорт
	
	ДокументИСЦЭДМ = Неопределено;
	Если ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВводеВОборотИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВводеВОборотИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВыводеИзОборотаИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВыводеИзОборотаИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.АктПриемаПередачиИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСЦЭДМ();
	ИначеЕсли ПараметрыЗаполнения.ТипДокументаИСМПТ = Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСЦЭДМ") Тогда
		ДокументИСЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСЦЭДМ();
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТЧ.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ДокументТЧ.Цена) КАК Цена,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка,
	|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура% КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.%ИмяДокумента%.Итоги КАК ДокументТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.%ИмяРСШтрихкоды% КАК ШтрихкодыНоменклатуры
	|		ПО ДокументТЧ.EAN = ШтрихкодыНоменклатуры.Штрихкод
	|ГДЕ
	|	ДокументТЧ.Ссылка В (&МассивИСЦЭДМДляСозданияПервичныхДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ШтрихкодыНоменклатуры.Упаковка,
	|	ШтрихкодыНоменклатуры.Характеристика,
	|	ДокументТЧ.Ссылка,
	|	ШтрихкодыНоменклатуры.%ИмяНоменклатура%
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Ссылка КАК ДокументОснование,
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВТ_Товары.Характеристика КАК Характеристика,
	|	ВТ_Товары.Упаковка КАК Упаковка,
	|	ВТ_Товары.Цена КАК Цена
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|
	|	УПОРЯДОЧИТЬ ПО
	|		ДокументОснование";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", 	  ДокументИСЦЭДМ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяНоменклатура%", ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяНоменклатурыДляРегистраШтрихкоды());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРСШтрихкоды%",  ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьИмяРегистраШтрихкодыНоменклатуры());
		
	Возврат ТекстЗапроса;
	
КонецФункции

//Универсальный запрос к первичным документам для получения данных для заполнения создаваемого документа ИС ЦЭДМ
//
Функция ПодготовитьТекстЗапросаДляЗаполненияДокументаИСЦЭДМ_Общий(ПараметрыЗаполнения) Экспорт
	
	ДокументыОснования 	 = ПараметрыЗаполнения.МассивДокументов;
	
	Если ТипЗнч(ДокументыОснования) = Тип("ДокументСсылка.ОтчетОПередачеКИОтНерезидентаРКИСЦЭДМ") Тогда
		ТекстЗапроса = ТекстЗапросаПоОтчетуОПередачеКИ();
		Возврат ТекстЗапроса;		
	КонецЕсли;

	ИмяДокументаОснования = ПолучитьИмяДокументаДляПроверкиПоТипу(ДокументыОснования);
	ТекстЗапроса = ТекстЗапросаОбщиеДанныеОснования("ИСЦЭДМ", ИмяДокументаОснования); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПодготовитьТекстЗапросаДляЗаполненияДокументаИСЦЭДМ_Перемещение(ПараметрыЗаполнения) Экспорт
	
	Если ТипЗнч(ПараметрыЗаполнения) = Тип("Структура") Тогда
		//При вводе на основании
		ИмяДокумента = ПолучитьИмяДокументаДляПроверкиПоТипу(ПараметрыЗаполнения.МассивДокументов);
	Иначе
		//При перезаполнении
		ИмяДокумента = ПолучитьИмяДокументаДляПроверкиПоТипу(ПараметрыЗаполнения);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТиповойДокумент.Ссылка КАК ДокументОснование,
	|	ТиповойДокумент.СкладОтправитель КАК СкладОтправитель,
	|	ТиповойДокумент.СкладПолучатель КАК СкладПолучатель,
	//Данные Поставщика и получателя
	|	ТиповойДокумент.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(200))) <> """"
	|			ТОГДА ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(200))
	|		ИНАЧЕ Организации.Наименование
	|	КОНЕЦ КАК ОрганизацияНаименование,
	|	ТиповойДокумент.Организация.ИНН КАК ОрганизацияИдентификационныйНомер,
	|	Неопределено КАК ВидОперации,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК КоличествоИсточник,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияБазовая,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Табачная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Обувная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Алкогольная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛегкаяПромышленность)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка)
	|	КОНЕЦ КАК ВидПродукции
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|
	|ИЗ
	|	Документ.%ИмяОснования%.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%ИмяОснования% КАК ТиповойДокумент
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|			ПО ТиповойДокумент.Организация = Организации.Ссылка
	|		ПО ТаблицаТовары.Ссылка = ТиповойДокумент.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТиповойДокумент.Ссылка В(&МассивДокументов)
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента.СкладОтправитель КАК СкладОтправитель,
	|	вт_ДанныеДокумента.СкладПолучатель КАК СкладПолучатель,
	|	вт_ДанныеДокумента.Организация КАК Организация,
	|	вт_ДанныеДокумента.ОрганизацияНаименование КАК ОрганизацияНаименование,
	|	вт_ДанныеДокумента.ОрганизацияИдентификационныйНомер КАК ОрганизацияИдентификационныйНомер,
	|	вт_ДанныеДокумента.ВидОперации КАК ВидОперации,
	|	вт_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДокумента.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_ДанныеДокумента.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДокумента.ВидПродукции КАК ВидПродукции
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|ГДЕ
	|	вт_ДанныеДокумента.КоличествоИсточник > 0 
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.СкладОтправитель КАК СкладОтправитель,
	|	вт_ДанныеДляЗаполнения.СкладПолучатель КАК СкладПолучатель,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.ОрганизацияНаименование КАК ОрганизацияНаименование,
	|	вт_ДанныеДляЗаполнения.ОрганизацияИдентификационныйНомер КАК ОрганизацияИдентификационныйНомер,
	|	вт_ДанныеДляЗаполнения.ВидОперации КАК ВидОперации
	|
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование
	|
	|;
	| 
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДляЗаполнения.ВидПродукции КАК ВидПродукции
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяОснования%", ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОбщиеДанныеОснования(ИнфСистема = "", ИмяДокумента)
	
	Если ИнфСистема = "" Тогда 
		ИнфСистема = "ИСМПТ";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТиповойДокумент.Ссылка КАК ДокументОснование,
	|	ТиповойДокумент.Склад КАК Склад,
	//Данные Поставщика и получателя
	|	ТиповойДокумент.Организация КАК Организация,
	|	ТиповойДокумент.Организация КАК Поставщик, 
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(200))) <> """"
	|			ТОГДА ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(200))
	|		ИНАЧЕ Организации.Наименование
	|	КОНЕЦ КАК ПоставщикНаименование,
	|	ТиповойДокумент.Организация.ИНН КАК ПоставщикИдентификационныйНомер,
	|";
	
	//Контрагент
	Если ИмяДокумента = "СписаниеТоваров" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"Неопределено КАК Контрагент,
		|	Неопределено КАК Получатель,
		|	Неопределено КАК ПолучательИдентификационныйНомер,
		|	Неопределено КАК ПолучательНаименование,
		|";
	Иначе
		ТекстЗапроса = ТекстЗапроса + 
		"ТиповойДокумент.Контрагент КАК Контрагент,
		|	ТиповойДокумент.Контрагент КАК Получатель,
		|	ТиповойДокумент.Контрагент.ИНН КАК ПолучательИдентификационныйНомер,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(200))) <> """"
		|			ТОГДА ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(200))
		|		ИНАЧЕ Контрагенты.Наименование
		|	КОНЕЦ КАК ПолучательНаименование,
		|";
	КонецЕсли;
	
	//Хозяйственная операция
	Если ИмяДокумента = "ОрдерНаПеремещениеТоваров" 
		Или ИмяДокумента = "ПеремещениеТоваров"
		Или ИмяДокумента = "РеализацияТоваров" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"Неопределено КАК ВидОперации,
		|";
	ИначеЕсли ИмяДокумента = "ПоступлениеТоваров" 
		Или ИмяДокумента = "ЗаказПоставщику" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ТиповойДокумент.ХозяйственнаяОперация КАК ВидОперации,
		|";
	Иначе
		ТекстЗапроса = ТекстЗапроса + 
		"ТиповойДокумент.АналитикаХозяйственнойОперации.ХозяйственнаяОперация КАК ВидОперации,
		|";
	КонецЕсли;
	
	//Таблица товаров
	ТекстЗапроса = ТекстЗапроса +
	"ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаТовары.Количество) КАК КоличествоИсточник,
	|";
	
	ТекстЗапроса = ТекстЗапроса + 
	"ТаблицаТовары.Цена КАК Цена,
	|";
		
	ТекстЗапроса = ТекстЗапроса +
	"	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияБазовая,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Табачная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Обувная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Алкогольная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛегкаяПромышленность)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка)
	|	КОНЕЦ КАК ВидПродукции
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|
	|ИЗ
	|	Документ.%ИмяДокумента%.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%ИмяДокумента% КАК ТиповойДокумент
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|			ПО ТиповойДокумент.Организация = Организации.Ссылка
	|";
	
	Если Не ИмяДокумента = "СписаниеТоваров" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ТиповойДокумент.Контрагент = Контрагенты.Ссылка
		|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"		ПО ТаблицаТовары.Ссылка = ТиповойДокумент.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТиповойДокумент.Ссылка В(&МассивДокументов)
	|";
	
	ТекстЗапроса = ТекстЗапроса +  ПолучитьКусокТекстаЗапросаПоВидуПродукции(ИнфСистема) + "
	|		
	|	СГРУППИРОВАТЬ ПО
	|	ТиповойДокумент.Ссылка,
	|	ТиповойДокумент.Склад,
	|	ТиповойДокумент.Организация,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(200))) <> """"
	|			ТОГДА ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(200))
	|		ИНАЧЕ Организации.Наименование
	|	КОНЕЦ,
	|	ТиповойДокумент.Организация.ИНН,
	|";
	
	Если ИмяДокумента = "СписаниеТоваров" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"Неопределено,
		|	Неопределено,
		|	Неопределено,
		|	Неопределено,
		|";
	Иначе
		ТекстЗапроса = ТекстЗапроса + 
		"ТиповойДокумент.Контрагент,
		|	ТиповойДокумент.Контрагент,
		|	ТиповойДокумент.Контрагент.ИНН,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(200))) <> """"
		|			ТОГДА ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(200))
		|		ИНАЧЕ Контрагенты.Наименование
		|	КОНЕЦ,
		|";
	КонецЕсли;
	
	//Хозяйственная операция (группировка)
	Если ИмяДокумента = "ОрдерНаПеремещениеТоваров" 
		Или ИмяДокумента = "ПеремещениеТоваров"
		Или ИмяДокумента = "РеализацияТоваров" Тогда
		ТекстЗапроса = ТекстЗапроса +
		"	НЕОПРЕДЕЛЕНО,
		|";
	ИначеЕсли ИмяДокумента = "ПоступлениеТоваров" 
		Или ИмяДокумента = "ЗаказПоставщику" Тогда
		ТекстЗапроса = ТекстЗапроса +
		"	ТиповойДокумент.ХозяйственнаяОперация,
		|";
	Иначе
		ТекстЗапроса = ТекстЗапроса +
		"	ТиповойДокумент.АналитикаХозяйственнойОперации.ХозяйственнаяОперация,
		|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	СправочникНоменклатура.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Табачная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Обувная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.Алкогольная)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛегкаяПромышленность)
	|		КОГДА СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка)
	|	КОНЕЦ,
	|";	
	
	ТекстЗапроса = ТекстЗапроса + 
	"ТаблицаТовары.Цена,
	|";
	
	ТекстЗапроса = ТекстЗапроса + 
	"	ТиповойДокумент.Организация
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента.Склад КАК Склад,
	|	вт_ДанныеДокумента.Организация КАК Организация,
	|	вт_ДанныеДокумента.Поставщик КАК Поставщик,
	|	вт_ДанныеДокумента.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДокумента.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДокумента.Контрагент КАК Контрагент,
	|	вт_ДанныеДокумента.Получатель КАК Получатель,
	|	вт_ДанныеДокумента.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДокумента.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДокумента.ВидОперации КАК ВидОперации,
	|	вт_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДокумента.Характеристика КАК Характеристика,
	|	вт_ДанныеДокумента.Цена КАК Цена,
	|	вт_ДанныеДокумента.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_ДанныеДокумента.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДокумента.ВидПродукции КАК ВидПродукции
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|ГДЕ
	|	вт_ДанныеДокумента.КоличествоИсточник > 0 
	|
	|;
	|
	//Таблица реквизитов
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Склад КАК Склад,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Поставщик КАК Поставщик,
	|	вт_ДанныеДляЗаполнения.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДляЗаполнения.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДляЗаполнения.Контрагент КАК Контрагент,
	|	вт_ДанныеДляЗаполнения.Получатель КАК Получатель,
	|	вт_ДанныеДляЗаполнения.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДляЗаполнения.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДляЗаполнения.ВидОперации КАК ВидОперации
	|
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование
	|
	|;
	| 
	//Таблица товаров
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.Характеристика КАК Характеристика,
	|	СУММА(вт_ДанныеДляЗаполнения.КоличествоИсточник) КАК КоличествоИсточник,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДляЗаполнения.ВидПродукции КАК ВидПродукции
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ДанныеДляЗаполнения.ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура,
	|	вт_ДанныеДляЗаполнения.Характеристика,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДляЗаполнения.ВидПродукции
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование
	|
	|;
	| 
	//Таблица итогов
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.Характеристика КАК Характеристика,
	|	вт_ДанныеДляЗаполнения.Цена КАК Цена,
	|	вт_ДанныеДляЗаполнения.КоличествоИсточник КАК Количество
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОбщиеДанныеОснования_Перемещение(ДокументОснование)
	
	Возврат ПодготовитьТекстЗапросаДляЗаполненияДокументаИСЦЭДМ_Перемещение(ДокументОснование); 
	
КонецФункции

Функция ТекстЗапросаПоОтчетуОПередачеКИ()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтчетОПередаче.Ссылка КАК ДокументОснование,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	ОтчетОПередаче.Организация КАК Организация,
	|	ОтчетОПередаче.Поставщик КАК Поставщик,
	|	ОтчетОПередаче.ПоставщикНаименование КАК ПоставщикНаименование,
	|	ОтчетОПередаче.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	ОтчетОПередаче.Получатель КАК Получатель,
	|	ОтчетОПередаче.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	ОтчетОПередаче.ПолучательНаименование КАК ПолучательНаименование,
	|	НЕОПРЕДЕЛЕНО КАК ВидОперации,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК КоличествоИсточник,
	|	НЕОПРЕДЕЛЕНО КАК Цена,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияБазовая,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты) КАК ВидПродукции
	|ПОМЕСТИТЬ вт_ДанныеДокумента
	|ИЗ
	|	Документ.ОтчетОПередачеКИОтНерезидентаРКИСЦЭДМ.Марки КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОПередачеКИОтНерезидентаРКИСЦЭДМ КАК ОтчетОПередаче
	|		ПО ТаблицаТовары.Ссылка = ОтчетОПередаче.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ОтчетОПередаче.Ссылка В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДокумента.Склад КАК Склад,
	|	вт_ДанныеДокумента.Организация КАК Организация,
	|	вт_ДанныеДокумента.Поставщик КАК Поставщик,
	|	вт_ДанныеДокумента.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДокумента.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДокумента.Контрагент КАК Контрагент,
	|	вт_ДанныеДокумента.Получатель КАК Получатель,
	|	вт_ДанныеДокумента.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДокумента.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДокумента.ВидОперации КАК ВидОперации,
	|	вт_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДокумента.Цена КАК Цена,
	|	вт_ДанныеДокумента.КоличествоИсточник КАК КоличествоИсточник,
	|	вт_ДанныеДокумента.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДокумента.ВидПродукции КАК ВидПродукции
	|ПОМЕСТИТЬ вт_ДанныеДляЗаполнения
	|ИЗ
	|	вт_ДанныеДокумента КАК вт_ДанныеДокумента
	|ГДЕ
	|	вт_ДанныеДокумента.КоличествоИсточник > 0
	|;
	|
	//Реквизиты
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Склад КАК Склад,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Поставщик КАК Поставщик,
	|	вт_ДанныеДляЗаполнения.ПоставщикНаименование КАК ПоставщикНаименование,
	|	вт_ДанныеДляЗаполнения.ПоставщикИдентификационныйНомер КАК ПоставщикИдентификационныйНомер,
	|	вт_ДанныеДляЗаполнения.Контрагент КАК Контрагент,
	|	вт_ДанныеДляЗаполнения.Получатель КАК Получатель,
	|	вт_ДанныеДляЗаполнения.ПолучательНаименование КАК ПолучательНаименование,
	|	вт_ДанныеДляЗаполнения.ПолучательИдентификационныйНомер КАК ПолучательИдентификационныйНомер,
	|	вт_ДанныеДляЗаполнения.ВидОперации КАК ВидОперации
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование
	|;
	|
	//Товары
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.Цена КАК Цена,
	|	СУММА(вт_ДанныеДляЗаполнения.КоличествоИсточник) КАК КоличествоИсточник,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДляЗаполнения.ВидПродукции КАК ВидПродукции
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ДанныеДляЗаполнения.Организация,
	|	вт_ДанныеДляЗаполнения.Цена,
	|	вт_ДанныеДляЗаполнения.Номенклатура,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДляЗаполнения.ВидПродукции,
	|	вт_ДанныеДляЗаполнения.ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование
	|;
	|
	//итоги (для документа не нужны, сохраняются для общего запроса)
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ДанныеДляЗаполнения.ДокументОснование КАК ДокументОснование,
	|	вт_ДанныеДляЗаполнения.Организация КАК Организация,
	|	вт_ДанныеДляЗаполнения.Номенклатура КАК Номенклатура,
	|	вт_ДанныеДляЗаполнения.Цена КАК Цена,
	|	СУММА(вт_ДанныеДляЗаполнения.КоличествоИсточник) КАК КоличествоИсточник,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмеренияБазовая КАК ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДляЗаполнения.ВидПродукции КАК ВидПродукции
	|ИЗ
	|	вт_ДанныеДляЗаполнения КАК вт_ДанныеДляЗаполнения
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ДанныеДляЗаполнения.Организация,
	|	вт_ДанныеДляЗаполнения.Цена,
	|	вт_ДанныеДляЗаполнения.Номенклатура,
	|	вт_ДанныеДляЗаполнения.ЕдиницаИзмеренияБазовая,
	|	вт_ДанныеДляЗаполнения.ВидПродукции,
	|	вт_ДанныеДляЗаполнения.ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#Область ЗаполнениеЗначенийПоУмолчаниюПриСозданииИСЦЭДМ

//Получение значений некоторых реквизитов документа ИС ЦЭДМ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС ЦЭДМ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияАктаПриемаПередачиИСЦЭДМ(ДокументыОснованияМассив) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("ВидОперации",  ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСЦЭДМ.РеализацияОтКонтрагента"));
	СтруктураРеквизитов.Вставить("ТипАкта", 	 ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исходный"));
	СтруктураРеквизитов.Вставить("Направление",  ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
	СтруктураРеквизитов.Вставить("Автор",  		 ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты"));
	СтруктураРеквизитов.Вставить("ЭтапОформления", ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе"));
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС ЦЭДМ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС ЦЭДМ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВводеИСЦЭДМ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("ПричинаВводаВОборот", ПредопределенноеЗначение("Перечисление.ПричиныПовторногоВводаКМВОборотИСЦЭДМ.Излишки"));
	СтруктураРеквизитов.Вставить("Автор",  		   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты"));
	СтруктураРеквизитов.Вставить("ЭтапОформления", ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе"));
	
	Если ТипЗнч(ДокументыОснования) = Тип("Массив") Тогда
		ДанныеДляРазбора = ДокументыОснования[0]; 
	Иначе
		ДанныеДляРазбора = ДокументыОснования;
	КонецЕсли;
	
	СтруктураРеквизитов.Вставить("ДокументОснованиеНаименование", Строка(ТипЗнч(ДанныеДляРазбора)));
	СтруктураРеквизитов.Вставить("ДокументОснованиеДата",  ДанныеДляРазбора.Дата);
	СтруктураРеквизитов.Вставить("ДокументОснованиеНомер", ДанныеДляРазбора.Номер);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС ЦЭДМ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС ЦЭДМ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВыводеИСЦЭДМ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("ПричинаВыводаИзОборота", ПредопределенноеЗначение("Перечисление.ПричиныВыводаКМИзОборотаИСЦЭДМ.МедицинскоеНазначение"));
	СтруктураРеквизитов.Вставить("Автор",  		   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ТоварнаяГруппа", ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты"));
	СтруктураРеквизитов.Вставить("ЭтапОформления", ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе"));
	
	Если ТипЗнч(ДокументыОснования) = Тип("Массив") Тогда
		ДанныеДляРазбора = ДокументыОснования[0]; 
	Иначе
		ДанныеДляРазбора = ДокументыОснования;
	КонецЕсли;
	
	СтруктураРеквизитов.Вставить("ДокументОснованиеНаименование", Строка(ТипЗнч(ДанныеДляРазбора)));
	СтруктураРеквизитов.Вставить("ДокументОснованиеДата",  ДанныеДляРазбора.Дата);
	СтруктураРеквизитов.Вставить("ДокументОснованиеНомер", ДанныеДляРазбора.Номер);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС ЦЭДМ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС ЦЭДМ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияАктаВнутреннегоПеремещенияИСЦЭДМ(ДокументыОснованияМассив) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("ВидОперации",    ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСЦЭДМ.ВнутреннееПеремещение"));
	СтруктураРеквизитов.Вставить("Автор",  		   ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	СтруктураРеквизитов.Вставить("ЭтапОформления", ПредопределенноеЗначение("Перечисление.ЭтапыОформленияДокументовИСЦЭДМ.ВРаботе"));
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС ЦЭДМ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС ЦЭДМ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВвозеЕАЭСИСЦЭДМ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Автор",  ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

//Получение значений некоторых реквизитов документа ИС ЦЭДМ, которые могут зависеть от выбранного документа-основания.
//Вызывается из обработки заполнения документа ИС ЦЭДМ в модуле объекта.
Функция ПолучитьЗначенияПоУмолчаниюДляСозданияУведомленияОВвозеИмпортИСЦЭДМ(ДокументыОснования) Экспорт
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Автор", ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ТекущийПользователь());
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

#КонецОбласти

//Универсальная функция для перезаполнения некоторых сведений в документе ИС ЦЭДМ по выбранному в нем документу-основанию
//
Функция ПолучитьДанныеДокументаОснованияДляПерезаполненияИСЦЭДМ_Общий(ДокументОснование, ДокументЦЭДМ) Экспорт
	
	ИмяДокументаОснования = ПолучитьИмяДокументаДляПроверкиПоТипу(ДокументОснование);
	ТекстЗапроса = ТекстЗапросаОбщиеДанныеОснования("ИСЦЭДМ", ИмяДокументаОснования);
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументов", ДокументОснование);
	
	РезультатЗапроса  = Запрос.ВыполнитьПакет();
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	ТаблицаРеквизиты = РезультатЗапроса[КоличествоПакетов - 3].Выгрузить();
	Реквизиты = Новый Структура();
	
	Для Каждого Строка Из ТаблицаРеквизиты Цикл
		
		Реквизиты.Вставить("Организация", Строка.Организация);
		Реквизиты.Вставить("Склад", 	  Строка.Склад); 
		ДанныеСкладаЦЭДМ = ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(Строка.Склад, Строка.Организация);
		Если Не ДанныеСкладаЦЭДМ = Неопределено Тогда
			Реквизиты.Вставить("СкладЦЭДМ", ДанныеСкладаЦЭДМ.СкладЦЭДМ);
			Если Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСЦЭДМ()
				И Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСЦЭДМ() Тогда
				Реквизиты.Вставить("ПоставщикСкладЦЭДМИдентификационныйНомер",  ДанныеСкладаЦЭДМ.ИдентификаторСклада);
			Иначе
				Реквизиты.Вставить("ПолучательСкладЦЭДМИдентификационныйНомер", ДанныеСкладаЦЭДМ.ИдентификаторСклада);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСЦЭДМ()
			И Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСЦЭДМ() Тогда
			
			Реквизиты.Вставить("Поставщик",  Строка.Поставщик);
			Реквизиты.Вставить("ПоставщикИдентификационныйНомер", Строка.ПоставщикИдентификационныйНомер);
			Реквизиты.Вставить("ПоставщикНаименование", Строка.ПоставщикНаименование);
		Иначе
			//У ввоза заполнение наоборот
			Реквизиты.Вставить("Получатель", Строка.Поставщик);
			Реквизиты.Вставить("ПолучательИдентификационныйНомер", Строка.ПоставщикИдентификационныйНомер);
			Реквизиты.Вставить("ПолучательНаименование", Строка.ПоставщикНаименование);
			Реквизиты.Вставить("Контрагент", Строка.Контрагент);
			Реквизиты.Вставить("Поставщик",  Строка.Получатель);
			Реквизиты.Вставить("ПоставщикИдентификационныйНомер", Строка.ПолучательИдентификационныйНомер);
			Реквизиты.Вставить("ПоставщикНаименование", Строка.ПолучательНаименование);
		КонецЕсли;
		
		Если Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВыводеИзОборотаИСЦЭДМ()
			И Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВводеВОборотИСЦЭДМ()
			И Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСЦЭДМ()
			И Не ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСЦЭДМ() Тогда
			Реквизиты.Вставить("Контрагент", Строка.Контрагент);
			Реквизиты.Вставить("Получатель", Строка.Получатель);
			Реквизиты.Вставить("ПолучательИдентификационныйНомер", Строка.ПолучательИдентификационныйНомер);
			Реквизиты.Вставить("ПолучательНаименование", Строка.ПолучательНаименование);
		КонецЕсли;
		
		Если ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСЦЭДМ()
			ИЛИ ДокументЦЭДМ = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСЦЭДМ() Тогда
			Реквизиты.Вставить("СтранаОтправления", ПолучитьСтрануОтправленияВвозИзЕАЭС(Строка.Контрагент));
		КонецЕсли;
		
		Реквизиты.Вставить("ДокументОснование", Строка.ДокументОснование);
		
	КонецЦикла;
	
	ТаблицаТовары = РезультатЗапроса[КоличествоПакетов - 2].Выгрузить(); 
	Товары = Новый Массив();
	
	Для Каждого Строка Из ТаблицаТовары Цикл
		
		СтрокаТовары = Новый Структура();
		СтрокаТовары.Вставить("Номенклатура", 	    Строка.Номенклатура);
		СтрокаТовары.Вставить("КоличествоИсточник", Строка.КоличествоИсточник);
		СтрокаТовары.Вставить("ЕдиницаИзмерения",   Строка.ЕдиницаИзмеренияБазовая);
		СтрокаТовары.Вставить("ВидПродукции",	    Строка.ВидПродукции);
		Товары.Добавить(СтрокаТовары);
		
	КонецЦикла;
	
	ТаблицаИтогов = РезультатЗапроса[КоличествоПакетов - 1].Выгрузить(); 
	Итоги = Новый Массив();
	Для Каждого Строка Из ТаблицаИтогов Цикл
		
		СтрокаИтоги = Новый Структура();
		СтрокаИтоги.Вставить("Номенклатура", 	  Строка.Номенклатура);
		СтрокаИтоги.Вставить("ДокументОснование", Строка.ДокументОснование);
		СтрокаИтоги.Вставить("Организация", 	  Строка.Организация);
		СтрокиДубли = ТаблицаИтогов.НайтиСтроки(Новый Структура("Номенклатура", Строка.Номенклатура));
		Если Не СтрокиДубли.Количество() = 1 Тогда
			СтрокаИтоги.Вставить("Цена", 0); //однозначно определить не можем, поэтому не заполняем вообще 
		Иначе
			СтрокаИтоги.Вставить("Цена", Строка.Цена);
		КонецЕсли;
		Итоги.Добавить(СтрокаИтоги);
		
	КонецЦикла;
	
	ДанныеДокументаОснования = Новый Структура();
	ДанныеДокументаОснования.Вставить("Реквизиты", Реквизиты);
	ДанныеДокументаОснования.Вставить("Товары",	   Товары);
	ДанныеДокументаОснования.Вставить("Итоги",	   Итоги);
	
	Возврат ДанныеДокументаОснования;
	
КонецФункции

Функция ПолучитьДанныеДокументаОснованияДляПерезаполненияИСЦЭДМ_Перемещение(ДокументОснование, ДокументЦЭДМ) Экспорт
	
	ТекстЗапроса = ТекстЗапросаОбщиеДанныеОснования_Перемещение(ДокументОснование);
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументов", ДокументОснование);
	
	РезультатЗапроса  = Запрос.ВыполнитьПакет();
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	ТаблицаРеквизиты = РезультатЗапроса[КоличествоПакетов - 2].Выгрузить();
	Реквизиты = Новый Структура();
	
	Для Каждого Строка Из ТаблицаРеквизиты Цикл
		
		СкладОтправительПоОснованию = Строка.СкладОтправитель;
		СкладПолучательПоОснованию  = Строка.СкладПолучатель;
		Реквизиты.Вставить("СкладПоставщик",  СкладОтправительПоОснованию);
		Реквизиты.Вставить("СкладПолучатель", СкладПолучательПоОснованию);
		//Пробуем найти соответствие складов ЦЭДМ
		СкладЦЭДМОтправитель = ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(СкладОтправительПоОснованию, Строка.Организация);
		СкладЦЭДМПолучатель  = ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(СкладПолучательПоОснованию,  Строка.Организация);
		Если Не СкладЦЭДМОтправитель = Неопределено Тогда
			Реквизиты.Вставить("Поставщик", 					  СкладЦЭДМОтправитель.СкладЦЭДМ);
			Реквизиты.Вставить("ПоставщикНаименование", 		  СкладЦЭДМОтправитель.Представление);
			Реквизиты.Вставить("ПоставщикИдентификационныйНомер", СкладЦЭДМОтправитель.ИдентификаторСклада);
		Иначе
			//заполняем данными организации
			Реквизиты.Вставить("Поставщик", 					  Строка.Организация);
			Реквизиты.Вставить("ПоставщикНаименование", 		  Строка.ОрганизацияНаименование);
			Реквизиты.Вставить("ПоставщикИдентификационныйНомер", Строка.ОрганизацияИдентификационныйНомер);	
		КонецЕсли;
		Если Не СкладЦЭДМПолучатель = Неопределено Тогда
			Реквизиты.Вставить("Получатель", 					   СкладЦЭДМПолучатель.СкладЦЭДМ);
			Реквизиты.Вставить("ПолучательНаименование", 		   СкладЦЭДМПолучатель.Представление);
			Реквизиты.Вставить("ПолучательИдентификационныйНомер", СкладЦЭДМПолучатель.ИдентификаторСклада);
		Иначе
			//заполняем данными организации
			Реквизиты.Вставить("Получатель", 					   Строка.Организация);
			Реквизиты.Вставить("ПолучательНаименование", 		   Строка.ОрганизацияНаименование);
			Реквизиты.Вставить("ПолучательИдентификационныйНомер", Строка.ОрганизацияИдентификационныйНомер);
		КонецЕсли;
		
		Реквизиты.Вставить("Организация", Строка.Организация);
		Реквизиты.Вставить("ВидОперации", ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСЦЭДМ.ВнутреннееПеремещение"));
		Реквизиты.Вставить("ДокументОснование", Строка.ДокументОснование);
		
	КонецЦикла;
	
	ТаблицаТовары = РезультатЗапроса[КоличествоПакетов - 1].Выгрузить(); 
	Товары = Новый Массив();
	
	Для Каждого Строка Из ТаблицаТовары Цикл
		
		СтрокаТовары = Новый Структура();
		СтрокаТовары.Вставить("Номенклатура", 	    Строка.Номенклатура);
		СтрокаТовары.Вставить("КоличествоИсточник", Строка.КоличествоИсточник);
		СтрокаТовары.Вставить("ЕдиницаИзмерения",   Строка.ЕдиницаИзмеренияБазовая);
		Товары.Добавить(СтрокаТовары);
		
	КонецЦикла;
	
	ДанныеДокументаОснования = Новый Структура();
	ДанныеДокументаОснования.Вставить("Реквизиты", Реквизиты);
	ДанныеДокументаОснования.Вставить("Товары",	   Товары);
	
	Возврат ДанныеДокументаОснования;
	
КонецФункции

//Проверяем, может ли первичный документ, из которого инициировали создание документа ИС ЦЭДМ, 
//служить основнаием для ввода этого документа. В некоторых случаях поддерживается только 
//установка связи документов через выбор существующего, а не ввод нового.
Функция ДокументНеПоддерживаетСозданиеНовогоДляИСЦЭДМ(Документ, ИмяКоманды = Неопределено) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ОприходованиеТоваров")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.СписаниеТоваров") 
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваров") 
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") 
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров")
		
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ОтчетОПередачеКИОтНерезидентаРКИСЦЭДМ") Тогда
		//Для этих документов всегда можем ввести новый документ маркировки
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваров") 
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		//Данные документы могут быть связаны со входящим АПП, но для него новый документ не создаем
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

//Получение универсальной структуры заполнения первичного документа при вводе на основании документа ИС ЦЭДМ
//
Функция ПолучитьПустуюСтруктуруДокументаОснованияДляИСЦЭДМ(Параметры = Неопределено) Экспорт
	
	Если Не Параметры = Неопределено Тогда
		ТипДокумента = Параметры.ТипСоздаваемогоДокумента;
	Иначе
		ТипДокумента = Параметры;
	КонецЕсли;
	
	СтруктураДокумента = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = "Организация," 
	+ "Контрагент,"
	+ "Партнер,"
	+ "Склад,"
	+ "ДокументОснование," 
	+ "ВидОперации,"
	+ "Дата,"
	+ "Автор,"
	;
	
	СтруктураДокумента.Вставить("Реквизиты", ИнтеграцияИСМПТКВызовСервера.ПолучитьТаблицуПараметров(СписокОбязательныхКолонок));
	
	СписокОбязательныхКолонок = 
	"Организация,"
	+ "Номенклатура,"
	+ "Цена,"
	+ "КодТНВЭД,"
	+ "Характеристика,"
	+ "Количество,"
	+ "Упаковка,"
	+ "КоличествоУпаковок,"
	+ "ЕдиницаИзмерения,"
	+ "ДокументОснование"
	;
	
	СтруктураДокумента.Вставить("Товары", ИнтеграцияИСМПТКВызовСервера.ПолучитьТаблицуПараметров(СписокОбязательныхКолонок));
	
	Возврат СтруктураДокумента;
	
КонецФункции	

Функция ПолучитьПустуюСтруктуруДокументаОснованияДляИСЦЭДМ_Перемещение(Параметры = Неопределено) Экспорт
	
	Если Не Параметры = Неопределено Тогда
		ТипДокумента = Параметры.ТипСоздаваемогоДокумента;
	Иначе
		ТипДокумента = Параметры;
	КонецЕсли;
	
	СтруктураДокумента = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = "Организация," 
	+ "СкладОтправитель,"
	+ "СкладПолучатель,"
	+ "ДокументОснование," 
	+ "ХозяйственнаяОперация,"
	+ "Дата,"
	+ "Ответственный,"
	;
	
	СтруктураДокумента.Вставить("Реквизиты", ИнтеграцияИСМПТКВызовСервера.ПолучитьТаблицуПараметров(СписокОбязательныхКолонок));
	
	СписокОбязательныхКолонок = 
	"Организация,"
	+ "Номенклатура,"
	+ "Характеристика,"
	+ "Количество,"
	+ "Упаковка,"
	+ "КоличествоУпаковок,"
	+ "ДокументОснование"
	;
	
	СтруктураДокумента.Вставить("Товары", ИнтеграцияИСМПТКВызовСервера.ПолучитьТаблицуПараметров(СписокОбязательныхКолонок));
	
	Возврат СтруктураДокумента;
	
КонецФункции

//Заполняем созданный первичный документ полученными сведениями из документа ИС ЦЭДМ
//
Процедура ЗаполнитьМассивПервичныхДокументовИСЦЭДМ(ПараметрыСоздания, МассивДанныхДляЗаполнения, СписокПервичныхДокументов, Отказ, ВидДвижения = "", ТипСоздаваемогоДокумента = "") Экспорт
	
	ПроводитьСозданныеДокументы	= ПараметрыСоздания.ПроводитьСозданныеДокументы;
	ФормироватьСписокДокументов = ПараметрыСоздания.ФормироватьСписокДокументов;
	
	Если МассивДанныхДляЗаполнения.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'В документе недостаточно данных для заполнения нового основания: проверьте таблицу ""Марки""!'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	Иначе
		
		Для Каждого ЭлементМассива Из МассивДанныхДляЗаполнения Цикл
			
			ТипСоздаваемогоДокумента = ПараметрыСоздания.ТипСоздаваемогоДокумента;
			ЗаписьПрошлаУспешно = Истина;
			ЗаполнитьМассивПервичныхДокументов_Общая(ЭлементМассива, СписокПервичныхДокументов, Отказ, ФормироватьСписокДокументов, ПроводитьСозданныеДокументы, ТипСоздаваемогоДокумента, ЗаписьПрошлаУспешно);
			Если Не ЗаписьПрошлаУспешно Тогда
				Продолжить;
			КонецЕсли;		
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	

//Функция позволяет получить информацию о СкладеЦЭДМ по переданной ссылке на типовой Склад. 
//
Функция ПолучитьДанныеСкладаЦЭДМПоСкладуУчета(Склад, Организация) Экспорт
	
	Если Не ЗначениеЗаполнено(Склад) 
		ИЛИ Не ЗначениеЗаполнено(Организация) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СоответствиеСкладовИСЦЭДМ.СкладИСЦЭДМ КАК СкладИСЦЭДМ,
	|	СоответствиеСкладовИСЦЭДМ.СкладИСЦЭДМ.ИИНБИНСклада КАК ИИНБИНСклада,
	|	СоответствиеСкладовИСЦЭДМ.СкладИСЦЭДМ.Представление КАК СкладИСЦЭДМПредставление
	|ИЗ
	|	РегистрСведений.СоответствиеСкладовИСЦЭДМ КАК СоответствиеСкладовИСЦЭДМ
	|ГДЕ
	|	СоответствиеСкладовИСЦЭДМ.Склад = &Склад
	|	И СоответствиеСкладовИСЦЭДМ.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", 		 Склад);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() <> 1 Тогда
		ДанныеСклада = Неопределено;
	Иначе
		Пока Выборка.Следующий() Цикл
			ДанныеСклада = Новый Структура();
			ДанныеСклада.Вставить("ИдентификаторСклада", Выборка.ИИНБИНСклада);
			ДанныеСклада.Вставить("СкладЦЭДМ", 			 Выборка.СкладИСЦЭДМ);
			ДанныеСклада.Вставить("Представление",		 Выборка.СкладИСЦЭДМПредставление);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеСклада;
	
КонецФункции 

//Функция позволяет получить полную информацию о СкладеЦЭДМ, а также соответствующем ему типовом Складе,
//по переданной ссылке на СкладЦЭДМ.
Функция ПолучитьРеквизитыСкладаЦЭДМ(СкладЦЭДМ, Организация) Экспорт
	
	Если Не ЗначениеЗаполнено(СкладЦЭДМ) 
		ИЛИ Не ЗначениеЗаполнено(Организация) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СкладыИСЦЭДМ.Ссылка КАК СкладИСЦЭДМ,
	|	СкладыИСЦЭДМ.ИИНБИНСклада КАК ИИНБИНСклада,
	|	СкладыИСЦЭДМ.Представление КАК СкладИСЦЭДМПредставление,
	|	ЕСТЬNULL(СоответствиеСкладовИСЦЭДМ.Склад, НЕОПРЕДЕЛЕНО) КАК Склад,
	|	&Организация КАК Организация,
	|	СкладыИСЦЭДМ.Идентификатор КАК СкладИСЦЭДМИдентификатор
	|ИЗ
	|	РегистрСведений.СоответствиеСкладовИСЦЭДМ КАК СоответствиеСкладовИСЦЭДМ
	|		ПРАВОЕ СОЕДИНЕНИЕ Справочник.СкладыИСЦЭДМ КАК СкладыИСЦЭДМ
	|		ПО СоответствиеСкладовИСЦЭДМ.СкладИСЦЭДМ = СкладыИСЦЭДМ.Ссылка
	|			И (СоответствиеСкладовИСЦЭДМ.Организация = &Организация)
	|ГДЕ
	|	СкладыИСЦЭДМ.Ссылка = &Склад";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", 		 СкладЦЭДМ);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ДанныеСклада = Новый Структура();
		ДанныеСклада.Вставить("СкладЦЭДМ", 		Выборка.СкладИСЦЭДМ);
		ДанныеСклада.Вставить("ИИНБИНСклада",	Выборка.ИИНБИНСклада);
		ДанныеСклада.Вставить("Представление",	Выборка.СкладИСЦЭДМПредставление);
		ДанныеСклада.Вставить("Идентификатор",	Выборка.СкладИСЦЭДМИдентификатор);
		ДанныеСклада.Вставить("Организация",	Выборка.Организация);
		
		Если Выборка.Количество() = 1 Тогда
			ДанныеСклада.Вставить("Склад",		Выборка.Склад);
		Иначе
			ДанныеСклада.Вставить("Склад", 		Неопределено);
		КонецЕсли;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеСклада;
	
КонецФункции 

//Данная функция позволяет по переданному идентификатору СкладаЦЭДМ подобрать ссылку на СкладЦЭДМ, 
//а также ссылку на соответствующий ТИПОВОЙ Склад. 
Функция ПолучитьДанныеСкладаПоИдентификаторуЦЭДМ(ИИНБИНСкладаЦЭДМ, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СоответствиеСкладовИСЦЭДМ.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
	|	СкладыИСЦЭДМ.Организация КАК Организация,
	|	СкладыИСЦЭДМ.Ссылка КАК СкладЦЭДМ
	|ИЗ
	|	Справочник.СкладыИСЦЭДМ КАК СкладыИСЦЭДМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеСкладовИСЦЭДМ КАК СоответствиеСкладовИСЦЭДМ
	|		ПО (СоответствиеСкладовИСЦЭДМ.СкладИСЦЭДМ = СкладыИСЦЭДМ.Ссылка)
	|ГДЕ
	|	СкладыИСЦЭДМ.Организация = &Организация
	|	И СкладыИСЦЭДМ.ИИНБИНСклада = &ИИНБИНСкладаЦЭДМ";
	
	Запрос.УстановитьПараметр("Организация", 	  Организация);
	Запрос.УстановитьПараметр("ИИНБИНСкладаЦЭДМ", ИИНБИНСкладаЦЭДМ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() <> 1 Тогда
		ДанныеСклада = Неопределено;
	Иначе
		Пока Выборка.Следующий() Цикл
			ДанныеСклада = Новый Структура();
			ДанныеСклада.Вставить("Склад",		 Выборка.Склад);
			ДанныеСклада.Вставить("Организация", Выборка.Организация);
			ДанныеСклада.Вставить("СкладЦЭДМ",   Выборка.СкладЦЭДМ);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеСклада;
	
КонецФункции 

Функция ПолучитьОрганизациюДляПользователяПриСинхронизации() Экспорт
	
	// Возможность подобрать организацию в форму получения документов из настроек пользователя 
	// или взять организацию по-умолчанию, если в базе не ведется учет нескольких организаций.
	
	Возврат Справочники.Организации.ПустаяСсылка();
	
КонецФункции

Функция ПолучитьИнформациюОЦенахИзОснования(ДокументОснование, ШтрихкодыДокумента) Экспорт
	
	СоответствиеЦен = Новый Соответствие();
	
	Если ДокументОснование = Неопределено Тогда
		Возврат СоответствиеЦен; 
	КонецЕсли;
	
	Если ДокументОснование.Товары.Выгрузить().Колонки.Найти("Цена") = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'В выбранном основании отсутствуют данные по ценам товаров, заполнение невозможно.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);	
		Возврат СоответствиеЦен; 
	КонецЕсли;
	
	ИмяДокумента = ДокументОснование.Метаданные().Имя;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокОснование.Цена КАК Цена,
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК Штрихкод,
	|	ДокОснование.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Предварительная
	|ИЗ
	|	Документ." + ИмяДокумента + ".Товары КАК ДокОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО ДокОснование.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|			И ДокОснование.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ГДЕ
	|	ДокОснование.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Предварительная.Цена КАК Цена,
	|	ВТ_Предварительная.Штрихкод КАК Штрихкод,
	|	ВТ_Предварительная.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Общая
	|ИЗ
	|	ВТ_Предварительная КАК ВТ_Предварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО ВТ_Предварительная.Номенклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
	|ГДЕ
	|	ВидыНоменклатуры.ОсобенностьУчета = &ОсобенностьУчета
	|	И ВТ_Предварительная.Штрихкод В(&Штрихкоды)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая.Штрихкод КАК Штрихкод,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Общая.Цена) КАК КоличествоЦен
	|ПОМЕСТИТЬ ВТ_ЦеныРазличные
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Общая.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая.Штрихкод КАК Штрихкод,
	|	ВТ_Общая.Цена КАК Цена,
	|	ВТ_ЦеныРазличные.КоличествоЦен КАК КоличествоЦен
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныРазличные КАК ВТ_ЦеныРазличные
	|		ПО ВТ_Общая.Штрихкод = ВТ_ЦеныРазличные.Штрихкод";	
	
	Запрос.УстановитьПараметр("Ссылка",    ДокументОснование);
	Запрос.УстановитьПараметр("ОсобенностьУчета", ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ЛекарственныеПрепараты"));
	Запрос.УстановитьПараметр("Штрихкоды", ШтрихкодыДокумента);
	Выборка = Запрос.Выполнить().Выбрать();
	
	НужноОповестить = Новый Массив();
	Если Выборка.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось получить информацию о ценах товаров в указанном документе-основании.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	Иначе	
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.Штрихкод) Тогда
				GTIN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.GTINПоШтрихкодуEAN(Выборка.Штрихкод);
				Если Выборка.КоличествоЦен = 1 Тогда
					Если Не Выборка.Цена = 0 Тогда
						СоответствиеЦен.Вставить(GTIN, Выборка.Цена);
					Иначе
						ТекстСообщения = НСтр("ru = 'Для GTIN %1 в документе-основании указана нулевая цена!'");
						ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, GTIN); 
						ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
					КонецЕсли;
				Иначе
					Если НужноОповестить.Найти(GTIN) = Неопределено Тогда
						НужноОповестить.Добавить(GTIN);
					КонецЕсли;
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не НужноОповестить.Количество() = 0 Тогда
		Для Каждого GTIN Из НужноОповестить Цикл
			ТекстСообщения = НСтр("ru = 'Для GTIN %1 не удалось однозначно определить цену по документу-основанию. Данные в строке таблицы ""Итоги"" для этого штрихкода нужно заполнить самостоятельно.'");
			ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, GTIN); 
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СоответствиеЦен;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВалютуРегламентированногоУчета() Экспорт
	
	Возврат Неопределено;
		
КонецФункции

Функция ПолучитьСписокСтранЕАЭС() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =  "ВЫБРАТЬ
	|	СтраныМира.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|ГДЕ
	|	СтраныМира.УчастникЕАЭС = ИСТИНА";
	
	МассивСтранЕАЭС = Новый Массив();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивСтранЕАЭС.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат МассивСтранЕАЭС;
	
КонецФункции

Функция ПолучитьКодСтраны(Страна) Экспорт
	
	Возврат Страна.Код;
	
КонецФункции

Функция ПолучитьКонтактноеЛицо(ТекущийПользователь) Экспорт
	
	Если ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ФизЛицо = ТекущийПользователь.ФизическоеЛицо;
		Возврат ?(ЗначениеЗаполнено(ФизЛицо), ФизЛицо.Наименование, "");
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ТаблицаЗначенийВМассив(ТаблицаЗначений) Экспорт
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаЗначений);
	
КонецФункции

Процедура УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

функция ВыводитьПодробнуюИнформациюПриЗагрузкеДанныхИСМПТ() Экспорт
	
	Возврат Константы.ВыводитьПодробнуюИнформациюПриОбменеССерверомИСМПТК.Получить();
	
КонецФункции

функция ВыводитьПодробнуюИнформациюПриЗагрузкеДанныхИСЦЭДМ() Экспорт
	
	Возврат Константы.ВыводитьПодробнуюИнформациюПриОбменеССерверомИСЦЭДМ.Получить();
	
КонецФункции

Функция ПроверитьДоступностьРоли(ИменаРолей, Пользователь, УчитыватьПривилегированныйРежим = Истина) Экспорт 
	
	Возврат Пользователи.РолиДоступны(ИменаРолей, Пользователь);
	
КонецФункции

Функция ПолучитьТипОрганизации() Экспорт
	
	Возврат Тип("СправочникСсылка.Организации");
	
КонецФункции

Функция ПолучитьПустоеЗначениеСтруктурнойЕдиницы() Экспорт
	
	Возврат Справочники.Организации.ПустаяСсылка();
	
КонецФункции

Функция ДополнитьТекстЗапросаПереопределяемымиУсловиями(ТекстЗапроса) Экспорт

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьИмяДокументаДляПроверкиПоТипу(СсылкаДокумент)
	
	Если ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		Возврат "РеализацияТоваров";
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Возврат "ПоступлениеТоваров";
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		Возврат "ВозвратТоваровОтПокупателя";
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		Возврат "ВозвратТоваровПоставщику";
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
		Возврат "ОприходованиеТоваров";
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.СписаниеТоваров") Тогда
		Возврат "СписаниеТоваров";
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		Возврат "ЗаказПоставщику";
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		Возврат "ПеремещениеТоваров"; 
	ИначеЕсли ТипЗнч(СсылкаДокумент) = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров") Тогда
		Возврат "ОрдерНаПеремещениеТоваров"; 
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСсылкаСтранаКазахастан() Экспорт
	
	Возврат Справочники.СтраныМира.Россия;
	
КонецФункции
	
#Область СтандартныеПодсистемы

Процедура ПриЗаполненииОграниченияДоступа(Ограничение, ИмяДокумента) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Переопределяемые

Функция ПолучитьПризнакУчетаНДС(Дата, Организация)
	
	Если РегистрыСведений.ПрименениеСистемНалогообложения.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация)).СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
		Возврат Истина;		
	Иначе
		Возврат Ложь;
	КонецЕсли;	       
	
КонецФункции

#Область Интерфейс

Функция ПолучитьВидимостьСсылкиДокументОснованиеВЗаказе() Экспорт
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьВидимостьПодбораНоменклатурыВЗаказе() Экспорт
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьВидимостьСсылкиНаВидыНоменклатуры() Экспорт
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область УказаниеЦенВИтогахВМодулеИСМПТ

Функция ПолучитьИнформациюОЦенахИзОснованияИСМПТ(ДокументОснование, ШтрихкодыДокумента) Экспорт
	
	//В документах ИС МПТ указание цен в ТЧ Итоги требуется для табачной продукции.
	//Также цена при этом должна быть указана без НДС, т.е. если учетный документ имеет признак ЦенаВключаетНДС,
	//ценовые показатели должны быть пересчитаны.
	
	ЦенаВключаетНДСВДокументе = ПолучитьПризнакУчетаНДСИзОснования(ДокументОснование);
    ИмяДокумента = ДокументОснование.Метаданные().Имя;
	
	Запрос = Новый Запрос;
    Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокОснование.Цена КАК ЦенаИсх,
	|	ДокОснование.СтавкаНДС КАК СтавкаНДС,
	|	ДокОснование.Сумма КАК Сумма,
	|	ДокОснование.СуммаНДС КАК СуммаНДС,
	|	ДокОснование.Количество КАК Количество,
    |	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК Штрихкод,
	|	ДокОснование.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Общая
	|ИЗ
	|	Документ." + ИмяДокумента + ".Товары КАК ДокОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО ДокОснование.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|			И ДокОснование.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ГДЕ
	|	ДокОснование.Ссылка = &Ссылка
    |;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая.Штрихкод КАК Штрихкод,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Общая.ЦенаИсх) КАК КоличествоЦен
	|ПОМЕСТИТЬ ВТ_ЦеныРазличные
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Общая.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая.Штрихкод КАК Штрихкод,
	|	ВТ_Общая.ЦенаИсх КАК ЦенаИсх,
	|	ВТ_Общая.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_Общая.Сумма КАК Сумма,
	|	ВТ_Общая.СуммаНДС КАК СуммаНДС,
	|	ВТ_Общая.Количество КАК Количество,
	|	ВТ_ЦеныРазличные.КоличествоЦен КАК КоличествоЦен
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныРазличные КАК ВТ_ЦеныРазличные
	|		ПО ВТ_Общая.Штрихкод = ВТ_ЦеныРазличные.Штрихкод";	
	
	Запрос.УстановитьПараметр("Ссылка",    ДокументОснование);
	Запрос.УстановитьПараметр("Штрихкоды", ШтрихкодыДокумента);
	Выборка = Запрос.Выполнить().Выбрать();
	
	СоответствиеЦен = Новый Соответствие();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Штрихкод) Тогда
			Если Выборка.КоличествоЦен = 1 Тогда
				Если Не Выборка.ЦенаИсх = 0 Тогда
					ДанныеПоШК = Новый Структура();
					ДанныеПоШК.Вставить("СтавкаНДС", Выборка.СтавкаНДС);
					Если ЦенаВключаетНДСВДокументе Тогда
						Цена = Окр((Выборка.Сумма - Выборка.СуммаНДС) / Выборка.Количество, 2);
						ДанныеПоШК.Вставить("Цена", Цена);
					Иначе
						ДанныеПоШК.Вставить("Цена", Выборка.ЦенаИсх);
					КонецЕсли;
					СоответствиеЦен.Вставить(Выборка.Штрихкод, ДанныеПоШК);
				Иначе
					GTIN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.GTINПоШтрихкодуEAN(Выборка.Штрихкод);
					ТекстСообщения = НСтр("ru = 'Для GTIN %1 в документе-основании указана нулевая цена!'");
					ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, GTIN); 
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
	Возврат СоответствиеЦен;
		
КонецФункции

// Возвращает значение признака учета НДС в документе-основании.
// Данная информация необходима для расчета итоговых показателей в документах ИС МПТ.
//
// Параметры:
// 	 ДокументОснование - ДокументСсылка - документ, с помощью которого документ библиотеки отражен в учете, 
//						 является источником информации о ценах товаров
//
// Возвращаемое значение:
// 	Булево - 
//
Функция ПолучитьПризнакУчетаНДСИзОснования(ДокументОснование)
	
	Возврат ДокументОснование.ЦенаВключаетНДС;
	
КонецФункции

#КонецОбласти