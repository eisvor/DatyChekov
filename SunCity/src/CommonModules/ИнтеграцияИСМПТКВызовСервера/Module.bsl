
#Область СоздатьПоАПИ

// См. ИнтерфейсИСМПТК.СоздатьИсходящиеАкты()
Процедура СоздатьИсходящиеАкты(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииАктовXML, КоллекцияSignedContentXML) Экспорт
	
	КоллекцияАктовXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеАкты(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияАктовXML, КоллекцияSignedContentXML);
	
	// После того, как переменная АдресКоллекцииАктовXML станет не нужна, 
	// необходимо самостоятельно очистить временное хранилище,
	// иначе значение будет удалено только после перезапуска сервера.
	АдресКоллекцииАктовXML = ПоместитьВоВременноеХранилище(КоллекцияАктовXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

Процедура СоздатьИсходящиеУведомленияОВводеВОборот(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииАктовXML, КоллекцияSignedContentXML) Экспорт
	
	КоллекцияУведомленийXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВводеВОборот(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияУведомленийXML, КоллекцияSignedContentXML);
	
	АдресКоллекцииАктовXML = ПоместитьВоВременноеХранилище(КоллекцияУведомленийXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

Процедура СоздатьИсходящиеУведомленияОВыводеИзОборота(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииУведомленийXML, КоллекцияSignedContentXML) Экспорт
	
	КоллекцияУведомленийXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВыводеИзОборота(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияУведомленийXML, КоллекцияSignedContentXML);
	
	АдресКоллекцииУведомленийXML = ПоместитьВоВременноеХранилище(КоллекцияУведомленийXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

Процедура СоздатьИсходящиеУведомленияОВвозе(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииУведомленийXML, КоллекцияSignedContentXML) Экспорт
	
	КоллекцияУведомленийXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВвозе(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияУведомленийXML, КоллекцияSignedContentXML);
	
	АдресКоллекцииУведомленийXML = ПоместитьВоВременноеХранилище(КоллекцияУведомленийXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

Процедура СоздатьИсходящиеУведомленияОВвозеИзТретьихСтран(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииУведомленийXML, КоллекцияSignedContentXML, ДанныеГрупповыхКодов) Экспорт
	
	КоллекцияУведомленийXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВвозеИзТретьихСтран(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияУведомленийXML, КоллекцияSignedContentXML, ДанныеГрупповыхКодов);
	
	АдресКоллекцииУведомленийXML = ПоместитьВоВременноеХранилище(КоллекцияУведомленийXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

Процедура СоздатьИсходящиеУведомленияОбЭкспортеЕАЭС(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииУведомленийXML, КоллекцияSignedContentXML) Экспорт
	
	КоллекцияУведомленийXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОбЭкспортеЕАЭС(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияУведомленийXML, КоллекцияSignedContentXML);
	
	АдресКоллекцииУведомленийXML = ПоместитьВоВременноеХранилище(КоллекцияУведомленийXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

Процедура СоздатьИсходящиеУведомлениеОПриемкеЕАЭС(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииУведомленийXML, КоллекцияSignedContentXML) Экспорт
	
	КоллекцияУведомленийXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомлениеОПриемкеЕАЭС(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияУведомленийXML, КоллекцияSignedContentXML);
	
	АдресКоллекцииУведомленийXML = ПоместитьВоВременноеХранилище(КоллекцияУведомленийXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

// См. ИнтерфейсИСМПТК.СоздатьИсходящиеАкты()
Процедура СоздатьИсходящиеУведомления(Знач МассивДокументов, Знач УстанавливатьПодпись, Знач ТипПодписи, АдресКоллекцииАктовXML, КоллекцияSignedContentXML) Экспорт
	
	КоллекцияДокументовXML = Неопределено;
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомления(МассивДокументов, УстанавливатьПодпись, ТипПодписи, КоллекцияДокументовXML, КоллекцияSignedContentXML);
	
	// После того, как переменная АдресКоллекцииАктовXML станет не нужна, 
	// необходимо самостоятельно очистить временное хранилище,
	// иначе значение будет удалено только после перезапуска сервера.
	АдресКоллекцииАктовXML = ПоместитьВоВременноеХранилище(КоллекцияДокументовXML, Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОтправитьПоАПИ

Функция ОтправитьИсходящиеАкты(Знач КоллекцияПодписей, ТокенАвторизации = Неопределено) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьИсходящиеАкты(КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

Функция ОтправитьЗаказНаЭмиссиюКодовМаркировки(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	Отказ = Ложь;
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		Результат = ЭлектронноеВзаимодействиеССервисамиМаркировка.ОтправитьЗаказНаЭмиссиюКодовМаркировки(МассивСУЗ, НастройкаОбменаСУЗ, Отказ);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияДокументОтправленВСУЗ();
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецФункции

Функция ОтправитьАгрегациюКодовМаркировки(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	Отказ = Ложь;
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		Результат = ЭлектронноеВзаимодействиеССервисамиМаркировка.ОтправитьАгрегациюКодовМаркировки(МассивСУЗ, НастройкаОбменаСУЗ, Отказ);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияДокументОтправленВСУЗ();
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецФункции

Функция ОтправитьНанесениеКодовМаркировки(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	Отказ = Ложь;
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		Результат = ЭлектронноеВзаимодействиеССервисамиМаркировка.ОтправитьНанесениеКодовМаркировки(МассивСУЗ, НастройкаОбменаСУЗ, Отказ);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияДокументОтправленВСУЗ();
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецФункции

Функция ОтправитьЗапросСтатусовЗаказовКМ(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		
		ОтветСервера = ИнтеграцияИСМПТК.ПолучитьСтатусЗаказаЭмиссииКМ(НастройкаОбменаСУЗ, Перечисления.ВидыПродукцииИСМПТК.Обувная);
		Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
			
			ТЗСтатусыДокументовЗаявок = СоответствиеСтатусовКодовМаркировкиВТЗ(ОтветСервера.ДанныеОСтатусах);
			Если НЕ ТЗСтатусыДокументовЗаявок = Неопределено Тогда 
				//Могла возникнуть ошибка при разборе ответа сервера, если в нем не содержится структура статусов документа 
				
				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ
				|	СтатусыДокументовЗаявок.orderId КАК orderId,
				|	СтатусыДокументовЗаявок.orderStatus КАК orderStatus
				|ПОМЕСТИТЬ СтатусыДокументов
				|ИЗ
				|	&СтатусыДокументовЗаявок КАК СтатусыДокументовЗаявок
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Ссылка КАК Ссылка,
				|	ЗаказНаЭмиссиюКодовМаркировкиСУЗ.OrderID КАК OrderID,
				|	ЕСТЬNULL(СтатусыДокументов.orderStatus, ""CLOSED"") КАК orderStatus
				|ИЗ
				|	Документ.ЗаказКодовМаркировкиСУЗИСМПТК КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗ
				|		ЛЕВОЕ СОЕДИНЕНИЕ СтатусыДокументов КАК СтатусыДокументов
				|		ПО ЗаказНаЭмиссиюКодовМаркировкиСУЗ.OrderID = СтатусыДокументов.orderId
				|ГДЕ
				|	ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Ссылка В(&МассивДокументов)
				|	И НЕ ЗаказНаЭмиссиюКодовМаркировкиСУЗ.OrderID = &ПустаяСтрока";
				
				Запрос.УстановитьПараметр("МассивДокументов", 		 МассивСУЗ);
				Запрос.УстановитьПараметр("СтатусыДокументовЗаявок", ТЗСтатусыДокументовЗаявок);
				Запрос.УстановитьПараметр("ПустаяСтрока", 			 "");
				
				Результат = Запрос.Выполнить();
				
				Если Не Результат.Пустой() Тогда 
					Выборка = Результат.Выбрать();
					Пока Выборка.Следующий() Цикл 
						
						СтатусЗаказа    = ИнтеграцияИСМПТК.СтатусБизнесЗаказа(Выборка.orderStatus);
						ДокументОбъект  = Выборка.Ссылка.ПолучитьОбъект();
						ДокументОбъект.Статус = СтатусЗаказа;
						
						Попытка
							ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
						Исключение
							ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаПриЗаписиСтатусНеИзменилсяСПараметрами();
							ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументОбъект, СтатусЗаказа);
							ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
						КонецПопытки;
						
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервера.ТекстОшибки);
			Иначе
				
				Для Каждого Документ Из МассивСУЗ Цикл
			
					Если Не Документ.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМПТК.Черновик") Тогда
						ДокументОбъект = Документ.ПолучитьОбъект();
						ДокументОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМПТК.Выполнен");
						Попытка
							ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
						Исключение
							ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаПриЗаписиДокументаСПараметром();
							ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументОбъект);
							ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
						КонецПопытки;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ОтправитьЗапросПричинОтказаЗаказовКМ(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказКодовМаркировкиСУЗИСМПТКТовары.Ссылка КАК Ссылка,
		|	ЗаказКодовМаркировкиСУЗИСМПТКТовары.Ссылка.OrderID КАК OrderID,
		|	ЗаказКодовМаркировкиСУЗИСМПТКТовары.Ссылка.Статус КАК Статус,
		|	ЗаказКодовМаркировкиСУЗИСМПТКТовары.GTIN КАК GTIN,
		|	ЗаказКодовМаркировкиСУЗИСМПТКТовары.Ссылка.ВидПродукции КАК ВидПродукции
		|ИЗ
		|	Документ.ЗаказКодовМаркировкиСУЗИСМПТК.Товары КАК ЗаказКодовМаркировкиСУЗИСМПТКТовары
		|ГДЕ
		|	ЗаказКодовМаркировкиСУЗИСМПТКТовары.Ссылка В(&МассивДокументов)
		|	И ЗаказКодовМаркировкиСУЗИСМПТКТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМПТК.НеПодтвержденИСМП)
		|ИТОГИ
		|	МАКСИМУМ(OrderID),
		|	МАКСИМУМ(Статус),
		|	МАКСИМУМ(GTIN),
		|	МАКСИМУМ(ВидПродукции)
		|ПО
		|	Ссылка";
		
		Запрос.УстановитьПараметр("МассивДокументов", МассивСУЗ);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда 
			Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока Выборка.Следующий() Цикл
				
				ОтветСервера = ИнтеграцияИСМПТК.ПолучитьПричинуОтказаЗаказаЭмиссииКМ(НастройкаОбменаСУЗ, Выборка.ВидПродукции, Выборка);
				
				Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
					
					Попытка
						ПричинаОтказа = ОтветСервера.ДанныеОСтатусах.Получить("rejectionReason");
						ДокументОбъект 								= Выборка.Ссылка.ПолучитьОбъект();
						ДокументОбъект.ПричинаОтклонения 			= ПричинаОтказа;
						ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
					Исключение
						
						ТекстСообщения = НСтр("ru = 'При записи документа ""%1"" произошла ошибка и для документа не была присвоена причина отклонения.'");
						ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументОбъект);
						
						ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
						
					КонецПопытки;
					
				Иначе
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервера.ТекстОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ОтправитьЗапросКМПоЗаказуНаЭмиссию(МассивСУЗ, СтруктурнаяЕдиница, РазмерБлокаКодов = 0, МассивКодовЗаказа = Неопределено) Экспорт
	
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	НастройкиРазбораКодаМаркировки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйПовтИсп.НастройкиРазбораКодаМаркировки();  
	
	Отказ = Ложь;
	
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию КАК ЗаказНаЭмиссию,
		|	ПулКодовМаркировкиСУЗИСМПТК.GTIN КАК GTIN,
		|	КОЛИЧЕСТВО(ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки) КАК КоличествоКодов
		|ПОМЕСТИТЬ КоличествоПолученныхКодов
		|ИЗ
		|	РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ПулКодовМаркировкиСУЗИСМПТК
		|ГДЕ
		|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию В(&МассивДокументов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию,
		|	ПулКодовМаркировкиСУЗИСМПТК.GTIN
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.Ссылка КАК Ссылка,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.Организация КАК Организация,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.ВидПродукции КАК ВидПродукции,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.OrderID КАК OrderID,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN КАК GTIN,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Шаблон КАК Шаблон,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество КАК КоличествоВЗаказе,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Номенклатура КАК Номенклатура,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Характеристика КАК Характеристика,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.СпособВводаВОборот КАК СпособВводаВОборот,
		|	&РазмерБлокаКодов КАК РазмерБлокаКодов,
		|	ВЫБОР
		|		КОГДА &РазмерБлокаКодов = 0
		|			ТОГДА ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &РазмерБлокаКодов <= ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество - ЕСТЬNULL(КоличествоПолученныхКодов.КоличествоКодов, 0)
		|					ТОГДА &РазмерБлокаКодов
		|				ИНАЧЕ ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество - ЕСТЬNULL(КоличествоПолученныхКодов.КоличествоКодов, 0)
		|			КОНЕЦ
		|	КОНЕЦ КАК Количество,
		|	ЕСТЬNULL(КоличествоПолученныхКодов.КоличествоКодов, 0) КАК КоличествоПолученныхКодов,
		|	ЕСТЬNULL(СтатусыЗаказовИСМПТК.Идентификатор, """") КАК ИдентификаторПоследнегоБлока
		|ИЗ
		|	Документ.ЗаказКодовМаркировкиСУЗИСМПТК.Товары КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоПолученныхКодов КАК КоличествоПолученныхКодов
		|		ПО ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = КоличествоПолученныхКодов.ЗаказНаЭмиссию
		|			И ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN = КоличествоПолученныхКодов.GTIN
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаказовИСМПТК КАК СтатусыЗаказовИСМПТК
		|		ПО ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = СтатусыЗаказовИСМПТК.ЗаказНаЭмиссию
		|			И ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN = СтатусыЗаказовИСМПТК.GTIN
		|ГДЕ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка В(&МассивДокументов)
		|	И ВЫБОР
		|			КОГДА &РазмерБлокаКодов = 0
		|				ТОГДА ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество
		|			ИНАЧЕ ВЫБОР
		|					КОГДА &РазмерБлокаКодов <= ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество - ЕСТЬNULL(КоличествоПолученныхКодов.КоличествоКодов, 0)
		|						ТОГДА &РазмерБлокаКодов
		|					ИНАЧЕ ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество - ЕСТЬNULL(КоличествоПолученныхКодов.КоличествоКодов, 0)
		|				КОНЕЦ
		|		КОНЕЦ > 0
		|	%ОтборПоGTIN";
		
		ТекстКоличество = ?(РазмерБлокаКодов = 0, "ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество КАК Количество", "&РазмерБлокаКодов КАК Количество");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Количество", ТекстКоличество);
		Запрос.УстановитьПараметр("МассивДокументов", МассивСУЗ);
		Запрос.УстановитьПараметр("РазмерБлокаКодов", РазмерБлокаКодов);
		
		Если Не МассивКодовЗаказа = Неопределено Тогда 
			Запрос.УстановитьПараметр("МассивКодовЗаказа", МассивКодовЗаказа);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоGTIN", "И ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN В(&МассивКодовЗаказа)");
		Иначе 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоGTIN", "");
		КонецЕсли;
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда 
			
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ОтветСервера = ИнтеграцияИСМПТК.ПолучитьКМПоЗаказуНаЭмиссию(НастройкаОбменаСУЗ, Выборка.ВидПродукции, Выборка.OrderID, Выборка.GTIN, Выборка.Количество, Выборка.ИдентификаторПоследнегоБлока);
				
				Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
					
					СтруктураОтвета = ОтветСервера.ДанныеОКМ;
					
					blockId  = СтруктураОтвета["blockId"];
					МассивКМ = СтруктураОтвета["codes"];
					
					УстановитьСтатусПолученияКМПоЗаказу(Выборка.Ссылка, Выборка.GTIN, Перечисления.СтатусыПодзаказовКодовМаркировкиСУЗИСМПТК.Запрошен, blockId, Выборка.Организация);
					
					НаборЗаписейПулКодовМаркировкиСУЗ = РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.СоздатьНаборЗаписей();
					НаборЗаписейПулКодовМаркировкиСУЗ.Отбор.ЗаказНаЭмиссию.Установить(Выборка.Ссылка, Истина);
					
					Для Каждого КМ Из МассивКМ Цикл
						
						ДанныеРазбора = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.РазобратьКодМаркировки(КМ, НастройкиРазбораКодаМаркировки.ДоступныеВидыПродукции, , НастройкиРазбораКодаМаркировки);
						ЗаписьНабора  = НаборЗаписейПулКодовМаркировкиСУЗ.Добавить();
						
						КодМаркировкиСтрока = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.УбратьСкобкиУИдентификатораПрименения_Обувь(ДанныеРазбора.НормализованныйКодМаркировки);
						ПолныйКодМаркировки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.УбратьСкобкиУИдентификатораПрименения_Обувь(ДанныеРазбора.КодМаркировки);
						КодМаркировкиBase64  	  = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.СтрокуВBase64(КодМаркировкиСтрока);
						ПолныйКодМаркировкиBase64 = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.СтрокуВBase64(КМ);
													
						// Измерения
						ЗаписьНабора.Организация 	= Выборка.Организация;
						ЗаписьНабора.ЗаказНаЭмиссию = Выборка.Ссылка;
						ЗаписьНабора.ХешСуммаКодаИдентификации = ОбщегоНазначенияИСМПТК.ХешированиеДанныхSHA256(ДанныеРазбора.НормализованныйКодМаркировки);
						ЗаписьНабора.Шаблон			= Выборка.Шаблон;
						
						// Ресурсы
						ЗаписьНабора.GTIN = Выборка.GTIN;
						ЗаписьНабора.КодИдентификации = ДанныеРазбора.НормализованныйКодМаркировки; 
						ЗаписьНабора.КодМаркировки 	  = ПолныйКодМаркировкиBase64;
						
						Если Выборка.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная") Тогда
							ЗаписьНабора.Статус = Перечисления.СтатусыКодовМаркировкиСУЗИСМПТК.Сгенерирован;
						Иначе
							ЗаписьНабора.Статус = Перечисления.СтатусыКодовМаркировкиСУЗИСМПТК.СгенерированНеНанесен;
						КонецЕсли;
						
						ЗаписьНабора.ДатаЭмиссии = ТекущаяДатаСеанса();
						
						//Реквизиты
						ЗаписьНабора.EAN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(Выборка.GTIN);
						ЗаписьНабора.Номенклатура  		= Выборка.Номенклатура;
						ЗаписьНабора.Характеристика  	= Выборка.Характеристика;
						ЗаписьНабора.СпособВводаВОборот = Выборка.СпособВводаВОборот;
						ЗаписьНабора.ТипШтрихкода  	 	= ДанныеРазбора.ТипШтрихкода;
						ЗаписьНабора.ВидПродукции  		= ?(ДанныеРазбора.ВидыПродукции.Количество() > 0, ДанныеРазбора.ВидыПродукции[0], Неопределено);
												
					КонецЦикла;
					
					НаборЗаписейПулКодовМаркировкиСУЗ.Записать(Ложь);
					
					Если (Выборка.Количество + Выборка.КоличествоПолученныхКодов) = Выборка.КоличествоВЗаказе Тогда
						
						//По данному GTIN получили все коды, можно закрыть строку заказа
						ОтветСервераЗакрытие = ИнтеграцияИСМПТК.ЗакрытьПодзаказПоGTIN(НастройкаОбменаСУЗ, Выборка.ВидПродукции, Выборка.OrderID, Выборка.GTIN, blockId);
						
						Если ЗначениеЗаполнено(ОтветСервераЗакрытие.ТекстОшибки) Тогда
							//Прерываться если закрыть не удалось не будем коды всё равно получили
							//просто сообщим об ошибке
							ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервераЗакрытие.ТекстОшибки,,,,Отказ);
						КонецЕсли;
						
						СтатусПолученныхКодов = Перечисления.СтатусыПодзаказовКодовМаркировкиСУЗИСМПТК.Получен;
					Иначе
						СтатусПолученныхКодов = Перечисления.СтатусыПодзаказовКодовМаркировкиСУЗИСМПТК.ПолученЧастично;
					КонецЕсли;

					УстановитьСтатусПолученияКМПоЗаказу(Выборка.Ссылка, Выборка.GTIN, СтатусПолученныхКодов, blockId, Выборка.Организация);

				Иначе
					
					//Краткий текст ошибки выводим пользователю
					ТекстОшибкиКраткий = НСтр("ru = 'При отправке запроса возникла ошибка: '") 
									   + Символы.ПС + ОтветСервера.РезультатОтправкиЗапроса.ТекстОшибки
									   + Символы.ПС + РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияПодробностиВЖурналеРегистрации();
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибкиКраткий,,,,Отказ);
									
					//Формируем для пользователя более информативное сообщение 
					ТекстСообщенияОшибка = "";
					Если ОтветСервера.РезультатОтправкиЗапроса.КодСостояния = 404 Тогда
						//Все коды уже получены 
						ТекстСообщенияОшибка = НСтр("ru = 'В буфере обмена СУЗ не содержится кодов маркировки по данному Заказу на эмиссию. Обновите статус документа.'");
						
					ИначеЕсли ОтветСервера.РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
						//Коды еще не помещены в буфер 
						ТекстСообщенияОшибка = НСтр("ru = 'Коды по Заказу не найдены. Возможно, сервер СУЗ еще обрабатывает данные для помещения в буфер обмена: повторите получение позднее. Или коды уже были получены ранее: обновите статус Заказа на эмиссию.'");
					КонецЕсли;
					Если ЗначениеЗаполнено(ТекстСообщенияОшибка) Тогда
						ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщенияОшибка);
					КонецЕсли;
					
					//Полный текст ошибки формируем в журнале 
					ТекстОшибки = ТекстСообщенияОшибка + Символы.ПС + ОтветСервера.ТекстОшибки;
					ИмяСобытия  = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаВФункцииСПараметром();
					ИмяСобытия  = СтрЗаменить(ИмяСобытия, "%ИмяФункции%", "ИнтеграцияИСМПТКВызовСервера.ОтправитьЗапросКМПоЗаказуНаЭмиссию");
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
										
				КонецЕсли;
			КонецЦикла;
		Иначе
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(НСтр("ru = 'Нет кодов маркировки для получения.'"),,,,Отказ);
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ТекстСообщения = НСтр("ru = 'Коды маркировки успешно получены.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецФункции

Функция ОтправитьПовторныйЗапросКМПоЗаказуНаЭмиссию(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	НастройкиРазбораКодаМаркировки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйПовтИсп.НастройкиРазбораКодаМаркировки();    
	
	Отказ = Ложь;
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.Ссылка КАК Ссылка,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.Организация КАК Организация,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.ВидПродукции КАК ВидПродукции,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.OrderID КАК OrderID,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN КАК GTIN,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Шаблон КАК Шаблон,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество КАК Количество,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Номенклатура КАК Номенклатура,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Характеристика КАК Характеристика,
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.СпособВводаВОборот КАК СпособВводаВОборот,
		|	СтатусыЗаказовИСМПТ.Статус КАК Статус,
		|	СтатусыЗаказовИСМПТ.Идентификатор КАК Идентификатор
		|ИЗ
		|	Документ.ЗаказКодовМаркировкиСУЗИСМПТК.Товары КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаказовИСМПТК КАК СтатусыЗаказовИСМПТ
		|		ПО ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = СтатусыЗаказовИСМПТ.ЗаказНаЭмиссию
		|			И ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN = СтатусыЗаказовИСМПТ.GTIN
		|ГДЕ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка В(&МассивДокументов)
		|	И СтатусыЗаказовИСМПТ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПодзаказовКодовМаркировкиСУЗИСМПТК.Запрошен)";
		
		Запрос.УстановитьПараметр("МассивДокументов", МассивСУЗ);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда 
			
			Выборка = Результат.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				ОтветСервера = ИнтеграцияИСМПТК.ПовторноПолучитьКМПоЗаказуНаЭмиссию(НастройкаОбменаСУЗ, Выборка.ВидПродукции,
				Выборка.OrderID, Выборка.GTIN, Выборка.Идентификатор, Выборка.Количество);
				
				Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
					
					СтруктураОтвета = ОтветСервера.ДанныеОКМ;
					
					blockId  = СтруктураОтвета["blockId"];
					МассивКМ = СтруктураОтвета["codes"];
					
					НаборЗаписейПулКодовМаркировкиСУЗ = РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.СоздатьНаборЗаписей();
					НаборЗаписейПулКодовМаркировкиСУЗ.Отбор.ЗаказНаЭмиссию.Установить(Выборка.Ссылка, Истина);
					
					Для Каждого КМ Из МассивКМ Цикл
						
						ДанныеРазбора 	= РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.РазобратьКодМаркировки(КМ, НастройкиРазбораКодаМаркировки.ДоступныеВидыПродукции, , НастройкиРазбораКодаМаркировки);
						ЗаписьНабора 	= НаборЗаписейПулКодовМаркировкиСУЗ.Добавить();
						КодМаркировкиСтрока = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.УбратьСкобкиУИдентификатораПрименения_Обувь(ДанныеРазбора.НормализованныйКодМаркировки);
						ПолныйКодМаркировки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.УбратьСкобкиУИдентификатораПрименения_Обувь(ДанныеРазбора.КодМаркировки);
						КодМаркировкиBase64  	  = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.СтрокуВBase64(КодМаркировкиСтрока);
						ПолныйКодМаркировкиBase64 = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.СтрокуВBase64(КМ);
												
						// Измерения
						ЗаписьНабора.Организация    = Выборка.Организация;
						ЗаписьНабора.ЗаказНаЭмиссию = Выборка.Ссылка;
						ЗаписьНабора.ХешСуммаКодаИдентификации = ОбщегоНазначенияИСМПТК.ХешированиеДанныхSHA256(ДанныеРазбора.НормализованныйКодМаркировки);
						ЗаписьНабора.Шаблон			= Выборка.Шаблон;
						
						// Ресурсы
						ЗаписьНабора.GTIN = Выборка.GTIN;
						ЗаписьНабора.КодИдентификации  = ДанныеРазбора.НормализованныйКодМаркировки;
						ЗаписьНабора.КодМаркировки 	   = ПолныйКодМаркировкиBase64;
						
						Если Выборка.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная") Тогда
							ЗаписьНабора.Статус = Перечисления.СтатусыКодовМаркировкиСУЗИСМПТК.Сгенерирован;
						Иначе
							ЗаписьНабора.Статус = Перечисления.СтатусыКодовМаркировкиСУЗИСМПТК.СгенерированНеНанесен;
						КонецЕсли;
						
						ЗаписьНабора.ДатаЭмиссии = ТекущаяДатаСеанса();
						
						//Реквизиты
						ЗаписьНабора.EAN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(Выборка.GTIN);
						ЗаписьНабора.Номенклатура  	    = Выборка.Номенклатура;
						ЗаписьНабора.Характеристика     = Выборка.Характеристика;
						ЗаписьНабора.СпособВводаВОборот = Выборка.СпособВводаВОборот;
						ЗаписьНабора.ТипШтрихкода  	    = ДанныеРазбора.ТипШтрихкода;
						ЗаписьНабора.ВидПродукции  	    = ?(ДанныеРазбора.ВидыПродукции.Количество() > 0, ДанныеРазбора.ВидыПродукции[0], Неопределено);
						
					КонецЦикла;
					
					НаборЗаписейПулКодовМаркировкиСУЗ.Записать(Ложь);
					
					//По данному GTIN получили все коды, можно закрыть строку заказа
					ОтветСервераЗакрытие = ИнтеграцияИСМПТК.ЗакрытьПодзаказПоGTIN(НастройкаОбменаСУЗ, Выборка.ВидПродукции, Выборка.OrderID, Выборка.GTIN, blockId);
					
					Если ЗначениеЗаполнено(ОтветСервераЗакрытие.ТекстОшибки) Тогда
						//Прерываться если закрыть не удалось не будем коды всё равно получили
						//просто сообщим об ошибке
						ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервераЗакрытие.ТекстОшибки,,,,Отказ);
					КонецЕсли;
					
					УстановитьСтатусПолученияКМПоЗаказу(Выборка.Ссылка, Выборка.GTIN, Перечисления.СтатусыПодзаказовКодовМаркировкиСУЗИСМПТК.Получен, blockId, Выборка.Организация);
					
				Иначе
					
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервера.ТекстОшибки,,,,Отказ);
						
				КонецЕсли;
			
			 КонецЦикла;
			
		Иначе
			
			ТекстОшибки = НСтр("ru='По заказу не обнаружено ошибок получения кодов маркировки.'");
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ТекстСообщения = НСтр("ru='Коды маркировки успешно получены.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецФункции

Функция ОтправитьЗапросСтатусовАгрегацииКМ(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	АгрегацияКодовМаркировкиСУЗ.Ссылка КАК Ссылка,
		|	АгрегацияКодовМаркировкиСУЗ.ВидПродукции КАК ВидПродукции,
		|	АгрегацияКодовМаркировкиСУЗ.OrderID КАК OrderID,
		|	АгрегацияКодовМаркировкиСУЗ.Статус КАК Статус
		|ИЗ
		|	Документ.АгрегацияКодовМаркировкиСУЗИСМПТК КАК АгрегацияКодовМаркировкиСУЗ
		|ГДЕ
		|	АгрегацияКодовМаркировкиСУЗ.Ссылка В(&МассивДокументов)
		|	И НЕ АгрегацияКодовМаркировкиСУЗ.OrderID = &ПустаяСтрока";
		
		Запрос.УстановитьПараметр("МассивДокументов", МассивСУЗ);
		Запрос.УстановитьПараметр("ПустаяСтрока", "");
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда 
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ОтветСервера = ИнтеграцияИСМПТК.ПолучитьСтатусАгрегацииКМ(НастройкаОбменаСУЗ, Выборка.ВидПродукции, Выборка.OrderID);
				
				Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
					
					СтатусОтчета = ИнтеграцияИСМПТК.СтатусОбработкиОтчета(ОтветСервера.ДанныеОСтатусах["reportStatus"]);
					
					Если Не Выборка.Статус = СтатусОтчета Тогда
						
						ДокументОбъект 		  = Выборка.Ссылка.ПолучитьОбъект();
						ДокументОбъект.Статус = СтатусОтчета;
						
						Попытка
							
							ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
							
						Исключение
							
							ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаПриЗаписиСтатусНеИзменилсяСПараметрами();
							ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументОбъект, СтатусОтчета);
							ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
							
						КонецПопытки;
					КонецЕсли;
				Иначе
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервера.ТекстОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ОтправитьЗапросСтатусовНанесенияКМ(МассивСУЗ, СтруктурнаяЕдиница) Экспорт
	
	НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(СтруктурнаяЕдиница);
	
	Если Не НастройкаОбменаСУЗ = Неопределено Тогда
				
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	НанесениеКодовМаркировкиСУЗИСМПТК.Ссылка КАК Ссылка,
		|	НанесениеКодовМаркировкиСУЗИСМПТК.ВидПродукции КАК ВидПродукции,
		|	НанесениеКодовМаркировкиСУЗИСМПТК.OrderID КАК OrderID,
		|	НанесениеКодовМаркировкиСУЗИСМПТК.ДокументОснование КАК ДокументОснование,
		|	НанесениеКодовМаркировкиСУЗИСМПТК.Статус КАК Статус
		|ИЗ
		|	Документ.НанесениеКодовМаркировкиСУЗИСМПТК КАК НанесениеКодовМаркировкиСУЗИСМПТК
		|ГДЕ
		|	НанесениеКодовМаркировкиСУЗИСМПТК.Ссылка В(&МассивДокументов)
		|	И НЕ НанесениеКодовМаркировкиСУЗИСМПТК.OrderID = &ПустаяСтрока";
		
		Запрос.УстановитьПараметр("МассивДокументов", МассивСУЗ);
		Запрос.УстановитьПараметр("ПустаяСтрока", "");
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда 
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ОтветСервера = ИнтеграцияИСМПТК.ПолучитьСтатусНанесенияКМ(НастройкаОбменаСУЗ, Выборка.ВидПродукции, Выборка.OrderID);
				
				Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
					
					СтатусОтчета = ИнтеграцияИСМПТК.СтатусОбработкиОтчета(ОтветСервера.ДанныеОСтатусах["reportStatus"]);
					
					Если Не Выборка.Статус = СтатусОтчета Тогда
						
						ДокументОбъект 		  = Выборка.Ссылка.ПолучитьОбъект();
						ДокументОбъект.Статус = СтатусОтчета;
												
						Попытка
							
							ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
							
							//Если новый Статус - Отправлен, необходимо изменить статусы кодов в регистре
							Если СтатусОтчета = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиОтчетовСУЗИСМПТК.Отправлен") Тогда
								
								ТаблицаКМВНанесении   = ДокументОбъект.Товары.Выгрузить();
								ТаблицаКМДляСравнения = ТаблицаКМВНанесении.Скопировать(, "КодМаркировки");
								
								МассивЗаказов = Новый Массив();
								МассивЗаказов.Добавить(Выборка.ДокументОснование.Ссылка);
								СписокКодов = ПолучитьКодыМаркировкиНанесение(МассивЗаказов, ТаблицаКМДляСравнения);
								
								Если СписокКодов = Неопределено Тогда
									ТекстСообщения = НСтр("ru = 'Не удалось определить требующие смены статуса коды маркировки в Пуле кодов.'");
									ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
								КонецЕсли;
								
								Для Каждого СтрокаКод Из СписокКодов Цикл
									
									МенеджерЗаписиРС = РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.СоздатьМенеджерЗаписи();
									ЗаполнитьЗначенияСвойств(МенеджерЗаписиРС, СтрокаКод);
									
									МенеджерЗаписиРС.Статус = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиСУЗИСМПТК.Сгенерирован");
									МенеджерЗаписиРС.Записать(Истина);
									
								КонецЦикла;								
							КонецЕсли;
						Исключение
							ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаПриЗаписиСтатусНеИзменилсяСПараметрами();
							ТекстСообщения = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументОбъект, СтатусОтчета);
							ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
						КонецПопытки;
					КонецЕсли;
				Иначе
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервера.ТекстОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ОтправитьИсходящиеУведомления(Знач КоллекцияДанныеКоллекцииАктовXML, Знач КоллекцияПодписей, Знач ТокенАвторизации) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьИсходящиеУведомления(КоллекцияДанныеКоллекцииАктовXML, КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

Функция ОтправитьИсходящиеУведомленияВВО(Знач КоллекцияДанныеКоллекцииАктовXML, Знач КоллекцияПодписей, Знач ТокенАвторизации) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьИсходящиеУведомленияВВО(КоллекцияДанныеКоллекцииАктовXML, КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

Функция ОтправитьИсходящиеУведомленияВИО(Знач КоллекцияДанныеКоллекцииАктовXML, Знач КоллекцияПодписей, Знач ТокенАвторизации) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьИсходящиеУведомленияВИО(КоллекцияДанныеКоллекцииАктовXML, КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

Функция ОтправитьУведомленияОВвозе(Знач КоллекцияДанныеКоллекцииАктовXML, Знач КоллекцияПодписей, Знач ТокенАвторизации) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьУведомленияОВвозе(КоллекцияДанныеКоллекцииАктовXML, КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

Функция ОтправитьУведомленияОВвозеИзТретьихСтран(Знач КоллекцияДанныеКоллекцииАктовXML, Знач КоллекцияПодписей, Знач ТокенАвторизации) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьУведомленияОВвозеИзТретьихСтран(КоллекцияДанныеКоллекцииАктовXML, КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

Функция ОтправитьУведомленияОбЭкспортеЕАЭС(Знач КоллекцияДанныеКоллекцииАктовXML, Знач КоллекцияПодписей, Знач ТокенАвторизации) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьУведомленияОбЭкспортеЕАЭС(КоллекцияДанныеКоллекцииАктовXML, КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

Функция ОтправитьУведомлениеОПриемкеЕАЭС(Знач КоллекцияДанныеКоллекцииАктовXML, Знач КоллекцияПодписей, Знач ТокенАвторизации) Экспорт
	
	Результат = ИнтеграцияИСМПТК.ОтправитьОтправитьУведомлениеОПриемкеЕАЭС(КоллекцияДанныеКоллекцииАктовXML, КоллекцияПодписей, ТокенАвторизации);
	
КонецФункции

#КонецОбласти

#Область ОбновитьПоАПИ

Процедура ОбновитьДокументыИзИСМПТ(Знач Параметры, Знач ДанныеПрофилей, СписокТокеновАвторизации) Экспорт
	
	ИнтеграцияИСМПТК.ОбновитьДокументыИзИСМПТ(Параметры, ДанныеПрофилей, СписокТокеновАвторизации);
	
КонецПроцедуры

#КонецОбласти

#Область ПодтвердитьПоАПИ

Функция ПодтвердитьВходящиеДокументыИСМПТ_СозданиеТитулов(Знач КоллекцияСгруппированныхДокументовИСМПТ, Знач СписокТокеновАвторизации) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПодтвердитьВходящиеАкты_СозданиеТитулов(КоллекцияСгруппированныхДокументовИСМПТ, СписокТокеновАвторизации);
	
КонецФункции

Процедура ПодтвердитьВходящиеДокументыИСМПТ_ПодписьТитуловЗавершение(Знач КоллекцияСгруппированныхДокументовИСМПТ, Знач СписокТокеновАвторизации) Экспорт
	
	ИнтеграцияИСМПТК.ПодтвердитьВходящиеАкты_ПодписьТитуловЗавершение(КоллекцияСгруппированныхДокументовИСМПТ, СписокТокеновАвторизации);
	
КонецПроцедуры

#КонецОбласти

#Область ОтклонитьПоАПИ

Функция ОтклонитьВходящиеДокументыИСМПТ_СозданиеТитулов(Знач ТипДокумента, Знач КоллекцияСгруппированныхДокументовИСМПТ, Знач СписокТокеновАвторизации) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ОтклонитьВходящиеАкты_СозданиеТитулов(ТипДокумента, КоллекцияСгруппированныхДокументовИСМПТ, СписокТокеновАвторизации);
	
КонецФункции

Процедура ОтклонитьВходящиеДокументыИСМПТ_ПодписьТитуловЗавершение(Знач ТипДокументаИСМПТ, Знач НоваяКоллекцияСгруппированныхДокументовИСМПТ, Знач СписокТокеновАвторизации) Экспорт
	
	ИнтеграцияИСМПТК.ОтклонитьВходящиеАкты_ПодписьТитуловЗавершение(ТипДокументаИСМПТ, НоваяКоллекцияСгруппированныхДокументовИСМПТ, СписокТокеновАвторизации);
	
КонецПроцедуры

#КонецОбласти

#Область ОтозватьПоАПИ

Функция ОтозватьИсходящиеДокументыИСМПТ_СозданиеТитулов(Знач ТипДокумента, Знач КоллекцияСгруппированныхДокументовИСМПТ, Знач СписокТокеновАвторизации) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ОтозватьИсходящиеДокументы_СозданиеТитулов(ТипДокумента, КоллекцияСгруппированныхДокументовИСМПТ, СписокТокеновАвторизации);
	
КонецФункции

Процедура ОтозватьИсходящиеДокументыИСМПТ_ПодписьТитуловЗавершение(Знач НоваяКоллекцияСгруппированныхДокументовИСМПТ, Знач СписокТокеновАвторизации) Экспорт
	
	ИнтеграцияИСМПТК.ОтозватьИсходящиеДокументы_ПодписьТитуловЗавершение(НоваяКоллекцияСгруппированныхДокументовИСМПТ, СписокТокеновАвторизации);
		
КонецПроцедуры

#КонецОбласти

#Область ПолучитьПоАПИ

// Заполняет структуру СтруктураКода.
// СтруктураКода (Структура) - структура для заполненеия
// СтруктураКода.GTIN - GTIN вложенной номенклатуры 
// СтруктураКода.Количество - Количество вложенной номенклатуры
//
// ВложенныеКоды (Соответствие) - данные по групповому коду полученные с сервера ИС МПТ
// ВидДокумента - дополнительный параметр для определения особенностей запроса состава упаковок.
//
Функция ПолучитьИнформациюПоГрупповомуКоду(ДанныеПоГрупповомуКоду, КодАгрегата, ВидУпаковки, ФорматBase64 = Ложь) Экспорт
	
	СтруктураКода = Новый Структура("GTIN, GTINВерхнегоУровня, EAN, EANВерхнегоУровня, Номенклатура, Характеристика, ВидУпаковки, ВидПродукции, Количество", 
									 "", "", "", Неопределено, Неопределено, Неопределено, Неопределено, Неопределено, 0);
	
	Если ФорматBase64 Тогда
		КодАгрегата = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.Base64ВШтрихкод(КодАгрегата);
	КонецЕсли;
	
	ВложенныеКоды = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[КодАгрегата];
	Если ВложенныеКоды = Неопределено И ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Групповая Тогда
		Если СтрНачинаетсяС(КодАгрегата, "(") Тогда 
			ВложенныеКоды = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[СтрШаблон("%1%2%3",
			Сред(КодАгрегата, 2, 2), Сред(КодАгрегата, 5,14), Сред(КодАгрегата, 23))];
		Иначе 
			ВложенныеКоды = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[СтрШаблон("(%1)%2(%3)%4",
			Сред(КодАгрегата, 1, 2), Сред(КодАгрегата, 3,14), Сред(КодАгрегата, 17,2), Сред(КодАгрегата, 19))];
		КонецЕсли;
	КонецЕсли;
	
	Если Не ВложенныеКоды = Неопределено Тогда
		
		Если Не ВложенныеКоды.Количество() = 0 Тогда
			ДанныеПоАгрегированномуКоду(ВложенныеКоды, СтруктураКода, КодАгрегата);
		Иначе
			ТекстСообщения = НСтр("ru = 'Для кода %КодМаркировки% не удалось получить информацию о составе вложенностей со стороны сервера.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КодМаркировки%", КодАгрегата);  
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
			
			ДанныеРазбора = ПолучитьКодыМаркировки(Новый Структура("Штрихкод, ФорматBase64, Количество", КодАгрегата, Ложь, 1));
			Если Не ДанныеРазбора = Неопределено 
				И Не ТипЗнч(ДанныеРазбора)= Тип("Строка") Тогда
				ЗаполнитьЗначенияСвойств(СтруктураКода, ДанныеРазбора);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат СтруктураКода;
	
КонецФункции

Функция ПолучитьИнформациюПоСоставуГрупповогоКода(ДанныеПоГрупповомуКоду, КодИдентификации, ВидУпаковки) Экспорт
	
	СтруктураКода = Новый Структура("GTIN, Количество", "", 0);
	
	ВложенныеКоды = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[КодИдентификации];
	Если ВложенныеКоды = Неопределено И ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Групповая Тогда
		Если СтрНачинаетсяС(КодИдентификации, "(") Тогда 
			ВложенныеКоды = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[СтрШаблон("%1%2%3",
			Сред(КодИдентификации, 2, 2), Сред(КодИдентификации, 5,14), Сред(КодИдентификации, 23))];
		Иначе 
			ВложенныеКоды = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[СтрШаблон("(%1)%2(%3)%4",
			Сред(КодИдентификации, 1, 2), Сред(КодИдентификации, 3,14), Сред(КодИдентификации, 17,2), Сред(КодИдентификации, 19))];
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВложенныеКоды;
	
КонецФункции

Функция ЗапроситьДанныеПоКодуАгрегации(СтруктураЗапрос, Организация, ТокенАвторизации, ВыводитьСообщениеПриЗапросе = Истина) Экспорт
	
	ДанныеПоГрупповомуКоду = ИнтеграцияИСМПТК.ЗапроситьДанныеОбАгрегацииКМ(СтруктураЗапрос, Организация, ТокенАвторизации, ВыводитьСообщениеПриЗапросе);
	 
	Возврат Новый Структура("ТекстОшибки, ДанныеОбАгрегации", ДанныеПоГрупповомуКоду.ТекстОшибки, ДанныеПоГрупповомуКоду.ДанныеОбАгрегации);
	
КонецФункции

Функция ПолучитьДанныеПоГрупповымКодамМаркировки(МассивДокументов, СтруктурнаяЕдиница, ТокенАвторизации) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("ДанныеОбАгрегации",              Неопределено);
	ВозвращаемоеЗначение.Вставить("СодержимоеНедоступно",           Ложь);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаМарки.Ссылка КАК Ссылка,
	|	ТаблицаМарки.КодИдентификации КАК КодИдентификации,
	|	ТаблицаМарки.ВидУпаковки КАК ВидУпаковки,
	|	ТаблицаМарки.ВидПродукции КАК ВидПродукции
	|ПОМЕСТИТЬ КодыДокумента
	|ИЗ
	|	Документ.УведомлениеОВвозеИзТретьихСтранИСМПТК.Марки КАК ТаблицаМарки
	|ГДЕ
	|	ТаблицаМарки.Ссылка В (&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыДокумента.Ссылка КАК Ссылка,
	|	КодыДокумента.КодИдентификации КАК КодИдентификации,
	|	КодыДокумента.ВидУпаковки КАК ВидУпаковки,
	|	КодыДокумента.ВидПродукции КАК ВидПродукции
	|ИЗ
	|	КодыДокумента КАК КодыДокумента
	|ГДЕ
	|	КодыДокумента.ВидУпаковки В (ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИСМПТК.Групповая), ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИСМПТК.Логистическая))
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	МассивГрупповыхКМ = Новый Массив;
	СоответствиеГрупповыхКМ = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		МассивГрупповыхКМ.Добавить(Выборка.КодИдентификации);
		СоответствиеГрупповыхКМ.Вставить(Новый Структура("КИ, ВипУпаковки", Выборка.КодИдентификации, Выборка.ВидУпаковки), Новый Массив);
	КонецЦикла;
	
	СтруктураКодов = Новый Структура("МассивКодов", МассивГрупповыхКМ);
	ДанныеПоГрупповомуКоду = ИнтеграцияИСМПТК.ЗапроситьДанныеОбАгрегацииСпискаКМ(СтруктураКодов, СтруктурнаяЕдиница, ТокенАвторизации);
	
	Если ЗначениеЗаполнено(ДанныеПоГрупповомуКоду.ТекстОшибки) Тогда
		
		ТекстОшибки = НСтр("ru = 'Не удалось получить информацию по агрегированным кодам со стороны сервера. Подробная информация добавлена в Журнал регистрации.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
		ЗаписатьОшибкуВЖурналРегистрации(ДанныеПоГрупповомуКоду.ТекстОшибки, , );
		
		ВозвращаемоеЗначение.ТекстОшибки = ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
		
	Иначе
		
		СодержаниеГрупповыхКодов = Новый Соответствие;
		
		Для каждого ГрупповойКод Из СоответствиеГрупповыхКМ Цикл
			СоставКода = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[ГрупповойКод.Ключ.КИ];
			Если СоставКода = Неопределено И ГрупповойКод.Ключ.ВипУпаковки = Перечисления.ВидыУпаковокИСМПТК.Групповая Тогда
				Если СтрНачинаетсяС(ГрупповойКод.Ключ.КИ, "(") Тогда
					КодПоиска = СтрШаблон("%1%2%3", Сред(ГрупповойКод.Ключ.КИ, 2, 2), Сред(ГрупповойКод.Ключ.КИ, 5,14), Сред(ГрупповойКод.Ключ.КИ, 23));
				Иначе
					КодПоиска = СтрШаблон("(%1)%2(%3)%4", Сред(ГрупповойКод.Ключ.КИ, 1, 2), Сред(ГрупповойКод.Ключ.КИ, 3,14), Сред(ГрупповойКод.Ключ.КИ, 17,2), Сред(ГрупповойКод.Ключ.КИ, 19));
				КонецЕсли;
				СоставКода = ДанныеПоГрупповомуКоду.ДанныеОбАгрегации[КодПоиска];
			КонецЕсли;
			
			Если СоставКода <> Неопределено Тогда
				Для каждого ВложенныйКод Из СоставКода Цикл
					ГрупповойКод.Значение.Добавить(ВложенныйКод.Ключ);
				КонецЦикла;
				СодержаниеГрупповыхКодов.Вставить(ГрупповойКод.Ключ.КИ, ГрупповойКод.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ВозвращаемоеЗначение.ДанныеОбАгрегации = СодержаниеГрупповыхКодов;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьУровеньАгрегированногоКода(КодМаркировки, Организация, ТокенАвторизации) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ЗапроситьУровеньАгрегированногоКода(КодМаркировки, Организация, ТокенАвторизации);
	
КонецФункции

#КонецОбласти

Функция АдресБиблиотекиКриптографии(Идентифкатор) Экспорт

	Возврат "";
	
КонецФункции

Функция ПолучитьСтатусGTINПоЗаказуНаЭмиссию(ЗаказКодов, GTIN) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Организация КАК СтруктурнаяЕдиница,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.ВидПродукции КАК ВидПродукции,
	|	ЗаказКодовМаркировкиСУЗИСМПТК.OrderID КАК OrderID
	|ИЗ
	|	Документ.ЗаказКодовМаркировкиСУЗИСМПТК КАК ЗаказКодовМаркировкиСУЗИСМПТК
	|ГДЕ
	|	ЗаказКодовМаркировкиСУЗИСМПТК.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ЗаказКодов);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		НастройкаОбменаСУЗ = ИнтеграцияИСМПТК.НастройкиОбменаСУЗ(Выборка.СтруктурнаяЕдиница);
		НастройкиРазбораКодаМаркировки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйПовтИсп.НастройкиРазбораКодаМаркировки();   
		
		Отказ = Ложь;
		Если Не НастройкаОбменаСУЗ = Неопределено Тогда
			
			НеПолучилосьОбновить = Ложь;
			ТекстОшибки = "";
						
			Если ТипЗнч(НастройкаОбменаСУЗ) = Тип("Массив") Тогда
				Для Каждого НастройкаСУЗПоОрганизации Из НастройкаОбменаСУЗ Цикл
					
					ОтветСервера = ИнтеграцияИСМПТК.ПолучитьСтатусGTINПоЗаказуНаЭмиссию(НастройкаСУЗПоОрганизации, Выборка.ВидПродукции, Выборка.OrderID, GTIN);
					Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
						СтруктураОтвета = ОтветСервера.ДанныеОКМ;
						blocks 	= СтруктураОтвета["blocks"];
						Если blocks = Неопределено Тогда 
							Возврат Неопределено;
						Иначе
							Возврат СтруктураОтвета["blocks"][0];
						КонецЕсли;
					Иначе
						НеПолучилосьОбновить = Истина;
						ТекстОшибки = ОтветСервера.ТекстОшибки;
					КонецЕсли;
				КонецЦикла;
				
				Если НеПолучилосьОбновить Тогда
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки,,,,Отказ);
				КонецЕсли;				
			Иначе
				ОтветСервера = ИнтеграцияИСМПТК.ПолучитьСтатусGTINПоЗаказуНаЭмиссию(НастройкаОбменаСУЗ, Выборка.ВидПродукции, Выборка.OrderID, GTIN);
				Если Не ЗначениеЗаполнено(ОтветСервера.ТекстОшибки) Тогда
					СтруктураОтвета = ОтветСервера.ДанныеОКМ;
					blocks 	= СтруктураОтвета["blocks"];
					Если blocks = Неопределено Тогда 
						Возврат Неопределено;
					Иначе
						Возврат СтруктураОтвета["blocks"][0];
					КонецЕсли;
				Иначе
					ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ОтветСервера.ТекстОшибки,,,,Отказ);
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура УстановитьСтатусПолученияКМПоЗаказу(Заказ, GTIN, Статус, ИдентификаторБлока, Организация)
	
	МенеджерЗаписи = РегистрыСведений.СтатусыЗаказовИСМПТК.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.ЗаказНаЭмиссию = Заказ;
	МенеджерЗаписи.GTIN			  = GTIN;
	МенеджерЗаписи.Статус		  = Статус;
	МенеджерЗаписи.Идентификатор  = ИдентификаторБлока;
	МенеджерЗаписи.Организация    = Организация;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция СоответствиеСтатусовКодовМаркировкиВТЗ(СтруктураСтатусов) Экспорт
	
	МассивСтатусов = СтруктураСтатусов.Получить("orderInfos");
	Если МассивСтатусов = Неопределено Тогда 
		//Если по какой-то причине сервер не возвращает информацию по идентификатору документа
		ТекстСообщения = НСтр("ru = 'Не удалось получить информацию со стороны сервера! Запрашиваемый документ отсутствует на сервере или удаленная система недоступна!'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	ТЗ = Новый ТаблицаЗначений();
	ТЗ.Колонки.Добавить("orderId", 	   Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТЗ.Колонки.Добавить("orderStatus", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	
	Для Каждого СоответствиеСтатуса Из МассивСтатусов Цикл 
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.orderId 	= СоответствиеСтатуса["orderId"];
		НоваяСтрока.orderStatus = СоответствиеСтатуса["orderStatus"];
	КонецЦикла;
	
	Возврат ТЗ;
	
КонецФункции

Процедура ДополнитьГрупповыеУпаковки(МассивДокументов, СтруктурнаяЕдиница, ТокенАвторизации) Экспорт
	
	НачатьТранзакцию();
	Попытка
		Для Каждого ДокументСсылка Из МассивДокументов Цикл
			
			ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			Для Каждого СтрокаТовары Из ДокументОбъект.Марки Цикл
				Если СтрокаТовары.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Логистическая 
					ИЛИ СтрокаТовары.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Групповая Тогда
					//получить информацию по коду
					СтруктураЗапрос = Новый Структура("Штрихкод, ВидУпаковки, ВидПродукции, ФорматBase64", СтрокаТовары.КодИдентификации,
													   СтрокаТовары.ВидУпаковки, СтрокаТовары.ВидПродукции, СтрокаТовары.ФорматBase64);
					
					ДанныеПоГрупповомуКоду = ИнтеграцияИСМПТК.ЗапроситьДанныеОбАгрегацииКМ(СтруктураЗапрос, СтруктурнаяЕдиница, ТокенАвторизации);
					КоличествоКодов = 0;
					Если ЗначениеЗаполнено(ДанныеПоГрупповомуКоду.ТекстОшибки) Тогда
						
						ТексСообщения = НСтр("ru = 'При дополнении информации по групповым кодам маркировки данными со стороны сервера произошла ошибка.'") + " "
									  + РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияПодробностиВЖурналеРегистрации();
						ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТексСообщения);
						
						ТекстОшибки = НСтр("ru = 'При дополнении информации по групповым кодам маркировки данными со стороны сервера произошла ошибка:'")
									+ Символы.ПС + ДанныеПоГрупповомуКоду.ТекстОшибки;
						ИмяСобытия  = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаВФункцииСПараметром();
						ИмяСобытия  = СтрЗаменить(ИмяСобытия, "%ИмяФункции%", "ИнтеграцияИСМПТКВызовСервера.ДополнитьГрупповыеУпаковки");
						ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
						ВызватьИсключение ТексСообщения;
					Иначе
						КодДляРазбора = Неопределено;
						Для каждого ДанныеПоКоду Из ДанныеПоГрупповомуКоду.ДанныеОбАгрегации Цикл
							КоличествоКодов = ДанныеПоКоду.Значение.Количество();
							Для каждого ВложенныйКод Из ДанныеПоКоду.Значение Цикл
								КодДляРазбора = ВложенныйКод.Ключ;
								Прервать;
							КонецЦикла;
							Если ЗначениеЗаполнено(КодДляРазбора) Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						//разбор кода
						НастройкиРазбораКодаМаркировки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйПовтИсп.НастройкиРазбораКодаМаркировки();   
						ДанныеРазбора = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.РазобратьКодМаркировки(КодДляРазбора, НастройкиРазбораКодаМаркировки.ДоступныеВидыПродукции, , НастройкиРазбораКодаМаркировки);
						Если ДанныеРазбора <> Неопределено Тогда
							СтрокаТовары.GTIN = ДанныеРазбора.СоставКодаМаркировки.GTIN;
							Если КоличествоКодов <> СтрокаТовары.Количество Тогда
								СтрокаТовары.Количество = КоличествоКодов;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ДокументОбъект.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТексСообщения = НСтр("ru = 'При поопытке дополнить информацию по групповым кодам маркировки данными со стороны сервера произошла ошибка.'") + " "
					  + РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияПодробностиВЖурналеРегистрации();
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТексСообщения);
		
		ТекстОшибки = НСтр("ru = 'При поопытке дополнить информацию по групповым кодам маркировки данными со стороны сервера произошла ошибка:'")
				    + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ИмяСобытия  = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаВФункцииСПараметром();
		ИмяСобытия  = СтрЗаменить(ИмяСобытия, "%ИмяФункции%", "ИнтеграцияИСМПТКВызовСервера.ДополнитьГрупповыеУпаковки");
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
	
	
КонецПроцедуры

// Заполняет структуру СтруктураКода.
// СтруктураКода (Структура) - структура для заполненеия
// СтруктураКода.GTIN - GTIN вложенной номенклатуры 
// СтруктураКода.Количество - Количество вложенной номенклатуры
//
// ВложенныеКоды (Соответствие) - данные по групповому коду полученные с сервера ИС МПТ
// //=======================
	//Возможные структуры упаковок по АПИ:
	//1) Транспортный SSCC (Групповые КМ (Потребительские КМ)) 
			//- только ТГ Табак - ВложенныеКоды при первом обходе это список групповых КМ
	//2) Транспортный SSCC (Потребительские КМ) 
			//- для любой ТГ - ВложенныеКоды при первом обходе это список потребительских КМ 
	//3) Групповой КМ (Потребительские КМ) 
			//- только ТГ Табак - ВложенныеКоды при первом обходе это список потребительских КМ
	//4) ТранспортныйАльтернативный Code128 (Групповые КМ (Потребительские КМ)) 
			//- только ТГ Табак - ВложенныеКоды при первом обходе это список групповых КМ
	//5) ТранспортныйАльтернативный Code128 (Потребительские КМ) 
			//- только ТГ Табак - ВложенныеКоды при первом обходе это список потребительских КМ
	//6) Транспортный SSCC (ТранспортныеАльтернативные Code128 (Групповые КМ (Потребительские КМ)))
			//- только ТГ Табак - ВложенныеКоды при первом обходе это список транспортных Code128 И групповых КМ
	//7) Транспортный SSCC (ТранспортныеАльтернативные Code128 (Потребительские КМ)) 
			//-  только ТГ Табак - ВложенныеКоды при первом обходе это список транспортных Code128 И потребительских КМ
	//=======================
Процедура ДанныеПоАгрегированномуКоду(ВложенныеКоды, СтруктураКода, КИВерхнегоУровня = Неопределено) Экспорт
	
	НоменклатураЗаполнена = Ложь;
	GTIN = Неопределено;
	GTINВерхнегоУровня = Неопределено;
	EAN  = Неопределено;
	EANВерхнегоУровня = Неопределено;
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	ВидПродукции   = Неопределено;

	ДанныеРазбора = ПолучитьКодыМаркировки(Новый Структура("Штрихкод, ФорматBase64, Количество", КИВерхнегоУровня, Ложь, 1));
	
	Если Не ДанныеРазбора = Неопределено 
		И Не ТипЗнч(ДанныеРазбора)= Тип("Строка") Тогда
		
		//Заполнеям данные в СтруктураКода по исходному коду верхнего уровня только если он групповой, т.е. имеет свой товарный GTIN,
		//во всех остальных случаях смотрим на его вложенности (ответ сервера на запрос), даже если это код GS128 и в нем тоже есть GTIN. 
		
		Если ДанныеРазбора.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда 
			
			СтруктураДанныхКМ = ЗаполнитьОсновнуюСтруктуруДанныхКМ(ДанныеРазбора,, ВложенныеКоды);
			Если ЗначениеЗаполнено(СтруктураДанныхКМ.Номенклатура) Тогда 
				
				Номенклатура   = СтруктураДанныхКМ.Номенклатура;
				Характеристика = СтруктураДанныхКМ.Характеристика;
				ВидПродукции   = СтруктураДанныхКМ.ВидПродукции;
				
				//EAN заполняется по самому верхнему уровню только если определилась номенклатура. В остальных случаях EAN - из кода вложенности.
				EANВерхнегоУровня = СтруктураДанныхКМ.EANВерхнегоУровня;
			 	НоменклатураЗаполнена = Истина;
				
			КонецЕсли;
			
			//GTINВерхнегоУровня заполняем в итоговой СтруктураКода только если исходный код групповой, в остальных случаях оставляем пустым.
			GTINВерхнегоУровня = СтруктураДанныхКМ.GTINВерхнегоУровня; //Заполняется всегда, т.к. он у кода упаковки верхнего уровня есть
			
		КонецЕсли;
		//Всегда берем по верхнему уровню, т.е. по КИВерхнегоУровня, чтобы корректно отразить иконку в дереве КМ в таблице.
		СтруктураКода.ВидУпаковки = ДанныеРазбора.ВидУпаковки;
	Иначе
		//Если не удалось разобрать указаный пользователем код, то по умолчанию - неопознанная коробка
		СтруктураКода.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Неопределен");
	КонецЕсли;
	
	//РЕАЛИЗОВАНО ВРЕМЕННОЕ РЕШЕНИЕ, возможен пересмотр, если будет изменено АПИ:
	//У упаковок с 3 уровнями вложенности (варианты 6-7 в схеме в описании этой процедуры) отличается структура ответа сервера при запросе состава.
	//Кратко: все вложенности, независимо от их уровня и подчиненности, у таких упаковок
	//возвращаются одним общим списком в подчинении самому верхнему коду упаковки.
	//То, что возвращается с сервера, разобрать универсальным методом невозможно, т.к. непонятна структура подчинения и нельзя обходить дерево, 
	//поэтому приходится использовать дополнительные проверки и обходные пути для разбора именно такой структуры.
	//Если АПИ будет пересмотрено в будущем, разбор упаковки можно будет упростить.
	ВидУпаковкиОтличаетсяОтПредыдущего = Ложь;
	ВидУпаковкиДляСравнения = Неопределено;
	
	//Сначала перебираем все КМ в ответе сервера и анализируем их вид, т.к. на текущий момент в ответе 
	//по разным видам упаковок табака может быть разная структура подчиненности и ее нужно разбирать разными способами. (См. выше описание временной схемы)
	СоответствиеДанныхКМ = Новый Соответствие();
	Для Каждого ВложенныйКод Из ВложенныеКоды Цикл
		
		Если ТипЗнч(ВложенныйКод) = Тип("Строка") Тогда
			ДанныеДляЗапроса = ВложенныйКод;
		Иначе
			ДанныеДляЗапроса = ВложенныйКод.Ключ;
		КонецЕсли;
		ДанныеКода = ПолучитьКодыМаркировки(Новый Структура("Штрихкод, ФорматBase64, Количество", ДанныеДляЗапроса, Ложь, 1));
		
		Если ДанныеКода = Неопределено Или ТипЗнч(ДанныеКода) = Тип("Строка") Тогда
			
			//Если не удалось разобрать полученный код упаковки.			
			ТекстОшибки = НСтр("ru = 'Не удалось разобрать формат кода маркировки %1 при получении данных состава агрегации.'");
			ТекстОшибки = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстОшибки, ДанныеДляЗапроса);
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
			ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки);
			Продолжить;
			
		Иначе
			
			//Сразу для каждого кода пытаемся определить сведения о номенклутуре и подсчитываем количество вложенных в него потребительских КМ.
			СтруктураДанныхКМ = ЗаполнитьОсновнуюСтруктуруДанныхКМ(ДанныеКода, ВложенныйКод, ВложенныеКоды);
			СоответствиеДанныхКМ.Вставить(ДанныеДляЗапроса, СтруктураДанныхКМ); 
			
			//Заполняем признаки для временной схемы: определяем, одного ли формата коды вложенности в ответе сервера по составу упаковки.
			Если Не ВидУпаковкиДляСравнения = Неопределено
				И НЕ ВидУпаковкиДляСравнения = ДанныеКода.ВидУпаковки Тогда
				ВидУпаковкиОтличаетсяОтПредыдущего = Истина;
			КонецЕсли;
			ВидУпаковкиДляСравнения = ДанныеКода.ВидУпаковки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщееКоличествоПотребительскихПоУпаковке = 0;
					
	//Ветка ВРЕМЕННОЕ РЕШЕНИЕ начало
	Если ВидУпаковкиОтличаетсяОтПредыдущего Тогда
		
		//В структуре ответа сервера в этом случае - все вложенности в одном списке без структуры, 
		//поэтому нельзя сказать какие КМ из списка нужно посчитать.
		//Мы знаем наверняка, что: 
		//1) на одном уровне должны находиться упаковки одного формата: только транспортные, только групповые или только потребительские, 
		//2) в ответе сервера по упаковке макисмальной вложенности могут быть одновременно или коды всех трех видов, или только двух (групповые и потр.),
		//или только одного (потр.), т.е. смешение разных типов вложенных кодов недопускается.
		//3) внутри групповых КМ могут лежать только потребительские коды, внутри транспортных SSCC - или потребительские, или групповые, 
		//или транспортные GS128. Внутри транспортных GS128 - только групповые или потребительские.
		//Во всех случаях нам нужно получить итоговое кол-во вложенностей в потребительских упаковках.
		
		//Единственный вариант, когда  сервер возвращает проблемную структуру ответа - 
		//В данном случае ответ сервера не позволяет нам последовательно спуститься по уровням каждой вложенности до потребительских КМ,
		//а восстановление структуры дерева вложенностей слишком затруднительно.
		//Принимаем решение: такая упаковка будет игнорировать приоритет групповой упаковки пред потребительской в вопросе определения номенклатуры,
		//т.е. итоговые данные для упаковки максимальной вложенности всегда будем выводить по потребительским кодам в ней.
		//При этом на текущем шаге разбора мы уже знаем для каждого кода из ответа сервера его количество в потребит.кодах. 
		//Теперь нужно определить только какого вида самая "большая" упаковка внутри исходного кода
		//и выбрать сумму только по строкам с этим видом упаковки, чтобы не посчитать дважды ее саму в целом и ее вложенности по отдельности.
		
		//Для этого используем простой перебор всего ранее построенного соответствия и считаем суммы по каждому виду упаковок. 
		//Исходя из вышеописанных аксиом методологии агрегаций, определяем приоритет выбора итоговой суммы таким:
		//ОбщаяСуммаТранспортныхАльтернативных > ОбщаяСуммаГрупповых  > ОбщаяСуммаПотребительских.
		
		ОбщаяСуммаПотребительских = 0;
		ОбщаяСуммаГрупповых = 0;
		ОбщаяСуммаТранспортныхАльтернативных = 0; //Code128
		
		Для Каждого РазобранныеДанные Из СоответствиеДанныхКМ Цикл
			
			//РазобранныеДанные - это сведения разбора каждой строки ответа сервера (вложенности). 
			//Значение содержит сведения КМ и номенклатуры для исходного кода вложенности (ключа) из ответа сервера.
			//Повторно разбирать данные и выполнять поиск товарной позиции не нужно.
						
			ВидУпаковкиКода = РазобранныеДанные.Значение.ВидУпаковки;
			КоличествоКода  = РазобранныеДанные.Значение.КоличествоПотр;
			
			Если ВидУпаковкиКода = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда
				
				//Соответствие разобранных данных может содержать потребительские коды только в двух случаях:
				//1) если это первый обход процедуры и исходная структура запрошенной упаковки - простая одноуровневая (любая упаковка(потребительские коды)),
				//2) если это повторный обход процедуры (для определения данных вложенности) - тогда нельзя сказать наверняка про всю структуру упаковки.
				//В любом случае, если дошли до потребительских - определяем данные уже по ним, независимо от их регистрации в базе.
				
				ОбщаяСуммаПотребительских = ОбщаяСуммаПотребительских + КоличествоКода;
				
				Если Не НоменклатураЗаполнена Тогда
					GTIN = РазобранныеДанные.Значение.GTIN;
					EAN  = РазобранныеДанные.Значение.EAN; 
					Номенклатура   = РазобранныеДанные.Значение.Номенклатура;
					Характеристика = РазобранныеДанные.Значение.Характеристика;
					ВидПродукции   = РазобранныеДанные.Значение.ВидПродукции;
					НоменклатураЗаполнена = Истина;
				КонецЕсли;
				
			ИначеЕсли ВидУпаковкиКода = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда
				
				//Групповая упаковка не может находиться внутри другой групповой, значит на этом этапе номенклатура еще может быть неопределена
				//(т.к. при первом обходе сначала пытались определить по исходному коду из документа, а в этом случае этот код - транспортный).
				//Поэтому если групповая упаковка опознана - берем данные товара из нее.
				Если ЗначениеЗаполнено(РазобранныеДанные.Значение.Номенклатура)
					И Не НоменклатураЗаполнена Тогда
					
					GTIN = РазобранныеДанные.Значение.GTIN;
					GTINВерхнегоУровня = РазобранныеДанные.Значение.GTINВерхнегоУровня;
					EAN  = РазобранныеДанные.Значение.EAN; 
					EANВерхнегоУровня = РазобранныеДанные.Значение.EANВерхнегоУровня; 
					Номенклатура   = РазобранныеДанные.Значение.Номенклатура;
					Характеристика = РазобранныеДанные.Значение.Характеристика;
					ВидПродукции   = РазобранныеДанные.Значение.ВидПродукции;	
					НоменклатураЗаполнена = Истина;
					
				КонецЕсли;
				
				ОбщаяСуммаГрупповых = ОбщаяСуммаГрупповых + КоличествоКода;
				
			Иначе
				
				//Если вложенность - транспортный код, определить по нему товар нельзя, считаем только количество.
				ОбщаяСуммаТранспортныхАльтернативных = ОбщаяСуммаТранспортныхАльтернативных + КоличествоКода;
				
			КонецЕсли;
			
		КонецЦикла;
		
		///Чтобы не посчитать ничего дважды, общее количество начинаем определять от большего уровня к меньшему в порядке приоритета вложенности, 
		//т.к. в самом верхнем уровне уже рассчитано полное вложенное кол-во.
		Если Не ОбщаяСуммаТранспортныхАльтернативных = 0 Тогда
			ОбщееКоличествоПотребительскихПоУпаковке = ОбщаяСуммаТранспортныхАльтернативных;
		ИначеЕсли Не ОбщаяСуммаГрупповых = 0 Тогда
			ОбщееКоличествоПотребительскихПоУпаковке = ОбщаяСуммаГрупповых;
		Иначе
			ОбщееКоличествоПотребительскихПоУпаковке = ОбщаяСуммаПотребительских;
		КонецЕсли;
		
	//Ветка ВРЕМЕННОЕ РЕШЕНИЕ завершение
	Иначе
		
		//В структуре ответа сервера соблюдена последовательность уровней, поэтому определяем данные в рамках одного этажа дерева - первого уровня ответа сервера.
		//Если не удается определить товар по этому уровню - переходим на уровень ниже.
		Для Каждого РазобранныеДанные Из СоответствиеДанныхКМ Цикл
			
			//Для этого перебираем все строки исходного ответа и суммируем количество по ним.
			//Данные номенклатуры можно определять из любого составляющего ветки дерева, т.к. они равнозначны.
			
			ОбщееКоличествоПотребительскихПоУпаковке = ОбщееКоличествоПотребительскихПоУпаковке + РазобранныеДанные.Значение.КоличествоПотр;
			
			Если Не НоменклатураЗаполнена 
				Или (СтруктураКода.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") 
					И НоменклатураЗаполнена) Тогда
				
				//Значит верхний уровень (исходный код из документа) - или транспортная упаковка любого формата, или групповая не зарегистрированная.
				//Поэтому сначала пытаемся получить сведения о товаре по первому уровню ее вложенности.
				
				Если ЗначениеЗаполнено(РазобранныеДанные.Значение.Номенклатура) Тогда
					
					//Т.к. в транспортной могут быть вложенные групповые, проверяем, какой GTIN брать
					Если РазобранныеДанные.Значение.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда
						GTINВерхнегоУровня = РазобранныеДанные.Значение.GTINВерхнегоУровня;
						EANВерхнегоУровня  = РазобранныеДанные.Значение.EANВерхнегоУровня; 
						
						Вложенность = ВложенныеКоды[РазобранныеДанные.Ключ][0];
						GTIN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.GTINИзКодаМаркировки(Вложенность, ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая"));
						EAN  = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(GTIN); 
					Иначе
						GTIN = РазобранныеДанные.Значение.GTIN;
						EAN  = РазобранныеДанные.Значение.EAN; 
					КонецЕсли;
					Номенклатура   = РазобранныеДанные.Значение.Номенклатура;
					Характеристика = РазобранныеДанные.Значение.Характеристика;
					ВидПродукции   = РазобранныеДанные.Значение.ВидПродукции;
					НоменклатураЗаполнена = Истина; // чтобы не проходить тут в следующих кругах цикла.
					
				Иначе
					
					//Если не удалось определить товар по первому уровню вложенности - спускаемся на уровень ниже.
					//Исходя из всех возможных шаблонов упаковок (см. описание процедуры) на уровне ниже на текущем этапе
					//в любом случае уже будут находиться потребительские коды, независимо от шаблона структуры упаковки.
					//Исключения, не являющиеся нормой: групповые ПУСТЫЕ упаковки. Такие агрегации технически возможны в ИС МПТ, 
					//но с точки зрения правил маркировки - некорректны. ОБрабатываем такую ситуацию отдельной проверкой.
					
					//ВРЕМЕННАЯ СХЕМА
					//В случае, если АПИ будет пересмотрено и примут изменения по 3-х уровневой упаковке табака, 
					//т.е. сведения о такой упаковке будут возвращаться не общим бесформенным списком, а с сохранением структуры вложенности, 
					//нужно будет пересмотреть обработку ответа на этом шаге.
					
					Если (РазобранныеДанные.Значение.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") 
						ИЛИ РазобранныеДанные.Значение.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Логистическая"))
						И ВложенныеКоды[РазобранныеДанные.Ключ].Количество() = 0 Тогда
						
						ТекстОшибки = НСтр("ru = 'При обработке состава упаковки обнаружена ошибка: групповая упаковка %1 не имеет вложенностей по данным сервера ИС МПТ.'");
						ТекстОшибки = ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстОшибки, РазобранныеДанные.Ключ);
						ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
						ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки);
						Продолжить;
					ИначеЕсли РазобранныеДанные.Значение.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда
						Номенклатура   = РазобранныеДанные.Значение.Номенклатура;
						Характеристика = РазобранныеДанные.Значение.Характеристика;
						EAN  = РазобранныеДанные.Значение.EAN;
						GTIN = РазобранныеДанные.Значение.GTIN;
						ВидПродукции = РазобранныеДанные.Значение.ВидПродукции;
						НоменклатураЗаполнена = Истина;
						Продолжить;
					КонецЕсли;
				
					ВложенныйКодСледующегоУровня = ВложенныеКоды[РазобранныеДанные.Ключ][0]; //подойдет первый попавшийся код из упаковки.
					ДанныеРазбораНижнийУровень   = ПолучитьКодыМаркировки(Новый Структура("Штрихкод, ФорматBase64, Количество", ВложенныйКодСледующегоУровня, Ложь, 1));
					
					Если Не ДанныеРазбораНижнийУровень = Неопределено Тогда
						//Код удалось разобрать, пытаемся найти по нему товар.
						СтруктураДанныхКМ = ЗаполнитьОсновнуюСтруктуруДанныхКМ(ДанныеРазбораНижнийУровень,, ВложенныеКоды[РазобранныеДанные.Ключ]);
						Если ЗначениеЗаполнено(СтруктураДанныхКМ.Номенклатура) Тогда 
							Номенклатура   = СтруктураДанныхКМ.Номенклатура;
							Характеристика = СтруктураДанныхКМ.Характеристика;
							EAN  = СтруктураДанныхКМ.EAN;
							GTIN = СтруктураДанныхКМ.GTIN;
							ВидПродукции = СтруктураДанныхКМ.ВидПродукции;
							НоменклатураЗаполнена = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураКода.GTIN = GTIN;
	СтруктураКода.GTINВерхнегоУровня = GTINВерхнегоУровня;
	СтруктураКода.EAN  = EAN;
	СтруктураКода.EANВерхнегоУровня = EANВерхнегоУровня; 
	СтруктураКода.Номенклатура   = Номенклатура;
	СтруктураКода.Характеристика = Характеристика;
	СтруктураКода.ВидПродукции   = ВидПродукции;
		
	СтруктураКода.Количество = ОбщееКоличествоПотребительскихПоУпаковке;
		
КонецПроцедуры

Функция ЗаполнитьОсновнуюСтруктуруДанныхКМ(ДанныеКода, ВложенныйКод = Неопределено, ВложенныеКоды)
	
	GTIN = ДанныеКода.GTIN;
	GTINВерхнегоУровня = ДанныеКода.GTINВерхнегоУровня;
	
	EAN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(GTIN);
	EANВерхнегоУровня = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(GTINВерхнегоУровня);
	
	Если ЗначениеЗаполнено(EANВерхнегоУровня)
		И ДанныеКода.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда 
		//В приоритете пытаемся определить номенклатуру по верхнему уровню упаковки (для групповых упаковок)
		ДанныеНоменклатуры = ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьДанныеНоменклатурыИзРегистраШтрихкодовПоШтрихкоду(, EANВерхнегоУровня);
		Если ДанныеНоменклатуры = Неопределено
			Или (Не ДанныеНоменклатуры = Неопределено
				И ДанныеНоменклатуры.Номенклатура.Пустая()) Тогда
			ДанныеНоменклатуры = ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьДанныеНоменклатурыИзРегистраШтрихкодовПоШтрихкоду(, EAN);
		КонецЕсли;
	Иначе
		ДанныеНоменклатуры = ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьДанныеНоменклатурыИзРегистраШтрихкодовПоШтрихкоду(, EAN);
	КонецЕсли;
	Номенклатура   = ?(Не ДанныеНоменклатуры = Неопределено, ДанныеНоменклатуры.Номенклатура, 	Неопределено);
	Характеристика = ?(Не ДанныеНоменклатуры = Неопределено, ДанныеНоменклатуры.Характеристика, Неопределено);
	
	Если Не ВложенныйКод = Неопределено Тогда
		Если ДанныеКода.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда
			Количество = 1;
		Иначе
			Количество = ПосчитатьКоличествоВложенныхПотребительских(ВложенныйКод.Значение, ВложенныеКоды);
		КонецЕсли;
	КонецЕсли;
	
	СтруктураДанныхКМ = Новый Структура();
	СтруктураДанныхКМ.Вставить("КодИдентификации", 	 ДанныеКода.КодИдентификации);
	СтруктураДанныхКМ.Вставить("КодМаркировки", 	 РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодВBase64(ДанныеКода.КодМаркировки));
	СтруктураДанныхКМ.Вставить("GTIN", 				 GTIN);
	СтруктураДанныхКМ.Вставить("GTINВерхнегоУровня", GTINВерхнегоУровня);
	СтруктураДанныхКМ.Вставить("EAN", 				 EAN);
	СтруктураДанныхКМ.Вставить("EANВерхнегоУровня",  EANВерхнегоУровня);
	СтруктураДанныхКМ.Вставить("ВидПродукции",		 ДанныеКода.ВидПродукции);
	СтруктураДанныхКМ.Вставить("ВидУпаковки", 		 ДанныеКода.ВидУпаковки);
	СтруктураДанныхКМ.Вставить("Номенклатура", 		 Номенклатура);
	СтруктураДанныхКМ.Вставить("Характеристика",	 Характеристика);
	Если Не ВложенныйКод = Неопределено Тогда
		СтруктураДанныхКМ.Вставить("КоличествоПотр", Количество);
	КонецЕсли;

	Возврат СтруктураДанныхКМ;
	
КонецФункции

Функция ПосчитатьКоличествоВложенныхПотребительских(ВложенностьКода, ВложенныеКодыОтветСервера)
	
	Если Не ТипЗнч(ВложенностьКода) = Тип("Массив") Тогда
		//Пробуем найти данные этого КМ в общем ответе сервера, т.к. для сложновложенной упаковки с сервера  
		//возвращается список без структуры подчиненности вложенных.
		НайденныеДанные = ВложенныеКодыОтветСервера.Получить(ВложенностьКода); 
		Если ТипЗнч(НайденныеДанные) = Тип("Массив") Тогда
			ВложенностьКода = НайденныеДанные;
		Иначе
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	
	КоличествоПотребительскихВЭтойУпаковке = 0;
	
	Для Каждого ВложеннаяПозиция Из ВложенностьКода Цикл
		
		ДанныеРазбора = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.РазобратьКодМаркировки(ВложеннаяПозиция);
		
		Если ДанныеРазбора = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеРазбора.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда 
			КоличествоПотребительскихВЭтойУпаковке = КоличествоПотребительскихВЭтойУпаковке + 1;
		Иначе
			КоличествоДляСуммы = ПосчитатьКоличествоВложенныхПотребительских(ВложеннаяПозиция, ВложенныеКодыОтветСервера);
			КоличествоПотребительскихВЭтойУпаковке = КоличествоПотребительскихВЭтойУпаковке + КоличествоДляСуммы;
		КонецЕсли;			
			
	КонецЦикла;
	
	Возврат КоличествоПотребительскихВЭтойУпаковке; 
	
КонецФункции

Функция ПолучитьКодыМаркировки(ИсходныеДанные, ДокументПоддерживаетСканированиеЕАН = Ложь) Экспорт 
	
	Возврат РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйВызовСервера.ПолучитьКодыМаркировки(ИсходныеДанные, ДокументПоддерживаетСканированиеЕАН);
	
КонецФункции

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработкаОшибок

// Выполняет запись ошибки в журнал регистрации, добавляя имя события Маркировка если оно не задано.
//
// Параметры:
//  ТекстОшибки      - Строка           - текст ошибки.
//  ИмяСобытия       - Строка           - имя события.
//  ОбъектМетаданных - ОбъектМетаданных - объект метаданных с ошибкой.
//  Данные           - Произвольный     - объект данных с ошибкой.
//
Процедура ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки, ИмяСобытия = Неопределено, ОбъектМетаданных = Неопределено, Данные = Неопределено) Экспорт
	
	Если ИмяСобытия = Неопределено Тогда
		ИмяСобытия = НСтр("ru = 'Выполнение методов сервиса 1С:Маркировка для Казахстана'", ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.КодОсновногоЯзыка());
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, ОбъектМетаданных, Данные, ТекстОшибки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

//ЗаказКМ, форма: ЗаполнитьПоДокументуОснованию()
//Функция последовательно проверяет константы учета марк. продукции из настроек и выставляет в качестве вида продукции по умолчанию
//первую из включенных ТГ.
//
Функция ПолучитьЗначениеПоУмолчаниюДляВидаПродукцииЗаказа() Экспорт
	
	Если Константы.ВестиУчетМаркируемойОбувиИСМПТК.Получить() Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная");
		
	ИначеЕсли Константы.ВестиУчетМаркируемогоТабакаИСМПТК.Получить() Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Табачная");
		
	ИначеЕсли Константы.ВестиУчетМаркируемойМолочкиИСМПТК.Получить() Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция");
		
	ИначеЕсли Константы.ВестиУчетМаркируемыхЛекарствИСМПТК.Получить() Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты");
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИмяДокументаПоСсылке(СсылкаНаДокумент) Экспорт
	
	Возврат СсылкаНаДокумент.Метаданные().Имя;	
	
КонецФункции

#Область ДокументыИСМПТ_БМ

Процедура ОбновитьАктуальныеДокументыИСМПТ(Знач СоответствиеДляОбновленияАктуальныхОснований, ИсключаемыеРеквизиты = Неопределено, ИсключаемыеТабличныеЧасти = Неопределено) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого ЭлементСоответствия Из СоответствиеДляОбновленияАктуальныхОснований Цикл
			
			СсылкаДокументИСМПТ = ЭлементСоответствия.Ключ.ПолучитьОбъект();
			Основание = ЭлементСоответствия.Значение;
			ОбщегоНазначенияИСМПТК.ОчиститьОбъект(СсылкаДокументИСМПТ, ИсключаемыеРеквизиты, ИсключаемыеТабличныеЧасти);
			ИнтеграцияИСМПТК.ЗаполнитьИсходящийДокументИСМПТ(Основание, СсылкаДокументИСМПТ);
			Если ТипЗнч(СсылкаДокументИСМПТ.Ссылка) = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСМПТК") Тогда
				ПерезаполнятьИИНБИНПоставщика = Ложь;
				НалоговыйНомер = Неопределено;
				ОбщегоНазначенияИСМПТКПереопределяемый.ПолучитьДанныеКонтрагентаИзЕАЭС(ПерезаполнятьИИНБИНПоставщика, НалоговыйНомер, СсылкаДокументИСМПТ.Поставщик);
				Если ПерезаполнятьИИНБИНПоставщика Тогда
					СсылкаДокументИСМПТ.ПоставщикИдентификационныйНомер = НалоговыйНомер;
				КонецЕсли;
			КонецЕсли;
			Если ТипЗнч(СсылкаДокументИСМПТ.Ссылка) = Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСМПТК")
				Или ТипЗнч(СсылкаДокументИСМПТ.Ссылка) = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСМПТК") Тогда
				СсылкаДокументИСМПТ.СтранаОтправления = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьСтрануОтправленияВвозИзЕАЭС(Основание.Контрагент);
			КонецЕсли;
			
			СсылкаДокументИСМПТ.Записать();
					
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТексСообщения = НСтр("ru = 'При попытке обновить документы по данным сервера произошла ошибка.'") + " "
					  + РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияПодробностиВЖурналеРегистрации();
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТексСообщения);
		
		ТекстОшибки = НСтр("ru = 'При попытке обновить документы по данным сервера произошла ошибка:'")
				    + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					
		ИмяСобытия  = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаВФункцииСПараметром();
		ИмяСобытия  = СтрЗаменить(ИмяСобытия, "%ИмяФункции%", "ИнтеграцияИСМПТКВызовСервера.ОбновитьАктуальныеДокументыИСМПТ");
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
			
	ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПолучитьТаблицуПараметров(СписокКолонок, ИсходнаяТаблица = Неопределено) Экспорт

	Если ИсходнаяТаблица = Неопределено Тогда
		
		ТаблицаРезультат = Новый ТаблицаЗначений;
		Колонки = Новый Структура(СписокКолонок);
		Для Каждого Колонка Из Колонки Цикл
			ТаблицаРезультат.Колонки.Добавить(Колонка.Ключ);
		КонецЦикла;
		Возврат ТаблицаРезультат;

	Иначе

		Возврат ИсходнаяТаблица.Скопировать(, СписокКолонок);

	КонецЕсли;

КонецФункции

Функция МассивGTINМаркированногоТовара(Номенклатура, Характеристика, Упаковка) Экспорт
	
	Возврат РаботаСДокументамиИСМПТКПереопределяемый.МассивGTINМаркированногоТовара(Номенклатура, Характеристика, Упаковка);
	
КонецФункции

#Область ОтправкаДокументовИСМПТ

//ВАЖНО! Массив изменяется внутрии функции, Знач не устанавливаем перед объявлением переменной
Функция ПроверитьВозможностьОтправкиДокументовПоставитьВОчередьОтправкиДокументИСМПТ(МассивИсходящихДокументовИСМПТ, ДополнительныеПараметры) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПроверитьВозможностьОтправкиДокументовПоставитьВОчередьОтправкиДокументИСМПТ(МассивИсходящихДокументовИСМПТ, ДополнительныеПараметры);
	
КонецФункции

Функция СгруппироватьДокументыИСМПТПоСтруктурнымЕдиницам(Знач МассивДокументовИСМПТ) Экспорт
	
	ТекстЗапроса = ТекстЗапросаСгруппироватьПоСтруктурамОбщийИСМПТ();
	
	Если ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.АктПриемаПередачиИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАктПриемаПередачиИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОРасхожденииИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОРасхожденииИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОВводеВОборотИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВводеВОборотИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОВыводеИзОборотаИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВыводеИзОборотаИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОВвозеИзЕАЭСИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзЕАЭСИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОВвозеИзТретьихСтранИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОВвозеИзТретьихСтранИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОбЭкспортеЕАЭСИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбЭкспортеЕАЭСИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОПриемкеЕАЭСИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОПриемкеЕАЭСИСМПТК());
		
	ИначеЕсли ТипЗнч(МассивДокументовИСМПТ[0]) = Тип("ДокументСсылка.УведомлениеОбОтгрузкеЕАЭСИСМПТК") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаУведомлениеОбОтгрузкеЕАЭСИСМПТК());
		
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументовИСМПТ", МассивДокументовИСМПТ);
	
	ВыборкаСтруктурнаяЕдиница = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоллекцияСгруппированныхДокументовИСМПТ = Новый Соответствие;
	
	Пока ВыборкаСтруктурнаяЕдиница.Следующий() Цикл
		
		СгруппированныйМассивДокументовИСМПТ = Новый Массив;
		
		ВыборкаДетальныеЗаписи = ВыборкаСтруктурнаяЕдиница.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СгруппированныйМассивДокументовИСМПТ.Добавить(ВыборкаДетальныеЗаписи.ДокументИСМПТ);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница) Тогда
			
			КоллекцияСгруппированныхДокументовИСМПТ.Вставить(ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница, СгруппированныйМассивДокументовИСМПТ);
			
		Иначе
			
			ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияНеУказанаОрганизациВСпискеДокументовСПараметром();
			
			СтрокаСписокДокументовИСМПТ = "";
			Для Каждого СсылкаДокументИСМПТ Из СгруппированныйМассивДокументовИСМПТ Цикл
				СтрокаСписокДокументовИСМПТ = СтрокаСписокДокументовИСМПТ + "- " + СсылкаДокументИСМПТ + Символы.ПС;
			КонецЦикла;
			СтрокаСписокДокументовИСМПТ = СокрЛП(СтрокаСписокДокументовИСМПТ);
			
			ПодстрокаЗамены = Символы.ПС + СтрокаСписокДокументовИСМПТ + Символы.ПС;
			ТекстСообщения  = СтрЗаменить(ТекстСообщения, "%СписокДокументов%", ПодстрокаЗамены);
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоллекцияСгруппированныхДокументовИСМПТ;
	
КонецФункции

Функция ТекстЗапросаСгруппироватьПоСтруктурамОбщийИСМПТ()
	
	Возврат "ВЫБРАТЬ
	|	ДокументИСМПТ.Организация КАК СтруктурнаяЕдиница,
	|	ДокументИСМПТ.Ссылка КАК ДокументИСМПТ
	|ИЗ
	|	Документ.%ИмяДокумента% КАК ДокументИСМПТ
	|ГДЕ
	|	ДокументИСМПТ.Ссылка В(&МассивДокументовИСМПТ)
	|ИТОГИ ПО
	|	СтруктурнаяЕдиница";

КонецФункции

#КонецОбласти

#Область ОтправкаДокументовИСМПТ

Функция ПроверитьВозможностьОтправкиДокументовПоставитьВОчередьОтправкиДокументСУЗ(МассивИсходящихДокументовСУЗ, ДополнительныеПараметры) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПроверитьВозможностьОтправкиДокументовПоставитьВОчередьОтправкиДокументСУЗ(МассивИсходящихДокументовСУЗ, ДополнительныеПараметры);
	
КонецФункции

Функция СгруппироватьДокументыСУЗПоСтруктурнымЕдиницам(Знач МассивДокументовСУЗ) Экспорт
	
	Если ТипЗнч(МассивДокументовСУЗ[0]) = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК") Тогда
		
		ТипДокумента = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаЗаказКодовМаркировкиСУЗИСМПТК();
		
	ИначеЕсли ТипЗнч(МассивДокументовСУЗ[0]) = Тип("ДокументСсылка.АгрегацияКодовМаркировкиСУЗИСМПТК") Тогда
		
		ТипДокумента = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаАгрегацияКодовМаркировкиСУЗИСМПТК();
		
	ИначеЕсли ТипЗнч(МассивДокументовСУЗ[0]) = Тип("ДокументСсылка.НанесениеКодовМаркировкиСУЗИСМПТК") Тогда
		
		ТипДокумента = ОбщегоНазначенияИСМПТКВызовСервера.ИмяДокументаНанесениеКодовМаркировкиСУЗИСМПТК();
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапросаСгруппироватьПоСтруктурамОбщийИСМПТ();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяДокумента%", ТипДокумента); 

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументовИСМПТ", МассивДокументовСУЗ);
	
	ВыборкаСтруктурнаяЕдиница = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоллекцияСгруппированныхДокументовСУЗ = Новый Соответствие;
	
	Пока ВыборкаСтруктурнаяЕдиница.Следующий() Цикл
		
		СгруппированныйМассивДокументовСУЗ = Новый Массив;
		
		ВыборкаДетальныеЗаписи = ВыборкаСтруктурнаяЕдиница.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СгруппированныйМассивДокументовСУЗ.Добавить(ВыборкаДетальныеЗаписи.ДокументИСМПТ);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница) Тогда
			
			КоллекцияСгруппированныхДокументовСУЗ.Вставить(ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница, СгруппированныйМассивДокументовСУЗ);
			
		Иначе
			
			ТекстСообщения = РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияНеУказанаОрганизациВСпискеДокументовСПараметром();
			
			СтрокаСписокДокументовСУЗ = "";
			Для Каждого СсылкаДокументСУЗ Из СгруппированныйМассивДокументовСУЗ Цикл
				СтрокаСписокДокументовСУЗ = СтрокаСписокДокументовСУЗ + "- " + СсылкаДокументСУЗ + Символы.ПС;
			КонецЦикла;
			СтрокаСписокДокументовСУЗ = СокрЛП(СтрокаСписокДокументовСУЗ);
			
			ПодстрокаЗамены = Символы.ПС + СтрокаСписокДокументовСУЗ + Символы.ПС;
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СписокДокументов%", ПодстрокаЗамены);
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоллекцияСгруппированныхДокументовСУЗ;
		
КонецФункции

#КонецОбласти

#Область ОбновлениеДокументовИСМПТ

#КонецОбласти

#Область ТокенАвторизации

Функция ПроверитьОбновитьКлючСессииИСМПТ(ПараметрыЗапроса, Знач СрокДействия = Неопределено, ОбновлятьКлючСессииНаСервере = Истина) Экспорт
	
	Возврат ИнтерфейсИСМПТК.ПроверитьОбновитьКлючСессииИСМПТ(ПараметрыЗапроса, СрокДействия, ОбновлятьКлючСессииНаСервере);
	
КонецФункции

Функция ИспользоватьАвтоОпределениеЛогистическихКодовИСМПТК() Экспорт
	
	Возврат Константы.АвтоОпределениеЛогистическихКодовИСМПТК.Получить();
	
КонецФункции

Функция ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() Экспорт
	
	Возврат Константы.АвтоПроверкаВалидностиКодаИСМПТК.Получить();
	
КонецФункции

#КонецОбласти

Процедура ОтметитьВыгрузкуКодовМаркировкиЗаказа(СписокКодов, ЗаказОснование = Неопределено) Экспорт 
	
	ИнтеграцияИСМПТК.ОтметитьВыгрузкуКодовМаркировкиЗаказа(СписокКодов);
	
КонецПроцедуры

Функция ПолучитьАдресаСерверовИС() Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПолучитьАдресаСерверовИС();
	
КонецФункции

Функция ОбработатьАдресИСМПТКонстанта(АдресКонстанта) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ОбработатьАдресИСМПТКонстанта(АдресКонстанта);
	
КонецФункции

Функция ПроверитьНаличиеПротоколаВАдресе(АдресСервера) Экспорт
	
	Возврат ОбщегоНазначенияИСМПТК.ПроверитьНаличиеПротоколаВАдресе(АдресСервера);
	
КонецФункции

Функция ПолучитьТекущиюВерсиюМакета() Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПолучитьТекущиюВерсиюМакета();
	
КонецФункции

Функция ПолучитьТекущиюВерсиюМобильногоПриложения() Экспорт
	
	Возврат ОбменДаннымиМобильноеПриложениеМаркировкаИСМПТК.ПолучитьТекущиюВерсиюМобильногоПриложения();
	
КонецФункции

Функция ПолучитьИдентификаторОрганизации(ОрганизацияСсылка) Экспорт
	
	Возврат ОбщегоНазначенияИСМПТК.ЗначениеРеквизитаОбъекта(ОрганизацияСсылка, 
		ИнтеграцияИСМПТКПереопределяемый.ПолучитьРеквизитИИНОрганизации());
	
КонецФункции

Функция ПрочитатьЗначениеJSON_Маркировка(СтрокаJSON, СвойстваДаты = "") Экспорт
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(СтрокаJSON);
	
	Результат = ПрочитатьJSON(Чтение, Ложь, СвойстваДаты);
	Чтение.Закрыть();
	
	Возврат Результат;
	
КонецФункции

Функция ЗаписатьЗначениеJSON_Маркировка(Значение) Экспорт
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, Значение);
	
	Возврат Запись.Закрыть();
	
КонецФункции

Функция ПолучитьШаблонДляНоменклатурыЗаказа(Номенклатура) Экспорт
	
	Возврат ИнтеграцияИСМПТКПереопределяемый.ПолучитьШаблонДляНоменклатурыЗаказа(Номенклатура);
	
КонецФункции

Функция ПолучитьКодыМаркировкиНанесение(СписокЗаказов, ТаблицаКМДляСравнения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию КАК ЗаказНаЭмиссию,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодИдентификации КАК КодИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.ХешСуммаКодаИдентификации КАК ХешСуммаКодаИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.Статус КАК Статус,
	|	ПулКодовМаркировкиСУЗИСМПТК.Организация КАК Организация,
	|	ПулКодовМаркировкиСУЗИСМПТК.Номенклатура КАК Номенклатура,
	|	ПулКодовМаркировкиСУЗИСМПТК.Характеристика КАК Характеристика,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки КАК КодМаркировки,
	|	ПулКодовМаркировкиСУЗИСМПТК.ДатаЭмиссии КАК ДатаЭмиссии,
	|	ПулКодовМаркировкиСУЗИСМПТК.ДатаПечати КАК ДатаПечати,
	|	ПулКодовМаркировкиСУЗИСМПТК.ДатаВыгрузки КАК ДатаВыгрузки,
	|	ПулКодовМаркировкиСУЗИСМПТК.GTIN КАК GTIN,
	|	ПулКодовМаркировкиСУЗИСМПТК.ВидПродукции КАК ВидПродукции,
	|	ПулКодовМаркировкиСУЗИСМПТК.СпособВводаВОборот КАК СпособВводаВОборот,
	|	ПулКодовМаркировкиСУЗИСМПТК.Шаблон КАК Шаблон,
	|	ПулКодовМаркировкиСУЗИСМПТК.ТипШтрихкода КАК ТипШтрихкода
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ПулКодовМаркировкиСУЗИСМПТК
	|ГДЕ
	|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию В(&СписокЗаказов)
	|	И ПулКодовМаркировкиСУЗИСМПТК.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКодовМаркировкиСУЗИСМПТК.СгенерированНеНанесен)
	|	И ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки В(&ТаблицаКМ)";
	
	Запрос.УстановитьПараметр("СписокЗаказов", СписокЗаказов);
	Запрос.УстановитьПараметр("ТаблицаКМ", 	   ТаблицаКМДляСравнения);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		МассивКодов = Новый Массив;
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			
			ДанныеЗаписи = Новый Структура();
			ДанныеЗаписи.Вставить("ЗаказНаЭмиссию",			  Выборка.ЗаказНаЭмиссию);
			ДанныеЗаписи.Вставить("КодИдентификации", 		  Выборка.КодИдентификации);
			ДанныеЗаписи.Вставить("ХешСуммаКодаИдентификации",Выборка.ХешСуммаКодаИдентификации);
			ДанныеЗаписи.Вставить("Организация", 			  Выборка.Организация);
			ДанныеЗаписи.Вставить("Номенклатура", 			  Выборка.Номенклатура);
			ДанныеЗаписи.Вставить("Характеристика", 		  Выборка.Характеристика);
			ДанныеЗаписи.Вставить("КодМаркировки", 	  		  Выборка.КодМаркировки);
			ДанныеЗаписи.Вставить("ДатаЭмиссии",			  Выборка.ДатаЭмиссии);
			ДанныеЗаписи.Вставить("GTIN", 					  Выборка.GTIN);
			ДанныеЗаписи.Вставить("ВидПродукции", 			  Выборка.ВидПродукции);
			ДанныеЗаписи.Вставить("СпособВводаВОборот", 	  Выборка.СпособВводаВОборот);
			ДанныеЗаписи.Вставить("Шаблон", 				  Выборка.Шаблон);
			ДанныеЗаписи.Вставить("ТипШтрихкода", 			  Выборка.ТипШтрихкода);
			
			МассивКодов.Добавить(ДанныеЗаписи);
			
		КонецЦикла;
		
		Возврат МассивКодов;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКодыМаркировкиЗаказов(СписокЗаказов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	|	ПулКодовМаркировкиСУЗИСМПТК.КодИдентификации КАК КодИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки КАК КодМаркировки,
	|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию КАК ЗаказНаЭмиссию
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ПулКодовМаркировкиСУЗИСМПТК
	|ГДЕ
	|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию В(&СписокЗаказов)
	|	И (ПулКодовМаркировкиСУЗИСМПТК.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКодовМаркировкиСУЗИСМПТК.Сгенерирован)
	|	ИЛИ ПулКодовМаркировкиСУЗИСМПТК.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКодовМаркировкиСУЗИСМПТК.Использован))";
	
	Запрос.УстановитьПараметр("СписокЗаказов", СписокЗаказов);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		МассивКодов = Новый Массив;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл 
			МассивКодов.Добавить(Новый Структура("КодИдентификации, КодМаркировки, ЗаказНаЭмиссию", 
								 Выборка.КодИдентификации, Выборка.КодМаркировки, Выборка.ЗаказНаЭмиссию));
		КонецЦикла;
		Возврат МассивКодов;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбменИСЦЭДМ

Функция ПроверитьВозможностьОтправкиДокументовПоставитьВОчередьОтправкиДокументИСЦЭДМ(МассивИсходящихДокументовИСЦЭДМ) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПроверитьВозможностьОтправкиДокументовПоставитьВОчередьОтправкиДокументИСЦЭДМ(МассивИсходящихДокументовИСЦЭДМ);
	
КонецФункции

Функция СгруппироватьДокументыИСЦЭДМПоСтруктурнымЕдиницам(Знач МассивДокументыИСЦЭДМ) Экспорт
	
	Возврат ИнтеграцияИСМПТК.Переопределяемый_СгруппироватьДокументыИСЦЭДМПоСтруктурнымЕдиницам(МассивДокументыИСЦЭДМ);
	
КонецФункции

Процедура ОбновитьДокументыИзИСЦЭДМ(Знач Параметры) Экспорт
	
	ИнтеграцияИСМПТК.ОбновитьДокументыИСЦЭДМ(Параметры);
	
КонецПроцедуры

Функция СозданиеТитуловИСЦЭДМИзМассиваДокументов(ДокументыОрганизации, ТокенАвторизации) Экспорт
	
	Возврат ИнтеграцияИСМПТК.СозданиеТитуловИСЦЭДМИзМассиваДокументов(ДокументыОрганизации, ТокенАвторизации);
	
КонецФункции

Функция СозданиеТитуловИСЦЭДМ(Знач КоллекцияСгруппированныхДокументовИСЦЭДМ) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПодготовитьСтруктуруТитуловИСЦЭДМ(КоллекцияСгруппированныхДокументовИСЦЭДМ);
	
КонецФункции

Процедура ОтправитьДокументыИСЦЭДМ_ПодписьТитуловЗавершение(Знач НоваяКоллекцияСгруппированныхДокументовИСЦЭДМ, Знач СписокТокеновАвторизации, БИНОрганизации = "") Экспорт
	
	ИнтеграцияИСМПТК.ОтправитьДокументы_ПодписьТитуловЗавершение_ИСЦЭДМ(НоваяКоллекцияСгруппированныхДокументовИСЦЭДМ, СписокТокеновАвторизации, БИНОрганизации);
	
КонецПроцедуры 

Процедура ОтправитьПодписанныйДокументыИСЦЭДМ(ДанныеПодписанногоТитула) Экспорт
	
	ИнтеграцияИСМПТК.ОтправитьПодписанныйДокументыИСЦЭДМ(ДанныеПодписанногоТитула);
	
КонецПроцедуры

#Область АПИ_ЦЭДМ_Входящие

Функция ПодтвердитьВходящиеДокументыИСЦЭДМ_СозданиеТитулов(Знач КоллекцияСгруппированныхДокументовИСЦЭДМ) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ПодтвердитьВходящиеАкты_СозданиеТитулов_ИСЦЭДМ(КоллекцияСгруппированныхДокументовИСЦЭДМ);
	
КонецФункции

Процедура ПодтвердитьВходящиеДокументыИСЦЭДМ_ПодписьТитуловЗавершение(Знач КоллекцияСгруппированныхДокументовИСЦЭДМ) Экспорт
	
	ИнтеграцияИСМПТК.ПодтвердитьВходящиеАкты_ПодписьТитуловЗавершение_ИСЦЭДМ(КоллекцияСгруппированныхДокументовИСЦЭДМ);
	
КонецПроцедуры

Функция ОтклонитьВходящиеДокументыИСЦЭДМ_СозданиеТитулов(Знач КоллекцияСгруппированныхДокументовИСЦЭДМ) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ОтклонитьВходящиеАкты_СозданиеТитулов_ИСЦЭДМ(КоллекцияСгруппированныхДокументовИСЦЭДМ);
	
КонецФункции

Процедура ОтклонитьВходящиеДокументыИСЦЭДМ_ПодписьТитуловЗавершение(Знач НоваяКоллекцияСгруппированныхДокументовИСЦЭДМ) Экспорт
	
	ИнтеграцияИСМПТК.ОтклонитьВходящиеАкты_ПодписьТитуловЗавершение_ИСЦЭДМ(НоваяКоллекцияСгруппированныхДокументовИСЦЭДМ);
	
КонецПроцедуры

#КонецОбласти

#Область АПИ_ЦЭДМ_Исходящие

Функция ОтозватьИсходящиеДокументыИСЦЭДМ_СозданиеТитулов(Знач КоллекцияСгруппированныхДокументовИСЦЭДМ) Экспорт
	
	Возврат ИнтеграцияИСМПТК.ОтозватьИсходящиеДокументы_СозданиеТитулов_ИСЦЭДМ(КоллекцияСгруппированныхДокументовИСЦЭДМ);
	
КонецФункции

Процедура ОтозватьИсходящиеДокументыИСЦЭДМ_ПодписьТитуловЗавершение(Знач НоваяКоллекцияСгруппированныхДокументовИСЦЭДМ) Экспорт
	
	ИнтеграцияИСМПТК.ОтозватьИсходящиеДокументы_ПодписьТитуловЗавершение_ИСЦЭДМ(НоваяКоллекцияСгруппированныхДокументовИСЦЭДМ);
	
КонецПроцедуры

#КонецОбласти

#Область АПИ_ЦЭДМ_Акты

Процедура СоздатьИсходящиеАктыИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьИсходящиеАктыИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры

#КонецОбласти

#Область АПИ_ЦЭДМ_Ввод_в_Оборот

Процедура СоздатьИсходящиеУведомленияОВводеВОборотИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВводеВОборотИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры

#КонецОбласти

#Область АПИ_ЦЭДМ_Вывод_из_оборота

Процедура СоздатьИсходящиеУведомленияОВыводеИзОборотаИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВыводеИзОборотаИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры

#КонецОбласти 

#Область АПИ_ЦЭДМ_Агрегация

Процедура СоздатьИсходящиеАгрегацииИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ЭлектронноеВзаимодействиеССервисамиМаркировка.СоздатьИсходящиеАгрегацииИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры 

Процедура ИзменитьДокументАгрегацииИСЦЭДМ(Знач МассивДокументов, ДополнительныеПараметры) Экспорт
	
	ЭлектронноеВзаимодействиеССервисамиМаркировка.ИзменитьДокументАгрегацииИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры

#КонецОбласти 

#Область АПИ_ЦЭДМ_Внутреннее_перемещение

Процедура СоздатьИсходящиеАктыВнутреннегоПеремещенияИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьИсходящиеАктыВнутреннегоПеремещенияИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры 

#КонецОбласти

#Область АПИ_ЦЭДМ_Отчет_Передачи_КИ_Импортёру

Процедура СоздатьИсходящиеОтчетыОПередачеКИОтНерезидентаРКИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьИсходящиеОтчетыОПередачеКИОтНерезидентаРКИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры 

#КонецОбласти 

#Область АПИ_ЦЭДМ_Уведомление_О_Ввозе_Из_ЕАЭС

Процедура СоздатьИсходящиеУведомленияОВвозеИзЕАЭСИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВвозеИзЕАЭСИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры 

#КонецОбласти 

#Область АПИ_ЦЭДМ_Уведомление_О_Ввозе_Из_Третьих_Стран

Процедура СоздатьИсходящиеУведомленияОВвозеИзТретьихСтранИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьИсходящиеУведомленияОВвозеИзТретьихСтранИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры 

#КонецОбласти 

#Область АПИ_ЦЭДМ_УведомлениеОРасхожденияхИСЦЭДМ

Процедура ОтозватьУведомленияОРасхожденияхИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ЭлектронноеВзаимодействиеССервисамиМаркировка.ОтозватьУведомленияОРасхожденияхИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры

Процедура ОтклонитьУведомленияОРасхожденияхИСЦЭДМ(Знач МассивДокументов) Экспорт
	
	ЭлектронноеВзаимодействиеССервисамиМаркировка.ОтклонитьУведомленияОРасхожденияхИСЦЭДМ(МассивДокументов);
	
КонецПроцедуры

#КонецОбласти

Функция СоздатьДокументыКорректировки(Знач МассивДокументов) Экспорт
	
	Возврат ИнтеграцияИСМПТК.СоздатьДокументыКорректировки(МассивДокументов);
	
КонецФункции

Процедура СоздатьУведомленияОРасхождении(МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СоздатьУведомленияОРасхождении(МассивДокументов);
	
КонецПроцедуры

Процедура СверитьМассивДокументов(Знач МассивДокументов) Экспорт
	
	ИнтеграцияИСМПТК.СверитьМассивДокументов(МассивДокументов);
	
КонецПроцедуры

Функция ИспользоватьАвтоЗапросДопИнформацииЦЭДМ() Экспорт
	
	Возврат Константы.АвтоЗаполнениеДополнительнойИнформацииПоКодамИСЦЭДМ.Получить();
	
КонецФункции

Функция ОбновитьДанныеПоЦенам(Знач УникальныйИдентификатор, Знач ДополнительныеПараметры) Экспорт
	
	НаименованиеЗадания = НСтр("ru = 'Обновление данных по ценам документа. '");
	ИмяМетода           = "ИнтеграцияИСМПТК.ЗаполнитьЦеныДокументов";
	ПараметрыВыполнения = ОбщегоНазначенияИСМПТКПереопределяемый.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ОбщегоНазначенияИСМПТКПереопределяемый.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, ДополнительныеПараметры);
	
КонецФункции

Функция ОбновитьДанныеПоСтатусамДокументов(Знач УникальныйИдентификатор, Знач ДополнительныеПараметры) Экспорт
	
	НаименованиеЗадания = НСтр("ru = 'Обновление данных по статусам документа. '");
	ИмяМетода           = "ИнтеграцияИСМПТК.ПроверитьСтатусДокументов";
	ПараметрыВыполнения = ОбщегоНазначенияИСМПТКПереопределяемый.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ОбщегоНазначенияИСМПТКПереопределяемый.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, ДополнительныеПараметры);
	
КонецФункции

#КонецОбласти
