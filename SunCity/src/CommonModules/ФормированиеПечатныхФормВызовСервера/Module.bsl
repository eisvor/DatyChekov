////////////////////////////////////////////////////////////////////////////////
// ФормированиеПечатныхФормВызовСервера содержит процедуры и функции для 
// формирования печатных форм и обработки действий пользователя с печатными формами.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет проведение документов перед формированием печатной формы.
//
// Параметры:
//	МассивДокументов - Массив - Документы, которые необходимо провести.
//
// Возвращаемое значение:
//	Массив - Документы, которые провести не удалось.
//
Функция ПровестиДокументы(МассивДокументов) Экспорт
	
	МассивНепроведенныхДокументов = Новый Массив;
	
	Для Каждого ДокументСсылка Из МассивДокументов Цикл
		
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		
		ВыполненоУспешно = Ложь;
		Если ДокументОбъект.ПроверитьЗаполнение() Тогда
	
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ВыполненоУспешно = Истина;
			Исключение
				
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Проведение документа'"),
				УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			КонецПопытки;
			
		КонецЕсли;
		
		Если Не ВыполненоУспешно Тогда
			МассивНепроведенныхДокументов.Добавить(ДокументСсылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивНепроведенныхДокументов;
	
КонецФункции

//Возвращает массив непроведенных документов.
//
// Параметры:
//	МассивДокументов - Массив - документы, которые необходимо проверить.
//
// Возвращаемое значение:
//	Массив - массив непроведенных документов.
//
Функция МассивНепроведенныхДокументов(МассивДокументов) Экспорт

	Если МассивДокументов.Количество() > 0 Тогда

		Запрос = Новый Запрос(
		"
		|ВЫБРАТЬ
		|	Документ.Ссылка КАК Ссылка
		|ИЗ
		|	Документ." + МассивДокументов[0].Метаданные().Имя + " КАК Документ
		|ГДЕ
		|	Документ.Ссылка В (&МассивДокументов)
		|	И Не Документ.Проведен
		|");
		Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
		Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Иначе

		Результат = Новый Массив;

	КонецЕсли;

	Возврат Результат;

КонецФункции

// Формирует сведения об указанном ЮрФизЛице. К сведениям относятся -
// наименование, адрес, номер телефона, банковские реквизиты.
//
// Параметры: 
//  ЮрФизЛицо - СправочникСсылка.Организации,
//              СправочникСсылка.ФизическиеЛица - организация или физическое лицо, о котором собираются сведения.
//  ДатаПериода - Дата - дата, на которую выбираются сведения о ЮрФизЛице.
//  ДляФизЛицаТолькоИнициалы - Булево - Для физ. лица выводить только инициалы имени и отчества.
//  БанковскийСчет - СправочникСсылка.БанковскиеСчета - Банковский счет, если счет не основной.
//
// Возвращаемое значение:
//  Структура - собранные сведения.
//
Функция СведенияОЮрФизЛице(ЮрФизЛицо, ДатаПериода, ДляФизЛицаТолькоИнициалы = Истина, Знач БанковскийСчет = Неопределено) Экспорт
	
	Возврат ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ЮрФизЛицо, ДатаПериода, ДляФизЛицаТолькоИнициалы, БанковскийСчет);
	
КонецФункции

// Возвращает информацию об ответственных лицах организации и их должностях.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация по которой выбираются ответственные.
//  ДатаСреза - Дата - дата на которую берется срез.
//  Исполнитель - СправочникСсылка.ФизическиеЛица - исполнитель операции.
//  ПолноеФИО - Строка - полное фио исполнителя.
//
// Возвращаемое значение:
//  ТаблицаЗначений - ответственные лица организаций.
//
Функция ОтветственныеЛицаОрганизаций(Организация, ДатаСреза, Исполнитель = Неопределено, ПолноеФИО = Ложь) Экспорт
	
	Возврат ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(Организация, ДатаСреза, Исполнитель);
	
КонецФункции
	
#КонецОбласти