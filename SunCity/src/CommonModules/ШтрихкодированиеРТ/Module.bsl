Процедура НайтиДисконтнуюКарту(
	Форма,
	СтруктураПараметров,
	Штрихкод,
	РежимПоискаКарты,
	НеизвестныеДанныеПО,
	НайденоОбъектов) Экспорт
	
	ЗапросПоиска 	 = Новый Запрос;
	
	Если РежимПоискаКарты = "НомерТелефона" Тогда
		
		ЗапросПоиска.Текст = ИнформационныеКартыВызовСервера.ТекстЗапросаПоискаКарты(1);
		ЗапросПоиска.УстановитьПараметр("ВведенноеЧисло", Штрихкод);
		ЗапросПоиска.УстановитьПараметр("ВведенноеЧислоСВосьмеркой", "8" + Сред(Штрихкод, 2));
		
	ИначеЕсли РежимПоискаКарты = "ФИО" Тогда
		
		ЗапросПоиска.Текст = ИнформационныеКартыВызовСервера.ТекстЗапросаПоискаКарты(2);
		ЗапросПоиска.УстановитьПараметр("Наименование", "%" + Штрихкод + "%");
		
		ТипыИнформации = Новый Массив;
		ТипыИнформации.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
		ТипыИнформации.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
		ЗапросПоиска.УстановитьПараметр("ТипыИнформации", ТипыИнформации);
		
	ИначеЕсли РежимПоискаКарты = "Email" Тогда
		
		ЗапросПоиска.Текст = ИнформационныеКартыВызовСервера.ТекстЗапросаПоискаКарты(3);
		ЗапросПоиска.УстановитьПараметр("АдресЭП", "%" + Штрихкод + "%");
		
	Иначе
		
		ЗапросПоиска.Текст = ИнформационныеКартыВызовСервера.ТекстЗапросаПоискаКарты(0);
		ЗапросПоиска.УстановитьПараметр("ВводимоеЧисло", ШтрихКод);
		
	КонецЕсли;
	
	ЗапросПоиска.УстановитьПараметр("ТипКарты", Перечисления.ТипыИнформационныхКарт.Дисконтная);
	
	Выборка = ЗапросПоиска.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Телефон = "";
		АдресЭП	= "";
		
		Если РежимПоискаКарты = "НомерТелефона" Тогда
			
			ШтрихКод 	= Выборка.НомерТелефона;
			Телефон 	= ШтрихкодированиеРТКлиентСервер.ПодготовитьТелефон(Выборка.НомерТелефона);
			
		ИначеЕсли РежимПоискаКарты = "Email" Тогда
			
			ШтрихКод = Выборка.АдресЭП;
			АдресЭП	 = Выборка.АдресЭП;

		Иначе
			
			ШтрихКод = Штрихкод;
			
		КонецЕсли;
		
		
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("ДанныеПО", 		Штрихкод);
		СтруктураШтрихкода.Вставить("Штрихкод", 		Штрихкод);
		СтруктураШтрихкода.Вставить("НомерТелефона", 	Телефон);
		СтруктураШтрихкода.Вставить("АдресЭП", 			АдресЭП);
		
		СтруктураШтрихкода.Вставить("МагнитныйКод", "");
		СтруктураШтрихкода.Вставить("Количество", 	1);

		СтруктураШтрихкода.Вставить("Карта", 					Выборка.Ссылка);
		СтруктураШтрихкода.Вставить("ТипКарты", 				Перечисления.ТипыИнформационныхКарт.Дисконтная);
		СтруктураШтрихкода.Вставить("ВладелецКарты", 			Выборка.Ссылка.ВладелецКарты);
		СтруктураШтрихкода.Вставить("ЭтоРегистрационнаяКарта", 	Ложь);
		
		СтруктураПараметров.ЗначенияПоиска.Добавить(СтруктураШтрихкода);
		СтруктураПараметров.Вставить("НомерТелефона", 	Телефон);
		СтруктураПараметров.Вставить("АдресЭП", 			АдресЭП);
		
	КонецЦикла;
	
	НайденоОбъектов = Выборка.Количество();
	
	Если НайденоОбъектов <> 0 Тогда
		НеизвестныеДанныеПО = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ТелефоныДисконтнойКартыПоДаннымШК(ДисконтнаяКарта) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокТелефонов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Карты.Ссылка КАК Ссылка,
	|	Карты.КодКарты КАК ШтрихКод
	|ИЗ
	|	Справочник.ИнформационныеКарты КАК Карты
	|ГДЕ
	|	Карты.Ссылка = &ДисконтнаяКарта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Штрихкоды.Владелец,
	|	Штрихкоды.Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК Штрихкоды
	|ГДЕ
	|	Штрихкоды.Владелец = &ДисконтнаяКарта
	|
	|УПОРЯДОЧИТЬ ПО
	|	Штрихкод";
	
	Запрос.УстановитьПараметр("ДисконтнаяКарта", ДисконтнаяКарта);
	
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		Если ШтрихкодированиеРТКлиентСервер.ТелефонКорректен(ВыборкаЗапроса.ШтрихКод) Тогда
						
			Телефон 		= ШтрихкодированиеРТКлиентСервер.ПодготовитьТелефон(ВыборкаЗапроса.ШтрихКод);
			ТелефонСтрокой 	= "";
			
			ПодключаемоеОборудованиеРТ.ПреобразоватьТелефонКПользовательскомуВиду(Телефон, ТелефонСтрокой);
			
			СписокТелефонов.Добавить(Телефон, ТелефонСтрокой);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокТелефонов;
	
КонецФункции

