#Область ПрограммныйИнтерфейс

// Возвращает Истина, если акцизная марка никогда раньше не продавалась. Ложь - в противном случае.
//
// Параметры:
//  Операция - Строка - текущая операция, для которой требуется осуществить контроль. Возможные значения:
//   "Продажа" - проверка пройдена, если продажи за минусом возвратов <= 0,
//   "Возврат" - проверка пройдена, если продажи за минусом возвратов >= 0,
//   "АктПостановкиНаБаланс" - проверка пройдена, если не было продаж, возвратов, актов постановок на баланс,
//   "АктСписания" - проверка пройдена, если продажи за минусом возвратов <= 0 и поставлено на баланс - списано >= 0.
//  КодАкцизнойМарки - Строка - код акцизной марки,
//  ТекстОшибки - Строка, ФорматированнаяСтрока - текст сообщения пользователю, если акцизная марка не уникальна. Выходной параметр.
Процедура ПроверитьУникальностьАкцизнойМарки(Операция, КодАкцизнойМарки, ТекстОшибки, ОрганизацияЕГАИС = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстОшибки = "";
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодАкцизнойМарки", КодАкцизнойМарки);
	Запрос.УстановитьПараметр("Возврат", Перечисления.ВидыОперацийЧекККМ.Возврат);
	Запрос.УстановитьПараметр("Продажа", Перечисления.ВидыОперацийЧекККМ.Продажа);
	
	Если Операция = "Продажа"
		ИЛИ Операция = "Возврат"
		ИЛИ Операция = "АктСписания" Тогда
		
		НевыгруженныеСтатусы = Новый Массив;
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.ОшибкаПередачи);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.ОшибкаПроведенияЕГАИС);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.Отменен);
		Запрос.УстановитьПараметр("НевыгруженныеСтатусы", НевыгруженныеСтатусы);
		Запрос.УстановитьПараметр("Списание", "Списание");
		Запрос.УстановитьПараметр("Реализована", Перечисления.СтатусыАкцизныхМарок.Реализована);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧекККМАкцизныеМарки.Ссылка КАК Ссылка,
		|	ЧекККМАкцизныеМарки.Ссылка.Дата КАК Дата,
		|	ЧекККМАкцизныеМарки.Ссылка.Номер КАК Номер,
		|	&Продажа КАК ПоследняяОперация
		|ИЗ
		|	Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМАкцизныеМарки
		|ГДЕ
		|	ЧекККМАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	И ЧекККМАкцизныеМарки.Ссылка.Проведен
		|	И ЧекККМАкцизныеМарки.Ссылка.ВидОперации = &Продажа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЧекККМВозвратАкцизныеМарки.Ссылка,
		|	ЧекККМВозвратАкцизныеМарки.Ссылка.Дата,
		|	ЧекККМВозвратАкцизныеМарки.Ссылка.Номер,
		|	&Возврат
		|ИЗ
		|	Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМВозвратАкцизныеМарки
		|ГДЕ
		|	ЧекККМВозвратАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	И ЧекККМВозвратАкцизныеМарки.Ссылка.Проведен
		|	И ЧекККМВозвратАкцизныеМарки.Ссылка.ВидОперации = &Возврат
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЧекЕГАИСАкцизныеМарки.Ссылка,
		|	ЧекЕГАИСАкцизныеМарки.Ссылка.Дата,
		|	ЧекЕГАИСАкцизныеМарки.Ссылка.Номер,
		|	&Продажа
		|ИЗ
		|	Документ.ЧекЕГАИС.АкцизныеМарки КАК ЧекЕГАИСАкцизныеМарки
		|ГДЕ
		|	(ЧекЕГАИСАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|			ИЛИ ЧекЕГАИСАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ЧекЕГАИСАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровАкцизныеМарки.Ссылка,
		|	РеализацияТоваровАкцизныеМарки.Ссылка.Дата,
		|	РеализацияТоваровАкцизныеМарки.Ссылка.Номер,
		|	&Продажа
		|ИЗ
		|	Документ.РеализацияТоваров.АкцизныеМарки КАК РеализацияТоваровАкцизныеМарки
		|ГДЕ
		|	(РеализацияТоваровАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|			ИЛИ РеализацияТоваровАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И РеализацияТоваровАкцизныеМарки.Ссылка.Проведен
		|	И РеализацияТоваровАкцизныеМарки.Ссылка.УдалитьПробитЧек
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЧекЕГАИСВозвратАкцизныеМарки.Ссылка,
		|	ЧекЕГАИСВозвратАкцизныеМарки.Ссылка.Дата,
		|	ЧекЕГАИСВозвратАкцизныеМарки.Ссылка.Номер,
		|	&Возврат
		|ИЗ
		|	Документ.ЧекЕГАИСВозврат.АкцизныеМарки КАК ЧекЕГАИСВозвратАкцизныеМарки
		|ГДЕ
		|	(ЧекЕГАИСВозвратАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|			ИЛИ ЧекЕГАИСВозвратАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ЧекЕГАИСВозвратАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка,
		|	ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка.Дата,
		|	ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка.Номер,
		|	&Возврат
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.АкцизныеМарки КАК ВозвратТоваровОтПокупателяАкцизныеМарки
		|ГДЕ
		|	(ВозвратТоваровОтПокупателяАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|			ИЛИ ВозвратТоваровОтПокупателяАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктСписанияЕГАИСАкцизныеМарки.Ссылка,
		|	АктСписанияЕГАИСАкцизныеМарки.Ссылка.Дата,
		|	АктСписанияЕГАИСАкцизныеМарки.Ссылка.Номер,
		|	&Продажа
		|ИЗ
		|	Документ.АктСписанияЕГАИС.АкцизныеМарки КАК АктСписанияЕГАИСАкцизныеМарки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
		|		ПО АктСписанияЕГАИСАкцизныеМарки.Ссылка = СтатусыДокументовЕГАИС.Документ
		|			И (НЕ СтатусыДокументовЕГАИС.Статус В (&НевыгруженныеСтатусы))
		|ГДЕ
		|	(АктСписанияЕГАИСАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|			ИЛИ АктСписанияЕГАИСАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И АктСписанияЕГАИСАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АкцизныеМаркиЕГАИС.Основание,
		|	АкцизныеМаркиЕГАИС.Основание.Дата,
		|	АкцизныеМаркиЕГАИС.Основание.Номер,
		|	&Продажа
		|ИЗ
		|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
		|ГДЕ
		|	АкцизныеМаркиЕГАИС.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки
		|	И АкцизныеМаркиЕГАИС.Статус = &Реализована
		|	И АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС = &ОрганизацияЕГАИС
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ,
		|	Ссылка УБЫВ";
		
		Если ОрганизацияЕГАИС = Неопределено Тогда 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС = &ОрганизацияЕГАИС", "");
		Иначе
			Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЕстьОшибка = Ложь;
			Если Операция = "Возврат" Тогда
				Если Выборка.ПоследняяОперация <> Перечисления.ВидыОперацийЧекККМ.Продажа Тогда
					ЕстьОшибка = Истина;
				КонецЕсли;
			Иначе
				Если Выборка.ПоследняяОперация = Перечисления.ВидыОперацийЧекККМ.Продажа
					ИЛИ Выборка.ПоследняяОперация = "Списание" Тогда
					ЕстьОшибка = Истина;
				КонецЕсли;
			КонецЕсли;
			Если ЕстьОшибка Тогда
				МассивСтрок = Новый Массив;
				МассивСтрок.Добавить(НСтр("ru = 'Считанная акцизная марка'"));
				МассивСтрок.Добавить(" " + КодАкцизнойМарки + " ");
				МассивСтрок.Добавить(НСтр("ru = 'ранее была учтена в документе:'"));
				МассивСтрок.Добавить(Символы.ПС);
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					ОбщегоНазначенияРТ.ПредставлениеДокумента(Выборка.Ссылка, Выборка.Номер, Выборка.Дата),,,,
					ПолучитьНавигационнуюСсылку(Выборка.Ссылка)));
				
				ТекстОшибки = Новый ФорматированнаяСтрока(МассивСтрок);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Операция = "АктПостановкиНаБаланс" Тогда
		НевыгруженныеСтатусы = Новый Массив;
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПередачи);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПроведенияЕГАИС);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.Отменен);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОшибка);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОшибка);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОшибка);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПринят);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОтмененКлиентом);
		НевыгруженныеСтатусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Отменен);
		
		Запрос.УстановитьПараметр("НевыгруженныеСтатусы", НевыгруженныеСтатусы);
		Запрос.УстановитьПараметр("Постановка", "Постановка");
		Запрос.УстановитьПараметр("Реализация", "Реализация");
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧекККМАкцизныеМарки.Ссылка КАК Ссылка,
		|	ЧекККМАкцизныеМарки.Ссылка.Дата КАК Дата,
		|	ЧекККМАкцизныеМарки.Ссылка.Номер КАК Номер,
		|	&Продажа КАК ПоследняяОперация
		|ИЗ
		|	Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМАкцизныеМарки
		|ГДЕ
		|	ЧекККМАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	И ЧекККМАкцизныеМарки.Ссылка.Проведен
		|	И ЧекККМАкцизныеМарки.Ссылка.ВидОперации = &Продажа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЧекККМВозвратАкцизныеМарки.Ссылка,
		|	ЧекККМВозвратАкцизныеМарки.Ссылка.Дата,
		|	ЧекККМВозвратАкцизныеМарки.Ссылка.Номер,
		|	&Возврат
		|ИЗ
		|	Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМВозвратАкцизныеМарки
		|ГДЕ
		|	ЧекККМВозвратАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	И ЧекККМВозвратАкцизныеМарки.Ссылка.Проведен
		|	И ЧекККМВозвратАкцизныеМарки.Ссылка.ВидОперации = &Возврат
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЧекЕГАИСАкцизныеМарки.Ссылка,
		|	ЧекЕГАИСАкцизныеМарки.Ссылка.Дата,
		|	ЧекЕГАИСАкцизныеМарки.Ссылка.Номер,
		|	&Продажа КАК ПоследняяОперация
		|ИЗ
		|	Документ.ЧекЕГАИС.АкцизныеМарки КАК ЧекЕГАИСАкцизныеМарки
		|ГДЕ
		|	(ЧекЕГАИСАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	ИЛИ ЧекЕГАИСАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ЧекЕГАИСАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЧекЕГАИСАкцизныеМарки.Ссылка,
		|	ЧекЕГАИСАкцизныеМарки.Ссылка.Дата,
		|	ЧекЕГАИСАкцизныеМарки.Ссылка.Номер,
		|	&Продажа КАК ПоследняяОперация
		|ИЗ
		|	Документ.ЧекЕГАИС.АкцизныеМарки КАК ЧекЕГАИСАкцизныеМарки
		|ГДЕ
		|	(ЧекЕГАИСАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	ИЛИ ЧекЕГАИСАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ЧекЕГАИСАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЧекЕГАИСВозвратАкцизныеМарки.Ссылка,
		|	ЧекЕГАИСВозвратАкцизныеМарки.Ссылка.Дата,
		|	ЧекЕГАИСВозвратАкцизныеМарки.Ссылка.Номер,
		|	&Возврат
		|ИЗ
		|	Документ.ЧекЕГАИСВозврат.АкцизныеМарки КАК ЧекЕГАИСВозвратАкцизныеМарки
		|ГДЕ
		|	(ЧекЕГАИСВозвратАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	ИЛИ ЧекЕГАИСВозвратАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ЧекЕГАИСВозвратАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка,
		|	ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка.Дата,
		|	ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка.Номер,
		|	&Возврат
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.АкцизныеМарки КАК ВозвратТоваровОтПокупателяАкцизныеМарки
		|ГДЕ
		|	(ВозвратТоваровОтПокупателяАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	ИЛИ ВозвратТоваровОтПокупателяАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ВозвратТоваровОтПокупателяАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПостановкиЕГАИСАкцизныеМарки.Ссылка,
		|	ПостановкиЕГАИСАкцизныеМарки.Ссылка.Дата,
		|	ПостановкиЕГАИСАкцизныеМарки.Ссылка.Номер,
		|	&Постановка
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС.АкцизныеМарки КАК ПостановкиЕГАИСАкцизныеМарки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
		|		ПО ПостановкиЕГАИСАкцизныеМарки.Ссылка = СтатусыДокументовЕГАИС.Документ
		|		И НЕ СтатусыДокументовЕГАИС.Статус В (&НевыгруженныеСтатусы)
		|ГДЕ
		|	(ПостановкиЕГАИСАкцизныеМарки.КодАкцизнойМарки = &КодАкцизнойМарки
		|	ИЛИ ПостановкиЕГАИСАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки)
		|	И ПостановкиЕГАИСАкцизныеМарки.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТТНИсходящаяЕГАИСАкцизныеМарки.Ссылка,
		|	ТТНИсходящаяЕГАИСАкцизныеМарки.Ссылка.Дата,
		|	ТТНИсходящаяЕГАИСАкцизныеМарки.Ссылка.Номер,
		|	&Реализация
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС.АкцизныеМарки КАК ТТНИсходящаяЕГАИСАкцизныеМарки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
		|		ПО ТТНИсходящаяЕГАИСАкцизныеМарки.Ссылка = СтатусыДокументовЕГАИС.Документ
		|ГДЕ
		|	ТТНИсходящаяЕГАИСАкцизныеМарки.АкцизнаяМарка.ЗначениеШтрихкода = &КодАкцизнойМарки
		|	И ТТНИсходящаяЕГАИСАкцизныеМарки.Ссылка.Проведен
		|	И НЕ СтатусыДокументовЕГАИС.Статус В (&НевыгруженныеСтатусы)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ,
		|	Ссылка УБЫВ";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.ПоследняяОперация = Перечисления.ВидыОперацийЧекККМ.Продажа
				ИЛИ Выборка.ПоследняяОперация = "Постановка"
				ИЛИ Выборка.ПоследняяОперация = "Реализация" Тогда
				МассивСтрок = Новый Массив;
				МассивСтрок.Добавить(НСтр("ru = 'Считанная акцизная марка ранее была учтена в документе:'"));
				МассивСтрок.Добавить(Символы.ПС);
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					ОбщегоНазначенияРТ.ПредставлениеДокумента(Выборка.Ссылка, Выборка.Номер, Выборка.Дата),,,,
					ПолучитьНавигационнуюСсылку(Выборка.Ссылка)));
				
				ТекстОшибки = Новый ФорматированнаяСтрока(МассивСтрок);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СлужебныеДанныеАлкогольнойПродукции(Товары, Результат) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.НомерСтроки,
	|	Т.ИдентификаторСтроки,
	|	Выразить(Т.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Выразить(Т.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	Выразить(Т.АлкогольнаяПродукция КАК Справочник.КлассификаторАлкогольнойПродукцииЕГАИС) КАК АлкогольнаяПродукция,
	|	Т.Справка2,
	|	Т.Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ НоменклатураЧастичногоВыбытия
	|ИЗ
	|	Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НастройкиЧастичногоВыбытияПродукцииИС
	|	ПО Товары.Номенклатура = НастройкиЧастичногоВыбытияПродукцииИС.НоменклатураЧастичногоВыбытия
	|		И Товары.Характеристика = НастройкиЧастичногоВыбытияПродукцииИС.ХарактеристикаЧастичногоВыбытия
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки          КАК НомерСтроки,
	|	Товары.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Товары.Справка2             КАК Справка2,
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	Товары.Количество           КАК Количество,
	|	0                           КАК КоличествоАкцизныхМарок,
	|	0                           КАК КоличествоПродукцииПоАкцизнымМаркам,
	|	ЕСТЬNULL(Товары.Справка2.Поштучная, ЛОЖЬ) КАК ПомарочныйУчет,
	|	ВЫБОР
	|		КОГДА Товары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			И Товары.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(Товары.Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
	|		ИНАЧЕ
	|			ЕСТЬNULL(Товары.АлкогольнаяПродукция.ВидПродукции.Маркируемый, ЛОЖЬ)
	|	КОНЕЦ КАК МаркируемаяПродукция,
	|	ЕСТЬNULL(Товары.АлкогольнаяПродукция.ТипПродукции, ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная))КАК ТипПродукции,
	|	ЕСТЬNULL(ОписаниеНоменклатурыИС.КоличествоВПотребительскойУпаковке, 1) КАК КоличествоВПотребительскойУпаковке,
	|	ЕСТЬNULL(ОписаниеНоменклатурыИС.ВариантЧастичногоВыбытия, НЕОПРЕДЕЛЕНО) КАК ВариантЧастичногоВыбытия,
	|	ЕСТЬNULL(ОписаниеНоменклатурыИС.УпаковкаЧастичногоВыбытия, НЕОПРЕДЕЛЕНО) КАК УпаковкаЧастичногоВыбытия,
	|	ЕСТЬNULL(ОписаниеНоменклатурыИС.ПотребительскаяУпаковка, НЕОПРЕДЕЛЕНО) КАК ПотребительскаяУпаковка,
	|	НЕ НоменклатураЧастичногоВыбытия.Номенклатура ЕСТЬ NULL КАК ЭтоНоменклатураЧастичногоВыбытия,
	|	ЕСТЬNULL(Товары.Номенклатура.ОбъемДАЛ, 0) КАК ОбъемДАЛ
	|ИЗ
	|	Товары КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
	|	ПО Товары.Номенклатура = ОписаниеНоменклатурыИС.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураЧастичногоВыбытия КАК НоменклатураЧастичногоВыбытия
	|	ПО Товары.Номенклатура = НоменклатураЧастичногоВыбытия.Номенклатура
	|		И Товары.Характеристика = НоменклатураЧастичногоВыбытия.Характеристика");
	
	Запрос.Параметры.Вставить("Товары", Товары);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат;
	
КонецПроцедуры

// Определяет, является ли номенклатура алкогольной маркируемой продукцией.
//
// Параметры:
//  Маркируемая  - Булево - признак маркируемой продукции (Истина если является)
//  Номенклатура - ОпределяемыйТип.Номенклатура - номенклатура.
Процедура ЗаполнитьПризнакМаркируемойПродукции(Маркируемая, Номенклатура) Экспорт
	
	Маркируемая = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ) КАК Маркируемый
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукции
	|		ПО Номенклатура.ВидАлкогольнойПродукцииЕГАИС = ВидыАлкогольнойПродукции.Ссылка
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Маркируемая = Выборка.Маркируемый;
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

Функция ШтрихкодыУпаковок(Источник, ОрганизацияЕГАИС, ИмяКолонкиАлкогольнаяПродукция = "АлкогольнаяПродукция", ПроверятьНаВидПродукции = Ложь) Экспорт
	
	ТаблицаШтрихкодов = Новый ТаблицаЗначений;
	ТаблицаШтрихкодов.Колонки.Добавить("ШтрихкодУпаковки",               Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаШтрихкодов.Колонки.Добавить("ШтрихкодРодительскойУпаковки",   Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаШтрихкодов.Колонки.Добавить("КлючСвязи",                      Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный)));
	ТаблицаШтрихкодов.Колонки.Добавить("АлкогольнаяПродукция",           Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("Справка2",                       Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("ОрганизацияЕГАИС",               Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеКоличество",     ОбщегоНазначения.ОписаниеТипаЧисло(18, 8));
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеВариантУчета",   Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыУчетаЧастичногоВыбытияИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеНоменклатура",   Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеХарактеристика", Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаШтрихкодов.Колонки.Добавить("ВыбытиеБутылки",                 Новый ОписаниеТипов("Булево"));
	
	Если ПроверятьНаВидПродукции Тогда
		СоответствиеШтрихкодовУпаковокИВидовПродукции = СоответствиеШтрихкодовУпаковокИВидовПродукции(
			Источник.АкцизныеМарки.Выгрузить().ВыгрузитьКолонку("АкцизнаяМарка"));
		
		ВидПродукции = Перечисления.ВидыПродукцииИС.Алкогольная;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Источник.АкцизныеМарки Цикл
		
		Если ПроверятьНаВидПродукции
			И СоответствиеШтрихкодовУпаковокИВидовПродукции[СтрокаТЧ.АкцизнаяМарка] <> ВидПродукции Тогда
				
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаШтрихкодов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
		НоваяСтрока.ШтрихкодУпаковки             = СтрокаТЧ.АкцизнаяМарка;
		НоваяСтрока.ШтрихкодРодительскойУпаковки = СтрокаТЧ.ШтрихкодУпаковки;
		НоваяСтрока.КлючСвязи                    = СтрокаТЧ.КлючСвязи;
		НоваяСтрока.Справка2                     = СтрокаТЧ.Справка2;
		НоваяСтрока.ОрганизацияЕГАИС             = ОрганизацияЕГАИС;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("КлючСвязи", СтрокаТЧ.КлючСвязи);
		НайденныеСтроки = Источник.Товары.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НоваяСтрока.АлкогольнаяПродукция = НайденныеСтроки[0][ИмяКолонкиАлкогольнаяПродукция];
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ИсходныеДанные.ОрганизацияЕГАИС               КАК ОрганизацияЕГАИС,
		|	ИсходныеДанные.КлючСвязи                      КАК ИдентификаторСтроки,
		|	ИсходныеДанные.АлкогольнаяПродукция           КАК АлкогольнаяПродукция,
		|	ИсходныеДанные.Справка2                       КАК Справка2,
		|	ИсходныеДанные.ШтрихкодУпаковки               КАК ШтрихкодУпаковки,
		|	ИсходныеДанные.ШтрихкодРодительскойУпаковки   КАК ШтрихкодРодительскойУпаковки,
		|	ИсходныеДанные.ЧастичноеВыбытиеКоличество     КАК ЧастичноеВыбытиеКоличество,
		|	ИсходныеДанные.ЧастичноеВыбытиеВариантУчета   КАК ЧастичноеВыбытиеВариантУчета,
		|	ИсходныеДанные.ЧастичноеВыбытиеНоменклатура   КАК ЧастичноеВыбытиеНоменклатура,
		|	ИсходныеДанные.ЧастичноеВыбытиеХарактеристика КАК ЧастичноеВыбытиеХарактеристика,
		|	ИсходныеДанные.ВыбытиеБутылки                 КАК ВыбытиеБутылки
		|ПОМЕСТИТЬ ВТВложенныеШтрихкодыИсходныеДанные
		|ИЗ
		|	&ИсходныеДанные КАК ИсходныеДанные");
	
	ПараметрыФормированияТекстаЗапроса = ШтрихкодированиеЕГАИС.ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов();
	ПараметрыФормированияТекстаЗапроса.ДокументСсылка                  = Неопределено;
	ПараметрыФормированияТекстаЗапроса.ИспользоватьИдентификаторСтроки = Истина;
	ПараметрыФормированияТекстаЗапроса.ИмяКолонкиСвязи = "КлючСвязи";
	
	ТекстыЗапроса.Добавить(
		ШтрихкодированиеЕГАИС.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
		"ВложенныеШтрихкоды");
	
	МенеджерВременнойТаблицы = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременнойТаблицы;
	Запрос.УстановитьПараметр("ИсходныеДанные", ТаблицаШтрихкодов);
	Запрос.УстановитьПараметр("ПустыеЗначенияНоменклатуры", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
	РезультатыЗапроса = ИнтеграцияИС.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
	
	ШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоВыборкеИМенеджеруВТ(
		РезультатыЗапроса.ВложенныеШтрихкоды.Выбрать(), МенеджерВременнойТаблицы);
	
	Возврат ШтрихкодыУпаковок;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Описание
// Определяет вид продукции по переданному списку штрихкодов упаковок.
// Параметры:
// 	ШтрихкодыУпаковок - Массив - Список типа СправочникСсылка.ШтрихкодыУпаковокТоваров
// Возвращаемое значение:
// 	Соответствие - Соответствие СправочникСсылка.ШтрихкодыУпаковокТоваров:ПеречислениеСсылка.ВидыПродукцииИС.
Функция СоответствиеШтрихкодовУпаковокИВидовПродукции(ШтрихкодыУпаковок)
	
	Результат = Новый Соответствие;
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтрихкодыУпаковок.Ссылка КАК ШтрихкодСсылка,
		|	ШтрихкодыУпаковок.Номенклатура,
		|	ШтрихкодыУпаковок.Характеристика,
		|	ШтрихкодыУпаковок.Серия
		|ПОМЕСТИТЬ ДанныеШтрихкодовУпаковок
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковок
		|ГДЕ
		|	ШтрихкодыУпаковок.Ссылка В (&ШтрихкодыУпаковок)
		|	И ШтрихкодыУпаковок.Номенклатура <> &ПустаяНоменклатура
		|");
	
	СписокЗапросов.Добавить(
		ШтрихкодированиеОбщегоНазначенияИС.ТекстЗапросаСвойстваМаркируемойПродукции());
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ДанныеШтрихкодовУпаковок.ШтрихкодСсылка   КАК ШтрихкодСсылка,
		|	СвойстваМаркируемойПродукции.ВидПродукции КАК ВидПродукции
		|ИЗ
		|	ДанныеШтрихкодовУпаковок КАК ДанныеШтрихкодовУпаковок
		|		ЛЕВОЕ СОЕДИНЕНИЕ СвойстваМаркируемойПродукции КАК СвойстваМаркируемойПродукции
		|		ПО ДанныеШтрихкодовУпаковок.Номенклатура = СвойстваМаркируемойПродукции.Номенклатура",
		"ВидыПродукцииПоШтрихкодам");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШтрихкодыУпаковок",  ШтрихкодыУпаковок);
	Запрос.УстановитьПараметр("ПустаяНоменклатура", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Номенклатура"));
	
	РезультатыЗапроса = ИнтеграцияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов, Ложь);
	
	Выборка = РезультатыЗапроса["ВидыПродукцииПоШтрихкодам"].Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ШтрихкодСсылка, Выборка.ВидПродукции);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти