
#Область ОбработчикиСобытийЭлементовФормы

// См. СобытияФормВЕТИСПереопределяемый.ПриИзмененииНоменклатуры
// Выполняет действия при изменении номенклатуры в таблице Товары.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма, в которой произошло событие.
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - Строка таблицы товаров.
//  КэшированныеЗначения - Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//  ПараметрыЗаполнения - (См. ИнтеграцияВЕТИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти).
//  ПараметрыУказанияСерий - Структура - Состав полей определен в функции 
//                                       НоменклатураКлиентСервер.ПараметрыУказанияСерий.
Процедура ПриИзмененииНоменклатуры(Форма,
								ТекущаяСтрока,
								КэшированныеЗначения,
								ПараметрыЗаполнения,
								ПараметрыУказанияСерий = Неопределено) Экспорт
	
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ЗаполнитьПродукциюВЕТИС",                    ПараметрыЗаполнения);
	
	Если ПараметрыЗаполнения.Свойство("ПересчитатьКоличествоЕдиницВЕТИС") 
		И ПараметрыЗаполнения.ПересчитатьКоличествоЕдиницВЕТИС Тогда
		ПересчитатьКоличествоЕдиницВЕТИС = Новый Структура("ЕдиницаИзмеренияВЕТИС, Суффикс");
		ЗаполнитьЗначенияСвойств(ПересчитатьКоличествоЕдиницВЕТИС, ПараметрыЗаполнения);
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиницВЕТИС", ПересчитатьКоличествоЕдиницВЕТИС);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("ПересчитатьКоличествоЕдиницПоВЕТИС")
		И ПараметрыЗаполнения.ПересчитатьКоличествоЕдиницПоВЕТИС Тогда
		ПересчитатьКоличествоЕдиницПоВЕТИС = Новый Структура("ЕдиницаИзмеренияВЕТИС, Суффикс");
		ЗаполнитьЗначенияСвойств(ПересчитатьКоличествоЕдиницПоВЕТИС, ПараметрыЗаполнения);
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиницПоВЕТИС", ПересчитатьКоличествоЕдиницПоВЕТИС);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("ПересчитатьКоличествоЕдиницВЕТИСПоЕдиницеИзмерения")
		И ПараметрыЗаполнения.ПересчитатьКоличествоЕдиницВЕТИСПоЕдиницеИзмерения Тогда
		ПересчитатьКоличествоЕдиницВЕТИСПоЕдиницеИзмерения = Новый Структура("ЕдиницаИзмеренияВЕТИС, ИмяПоляКоличества");
		ЗаполнитьЗначенияСвойств(ПересчитатьКоличествоЕдиницВЕТИСПоЕдиницеИзмерения, ПараметрыЗаполнения);
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиницВЕТИСПоЕдиницеИзмерения",
									ПересчитатьКоличествоЕдиницВЕТИСПоЕдиницеИзмерения);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("ПроверитьСериюРассчитатьСтатус")
		И ПараметрыЗаполнения.ПроверитьСериюРассчитатьСтатус Тогда
		
		Если ПараметрыУказанияСерий <> Неопределено
			И ЗначениеЗаполнено(ПараметрыУказанияСерий.ИмяПоляСклад)
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма[ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта], ПараметрыУказанияСерий.ИмяПоляСклад) Тогда
			
			Склад = Форма[ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта][ПараметрыУказанияСерий.ИмяПоляСклад];
			
		Иначе
			Склад = Неопределено;
		КонецЕсли;
		
		ПроверитьСериюРассчитатьСтатус = Новый Структура("ПараметрыУказанияСерий, Склад", ПараметрыУказанияСерий, Склад);
		
		СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", ПроверитьСериюРассчитатьСтатус);
		
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	Если ТипЗнч(Форма) = Тип("УправляемаяФорма") Тогда
		Если Форма.ИмяФормы = "РегистрСведений.СоответствиеНоменклатурыВЕТИС.Форма.ФормаЗаписи" Тогда
		
			Форма.ХарактеристикиИспользуются = ТекущаяСтрока.ХарактеристикиИспользуются;
		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// См. СобытияФормВЕТИСПереопределяемый.ПриИзмененииХарактеристики
// Выполняет действия при изменении номенклатуры в таблице Товары.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма, в которой произошло событие.
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - Строка таблицы товаров.
//  КэшированныеЗначения - Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//  ПараметрыЗаполнения - (См. ИнтеграцияВЕТИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти).
//  ПараметрыУказанияСерий - Структура - Состав полей определен в функции 
//                                       НоменклатураКлиентСервер.ПараметрыУказанияСерий.
Процедура ПриИзмененииХарактеристики(Форма,
								ТекущаяСтрока,
								КэшированныеЗначения,
								ПараметрыЗаполнения,
								ПараметрыУказанияСерий = Неопределено) Экспорт
	
	ИмяТЧ = ПараметрыЗаполнения.ИмяТабличнойЧасти;
	
	ХарактеристикаПриИзмененииПереопределяемый = Новый Структура("ИмяФормы, ИмяТабличнойЧасти", Форма.ИмяФормы, ИмяТЧ);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ХарактеристикаПриИзмененииПереопределяемый", ХарактеристикаПриИзмененииПереопределяемый);
	СтруктураДействий.Вставить("ЗаполнитьПродукциюВЕТИС",                    ПараметрыЗаполнения);
	
	ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	Возврат;
	
КонецПроцедуры

// См. СобытияФормВЕТИСПереопределяемый.ПриИзмененииКоличестваВЕТИС
// Выполняет действия при изменении количества ВЕТИС в таблице Товары.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма, в которой произошло событие.
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - Строка таблицы товаров.
//  КэшированныеЗначения - Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//  ПараметрыЗаполнения - (См. ИнтеграцияВЕТИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти).
Процедура ПриИзмененииКоличестваВЕТИС(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	СтруктураДействий = Новый Структура;
	
	Если ПараметрыЗаполнения.ПересчитатьКоличествоЕдиницПоВЕТИС Тогда
		ПересчитатьКоличествоЕдиницПоВЕТИС = Новый Структура("ЕдиницаИзмеренияВЕТИС, Суффикс");
		ЗаполнитьЗначенияСвойств(ПересчитатьКоличествоЕдиницПоВЕТИС, ПараметрыЗаполнения);
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиницПоВЕТИС", ПересчитатьКоличествоЕдиницПоВЕТИС);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

// См. СобытияФормВЕТИСПереопределяемый.ПриИзмененииКоличества
// Выполняет действия при изменении количества в таблице Товары.
//
// Параметры:
//	Форма - УправляемаяФорма - Форма, в которой произошло событие.
//	ТекущаяСтрока - ДанныеФормыЭлементКоллекции - Строка таблицы товаров.
//	КэшированныеЗначения - Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//	ПараметрыЗаполнения - (См. ИнтеграцияВЕТИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти).
//
Процедура ПриИзмененииКоличества(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	СтруктураДействий = Новый Структура;
	
	Если ПараметрыЗаполнения.ПересчитатьКоличествоЕдиницВЕТИС Тогда
		ПересчитатьКоличествоЕдиницВЕТИС = Новый Структура("ЕдиницаИзмеренияВЕТИС, Суффикс");
		ЗаполнитьЗначенияСвойств(ПересчитатьКоличествоЕдиницВЕТИС, ПараметрыЗаполнения);
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиницВЕТИС", ПересчитатьКоличествоЕдиницВЕТИС);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// См. СобытияФормВЕТИСПереопределяемый.ОбработкаРезультатаПодбораНоменклатуры
// Заполняет табличную часть Товары подобранными товарами.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой производится подбор,
//  ВыбранноеЗначение - Произвольный - данные, содержащие подобранную пользователем номенклатуру,
//  ПараметрыЗаполнения - Структура - см. функцию СобытияФормВЕТИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//  ПараметрыУказанияСерий - Структура - Состав полей определен в функции 
//                                     НоменклатураКлиентСервер.ПараметрыУказанияСерий.
Процедура ОбработкаРезультатаПодбораНоменклатуры(Форма,
												ВыбранноеЗначение,
												ПараметрыЗаполнения,
												ПараметрыУказанияСерий = Неопределено) Экспорт
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресТоваровВХранилище);
	КэшированныеЗначения = ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураКэшируемыхЗначений();
	
	ИмяТабличнойЧасти = Неопределено;
	Если НЕ ПараметрыЗаполнения.Свойство("ИмяТабличнойЧасти", ИмяТабличнойЧасти) Тогда
		ИмяТабличнойЧасти = "Товары";
	КонецЕсли;
	
	ТекущаяСтрока = Неопределено;
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Форма.Объект[ИмяТабличнойЧасти].Добавить();
		
		СписокСвойств = "Номенклатура, Характеристика";
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, СписокСвойств);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
		
		Если ПараметрыЗаполнения.Свойство("ЗаполнитьПродукциюВЕТИС") Тогда
			СтруктураДействий.Вставить("ЗаполнитьПродукциюВЕТИС", ПараметрыЗаполнения);
		КонецЕсли;
		
		СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый",
			Новый Структура("ИмяФормы, ИмяТабличнойЧасти", Форма.ИмяФормы, ИмяТабличнойЧасти));
		
		ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Форма.Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
