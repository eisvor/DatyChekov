
#Область СлужебныеПроцедурыИФункции

// Инициализировать структуру параметров запроса для получения ключа сессии.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * Организация          - ОпределяемыйТип.Организация - Организация от имени которой необходимо авторизоваться.
// * ПредставлениеСервиса - Строка                      - Представление сервиса.
// * Сервер               - Строка                      - Адрес сервера, например: stable.ismotp.crpt.ru
// * Порт                 - Число                       - Например: 443.
// * АдресЗапросаКлючаСессии           - Строка - Например: api/v3/auth/cert/
// * АдресЗапросаПараметровАвторизации - Строка - Например: api/v3/auth/cert/key
//
Функция ПараметрыЗапросаКлючаСессииИСМПТ() Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ПредставлениеСервиса",            "");
	ПараметрыЗапроса.Вставить("Организация",                     Неопределено);
	
	ПараметрыЗапроса.Вставить("Сервер",                           "");
	ПараметрыЗапроса.Вставить("Порт",                             443);
	ПараметрыЗапроса.Вставить("Таймаут",                          60);
	ПараметрыЗапроса.Вставить("ИспользоватьЗащищенноеСоединение", Истина);
	
	ПараметрыЗапроса.Вставить("ИмяПараметраСеанса",                "");
	ПараметрыЗапроса.Вставить("АдресЗапросаПараметровАвторизации", "api/v3/auth/cert/key");
	ПараметрыЗапроса.Вставить("АдресЗапросаКлючаСессии",           "api/v3/auth/cert/");
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Инициализировать структуру параметров запроса в ИС ЦЭДМ для получения ключа сессии.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * Организация          - ОпределяемыйТип.Организация - Организация от имени которой необходимо авторизоваться.
// * ПредставлениеСервиса - Строка                      - Представление сервиса, например: ИС ЦЭДМ.
// * Сервер               - Строка                      - Адрес сервера
// * Порт                 - Число                       - Например: 443.
// * АдресЗапросаКлючаСессии           - Строка - Например: api/v3/auth/cert/
// * АдресЗапросаПараметровАвторизации - Строка - Например: api/v3/auth/cert/key
//
Функция ПараметрыЗапросаКлючаСессииИСЦЭДМ() Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ПредставлениеСервиса",    "");
	ПараметрыЗапроса.Вставить("ПрофильДоступаКЭДО",      Неопределено);
	
	ПараметрыЗапроса.Вставить("Сервер",                  "");
	ПараметрыЗапроса.Вставить("Порт",                    443);
	ПараметрыЗапроса.Вставить("Таймаут",                 60);
	ПараметрыЗапроса.Вставить("ИспользоватьЗащищенноеСоединение", Истина);
	
	ПараметрыЗапроса.Вставить("ИмяПараметраСеанса",      "");
	ПараметрыЗапроса.Вставить("АдресЗапросаКлючаСессии", "apiUot/api/v1/private/get-token");
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет индекс картинки типа упаковки для строки дерева маркируемой продукции.
// 
// Параметры:
// 	ТекущаяСтрока - ДанныеФормыЭлементДерева - строка дерева, содержащая данные упаковки или маркируемой продукции.
//
Процедура УстановитьИндексКартинкиТипаУпаковки(ТекущаяСтрока) Экспорт
	
	ВидУпаковки  = ТекущаяСтрока.ВидУпаковки;
	ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ПустаяСсылка");
	Попытка
		ВидПродукции = ТекущаяСтрока.ВидПродукции;
	Исключение
	КонецПопытки;
	
	Если ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда
		Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 7;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Табачная") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 1;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Алкогольная") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 11;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.БезалкогольныеНапитки") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 15;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛегкаяПромышленность") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 13;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 9;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 17;
		Иначе
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 6;
		КонецЕсли;
		
	ИначеЕсли ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда
		
		Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Обувная") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 8;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Табачная") Тогда 
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 2;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.Алкогольная") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 12;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.БезалкогольныеНапитки") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 16;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛегкаяПромышленность") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 14;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.МолочнаяПродукция") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 10;
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИСМПТК.ЛекарственныеПрепараты") Тогда
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 18;
		Иначе
			ТекущаяСтрока.ИндексКартинкиТипУпаковки = 6;
		КонецЕсли;
			
	ИначеЕсли ТекущаяСтрока.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Логистическая") Тогда
		
		ТекущаяСтрока.ИндексКартинкиТипУпаковки = 0;
		
	Иначе
		
		ТекущаяСтрока.ИндексКартинкиТипУпаковки = 6;
		
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьИндексКартинкиСостояниеСтрокиКМ(ТекущаяСтрока, Организация) Экспорт
	
	ТекущийВладелец			= ТекущаяСтрока.Владелец;
	ВыполненЗапросВладельца = ТекущаяСтрока.ВыполненЗапросВладельца;
	
	Если ВыполненЗапросВладельца И ТекущийВладелец = Организация Тогда
		ТекущаяСтрока.ИндексКартинкиСостояниеСтроки = 1;
	ИначеЕсли ВыполненЗапросВладельца Тогда
		ТекущаяСтрока.ИндексКартинкиСостояниеСтроки = 2;
	Иначе
		ТекущаяСтрока.ИндексКартинкиСостояниеСтроки = 0;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти