////////////////////////////////////////////////////////////////////////////////
// ПодборТоваровКлиентСервер содержит процедуры и функции для работы с 
// функциональностью подбора товаров.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура установки отбора по видам номенклатуры.
//
// Параметры:
//  МассивКатегорий - Массив - категории номенклатуры.
//  ПодборТаблицаНоменклатуры - ОтборКомпоновкиДанных - отбор номенклатур.
//  ПодборТаблицаХарактеристик - ОтборКомпоновкиДанных - отбор характеристик.
//
Процедура УстановитьОтборПоКатегориям(МассивКатегорий, ПодборТаблицаНоменклатура, ПодборТаблицаХарактеристики) Экспорт
	
	ПоискПоКатегориям = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ПодборТаблицаНоменклатура.Отбор.Элементы,
		"ПоискПоКатегориям",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	Для Каждого Категория Из МассивКатегорий Цикл
		
		ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
			ПоискПоКатегориям,
			ИмяПоля("Номенклатура.[%ИмяПоля%]", Категория),
			Истина,
			Истина, ВидСравненияКомпоновкиДанных.Равно);
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура установки отбора по видам номенклатуры.
//
// Параметры:
//  ДанныеОтбора - ГруппаЭлементовОтбораКомпоновкиДанных - категории номенклатуры.
//  ПодборТаблицаНоменклатуры - ОтборКомпоновкиДанных - отбор номенклатур.
//  ПодборТаблицаХарактеристик - ОтборКомпоновкиДанных - отбор характеристик.
//
Процедура УстановитьОтборПоВидамНоменклатуры(ДанныеОтбора, ПодборТаблицаНоменклатура, ПодборТаблицаХарактеристики) Экспорт
	
	ЭлементыОтбора = ПодборТаблицаНоменклатура.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	ПоискПоСвойствамВТаблицеНоменклатуры = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ЭлементыОтбора,
		"ПоискПоСвойствамФильтрТаблицыОстатков",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ПоискПоСвойствамВТаблицеНоменклатурыОсновная = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ЭлементыОтбора,
		"ПоискПоСвойствамФильтрОсновнойТаблицы",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ПоискПоСвойствамХарактеристикВТаблицеХарактеристик = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ЭлементыОтбора,
		"ПоискПоСвойствамФильтрТаблицыОстатков",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	Счетчик = 0;
	Для каждого ЭлементДанных Из ДанныеОтбора Цикл
		
		Счетчик = Счетчик + 1;
		
		ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатуры = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
			ПоискПоСвойствамВТаблицеНоменклатуры.Элементы,
			"ПоискПоСвойствам" + Счетчик,
			ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыОсновная = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
			ПоискПоСвойствамВТаблицеНоменклатурыОсновная.Элементы,
			"ПоискПоСвойствам" + Счетчик,
			ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		ПоискПоСвойствамВидаНоменклатурыВТаблицеХарактеристик = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
			ПоискПоСвойствамХарактеристикВТаблицеХарактеристик.Элементы,
			"ПоискПоСвойствам" + Счетчик,
			ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		// Перечисление свойств
		Для каждого СтруктураЗначенияСвойств Из ЭлементДанных.ЗначенияСвойств Цикл
			
			ИмяПоляНоменклатураОтбор = "НоменклатураОтбор.[%ИмяПоля%]";
			ИмяПоляНоменклатураОтбор = СтрЗаменить(ИмяПоляНоменклатураОтбор, "%ИмяПоля%", СтруктураЗначенияСвойств.Свойство);
			
			ИмяПоляНоменклатура = "Номенклатура.[%ИмяПоля%]";
			ИмяПоляНоменклатура = СтрЗаменить(ИмяПоляНоменклатура, "%ИмяПоля%", СтруктураЗначенияСвойств.Свойство);
			
			ИмяПоляХарактеристикаОтбор = "ХарактеристикаОтбор.[%ИмяПоля%]";
			ИмяПоляХарактеристикаОтбор = СтрЗаменить(ИмяПоляХарактеристикаОтбор, "%ИмяПоля%", СтруктураЗначенияСвойств.Свойство);
			
			ИмяПоляХарактеристика = "Характеристика.[%ИмяПоля%]";
			ИмяПоляХарактеристика = СтрЗаменить(ИмяПоляХарактеристика, "%ИмяПоля%", СтруктураЗначенияСвойств.Свойство);
			
			Если СтруктураЗначенияСвойств.ДобавитьНеЗаполнено Тогда
				
				ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыВложеннаяИЛИ = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
					ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатуры.Элементы,
					"ПоискПоСвойствам" + Счетчик,
					ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
				
				ПоискПоСвойствамВидаНоменклатурыВТаблицеХарактеристикВложеннаяИЛИ = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
					ПоискПоСвойствамВидаНоменклатурыВТаблицеХарактеристик.Элементы,
					"ПоискПоСвойствам" + Счетчик,
					ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
				
				// Основная
				ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыОсновнаяВложеннаяИЛИ = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
					ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыОсновная.Элементы,
					"ПоискПоСвойствам" + Счетчик,
					ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
				
			КонецЕсли;
			
			Если СтруктураЗначенияСвойств.СвойствоХарактеристики Тогда
				
				ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
					?(СтруктураЗначенияСвойств.ДобавитьНеЗаполнено, ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыВложеннаяИЛИ, ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатуры),
					ИмяПоляХарактеристикаОтбор,
					СтруктураЗначенияСвойств.Значение,
					Истина, ВидСравненияКомпоновкиДанных.ВСписке);
				
				ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
					?(СтруктураЗначенияСвойств.ДобавитьНеЗаполнено, ПоискПоСвойствамВидаНоменклатурыВТаблицеХарактеристикВложеннаяИЛИ, ПоискПоСвойствамВидаНоменклатурыВТаблицеХарактеристик),
					ИмяПоляХарактеристика,
					СтруктураЗначенияСвойств.Значение,
					Истина, ВидСравненияКомпоновкиДанных.ВСписке);
				
				Если СтруктураЗначенияСвойств.ДобавитьНеЗаполнено Тогда
				
					ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
						ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыВложеннаяИЛИ,
						ИмяПоляХарактеристикаОтбор,
						,
						Истина, ВидСравненияКомпоновкиДанных.НеЗаполнено);
					
					ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
						ПоискПоСвойствамВидаНоменклатурыВТаблицеХарактеристикВложеннаяИЛИ,
						ИмяПоляХарактеристика,
						,
						Истина, ВидСравненияКомпоновкиДанных.НеЗаполнено);
				
				КонецЕсли;
				
			Иначе
				
				ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
					?(СтруктураЗначенияСвойств.ДобавитьНеЗаполнено, ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыВложеннаяИЛИ, ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатуры),
					ИмяПоляНоменклатураОтбор,
					СтруктураЗначенияСвойств.Значение,
					Истина, ВидСравненияКомпоновкиДанных.ВСписке);
				
				Если СтруктураЗначенияСвойств.ДобавитьНеЗаполнено Тогда
					ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
						ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыВложеннаяИЛИ,
						ИмяПоляНоменклатураОтбор,
						Неопределено,
						Истина, ВидСравненияКомпоновкиДанных.НеЗаполнено);
				КонецЕсли;
				
				ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
					?(СтруктураЗначенияСвойств.ДобавитьНеЗаполнено, ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыОсновнаяВложеннаяИЛИ, ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыОсновная),
					ИмяПоляНоменклатура,
					СтруктураЗначенияСвойств.Значение,
					Истина, ВидСравненияКомпоновкиДанных.ВСписке);
				
				Если СтруктураЗначенияСвойств.ДобавитьНеЗаполнено Тогда
					ОтборыСписковКлиентСервер.УстановитьЭлементОтбораГруппыСписка(
						ПоискПоСвойствамВидаНоменклатурыВТаблицеНоменклатурыОсновнаяВложеннаяИЛИ,
						ИмяПоляНоменклатура,
						Неопределено,
						Истина, ВидСравненияКомпоновкиДанных.НеЗаполнено);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла; // Цикл по свойствам вида номенклатуры.
		
	КонецЦикла; // Цикл по видам номенклатуры.
	
КонецПроцедуры

// Формирует заголовок формы подбора, состоящий из заголовка формы и представления документа.
//
// Параметры:
//  Заголовок - Строка - заголовок формы.
//  Документ  - ДокументСсылка - ссылка на документ, из которого открывается подбор.
//
// Возвращаемое значение:
//  Строка - заголовок формы подбора
//
Функция СформироватьЗаголовокФормыПодбора(Заголовок, Документ) Экспорт
	
	Если Документ <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(Документ) Тогда
			
			Заголовок = Заголовок + ": " + Документ;
			
		Иначе
			
			ТекстДокумент = НСтр("ru='%ТипДокумента% (новый)'");
			ТекстДокумент = СтрЗаменить(ТекстДокумент, "%ТипДокумента%", Документ.Метаданные().Синоним);
			Заголовок = Заголовок + ": " + ТекстДокумент;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции // СформироватьЗаголовокПодбора()

// Формирует заголовок формы подбора, состоящий из заголовка формы и представления документа.
//
// Параметры:
// ИнформационнаяНадпись - Строка - реквизит, в который будет помещена информационная надпись.
// ТаблицаТоваров        - ДанныеФормыКоллекция - таблица, по которой будет подсчитано количество и сумма подобранных
//                                                товаров.
//
Процедура СформироватьИнформационнуюНадписьПодобранныхТоваров(ИнформационнаяНадпись, ТаблицаТоваров) Экспорт
	
	ВыбранныеСтроки = ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина));
	
	КоличествоТоваров = ВыбранныеСтроки.Количество();
	
	СуммаТоваров = 0;
	Для Каждого ТекСтрока Из ТаблицаТоваров Цикл
		Если ТекСтрока.Выбран Тогда
			СуммаТоваров = СуммаТоваров + ТекСтрока.СуммаВсего;
		КонецЕсли;
	КонецЦикла;
	
	ИнформационнаяНадпись = НСтр("ru = 'Всего подобрано позиций %КоличествоТоваров% на сумму %СуммаТоваров% %Валюта%'");
	ИнформационнаяНадпись = СтрЗаменить(ИнформационнаяНадпись,"%КоличествоТоваров%", Формат(КоличествоТоваров, "ЧН=0"));
	ИнформационнаяНадпись = СтрЗаменить(ИнформационнаяНадпись,"%СуммаТоваров%",      Формат(СуммаТоваров, "ЧЦ=15; ЧДЦ=2; ЧН=0,00"));
	ИнформационнаяНадпись = СтрЗаменить(ИнформационнаяНадпись,"%Валюта%",            ОбщегоНазначенияРТКлиентСервер.ПредставлениеВалютыУчета().КодСимвольный);
	
КонецПроцедуры // СформироватьИнформационнуюНадписьПодобранныхТоваров()

// Проверяет, является ли форма формой подбора.
//
// Параметры:
//	Форма - УправляемаяФорма - форма подбора, форма списка.
//
// Возвращаемое значение:
//	Булево - истина, если это любая из форм подбора.
//
Функция ЭтоФормаПодбора(Форма) Экспорт
	
	КодФормы = Форма.КодФормы;
	
	Если КодФормы = "Обработка_ПодборТоваровВДокументПродажи_Форма"
		Или КодФормы = "Обработка_ПодборТоваровВДокументЗакупки_Форма" 
		Или КодФормы = "Обработка_ПодборПоТоварнымКатегориям_Форма" Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет, является ли формой подбора в документы закупки.
//
// Параметры:
//	Форма - УправляемаяФорма - форма подбора, форма списка.
//
// Возвращаемое значение:
//	Булево - Истина, если это форма подбора в документы закупки.
//
Функция ЭтоФормаПодбораВДокументыЗакупки(Форма) Экспорт
	
	Возврат (Форма.КодФормы = "Обработка_ПодборТоваровВДокументЗакупки_Форма");
	
КонецФункции

// Проверяет, является ли формой подбора в документы продажи.
//
// Параметры:
//	Форма - УправляемаяФорма - форма подбора, форма списка.
//
// Возвращаемое значение:
//	Булево - истина, если это форма подбора в документы продажи.
//
Функция ЭтоФормаПодбораВДокументыПродажи(Форма) Экспорт
	
	Возврат (Форма.КодФормы = "Обработка_ПодборТоваровВДокументПродажи_Форма");
	
КонецФункции

// Проверяет, является ли формой выбора справочника "Характеристики номенклатуры".
//
// Параметры:
//	Форма - УправляемаяФорма - форма.
//
// Возвращаемое значение:
//	Булево - истина, если это форма выбора характеристики.
//
Функция ЭтоФормаВыбораХарактеристик(Форма) Экспорт
	
	Возврат (Форма.КодФормы = "Справочник_ХарактеристикиНоменклатуры_ФормаВыбора");
	
КонецФункции

// Проверяет, является ли формой выбора справочника "Номенклатура".
//
// Параметры:
//	Форма - УправляемаяФорма - форма.
//
// Возвращаемое значение:
//	Булево - истина, если это форма выбора номенклатуры.
//
Функция ЭтоФормаВыбораНоменклатуры(Форма) Экспорт
	
	Возврат (Форма.КодФормы = "Справочник_Номенклатура_ФормаВыбора");
	
КонецФункции

// Проверяет, является ли формой списка справочника "Номенклатура".
//
// Параметры:
//	Форма - УправляемаяФорма - форма.
//
// Возвращаемое значение:
//	Булево - Истина, если это форма списка номенклатуры.
//
Функция ЭтоФормаСпискаНоменклатуры(Форма) Экспорт
	
	Возврат (Форма.КодФормы = "Справочник_Номенклатура_ФормаСписка");
	
КонецФункции

// Проверяет, является ли формой подобных товаров.
//
// Параметры:
//	Форма - УправляемаяФорма - форма.
//
// Возвращаемое значение:
//	Булево - истина, если это форма подобных товаров номенклатуры.
//
Функция ЭтоФормаПодобныеТоварыНоменклатуры(Форма) Экспорт
	
	Возврат (Форма.КодФормы = "Справочник_Номенклатура_ПодобныеТовары");
	
КонецФункции

// Проверяет, является ли формой подбора по товарным категориям.
//
// Параметры:
//	Форма - УправляемаяФорма - форма подбора товаров по товарным категориям.
//
// Возвращаемое значение:
//	Булево - Истина, если это форма подбора товаров по категориям.
//
Функция ЭтоФормаПодбораТоваровПоКатегориям(Форма) Экспорт
	
	Возврат (Форма.КодФормы = "Обработка_ПодборПоТоварнымКатегориям_Форма");
	
КонецФункции

// Проверяет, является ли формой прайс-листа.
//
// Параметры:
//	Форма - УправляемаяФорма - форма обработки.
//
// Возвращаемое значение:
//	Булево - Истина, если это форма прайс-листа.
//
Функция ЭтоФормаПрайсЛиста(Форма) Экспорт
	
	КодФормы = Форма.КодФормы;
	
	Если КодФормы = "Обработка_ПрайсЛист_Форма"
		Или КодФормы = "Обработка_ПрайсЛист_ФормаНастройки" Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// Проверяет, является ли формой прайс-листа.
//
// Параметры:
//	Форма - УправляемаяФорма - форма обработки.
//
// Возвращаемое значение:
//	Булево - Истина, если это форма прайс-листа.
//
Функция ЭтоФормаПрайсЛистаПоставщиков(Форма) Экспорт
	КодФормы = Форма.КодФормы;
	
	Если КодФормы = "Обработка_ПрайсЛистПоставщика_Форма"
		Или КодФормы = "Обработка_ПрайсЛистПоставщика_ФормаНастройки" Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// Устанавливает значения доступности элементов фильтров в зависимости от значения
// флажка использования фильтров.
//
// Параметры:
//	Форма - УправляемаяФорма - форма списка номенклатуры или форма подбора.
//
Процедура УстановитьДоступностьЭлементовФильтров(Форма) Экспорт
	
	ЭтоФормаПодбора = ПодборТоваровКлиентСервер.ЭтоФормаПодбора(Форма);
	ЭтоФормаВыбораХарактеристик = ПодборТоваровКлиентСервер.ЭтоФормаВыбораХарактеристик(Форма);
	ЭтоФормаСпискаНоменклатуры = ПодборТоваровКлиентСервер.ЭтоФормаСпискаНоменклатуры(Форма);
	ЭтоФормаВыбораНоменклатуры = ПодборТоваровКлиентСервер.ЭтоФормаВыбораНоменклатуры(Форма);
	
	Если Не ЭтоФормаВыбораХарактеристик Тогда
		
		Форма.Элементы.ИерархияНоменклатуры.Доступность = Форма.ИспользоватьФильтры;
		Если Не ПодборТоваровКлиентСервер.ЭтоФормаПодобныеТоварыНоменклатуры(Форма) Тогда
			Форма.Элементы.ВидыНоменклатуры.Доступность = Форма.ИспользоватьФильтры;
		КонецЕсли;
		Форма.Элементы.ВидНоменклатуры.Доступность = Форма.ИспользоватьФильтры;
		Форма.Элементы.ДеревоОтборов.Доступность = Форма.ИспользоватьФильтры;
		
	Иначе
		
		Форма.Элементы.ДеревоОтборов.Доступность = Форма.ИспользоватьФильтры;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает видимость шапки. Если колонка одна, шапка отключается.
// Параметры:
//  ТаблицаФормы - Тип ТаблицаФормы
Процедура УстановитьВидимостьШапкиТаблицаФормы(ТаблицаФормы) Экспорт
	
	Если ТаблицаФормы.Видимость Тогда
		КоличествоВидимыйКолонок = 0;
		Для каждого ПодчиненныйЭлемент Из ТаблицаФормы.ПодчиненныеЭлементы Цикл
			
			Если ПодчиненныйЭлемент.Видимость Тогда
				КоличествоВидимыйКолонок = КоличествоВидимыйКолонок + 1;
			КонецЕсли;
			
			Если КоличествоВидимыйКолонок >= 2 Тогда
				ТаблицаФормы.Шапка = Истина;
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
		ТаблицаФормы.Шапка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяПоля(Шаблон, ИмяПоля)
	Возврат СтрЗаменить(Шаблон, "%ИмяПоля%", ИмяПоля);
КонецФункции

#КонецОбласти