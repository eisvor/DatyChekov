////////////////////////////////////////////////////////////////////////////////
// МаркетинговыеАкцииРМКВызовСервера содержит процедуры и функции 
// для работы с маркетинговыми акциями.
//  
////////////////////////////////////////////////////////////////////////////////


#Область ПрограммныйИнтерфейс

// Выполняет аутентификацию пользователя по владельцу информационной карты.
// 
// Параметры:
//  ВладелецКарты - СправочникСсылка.Пользователи,
//                  СправочникСсылка.ФизическиеЛица - владелец карты маркетинговой акции.
//  ТекущийМагазин - СправочникСсылка.Магазины - 
//
// Возвращаемое значение:
//  СправочникСсылка.Пользователи,
//  СправочникСсылка.ФизическиеЛица - пользователь карты.
//
Функция АвторизацияПользователяПоВладельцуКарты(ВладелецКарты, ТекущийМагазин) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(ВладелецКарты) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Если ЗначениеЗаполнено(ВладелецКарты.ИдентификаторПользователяИБ)  Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ВладелецКарты.ИдентификаторПользователяИБ);
		Иначе
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ВладелецКарты.Наименование);
		КонецЕсли;
		
		Если ПользовательИБ = Неопределено Тогда
		
			Возврат Неопределено;
		
		КонецЕсли;
		
		ПользовательКарты = ВладелецКарты;
		
	ИначеЕсли ТипЗнч(ВладелецКарты) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		ПользовательКарты = ОпределитьПользователяПоФизическомуЛицу(ВладелецКарты, ТекущийМагазин);
		Если Не ЗначениеЗаполнено(ПользовательКарты) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ПользовательКарты;

КонецФункции

// Возвращает случайное число для проверки.
//
// Возвращаемое значение:
//  Строка - число в формате от 0 до 9999.
//
Функция КодПроверкиДисконтнойКарты() Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса();
	НачальноеЧисло = Год(ТекущаяДата) + Месяц(ТекущаяДата) + День(ТекущаяДата) + Час(ТекущаяДата) +
	Минута(ТекущаяДата) + Секунда(ТекущаяДата);
	Генератор = Новый ГенераторСлучайныхЧисел(НачальноеЧисло);
	КодПроверки = Генератор.СлучайноеЧисло(0, 9999);
	Если КодПроверки < 1000 Тогда
		КодПроверкиСтрокой = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Строка(КодПроверки), 4, "0");
	Иначе
		КодПроверкиСтрокой = Формат(КодПроверки, "ЧГ=0");
	КонецЕсли;
	
	Возврат КодПроверкиСтрокой;
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает список пользователей, связанных
// с пользователями текущей информационной базы.
//
Функция СписокПользователейИнформационнойБазы()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивПользователейИБ = Новый Массив;
	МассивПользователейБазы = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Для Каждого ПользовательИБ Из МассивПользователейБазы Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ 
		|	Пользователи.Ссылка
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Наименование = &ИдентификаторНаименованияПользователяИБ
		|	ИЛИ Пользователи.ИдентификаторПользователяИБ = &ИдентификаторПользователяИБ";
		
		Запрос.УстановитьПараметр("ИдентификаторПользователяИБ", ПользовательИБ.УникальныйИдентификатор);
		Запрос.УстановитьПараметр("ИдентификаторНаименованияПользователяИБ", ПользовательИБ.Имя);
		
		Результат = Запрос.Выполнить();
		ТаблицаЗапроса = Результат.Выгрузить();
		
		Для каждого СтрокаЗапроса  Из ТаблицаЗапроса Цикл
			МассивПользователейИБ.Добавить(СтрокаЗапроса.Ссылка);
		КонецЦикла; 
		
	КонецЦикла;
	
	Возврат МассивПользователейИБ;
	
КонецФункции

// Определяет пользователя по переданному
// физическому лицу.
//
Функция ОпределитьПользователяПоФизическомуЛицу(ФизЛицо, ТекущийМагазин)
	
	Результат = Справочники.Пользователи.ПустаяСсылка();
	
	МассивПользователейИБ = СписокПользователейИнформационнойБазы();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Пользователи.Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.ФизическоеЛицо = &ФизЛицо
	|	И Пользователи.ФизическоеЛицо.ПометкаУдаления = ЛОЖЬ
	|	И Пользователи.Ссылка В(&МассивПользователейИБ)";
	
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("МассивПользователейИБ", МассивПользователейИБ);
	Запрос.УстановитьПараметр("Магазин", ?(ЗначениеЗаполнено(ТекущийМагазин), ТекущийМагазин, ПараметрыСеанса.ТекущийМагазин));
	Выборка = Запрос.Выполнить().Выгрузить();
	
	Если Выборка.Количество() >= 1 Тогда
		
		Результат = Выборка[0].Ссылка;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
