#Область ПрограммныйИнтерфейс

// Записывает действие кассира.
//
// Параметры:
//  ВидДействияКассираВРМК - Строка - идентификатор вида операции кассира в РМК.
//  СтруктураЛога - Структура - структура записываемая в регистр.
//
Процедура ЗаписатьЛогОперации(ВидДействияКассираВРМК, СтруктураЛога) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураЛога.Вставить("ВидДействияКассираВРМК", Перечисления.ВидыДействийКассираВРМК[ВидДействияКассираВРМК]);
	МенеджерЗаписи = РегистрыСведений.ЛогДействийКассираВРМК.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураЛога);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры // ЗаписатьЛогОперации()

// Заполняет предварительные записи лога.
//
// Параметры:
//  ЧекККМ  - ДокументСсылка.ЧекККМ - чек ккм, вносимый в данные регистра
//  ИдентификаторФормыПредварительныхДанных - УникальныйИдентификатор - уникальный идентификатор по которому подбираются предварительные данные.
//
Процедура ЗаполнитьПредварительныйЛог(ИдентификаторФормыПредварительныхДанных, ЧекККМ) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЛогДействийКассираВРМК");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		УстановитьПривилегированныйРежим(Истина);
		
		НаборЗаписей = РегистрыСведений.ЛогДействийКассираВРМК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторФормыПредварительныхДанных.Установить(ИдентификаторФормыПредварительныхДанных);
		НаборЗаписей.Прочитать();
		ТаблицаНабораЗаписей = НаборЗаписей.Выгрузить();
		ТаблицаНабораЗаписей.ЗаполнитьЗначения(ЧекККМ, "ЧекККМ");
		НаборЗаписей.Загрузить(ТаблицаНабораЗаписей);
		НаборЗаписей.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
		УстановитьПривилегированныйРежим(Ложь);
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры // ЗаполнитьПредварительныйЛог()

Процедура ОчиститьЛогДействийКассира(Отказ, СообщениеОбОшибке) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЛогДействийКассираВРМК");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		УстановитьПривилегированныйРежим(Истина);
		
		НаборЗаписей = РегистрыСведений.ЛогДействийКассираВРМК.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		УстановитьПривилегированныйРежим(Ложь);
		
	Исключение
		
		ОтменитьТранзакцию();
		Отказ = Истина;
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
КонецПроцедуры
#КонецОбласти