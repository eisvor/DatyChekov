

////////////////////////////////////////////////////////////////////////////////
// ЭлектронноеВзаимодействиеРТ: общий механизм обмена электронными документами.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область НастройкиПрофилейДоступа

// Дополняет профиль ролями БЭД для выполнения базовых функций работы с РТ
//
// Параметры:
//  ОписаниеПрофиля - Структура - описание поставляемого профиля 
//                    (см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа)
//
Процедура ДополнитьПрофильОбязательнымиРолямиБЭД(ОписаниеПрофиля)Экспорт
	
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваЭД");
	ОписаниеПрофиля.Роли.Добавить("ИспользованиеОблачныхКлассификаторов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДанныхРаботаСНоменклатурой");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеМаршрутовПодписания");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНастроекБизнесСеть");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНастроекОбменаСКонтрагентами");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНастроекТорговыхПредложений");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНастроекЭлектронногоВзаимодействия");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНоменклатурыКонтрагентовБЭД");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваСервисДоставки");
	ОписаниеПрофиля.Роли.Добавить("ОтправкаЗаказовНаДоставкуПеревозчикуСервисДоставки");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеОбменовОнлайнОплаты");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНовостей");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНастроекОбменСБанками");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеОбменСБанками");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеМЧДЭДО");
	
КонецПроцедуры

// Дополняет профиль ролями БЭД для чтения и просмотра документов и журналов БЭД
//
// Параметры:
//  ОписаниеПрофиля - Структура - описание поставляемого профиля 
//                    (см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа)
//
Процедура ДополнитьПрофильПравамиЧтенияДокументовБЭД(ОписаниеПрофиля)Экспорт
	
	ОписаниеПрофиля.Роли.Добавить("ЧтениеЭлектронныхДокументов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеЗапросыКоммерческихПредложенийПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеКоммерческихПредложенийПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеОснованияЭДОПоЗапросамКоммерческихПредложенийПоставщиков");
	
КонецПроцедуры

// Дополняет профиль ролями БЭД для чтения и просмотра справочников и регистров БЭД
//
// Параметры:
//  ОписаниеПрофиля - Структура - описание поставляемого профиля 
//                    (см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа)
//
Процедура ДополнитьПрофильПравамиЧтенияСправочнойИнформацииБЭД(ОписаниеПрофиля)Экспорт
	Возврат;
КонецПроцедуры

// Дополняет профиль ролями БЭД для создания и изменения настроек БЭД
//
// Параметры:
//  ОписаниеПрофиля - Структура - описание поставляемого профиля 
//                    (см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа)
//
Процедура ДополнитьПрофильПравамиНастройкиБЭД(ОписаниеПрофиля)Экспорт
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНастроекБизнесСеть");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНастроекОбменаСКонтрагентами");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНастроекТорговыхПредложений");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНастроекЭлектронногоВзаимодействия");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНастроекБизнесСеть");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеОбменовОнлайнОплаты");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНастроекОбменСБанками");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеОбменСБанками");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеМЧДКонтрагентовЭДО");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеМЧДОрганизацийЭДО");
	
КонецПроцедуры

// Дополняет профиль ролями БЭД для создания документов и выполнения обменов БЭД
//
// Параметры:
//  ОписаниеПрофиля - Структура - описание поставляемого профиля 
//                    (см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа)
//
Процедура ДополнитьПрофильПравамиОбменаБЭД(ОписаниеПрофиля)Экспорт
	
	ОписаниеПрофиля.Роли.Добавить("ВыполнениеОбменаДокументамиБизнесСеть");
	ОписаниеПрофиля.Роли.Добавить("ВыполнениеОбменаСКонтрагентами");
	ОписаниеПрофиля.Роли.Добавить("ВыполнениеОбменовДаннымиССайтом");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеМаршрутовПодписания");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеЭлектронныхДокументов");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеЗапросыКоммерческихПредложенийПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеКоммерческихПредложенийПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеОснованияЭДОПоЗапросамКоммерческихПредложенийПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ПоискТорговыхПредложенийБизнесСеть");
	ОписаниеПрофиля.Роли.Добавить("ПоискЗапросовКоммерческихПредложений");
	ОписаниеПрофиля.Роли.Добавить("ВыполнениеОбменовОнлайнОплаты");
	ОписаниеПрофиля.Роли.Добавить("ПолучениеПлатежнойСсылкиДляОнлайнОплаты");
	ОписаниеПрофиля.Роли.Добавить("ВыполнениеОбменаОбменСБанками");
	
КонецПроцедуры

// Дополняет профиль ролями БЭД для добавления и изменения справочников и регистров БЭД
//
// Параметры:
//  ОписаниеПрофиля - Структура - описание поставляемого профиля 
//                    (см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа)
//
Процедура ДополнитьПрофильПравамиСправочнойИнформацииБЭД(ОписаниеПрофиля)Экспорт
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНоменклатурыКонтрагентовБЭД");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеДанныхРаботаСНоменклатурой");
	ОписаниеПрофиля.Роли.Добавить("ИспользованиеВыгрузкиРаботаСНоменклатурой");
	ОписаниеПрофиля.Роли.Добавить("ИспользованиеУведомленийЭДО");
	
КонецПроцедуры

// Дополняет профиль ролями БЭД для работы с отчетами БЭД
//
// Параметры:
//  ОписаниеПрофиля - Структура - описание поставляемого профиля 
//                    (см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа)
//
Процедура ДополнитьПрофильПравамиПросмотраОтчетовБЭД(ОписаниеПрофиля)Экспорт
	Возврат;
КонецПроцедуры

#КонецОбласти

// См. ЭлектронноеВзаимодействиеПереопределяемый.ЗаполнитьРегистрационныеДанныеОрганизации.
// Процедура возвращает данные для заполнения заявки на получение уникального
// идентификатора абонента, добавления сертификата абонента.
//
// Параметры:
//  Организация - Произвольный - ссылка на элемент справочника Организации;
//  ДанныеОрганизации - Структура - данные об организации:
//    * Индекс - Строка - почтовый индекс организации.
//    * Регион - Строка - код региона организации.
//    * Район - Строка - Район.
//    * Город - Строка - Город.
//    * НаселенныйПункт - Строка - населенный пункт расположения организации.
//    * Улица - Строка - Улица.
//    * Дом - Строка - Дом.
//    * Корпус - Строка - Корпус.
//    * Квартира - Строка - Квартира.
//    * Телефон - Строка - телефон организации.
//    * Наименование - Строка - наименование организации.
//    * ИНН - Строка - ИНН организации.
//    * КПП - Строка - КПП организации.
//    * ОГРН - Строка - ОГРН организации.
//    * КодИМНС - Строка - код ИМНС организации.
//    * ЮрФизЛицо - Строка - вид лица, возможные значения: "ЮрЛицо" или "ФизЛицо".
//    * Фамилия - Строка - фамилия руководителя.
//    * Имя - Строка - имя руководителя.
//    * Отчество - Строка - отчество руководителя.
//
// Пример:
//
////// Пример для "Управление торговлей 11"
//
//	ОрганизацияОбъект = Неопределено;
//	Попытка
//		ОрганизацияОбъект = Организация.ПолучитьОбъект();
//	Исключение
//	КонецПопытки;
//
//	ДанныеОрганизации.Очистить();
//
////// Возвращаемая структура должна содержать все перечисленные ниже
////// ключи и их значения - строки
////// Проверка свойств в дальнейшем не выполняется.
//
//	ДанныеОрганизации.Вставить("ОрганизацияСсылка", Организация);
//
//// в конфигурации "Управление торговлей" не реализовано хранение
//// компонентов адреса, поэтому компоненты адреса остаются пустыми.
//
//	ДанныеОрганизации.Вставить("Индекс"         , "");
//	ДанныеОрганизации.Вставить("Регион"         , "");
//	ДанныеОрганизации.Вставить("Район"          , "");
//	ДанныеОрганизации.Вставить("Город"          , "");
//	ДанныеОрганизации.Вставить("НаселенныйПункт", "");
//	ДанныеОрганизации.Вставить("Улица"          , "");
//	ДанныеОрганизации.Вставить("Дом"            , "");
//	ДанныеОрганизации.Вставить("Корпус"         , "");
//	ДанныеОрганизации.Вставить("Квартира"       , "");
//
//	Если ОрганизацияОбъект = Неопределено Тогда
//		
//		ДанныеОрганизации.Вставить("Наименование"   , "");
//		ДанныеОрганизации.Вставить("ИНН"            , "");
//		ДанныеОрганизации.Вставить("КПП"            , "");
//		ДанныеОрганизации.Вставить("ОГРН"           , "");
//		ДанныеОрганизации.Вставить("КодИМНС"        , "");
//		ДанныеОрганизации.Вставить("ЮрФизЛицо"      , "ЮрЛицо");
//		
//		ДанныеОрганизации.Вставить("Фамилия"        , "");
//		ДанныеОрганизации.Вставить("Имя"            , "");
//		ДанныеОрганизации.Вставить("Отчество"       , "");
//	
//		Возврат;
//		
//	КонецЕсли;
//
//// получение реквизитов организации
//
//	ДанныеОрганизации.Вставить("Наименование"   , ОрганизацияОбъект.НаименованиеПолное);
//	ДанныеОрганизации.Вставить("ИНН"            , ОрганизацияОбъект.ИНН);
//	ДанныеОрганизации.Вставить("КПП"            , ОрганизацияОбъект.КПП);
//	ДанныеОрганизации.Вставить("ОГРН"           , ОрганизацияОбъект.ОГРН);
//	ДанныеОрганизации.Вставить("КодИМНС"        , "");
//
//	ВидыЛиц = Перечисления.ЮрФизЛицо;
//	Если ОрганизацияОбъект.ЮрФизЛицо = ВидыЛиц.ЮрЛицо
//		ИЛИ ОрганизацияОбъект.ЮрФизЛицо = ВидыЛиц.ЮрЛицоНеРезидент Тогда
//		ДанныеОрганизации.Вставить("ЮрФизЛицо"      , "ЮрЛицо");
//	Иначе
//		ДанныеОрганизации.Вставить("ЮрФизЛицо"      , "ФизЛицо");
//	КонецЕсли;
//
//	ДанныеОрганизации.Вставить("Фамилия" , "");
//	ДанныеОрганизации.Вставить("Имя"     , "");
//	ДанныеОрганизации.Вставить("Отчество", "");
//
//	Руководитель = ОрганизацияОбъект.ТекущийРуководитель;
//	Если НЕ Руководитель.Пустая() Тогда
//		
//		ФИОМассив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Руководитель.Наименование, " ");
//		КоличествоЭлементов = ФИОМассив.Количество();
//		
//		Если КоличествоЭлементов > 0 Тогда
//			ДанныеОрганизации.Фамилия = ФИОМассив[0];
//		КонецЕсли;
//		
//		Если КоличествоЭлементов > 1 Тогда
//			ДанныеОрганизации.Имя = ФИОМассив[1];
//		КонецЕсли;
//		
//		Если КоличествоЭлементов > 2 Тогда
//			ДанныеОрганизации.Отчество = ФИОМассив[2];
//		КонецЕсли;
//		
//	КонецЕсли;
//
//	ДанныеОрганизации.Вставить("Телефон", "");
//
//	СтруктураПоиска = Новый Структура;
//	СтруктураПоиска.Вставить("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
//	СтруктураПоиска.Вставить("Вид", Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
//	СтрокиТелефона = ОрганизацияОбъект.КонтактнаяИнформация.НайтиСтроки(СтруктураПоиска);
//
//	Если СтрокиТелефона.Количество() > 0 Тогда
//		ДанныеОрганизации.Телефон = СтрокиТелефона[0].НомерТелефона;
//	КонецЕсли;
//
//
////// Пример для "Бухгалтерия предприятия, редакция 3.0":
//
//	СвойстваОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, 
//			"НаименованиеПолное, ИНН, КПП, ОГРН, КодНалоговогоОргана, ЮридическоеФизическоеЛицо");
//	
//	ОрганизацияФизЛицо = СвойстваОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
//	
//	ДанныеОрганизации.Вставить("ОрганизацияСсылка", Организация);
//	
//	ДанныеОрганизации.Вставить("Наименование"   , СвойстваОрганизации.НаименованиеПолное);
//	ДанныеОрганизации.Вставить("ИНН"            , СвойстваОрганизации.ИНН);
//	ДанныеОрганизации.Вставить("КПП"            , СвойстваОрганизации.КПП);
//	ДанныеОрганизации.Вставить("ОГРН"           , СвойстваОрганизации.ОГРН);
//	ДанныеОрганизации.Вставить("КодИМНС"        , СвойстваОрганизации.КодНалоговогоОргана);
//	
//	Если ОрганизацияФизЛицо Тогда
//		ДанныеОрганизации.Вставить("ЮрФизЛицо"      , "ФизЛицо");
//	Иначе
//		ДанныеОрганизации.Вставить("ЮрФизЛицо"      , "ЮрЛицо");
//	КонецЕсли;
//	
//	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, ТекущаяДатаСеанса());
//	ДанныеОрганизации.Вставить("Фамилия" , ОтветственныеЛица.РуководительФИО.Фамилия);
//	ДанныеОрганизации.Вставить("Имя"     , ОтветственныеЛица.РуководительФИО.Имя);
//	ДанныеОрганизации.Вставить("Отчество", ОтветственныеЛица.РуководительФИО.Отчество);
//	
//
//	Если ОрганизацияФизЛицо Тогда
//		ОбъектКонтактнойИнформации = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Организация, "ИндивидуальныйПредприниматель");
//		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица;
//		ИмяСправочника = "ФизическиеЛица";
//	Иначе
//		ОбъектКонтактнойИнформации = Организация;
//		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
//		ИмяСправочника = "Организации";
//	КонецЕсли;
//	
//	ДанныеОрганизации.Вставить("Индекс"         , "");
//	ДанныеОрганизации.Вставить("Регион"         , "");
//	ДанныеОрганизации.Вставить("КодРегиона"     , "");
//	ДанныеОрганизации.Вставить("Район"          , "");
//	ДанныеОрганизации.Вставить("Город"          , "");
//	ДанныеОрганизации.Вставить("НаселенныйПункт", "");
//	ДанныеОрганизации.Вставить("Улица"          , "");
//	ДанныеОрганизации.Вставить("Дом"            , "");
//	ДанныеОрганизации.Вставить("Корпус"         , "");
//	ДанныеОрганизации.Вставить("Квартира"       , "");
//	
//	ТекстЗапроса =
//	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
//	|	КонтактнаяИнформация.Значение
//	|ИЗ
//	|	Справочник." + ИмяСправочника + ".КонтактнаяИнформация КАК КонтактнаяИнформация
//	|ГДЕ
//	|	КонтактнаяИнформация.Ссылка = &Ссылка
//	|	И КонтактнаяИнформация.Вид = &Вид";
//	
//	Запрос = Новый Запрос;
//	Запрос.Текст = ТекстЗапроса;
//	Запрос.УстановитьПараметр("Ссылка", ОбъектКонтактнойИнформации);
//	Запрос.УстановитьПараметр("Вид",    ВидКонтактнойИнформации);
//	Выборка = Запрос.Выполнить().Выбрать();
//	Если Выборка.Следующий() Тогда
//		
//		АдресСтруктурой = РаботаСАдресами.СведенияОбАдресе(Выборка.Значение);
//		Если АдресСтруктурой.Свойство("Индекс") Тогда
//			ДанныеОрганизации.Индекс = АдресСтруктурой.Индекс;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("Регион") Тогда
//			ДанныеОрганизации.Регион = АдресСтруктурой.Регион;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("КодРегиона") Тогда
//			ДанныеОрганизации.КодРегиона = АдресСтруктурой.КодРегиона;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("Район") Тогда
//			ДанныеОрганизации.Район = АдресСтруктурой.Район;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("Город") Тогда
//			ДанныеОрганизации.Город = АдресСтруктурой.Город;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("НаселенныйПункт") Тогда
//			ДанныеОрганизации.НаселенныйПункт = АдресСтруктурой.НаселенныйПункт;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("Улица") Тогда
//			ДанныеОрганизации.Улица = АдресСтруктурой.Улица;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("Здание") И ЗначениеЗаполнено(АдресСтруктурой.Здание) Тогда
//			ДанныеОрганизации.Дом = АдресСтруктурой.Здание.Номер;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("Корпуса") И ЗначениеЗаполнено(АдресСтруктурой.Корпуса) Тогда
//			ДанныеОрганизации.Корпус = АдресСтруктурой.Корпуса[0].Номер;
//		КонецЕсли;
//		Если АдресСтруктурой.Свойство("Помещения") И ЗначениеЗаполнено(АдресСтруктурой.Помещения) Тогда
//			ДанныеОрганизации.Квартира = АдресСтруктурой.Помещения[0].Номер;
//		КонецЕсли;
//		
//	КонецЕсли;
//	
//	ДанныеОрганизации.Вставить("Телефон", УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
//				Организация, ?(ОрганизацияФизЛицо, Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица, Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации)));
//
Процедура ЗаполнитьРегистрационныеДанныеОрганизации(Организация, ДанныеОрганизации) Экспорт
	
	СвойстваОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, 
			"НаименованиеПолное, ИНН, КПП, ОГРН, ЮрФизЛицо");
	
	ОрганизацияФизЛицо = СвойстваОрганизации.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
							ИЛИ СвойстваОрганизации.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
	
	ДанныеОрганизации.Вставить("ОрганизацияСсылка", Организация);
	
	ДанныеОрганизации.Вставить("Наименование"   , СвойстваОрганизации.НаименованиеПолное);
	ДанныеОрганизации.Вставить("ИНН"            , СвойстваОрганизации.ИНН);
	ДанныеОрганизации.Вставить("КПП"            , СвойстваОрганизации.КПП);
	ДанныеОрганизации.Вставить("ОГРН"           , СвойстваОрганизации.ОГРН);
	
	Если ОрганизацияФизЛицо Тогда
		ДанныеОрганизации.Вставить("ЮрФизЛицо"      , "ФизЛицо");
	Иначе
		ДанныеОрганизации.Вставить("ЮрФизЛицо"      , "ЮрЛицо");
	КонецЕсли;
	
	ЗаполнитьДанныеРуководителя(ДанныеОрганизации);
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	
	ДанныеОрганизации.Вставить("Индекс"         , "");
	ДанныеОрганизации.Вставить("Регион"         , "");
	ДанныеОрганизации.Вставить("КодРегиона"     , "");
	ДанныеОрганизации.Вставить("Район"          , "");
	ДанныеОрганизации.Вставить("Город"          , "");
	ДанныеОрганизации.Вставить("НаселенныйПункт", "");
	ДанныеОрганизации.Вставить("Улица"          , "");
	ДанныеОрганизации.Вставить("Дом"            , "");
	ДанныеОрганизации.Вставить("Корпус"         , "");
	ДанныеОрганизации.Вставить("Квартира"       , "");
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Значение
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Ссылка
	|	И КонтактнаяИнформация.Вид = &Вид";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Организация);
	Запрос.УстановитьПараметр("Вид",    ВидКонтактнойИнформации);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		АдресСтруктурой = РаботаСАдресами.СведенияОбАдресе(Выборка.Значение);
		Если АдресСтруктурой.Свойство("Индекс") Тогда
			ДанныеОрганизации.Индекс = АдресСтруктурой.Индекс;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Регион") Тогда
			ДанныеОрганизации.Регион = АдресСтруктурой.Регион;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("КодРегиона") Тогда
			ДанныеОрганизации.КодРегиона = АдресСтруктурой.КодРегиона;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Район") Тогда
			ДанныеОрганизации.Район = АдресСтруктурой.Район;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Город") Тогда
			ДанныеОрганизации.Город = АдресСтруктурой.Город;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("НаселенныйПункт") Тогда
			ДанныеОрганизации.НаселенныйПункт = АдресСтруктурой.НаселенныйПункт;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Улица") Тогда
			ДанныеОрганизации.Улица = АдресСтруктурой.Улица;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Здание") И ЗначениеЗаполнено(АдресСтруктурой.Здание) Тогда
			ДанныеОрганизации.Дом = АдресСтруктурой.Здание.Номер;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Корпуса") И ЗначениеЗаполнено(АдресСтруктурой.Корпуса) Тогда
			ДанныеОрганизации.Корпус = АдресСтруктурой.Корпуса[0].Номер;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Помещения") И ЗначениеЗаполнено(АдресСтруктурой.Помещения) Тогда
			ДанныеОрганизации.Квартира = АдресСтруктурой.Помещения[0].Номер;
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеОрганизации.Вставить("Телефон", УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
		Организация, Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации, ТекущаяДатаСеанса()));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработка метаданных

// См. ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьСоответствиеСправочников.
// Определяет соответствие справочников библиотеки и прикладного решения.
//
// Параметры:
//  СоответствиеСправочников - Соответствие - список справочников.
//
// Пример:
//    СоответствиеСправочников.Вставить("Организации",                 "Организации");
//    СоответствиеСправочников.Вставить("Контрагенты",                 "Контрагенты");
//    СоответствиеСправочников.Вставить("ДоговорыКонтрагентов",        "ДоговорыКонтрагентов");
//    СоответствиеСправочников.Вставить("Номенклатура",                "Номенклатура");
//    СоответствиеСправочников.Вставить("ЕдиницыИзмерения",            "ЕдиницыИзмерения");
//    СоответствиеСправочников.Вставить("НоменклатураПоставщиков",     "НоменклатураПоставщиков");
//    СоответствиеСправочников.Вставить("Валюты",                      "Валюты");
//    СоответствиеСправочников.Вставить("Банки",                       "КлассификаторБанков");
//    СоответствиеСправочников.Вставить("БанковскиеСчетаОрганизаций",  "БанковскиеСчета");
//    СоответствиеСправочников.Вставить("БанковскиеСчетаКонтрагентов", "БанковскиеСчета");
//    СоответствиеСправочников.Вставить("УпаковкиНоменклатуры",        "ЕдиницыИзмерения");
//    СоответствиеСправочников.Вставить("ФизическиеЛица",              "ФизическиеЛица");
//    СоответствиеСправочников.Вставить("Партнеры",                    "Партнеры");
//    СоответствиеСправочников.Вставить("ХарактеристикиНоменклатуры",  "ХарактеристикиНоменклатуры");
//
Процедура ПолучитьСоответствиеСправочников(СоответствиеСправочников) Экспорт
	
	// Электронные документы
	СоответствиеСправочников.Вставить("Организации", "Организации");
	СоответствиеСправочников.Вставить("Контрагенты", "Контрагенты");
	СоответствиеСправочников.Вставить("ДоговорыКонтрагентов", "ДоговорыКонтрагентов");
	СоответствиеСправочников.Вставить("Номенклатура", "Номенклатура");
	СоответствиеСправочников.Вставить("ХарактеристикиНоменклатуры", "ХарактеристикиНоменклатуры");
	СоответствиеСправочников.Вставить("УпаковкиНоменклатуры", "УпаковкиНоменклатуры");
	СоответствиеСправочников.Вставить("ФизическиеЛица", "ФизическиеЛица");
	СоответствиеСправочников.Вставить("ЕдиницыИзмерения", "БазовыеЕдиницыИзмерения");
	СоответствиеСправочников.Вставить("Банки", "КлассификаторБанков");
	СоответствиеСправочников.Вставить("БанковскиеСчетаОрганизаций", "БанковскиеСчета");
	СоответствиеСправочников.Вставить("БанковскиеСчетаКонтрагентов", "БанковскиеСчета");
	
КонецПроцедуры

// См. ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьСоответствиеНаименованийОбъектовМДИРеквизитов.
// В процедуре формируется соответствие для сопоставления имен переменных библиотеки,
// наименованиям объектов и реквизитов метаданных прикладного решения.
// Если в прикладном решении есть документы, на основании которых формируется ЭД,
// причем названия реквизитов данных документов отличаются от общепринятых "Организация", "Контрагент", "СуммаДокумента",
// то для этих реквизитов необходимо добавить в соответствие записи виде:
// Ключ = "ДокументВМетаданных.ОбщепринятоеНазваниеРеквизита", Значение - "ДокументВМетаданных.ДругоеНазваниеРеквизита".
// Например:
//  СоответствиеРеквизитовОбъекта.Вставить("МЗ_Покупка.Организация", "МЗ_Покупка.Учреждение");
//  СоответствиеРеквизитовОбъекта.Вставить("МЗ_Покупка.Контрагент",  "МЗ_Покупка.Грузоотправитель");
//  СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданный.СуммаДокумента",  "СчетФактураВыданный.Основание.СуммаДокумента");
// 
// Для подсистемы ОбменСБанками требуется определение следующих полей:
//   "ПлатежноеПоручениеВМетаданных" (необязательное)
//   "БанковскийСчетОрганизации.Закрыт" (необязательное)
//   "ИНН" (обязательное)
//   "СокращенноеНаименованиеОрганизации" (необязательное)
//   "БанковскийСчетОрганизации.Организация" (обязательное для писем)
//   "БанковскийСчетОрганизации.Банк" (обязательное для писем)
//   "ПлатежноеПоручение.СчетОрганизации" (обязательное для писем)
//   "ПлатежноеПоручение.Организация" (обязательное для писем)
//
// Параметры:
//  СоответствиеРеквизитовОбъекта - Соответствие - содержит:
//    * Ключ - Строка - имя переменной, используемой в коде библиотеки;
//    * Значение - Строка - наименование объекта метаданных или реквизита объекта в прикладном решении.
//
Процедура ПолучитьСоответствиеНаименованийОбъектовМДИРеквизитов(СоответствиеРеквизитовОбъекта) Экспорт
	
	// Обмен с банками начало
	СоответствиеРеквизитовОбъекта.Вставить("БанковскийСчетОрганизации.Банк", 		"Банк");
	СоответствиеРеквизитовОбъекта.Вставить("БанковскийСчетОрганизации.Организация", "Владелец");
	СоответствиеРеквизитовОбъекта.Вставить("БанковскийСчетОрганизации.НомерСчета", 	"НомерСчета");
	СоответствиеРеквизитовОбъекта.Вставить("Банк.БИК",                              "Код");
	// Обмен с банками конец
	
	// Обмен с контрагентами начало
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданныйВМетаданных",       	"СчетФактураВыданный");
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданныйАвансВМетаданных",  	"СчетФактураВыданный");
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураПолученныйВМетаданных",     	"СчетФактураПолученный");
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураПолученныйАвансВМетаданных",	"СчетФактураПолученный");
	СоответствиеРеквизитовОбъекта.Вставить("РеализацияТоваровУслугВМетаданных",    	"РеализацияТоваров");
	СоответствиеРеквизитовОбъекта.Вставить("ПоступлениеТоваровУслугВМетаданных",   	"ПоступлениеТоваров");
	СоответствиеРеквизитовОбъекта.Вставить("ИННКонтрагента",                       	"ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("КППКонтрагента",                       	"КПП");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеКонтрагента",              	"Наименование");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеКонтрагентаДляСообщенияПользователю", "Наименование");
	СоответствиеРеквизитовОбъекта.Вставить("ВнешнийКодКонтрагента",                	"ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("ИННОрганизации",                       	"ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("КППОрганизации",                       	"КПП");
	СоответствиеРеквизитовОбъекта.Вставить("ОГРНОрганизации",                      	"ОГРН");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеОрганизации",              	"Наименование");
	СоответствиеРеквизитовОбъекта.Вставить("СокращенноеНаименованиеОрганизации",    "НаименованиеСокращенное");
	СоответствиеРеквизитовОбъекта.Вставить("ПолноеНаименованиеОрганизации",        	"НаименованиеПолное");
	СоответствиеРеквизитовОбъекта.Вставить("ВнешнийКодОрганизации",                	"ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("ЮридическоеФизическоеЛицо",            	"ЮрФизЛицо");
	
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданныйАванс.СуммаДокумента", "ДокументОснование.СуммаДокумента");
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданный.СуммаДокумента", "ДокументОснование.СуммаДокумента");
	// Обмен с контрагентами конец
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Получение данных

// См. ЭлектронноеВзаимодействиеПереопределяемый.НайтиСсылкуНаОбъект.
// Находит ссылку на объект ИБ по типу, ИД и дополнительным реквизитам.
// 
// Параметры:
//  ТипОбъекта - Строка - идентификатор типа объекта, который необходимо найти;
//  ИДОбъекта - Строка - идентификатор объекта заданного типа;
//  ДополнительныеРеквизиты - Структура - набор дополнительных полей объекта для поиска;
//  Результат - Ссылка - ссылка на найденный объект.
//
Процедура НайтиСсылкуНаОбъект(ТипОбъекта, Результат, ИдОбъекта = "", ДополнительныеРеквизиты = Неопределено) Экспорт
	
	Если ДополнительныеРеквизиты = Неопределено Тогда
		ДополнительныеРеквизиты = Новый Структура;
	КонецЕсли;
	
	Результат = Неопределено;
	Если ТипОбъекта = "Контрагенты" ИЛИ ТипОбъекта = "Организации" Тогда
		
		ПараметрПоиска = "";
		ИНН = Неопределено;
		ДополнительныеРеквизиты.Свойство("ИНН", ИНН);
		Если ИНН = Неопределено Тогда
			ИНН = "";
		КонецЕсли;
		КПП = Неопределено;
		ДополнительныеРеквизиты.Свойство("КПП", КПП);
		Если КПП = Неопределено Тогда
			КПП = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(ИНН + КПП) Тогда // по ИНН + КПП
			ОбменСКонтрагентамиПереопределяемый.СсылкаНаОбъектПоИННКПП(ТипОбъекта, ИНН, КПП, Результат); 
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Результат) И ДополнительныеРеквизиты.Свойство("Наименование", ПараметрПоиска) Тогда
			// по Наименованию
			ИмяМетаданных = ЭлектронноеВзаимодействиеРТПовтИсп.ИмяПрикладногоСправочника(ТипОбъекта);
			Если ТипОбъекта = "Организации" Тогда
				Результат = НайтиСсылкуНаОбъектПоРеквизиту(ИмяМетаданных, "НаименованиеСокращенное", ПараметрПоиска);
			Иначе
				Результат = НайтиСсылкуНаОбъектПоРеквизиту(ИмяМетаданных, "Наименование", ПараметрПоиска);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "НоменклатураПоставщиков" И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		
		Идентификатор = "";
		Если ДополнительныеРеквизиты.Свойство("Ид") Тогда
			Идентификатор = ДополнительныеРеквизиты.Ид;
		ИначеЕсли ДополнительныеРеквизиты.Свойство("Идентификатор") Тогда
			Идентификатор = ДополнительныеРеквизиты.Идентификатор;
		Иначе
			Возврат;
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Идентификатор",Идентификатор);
		Если ДополнительныеРеквизиты.Свойство("Владелец") Тогда
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	НоменклатураПоставщиков.Номенклатура КАК Номенклатура
			|ИЗ
			|	РегистрСведений.НоменклатураПоставщиков КАК НоменклатураПоставщиков
			|ГДЕ
			|	НоменклатураПоставщиков.Идентификатор = &Идентификатор
			|	И НоменклатураПоставщиков.Поставщик = &Поставщик";
				
			Запрос.УстановитьПараметр("Поставщик", ДополнительныеРеквизиты.Владелец);
		Иначе
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	НоменклатураПоставщиков.Номенклатура КАК Номенклатура
			|ИЗ
			|	РегистрСведений.НоменклатураПоставщиков КАК НоменклатураПоставщиков
			|ГДЕ
			|	НоменклатураПоставщиков.Идентификатор = &Идентификатор
			|";
				
		КонецЕсли;
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Выборка.Номенклатура;
		КонецЕсли;
	ИначеЕсли ТипОбъекта = "ВидыКонтактнойИнформации" Тогда
		Если ИдОбъекта = "ФаксКонтрагента" Тогда
			Результат = Неопределено;
		Иначе
			Результат = Справочники[ТипОбъекта][ИдОбъекта];
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "ЕдиницыИзмерения" Тогда
		
		КодЕИ = Неопределено;
		
		Если ДополнительныеРеквизиты.Свойство("Код", КодЕИ) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	БазовыеЕдиницыИзмерения.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.БазовыеЕдиницыИзмерения КАК БазовыеЕдиницыИзмерения
			|ГДЕ
			|	БазовыеЕдиницыИзмерения.Код = &Код";
			
			Запрос.УстановитьПараметр("Код", КодЕИ);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				Результат = Выборка.Ссылка;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "СтраныМира" Тогда
		
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("СтраныМира", "Код", ИдОбъекта);
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			Результат = УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(ИдОбъекта);
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "ТаможенныеДекларации" Тогда
		
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("НомераГТД", "Код", ИдОбъекта);
		
	ИначеЕсли ТипОбъекта = "Банки" Тогда
		
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("КлассификаторБанков", "Код", ИдОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

// См. ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьПечатныйНомерДокумента.
// Получает печатный номер документа.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на документ информационной базы.
//  Результат - Строка - номер документа.
//
Процедура ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект, Результат) Экспорт
	
	Результат = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СсылкаНаОбъект.Номер, Истина, Истина);
	
КонецПроцедуры

// См. ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица.
// Получает данные о юридическом (физическом) лице по ссылке.
//
// Параметры:
//  ЮрФизЛицо - Ссылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - Структура - возвращаемые данные:
//   * ИНН                     - Строка - ИНН.
//   * КПП                     - Строка - КПП.
//   * ПолноеНаименование      - Строка - полное наименование.
//   * Наименование            - Строка - наименование.
//   * СокращенноеНаименование - Строка - сокращенное наименование.
//   * ОфициальноеНаименование - Строка - наименование юридического лица.
//   * КодПоОКПО               - Строка - код ОКПО.
//   * ЮридическийАдрес        - Строка - представление юридического адреса.
//   * ФактическийАдрес        - Строка - представление фактического адреса.
//   * Телефоны                - Строка - представление телефонов.
//   * ЭлектроннаяПочта        - Строка - представление электронной почты.
//   * Фамилия                 - Строка - фамилия.
//   * Имя                     - Строка - имя.
//   * Отчество                - Строка - отчество.
//
Процедура ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, Сведения) Экспорт
	
	Сведения = Новый Структура("Ссылка, ОфициальноеНаименование, Наименование, Представление, ПолноеНаименование,
	| ЮрФизЛицо, КодПоОКПО, ИНН, КПП, ОГРН, Фамилия, Имя, Отчество, СвидетельствоСерияНомер, СвидетельствоДатаВыдачи,
	| Банк, БИК, КоррСчет, НомерСчета, ЮридическийАдрес, ЮридическийАдресXML, ФактическийАдрес, ФактическийАдресИдентификатор, ФактическийАдресXML,
	| ПочтовыйАдрес, ПочтовыйАдресXML, Телефоны, ТелефоныXML,ЭлектроннаяПочта ");
	
	Если НЕ ЗначениеЗаполнено(ЮрФизЛицо) Тогда
		Возврат
	КонецЕсли;
	
	Сведения.Ссылка = ЮрФизЛицо;
	
	Реквизиты = "ЮрФизЛицо, НаименованиеПолное, Наименование, ИНН, КПП, КодПоОКПО";
	
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
		Реквизиты = Реквизиты + ", ОГРН, СвидетельствоСерияНомер, СвидетельствоДатаВыдачи";
	КонецЕсли;
	
	СтруктураДанных = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЮрФизЛицо, Реквизиты);
	
	СтруктураДанных.Свойство("ЮрФизЛицо",          Сведения.ЮрФизЛицо);
	СтруктураДанных.Свойство("НаименованиеПолное", Сведения.ПолноеНаименование);
	СтруктураДанных.Свойство("НаименованиеПолное", Сведения.ОфициальноеНаименование);
	СтруктураДанных.Свойство("Наименование",       Сведения.Наименование);
	СтруктураДанных.Свойство("ИНН",                Сведения.ИНН);
	СтруктураДанных.Свойство("КПП",                Сведения.КПП);
	СтруктураДанных.Свойство("КодПоОКПО",          Сведения.КодПоОКПО);
	
	СтруктураДанных.Свойство("ОГРН",                    Сведения.ОГРН);
	СтруктураДанных.Свойство("СвидетельствоСерияНомер", Сведения.СвидетельствоСерияНомер);
	СтруктураДанных.Свойство("СвидетельствоДатаВыдачи", Сведения.СвидетельствоДатаВыдачи);
	
	Если Сведения.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
		ИЛИ Сведения.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		
		Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
			ФизЛицоФИО = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(, Новый Структура("ФизЛицо", ЮрФизЛицо));
		Иначе
			СтруктураНаименования = ОбменСКонтрагентамиРТКлиентСервер.РазложитьСтрокуНаименованияОрганизации(
				Сведения.ПолноеНаименование, СтруктураДанных.ЮрФизЛицо);
			ФизЛицоФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(СтруктураНаименования.Наименование);
		КонецЕсли;
		
		Сведения.Вставить("Фамилия",  ФизЛицоФИО.Фамилия);
		Сведения.Вставить("Имя",      ФизЛицоФИО.Имя);
		Сведения.Вставить("Отчество", ФизЛицоФИО.Отчество);
	КонецЕсли;
	
	ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ЮрФизЛицо,,ТекущаяДатаСеанса(),Ложь);
	
	Для Каждого СтрокаТаблицыКИ Из ТаблицаКИ Цикл
		
		Если СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента Тогда
			
			Сведения.ЮридическийАдресXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.ЮридическийАдрес = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента Тогда
			
			Сведения.ФактическийАдресXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.ФактическийАдрес = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента Тогда
			
			Сведения.ПочтовыйАдресXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.ПочтовыйАдрес = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента Тогда
			
			Сведения.ТелефоныXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.Телефоны = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента Тогда
			
			Сведения.ЭлектроннаяПочта = СтрокаТаблицыКИ.Представление;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// См. ЭлектронноеВзаимодействиеПереопределяемый.ОписаниеОрганизации.
// Возвращает текстовое описание организации по параметрам.
//
// Параметры:
//  СведенияОрганизации - Структура - сведения об организации, по которой надо составить описание.
//  Список - Строка - список запрашиваемых параметров организации.
//  СПрефиксом - Булево - признак вывода префикса параметра организации.
//  Результат - Строка - описание организации.
//
Процедура ОписаниеОрганизации(СведенияОрганизации, Результат, Список = "", СПрефиксом = Истина) Экспорт
	
	Результат = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОрганизации, Список, СПрефиксом);
	
КонецПроцедуры

// См. ЭлектронноеВзаимодействиеПереопределяемый.ЕстьПравоОткрытияЖурналаРегистрации.
// Проверяет наличие прав на открытие журнала регистрации.
//
// Параметры:
//  Результат - Булево - если пользователь имеет право на открытие журнала регистрации,
//                       в этой переменной должна быть установлена Истина.
//
Процедура ЕстьПравоОткрытияЖурналаРегистрации(Результат) Экспорт
	
	Результат = ПравоДоступа("ЖурналРегистрации", Метаданные);
	
КонецПроцедуры


#Область РаботаСБлокировкой

// Устанавливает управляемую блокировку.
//
// Параметры:
//  ПространствоБлокировки - Строка - пространство блокировки.
//  Отбор - Структура - ключ задает имя поля пространства блокировки, а значение блокируемый диапазон.
//  Режим - РежимБлокировкиДанных - режим блокировки.
//        - Строка - допустимые значения "Разделяемый" и "Исключительный".
//        - Неопределено - значение по умолчанию "Исключительный".
//
Процедура УстановитьУправляемуюБлокировкуПоОтбору(Знач ПространствоБлокировки, Знач Отбор = Неопределено, Знач Режим = Неопределено) Экспорт
	
	Если Режим = Неопределено Тогда
		Режим = РежимБлокировкиДанных.Исключительный;
	ИначеЕсли ТипЗнч(Режим) = Тип("Строка") Тогда
		Если ВРег(Режим) = ВРег("Разделяемый") Тогда
			Режим = РежимБлокировкиДанных.Разделяемый;
		Иначе
			Режим = РежимБлокировкиДанных.Исключительный;
		КонецЕсли;
	КонецЕсли;
	
	Если Отбор = Неопределено Тогда
		Отбор = Новый Структура;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить(ПространствоБлокировки);
	Для каждого КлючЗначение Из Отбор Цикл
		ЭлементБлокировки.УстановитьЗначение(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	ЭлементБлокировки.Режим = Режим;
	Блокировка.Заблокировать();
	
КонецПроцедуры

// Устанавливает управляемую блокировку по ссылке на объект.
//
// Параметры:
//  Ссылка - ЛюбаяСсылка - ссылка, по которой нужно установить блокировку.
//  Режим - РежимБлокировкиДанных - режим блокировки.
//        - Строка - допустимые значения "Разделяемый" и "Исключительный".
//        - Неопределено - значение по умолчанию "Исключительный".
//
//
Процедура УстановитьУправляемуюБлокировкуПоСсылке(Знач Ссылка, Знач Режим = Неопределено) Экспорт
	
	МетаданныеОбъекта = Ссылка.Метаданные();
	ПространствоБлокировки = МетаданныеОбъекта.ПолноеИмя();
	
	Отбор = Новый Структура("Ссылка", Ссылка);
	
	УстановитьУправляемуюБлокировкуПоОтбору(ПространствоБлокировки, Отбор, Режим);
	
КонецПроцедуры

// Устанавливает управляемую блокировку по набору записей.
// Используются установленные отборы в наборе.
//
// Параметры:
//  Набор - НаборЗаписей - набор, по которому нужно установить блокировку.
//  Режим - РежимБлокировкиДанных - режим блокировки.
//        - Строка - допустимые значения "Разделяемый" и "Исключительный".
//        - Неопределено - значение по умолчанию "Исключительный".
//
//
Процедура УстановитьУправляемуюБлокировкуПоНаборуЗаписей(Знач Набор, Знач Режим = Неопределено) Экспорт
	
	МетаданныеОбъекта = Набор.Метаданные();
	ПространствоБлокировки = МетаданныеОбъекта.ПолноеИмя();
	
	Отбор = Новый Структура;
	Для каждого ЭлементОтбора Из Набор.Отбор Цикл
		Если ЭлементОтбора.Использование Тогда
			Отбор.Вставить(ЭлементОтбора.ПутьКДанным, ЭлементОтбора.Значение);
		КонецЕсли;
	КонецЦикла;
	
	УстановитьУправляемуюБлокировкуПоОтбору(ПространствоБлокировки, Отбор, Режим);
	
КонецПроцедуры

// Получает объект по ссылке для дальнейшего изменения. 
// Устанавливает управляемую и пессимистическую блокировку.
// Возможен вызов исключения, если не удалось установить пессимистическую блокировку.
//
// Параметры:
//  Ссылка - ЛюбаяСсылка - ссылка, объект которой нужно получить.
//
// Возвращаемое значение:
//  Объект,Неопределено - объект по ссылке. Неопределено, если объект отсутствует.
//
Функция ОбъектПоСсылкеДляИзменения(Знач Ссылка) Экспорт
	
	УстановитьУправляемуюБлокировкуПоСсылке(Ссылка);
	Объект = Ссылка.ПолучитьОбъект();
	Если Объект <> Неопределено Тогда
		Объект.Заблокировать();
	КонецЕсли;
	
	Возврат Объект;
	
КонецФункции

#КонецОбласти

#Область МЧД

// Получает ссылку на физическое лицо по сведениям.
// 
// Параметры:
//  СведенияФизЛица - структура:
//  	* ИНН - строка
// 
// Возвращаемое значение:
//  - СправочникСсылка.ФизическиеЛица
//  - Неопределено - если не элемент не найден в ИБ.
//
Функция ПолучитьФизЛицоМЧД(СведенияФизЛица) Экспорт
	
	ФизическоеЛицо = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", СведенияФизЛица.ИНН);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.ИНН = &ИНН";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ФизическоеЛицо = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ФизическоеЛицо;
	
КонецФункции

// Получить типы организации представителя МЧД.
// 
// Параметры:
//  Типы - массив
Процедура ПолучитьТипыОрганизацииПредставителяМЧД(Типы) Экспорт
	
	Типы.Добавить(Тип("СправочникСсылка.Организации"));
	Типы.Добавить(Тип("СправочникСсылка.Контрагенты"));
	
КонецПроцедуры

// Получить ссылку на организацию-представителя МЧД.
// 
// Параметры:
//  СведенияЮрЛица - структура:
// 		* ИНН 	- строка
// 		* КПП 	- строка
// 		* ОГРН 	- строка
// 
// Возвращаемое значение:
// 	СправочникСсылка - ссылка на элемент справочника, Неопределено
//  
Функция ПолучитьОрганизациюМЧД(СведенияЮрЛица) Экспорт
	
	ИНН = СведенияЮрЛица.ИНН;
	
	КПП = СведенияЮрЛица.КПП;
	КППЗаполнен = ЗначениеЗаполнено(КПП);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН = &ИНН
	|	И (Контрагенты.КПП = &КПП
	|			ИЛИ &КППЗаполнен = ЛОЖЬ)
	|	И НЕ Контрагенты.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО Контрагенты.ФизЛицо = ФизическиеЛица.Ссылка
	|			И (ФизическиеЛица.ИНН = &ИНН)
	|			И (НЕ ФизическиеЛица.ПометкаУдаления)
	|			И (НЕ Контрагенты.ПометкаУдаления)";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	Запрос.УстановитьПараметр("КППЗаполнен", КППЗаполнен);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления
	|	И Организации.ИНН = &ИНН
	|	И (Организации.КПП = &КПП
	|			ИЛИ &КППЗаполнен = ЛОЖЬ)";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	Запрос.УстановитьПараметр("КППЗаполнен", КППЗаполнен);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Находит ссылку на справочник по переданному реквизиту.
//
// Параметры:
//  ИмяСправочника - Строка, имя справочника, объект которого надо найти,
//  ИмяРеквизита - Строка, имя реквизита, по которому будет проведен поиск,
//  ЗначРеквизита - произвольное значение, значение реквизита, по которому будет проведен поиск,
//  Владелец - Ссылка на владельца для поиска в иерархическом справочнике.
//
Функция НайтиСсылкуНаОбъектПоРеквизиту(ИмяСправочника, ИмяРеквизита, ЗначРеквизита, Владелец = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	ОбъектМетаданных = Метаданные.Справочники[ИмяСправочника];
	Если НЕ ОбщегоНазначения.ЭтоСтандартныйРеквизит(ОбъектМетаданных.СтандартныеРеквизиты, ИмяРеквизита) // нестандартный реквизит
		И НЕ ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита)<> Неопределено Тогда // другой реквизит
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИскСправочник.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник."+ИмяСправочника+" КАК ИскСправочник
	|ГДЕ
	|	ИскСправочник."+ИмяРеквизита+" = &ЗначРеквизита";
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		Запрос.Текст = Запрос.Текст + " И ИскСправочник.Владелец = &Владелец";
		Запрос.УстановитьПараметр("Владелец", 	Владелец);
	КонецЕсли;
	Запрос.УстановитьПараметр("ЗначРеквизита", ЗначРеквизита);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДанныеРуководителя(ДанныеОрганизации)
	
	Руководители = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(ДанныеОрганизации.ОрганизацияСсылка, ТекущаяДатаСеанса(),,Истина);
	
	ФИОПолное = Руководители.Руководитель;
	
	ЧастиИмени = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИОПолное);
	
	ДанныеОрганизации.Вставить("Фамилия"  , ЧастиИмени.Фамилия);
	ДанныеОрганизации.Вставить("Имя"      , ЧастиИмени.Имя);
	ДанныеОрганизации.Вставить("Отчество" , ЧастиИмени.Отчество);
	ДанныеОрганизации.Вставить("Должность", Руководители.РуководительДолжность);
	
КонецПроцедуры

#КонецОбласти




