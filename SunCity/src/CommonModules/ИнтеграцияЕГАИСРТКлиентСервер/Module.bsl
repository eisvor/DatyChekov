
#Область СлужебныйПрограммныйИнтерфейс

Функция ПроверитьДанныеАлкогольнойПродукции(Форма, ДанныеПродукции, Номенклатура, ТекстСообщения) Экспорт
	
	Если НЕ ДанныеПродукции.ЭтоАлкогольнаяПродукция Тогда
		ТекстСообщения = НСтр("ru = 'Номенклатура %1 не является алкогольной продукцией.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Номенклатура);
		Возврат Ложь;
	КонецЕсли;
	
	Если Форма.ИмяФормы = "Документ.ЧекЕГАИС.Форма.ФормаДокумента" Тогда
		Если НЕ ДанныеПродукции.Маркируемый Тогда
			ТекстСообщения = НСтр("ru = 'Номенклатура %1 не является маркируемой алкогольной продукцией. Для немаркируемой продукции следует использовать акт списания с признаком ""Реализация""'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Номенклатура);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПараметрыСканированияАкцизнойМарки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТекущееПоле"                    , "");
	Результат.Вставить("АдресВХранилище"                , "");
	Результат.Вставить("ОткрытьФормуМножественногоВвода", Ложь);
	Результат.Вставить("ЗапретитьУдалениеМарки"         , Ложь);
	Результат.Вставить("Операция"                       , "Продажа");
	
	Возврат Результат;
	
КонецФункции


Процедура ЗаполнитьКоличествоАкцизныхМарок(РеквизитОбъектаТовары, РеквизитОбъектаАкцизныеМарки) Экспорт
	
	Для Каждого СтрокаТаблицы Из РеквизитОбъектаТовары Цикл
		
		ЗаполнитьКоличествоАкцизныхМарокВСтроке(СтрокаТаблицы, РеквизитОбъектаАкцизныеМарки);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКоличествоАкцизныхМарокВСтроке(СтрокаТаблицыТовары, РеквизитОбъектаАкцизныеМарки) Экспорт
	
	Если СтрокаТаблицыТовары = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоискаАМ = Новый Структура("КлючСвязи");
	СтруктураПоискаАМ.КлючСвязи = СтрокаТаблицыТовары.КлючСвязи;
	СтрокиМарок = РеквизитОбъектаАкцизныеМарки.НайтиСтроки(СтруктураПоискаАМ);
	СтрокаТаблицыТовары.КоличествоАкцизныхМарок = СтрокиМарок.Количество();
	
КонецПроцедуры

Процедура УстановитьЗначениеСтатусАкцизныхМарок(РеквизитОбъектаТовары) Экспорт
	
	Для Каждого СтрокаТаблицы Из РеквизитОбъектаТовары Цикл
		
		УстановитьЗначениеСтатусАкцизныхМарокВСтроке(СтрокаТаблицы)
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьЗначениеСтатусАкцизныхМарокВСтроке(СтрокаТаблицыТовары) Экспорт
	
	Если СтрокаТаблицыТовары <> Неопределено Тогда
		Если СтрокаТаблицыТовары.НеобходимостьВводаАкцизнойМарки Тогда
			
			Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыТовары, "СтатусУказанияАкцизныхМарок") Тогда
				Возврат;
			КонецЕсли; 
			
			Если СтрокаТаблицыТовары.Количество = СтрокаТаблицыТовары.КоличествоАкцизныхМарок
				И СтрокаТаблицыТовары.Количество <> 0 Тогда
				СтрокаТаблицыТовары.СтатусУказанияАкцизныхМарок = 1;
			Иначе
				СтрокаТаблицыТовары.СтатусУказанияАкцизныхМарок = 2;
			КонецЕсли;
		Иначе
			СтрокаТаблицыТовары.СтатусУказанияАкцизныхМарок = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьЗначениеСтатусКодовМаркировкиРМК(РеквизитОбъектаТовары, ОперацияСДенежнымиСредствами) Экспорт
	
	Для Каждого СтрокаТаблицы Из РеквизитОбъектаТовары Цикл
		
		УстановитьЗначениеСтатусКодовМаркировкиРМКВСтроке(СтрокаТаблицы, ОперацияСДенежнымиСредствами);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьЗначениеСтатусКодовМаркировкиРМКВСтроке(СтрокаТаблицыТовары, ОперацияСДенежнымиСредствами) Экспорт
	
		Если НЕ СтрокаТаблицыТовары.НеобходимостьВводаАкцизнойМарки Тогда
			Если СтрокаТаблицыТовары.НеобходимостьВводаКодаМаркировки Тогда
				
				Если СтрокаТаблицыТовары.КоличествоУпаковок = СтрокаТаблицыТовары.КоличествоКодовМаркировки
					И СтрокаТаблицыТовары.КоличествоУпаковок <> 0 Тогда
					
					СтрокаТаблицыТовары.СтатусУказанияАкцизныхМарок = 1;
					
				Иначе
					
					Если ОперацияСДенежнымиСредствами Тогда 
						СтрокаТаблицыТовары.СтатусУказанияАкцизныхМарок = 0;
					Иначе 
						СтрокаТаблицыТовары.СтатусУказанияАкцизныхМарок = 2;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				
				СтрокаТаблицыТовары.СтатусУказанияАкцизныхМарок = 0;
				
			КонецЕсли;
		КонецЕсли;
	
КонецПроцедуры

// Формирует заголовок формы документа ЧекЕГАИС с видом операции вскрытие тары.
//
Процедура УстановитьЗаголовокВскрытиеТары(Форма) Экспорт
	
	Форма.АвтоЗаголовок = Ложь;
	
	Если ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		Заголовок = НСтр("ru='Вскрытие тары ЕГАИС %1 от %2'");
		Заголовок = СтрШаблон(Заголовок, Форма.Объект.Номер, Форма.Объект.Дата);
	Иначе
		Заголовок = НСтр("ru='Вскрытие тары ЕГАИС (создание)'");
	КонецЕсли;
	
	Форма.Заголовок = Заголовок;
	
КонецПроцедуры

#КонецОбласти