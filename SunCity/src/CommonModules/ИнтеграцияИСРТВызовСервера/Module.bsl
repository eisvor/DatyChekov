
#Область ПрограммныйИнтерфейс

// Получает массив GTIN для переданного товара и характеристики
//
// Параметры:
//  Номенклатура   - СправочникСсылка.Номенклатура - номенклатура (маркируемый товар).
//  Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика номенклатуры (маркируемого товара).
// 
// Возвращаемое значение:
//  Массив - массив GTIN
//
Функция МассивGTINМаркированногоТовара(Номенклатура, Характеристика) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Владелец = &Номенклатура
	|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика");
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	МассивШтрихкодов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Штрихкод");
	СписокGTIN =  Новый Массив;
	
	Для Каждого Штрихкод Из МассивШтрихкодов Цикл
		
		Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(Штрихкод) Тогда
			СписокGTIN.Добавить(Штрихкод);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокGTIN;
	
КонецФункции

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

Функция ВидыПродукцииВТоварах(Знач Товары) Экспорт
	
	Возврат ИнтеграцияИСРТ.ВидыПродукцииВТоварах(Товары);
	
КонецФункции

Функция ЭтоРозничныйКлиент(Контрагент) Экспорт
	
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ЮрФизЛицо");
	Возврат ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо");
	
КонецФункции

// Дополняет параметры переопределения организации ГОСИС значениями,
// требующими серверного вызова.
//
// Параметры:
//  ПараметрыПереопределения - См. ИнтеграцияИСРТКлиентСервер.ПараметрыПереопределенияОрганизацииГОСИС.
//
Процедура ДополнитьПараметрыПереопределенияОрганизацииГОСИС(ПараметрыПереопределения) Экспорт
	
	// Заполним Организацию по ОрганизацииГОСИС.
	Если НЕ ЗначениеЗаполнено(ПараметрыПереопределения.Организация)
		И ЗначениеЗаполнено(ПараметрыПереопределения.ОрганизацияГОСИС) Тогда
		
		Если ПараметрыПереопределения.СуффиксПродукции = "ЕГАИС" Тогда
			ПараметрыПереопределения.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ПараметрыПереопределения.ОрганизацияГОСИС, "Контрагент");
		ИначеЕсли ПараметрыПереопределения.СуффиксПродукции = "ИСМП" Тогда
			ПараметрыПереопределения.Организация = ПараметрыПереопределения.ОрганизацияГОСИС;
		КонецЕсли;
		
	КонецЕсли;
	
	// Заполним Организацию по Номенклатуре.
	Если НЕ ЗначениеЗаполнено(ПараметрыПереопределения.Организация)
		И ЗначениеЗаполнено(ПараметрыПереопределения.Номенклатура) Тогда
		ПараметрыПереопределения.Организация = ИнтеграцияИСРТ.ОрганизацияВзаимодействияСИС(
			ПараметрыПереопределения.Номенклатура,
			ПараметрыПереопределения.ДанныеОРабочемМесте);
	КонецЕсли;
	
	// Заполним ОрганизациюГОСИС по Организации.
	Если НЕ ЗначениеЗаполнено(ПараметрыПереопределения.ОрганизацияГОСИС)
		И ЗначениеЗаполнено(ПараметрыПереопределения.Организация) Тогда
		
		Если ПараметрыПереопределения.СуффиксПродукции = "ЕГАИС" Тогда
			ПараметрыПереопределения.ОрганизацияГОСИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
				ПараметрыПереопределения.Организация,
				ПараметрыПереопределения.ДанныеОРабочемМесте.Магазин);
		ИначеЕсли ПараметрыПереопределения.СуффиксПродукции = "ИСМП" Тогда
			ПараметрыПереопределения.ОрганизацияГОСИС = ПараметрыПереопределения.Организация;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает заводской номер ФН
// 
// Параметры:
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства
// 
// Возвращаемое значение:
//  Строка - номер ФН
Функция ЗаводскойНомерФискальногоНакопителя(ИдентификаторУстройства) Экспорт
	
	ЗаводскойНомерФН = "";
	
	Возврат ЗаводскойНомерФН;
	
КонецФункции

#КонецОбласти