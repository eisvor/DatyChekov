
#Область Штрихкодирование

 Функция ТипШтрихкода(Штрихкод) Экспорт
	
	ТипШтрихкодаРТ = Неопределено;
	
	ТипШтрихкодаРТ   = ПланыВидовХарактеристик.ТипыШтрихкодов.ПустаяСсылка();
	ТипШтрихкодаВрем = МенеджерОборудованияВызовСервера.ОпределитьТипШтрихкода(Штрихкод);
	
	Если ТипШтрихкодаВрем = "EAN8" Тогда
		ТипШтрихкодаРТ = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN8;
	ИначеЕсли ТипШтрихкодаВрем = "EAN13" Тогда
		ТипШтрихкодаРТ = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN13;
	ИначеЕсли ТипШтрихкодаВрем = "EAN128" Тогда
		ТипШтрихкодаРТ = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN128;
	ИначеЕсли ТипШтрихкодаВрем = "CODE39" Тогда
		ТипШтрихкодаРТ = ПланыВидовХарактеристик.ТипыШтрихкодов.CODE39;
	ИначеЕсли ТипШтрихкодаВрем = "CODE128" Тогда
		ТипШтрихкодаРТ = ПланыВидовХарактеристик.ТипыШтрихкодов.CODE128;
	ИначеЕсли ТипШтрихкодаВрем = "ITF14" Тогда
		ТипШтрихкодаРТ = ПланыВидовХарактеристик.ТипыШтрихкодов.ITF14;
	КонецЕсли;
	
	Возврат ТипШтрихкодаРТ;
	
КонецФункции

Функция ПодобратьGTINДляЗаказа(Номенклатура, Упаковка, Характеристика, ХарактеристикиИспользуются, ЭтоТабак = Ложь) Экспорт
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры  	
    |ГДЕ
	|	ШтрихкодыНоменклатуры.Владелец = &Номенклатура
	|	<Характеристики>
	|	<Упаковка>";
	
	Если ХарактеристикиИспользуются Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<Характеристики>", "И ШтрихкодыНоменклатуры.Характеристика = &Характеристика");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<Характеристики>", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Упаковка) Тогда//Для всех ТГ учитываем упаковку только если она есть и не равна штучной упаковке (коэф-т 1)
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<Упаковка>", "И ШтрихкодыНоменклатуры.Упаковка = &Упаковка");
	Иначе
		Если ЭтоТабак Тогда 
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<Упаковка>", "И ШтрихкодыНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)");	
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<Упаковка>", "");
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Если ХарактеристикиИспользуются Тогда
		Запрос.УстановитьПараметр("Характеристика", Характеристика);
	КонецЕсли;
	Если ЗначениеЗаполнено(Упаковка) Тогда
		Запрос.УстановитьПараметр("Упаковка", Упаковка);
	КонецЕсли;

	Выборка = Запрос.Выполнить().Выбрать();
	МассивШтрихкодов = Новый Массив();
	Пока Выборка.Следующий() Цикл
		МассивШтрихкодов.Добавить(РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.GTINПоШтрихкодуEAN(Выборка.Штрихкод));
	КонецЦикла;

	Возврат МассивШтрихкодов;
	
КонецФункции

#КонецОбласти
