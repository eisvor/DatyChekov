
#Область ПрограммныйИнтерфейс

// Возвращает пустую структуру,
// которая в последующем заполняется значениями и используется 
// при проверке ассортимента в табличной части.
//
// Параметры:
// 		Объект - ДокументОбъект.ЗаказНаПеремещение, ДокументОбъект.ЗаказПокупателя, ДокументОбъект.ЗаказПоставщику,
// 				 ДокументОбъект.ПоступлениеТоваров, ДокументОбъект.ПриходныйОрдерНаТовары, 
// 				 ДокументОбъект.ЧекККМ - документ по которому проводится проверка ассортимента.
// 		ЭтоПродажа - Булево - признак продажи.
// 		ИмяРеквизитаЗаказ - Строка - наименование реквизита заказ документа.
// 		ИмяТабличнойЧасти - Строка - наименование табличной части документа.
// 		ИмяРеквизитаМагазин - Строка - наименование реквизита магазин документа.
// 		
// Возвращаемое значение: 
// 		СтруктураПроверкиАссортимента - Структура.
Функция ПараметрыПроверкиАссортимента(
	Объект,
	ЭтоПродажа = Истина,
	ИмяРеквизитаЗаказ = "",
	ИмяТабличнойЧасти = "Товары",
	ИмяРеквизитаМагазин = "Магазин") Экспорт 
	
	СтруктураПроверкиАссортимента = Новый Структура;
	
	СтруктураПроверкиАссортимента.Вставить("Ссылка", Объект.Ссылка);
	СтруктураПроверкиАссортимента.Вставить("Магазин", Объект[ИмяРеквизитаМагазин]);
	СтруктураПроверкиАссортимента.Вставить("Дата", Объект.Дата);
	
	Если ЭтоПродажа Тогда
		СтруктураПроверкиАссортимента.Вставить("ТекстСообщения", НСтр("ru = 'Номенклатура ""%1"" не включена в ассортимент магазина или запрещена к продаже.'"));
		СтруктураПроверкиАссортимента.Вставить("ИмяРесурсаАссортимента",  "РазрешеныПродажи");
		СтруктураПроверкиАссортимента.Вставить("РазрешатьДобавление", 	  Ложь);
	Иначе
		СтруктураПроверкиАссортимента.Вставить("ТекстСообщения", НСтр("ru = 'Номенклатура ""%1"" не включена в ассортимент магазина или запрещена к закупке. Принимать ее не рекомендуется.'"));
		СтруктураПроверкиАссортимента.Вставить("ИмяРесурсаАссортимента",  "РазрешеныЗакупки");
		СтруктураПроверкиАссортимента.Вставить("РазрешатьДобавление", 	  Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяРеквизитаЗаказ) Тогда
		СтруктураПроверкиАссортимента.Вставить("ПроверитьТоварВЗаказе", Объект[ИмяРеквизитаЗаказ]);
	Иначе
		СтруктураПроверкиАссортимента.Вставить("ПроверитьТоварВЗаказе", Неопределено);
	КонецЕсли;
	
	СтруктураПроверкиАссортимента.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
	
	Возврат СтруктураПроверкиАссортимента;
	
КонецФункции

// Функция возвращает пустую структуру,
// которая в последующем заполняется значениями и используется 
// при формировании условия в ряде отчетов по ассортименту, не использующих СКД.
// Возвращаемое значение: 
// 		СтруктураПараметровУсловия - Структура
Функция ПараметрыДляУсловия() Экспорт 
	
	СтруктураПараметровУсловия = Новый Структура;
	
	СтруктураПараметровУсловия.Вставить("СчетчикУсловий", 0);
	СтруктураПараметровУсловия.Вставить("СчетчикПараметров", 0);
	СтруктураПараметровУсловия.Вставить("ТекстУсловия", "");
	СтруктураПараметровУсловия.Вставить("ТекстНачальногоУсловия", "");
	СтруктураПараметровУсловия.Вставить("УсловиеГруппы", "");
	СтруктураПараметровУсловия.Вставить("СоответствиеПолейУсловия", Новый Соответствие);
	
	Возврат СтруктураПараметровУсловия;
	
КонецФункции

#КонецОбласти
