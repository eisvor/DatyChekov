#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	ЗарегистрироватьШтрихкодыУпаковокПродукцииИзНатуральногоМеха();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗарегистрироватьШтрихкодыУпаковокПродукцииИзНатуральногоМеха()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ")
		И НЕ ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДвиженияСерий = ЭтотОбъект.Выгрузить(, "Серия, Номенклатура, Характеристика");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДвиженияСерий", ТаблицаДвиженияСерий);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДвиженияСерий.Номенклатура КАК Номенклатура,
	|	ТаблицаДвиженияСерий.Характеристика КАК Характеристика,
	|	ТаблицаДвиженияСерий.Серия КАК Серия
	|ПОМЕСТИТЬ втТаблицаДвиженияСерий
	|ИЗ
	|	&ТаблицаДвиженияСерий КАК ТаблицаДвиженияСерий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаДвиженияСерий.Номенклатура КАК Номенклатура,
	|	втТаблицаДвиженияСерий.Характеристика КАК Характеристика,
	|	втТаблицаДвиженияСерий.Серия КАК Серия,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК Штрихкод,
	|	СерииНоменклатуры.RFIDTID КАК RFIDTID,
	|	СерииНоменклатуры.RFIDUser КАК RFIDUser,
	|	СерииНоменклатуры.RFIDEPC КАК RFIDEPC,
	|	СерииНоменклатуры.EPCGTIN КАК EPCGTIN,
	|	СерииНоменклатуры.RFIDМеткаНеЧитаемая КАК RFIDМеткаНеЧитаемая
	|ПОМЕСТИТЬ втДанныеСерий
	|ИЗ
	|	втТаблицаДвиженияСерий КАК втТаблицаДвиженияСерий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО втТаблицаДвиженияСерий.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО втТаблицаДвиженияСерий.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ <> """"
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСерий.Номенклатура КАК Номенклатура,
	|	втДанныеСерий.Характеристика КАК Характеристика,
	|	втДанныеСерий.Серия КАК Серия,
	|	втДанныеСерий.Штрихкод КАК Штрихкод
	|ИЗ
	|	втДанныеСерий КАК втДанныеСерий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|		ПО втДанныеСерий.Штрихкод = ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
	|ГДЕ
	|	ШтрихкодыУпаковокТоваров.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСерий.Штрихкод КАК ЗначениеШтрихкода,
	|	втДанныеСерий.RFIDTID КАК RFIDTID,
	|	втДанныеСерий.RFIDUser КАК RFIDUser,
	|	втДанныеСерий.RFIDEPC КАК RFIDEPC,
	|	втДанныеСерий.EPCGTIN КАК EPCGTIN,
	|	втДанныеСерий.RFIDМеткаНеЧитаемая КАК RFIDМеткаНеЧитаемая
	|ИЗ
	|	втДанныеСерий КАК втДанныеСерий
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеRFIDИСМП КАК ДанныеRFIDИСМП
	|		ПО втДанныеСерий.RFIDTID = ДанныеRFIDИСМП.RFIDTID
	|ГДЕ
	|	НЕ втДанныеСерий.RFIDTID = """"
	|	И ДанныеRFIDИСМП.RFIDTID ЕСТЬ NULL";
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	// Создание ШтрихкодаУпаковки.
	ВыборкаШтрихкоды = РезультатПакета[РезультатПакета.Количество() - 2].Выбрать();
	Пока ВыборкаШтрихкоды.Следующий() Цикл
		ДанныеШтрихкода = ИнтеграцияИСРТ.ДанныеШтрихкодаПродукцииИзНатуральногоМеха();
		ЗаполнитьЗначенияСвойств(ДанныеШтрихкода, ВыборкаШтрихкоды);
		
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеФормироватьКИЗ");
		
		ШтрихкодУпаковки = Справочники.ШтрихкодыУпаковокТоваров.СоздатьШтрихкодУпаковки(ДанныеШтрихкода,, ПараметрыЗаписи);
	КонецЦикла;
	
	// Регистрация данных RFID.
	ВыборкаRFID = РезультатПакета[РезультатПакета.Количество() - 1].Выбрать();
	Пока ВыборкаRFID.Следующий() Цикл
		ДанныеRFID = РегистрыСведений.ДанныеRFIDИСМП.НовыйЭлементЗаписиДанных();
		ЗаполнитьЗначенияСвойств(ДанныеRFID, ВыборкаRFID);
		
		Попытка
			РегистрыСведений.ДанныеRFIDИСМП.ЗаписатьДанные(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеRFID));
		Исключение
			ТекстСообщения = НСтр("ru='Не удалось записать данные RFID в регистр ""Данные RFID ИС МП"".'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли