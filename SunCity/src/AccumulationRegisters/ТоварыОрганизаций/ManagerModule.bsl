#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Возвращает коэффициенты по РНПТ для указанной Организации и списка товаров.
//
// Параметры:
//	ИсключаемыйДокумент - ДокументСсылка - документ, движения которого исключаются при расчета коэффициента по РНПТ.
//	Организация - СправочникСсылка.Организации - организация, для которой рассчитываются коэффициенты по РНПТ.
//	Товары - ТаблицаЗначений - таблица, содержащая сведения о товарах.
//	
// Возвращаемое значение:
//	ТаблицаЗначений - содержит следующие колонки:
//		* Номенклатура - СправочникСсылка.Номенклатура - номенклатура, для которой необходимо получить значение
//															коэффициента по РНПТ.
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика номенклатуры, для которой
//																			необходимо получить значение коэффициента
//																			по РНПТ.
//		* МестоХранения - СправочникСсылка.Склады, СправочникСсылка.ДоговорыКонтрагентов - место хранения, 
//											где хранится номенклатура, для которой необходимо получить значение 
//											коэффициента по РНПТ.
//		* Коэффициент - Число - значение коэффициента по РНПТ.
//
Функция ПолучитьКоэффициентыПрослеживаемыхТоваров(ИсключаемыйДокумент, Организация, Товары) Экспорт
	
	КлючеваяОперация	= "РегистрНакопления.ТоварыОрганизаций.МодульМенеджера.ПолучитьКоэффициентыПрослеживаемыхТоваров";
	ОписаниеЗамера		= ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	
	ВозвращаемоеЗначение = КоэффициентыПрослеживаемыхТоваров(ИсключаемыйДокумент, Организация, Товары);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, ВозвращаемоеЗначение.Количество());
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КоэффициентыПрослеживаемыхТоваров(ИсключаемыйДокумент, Организация, Товары)
	
	ВозвращаемоеЗначение = Новый ТаблицаЗначений;
	ВозвращаемоеЗначение.Колонки.Добавить("Номенклатура",		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ВозвращаемоеЗначение.Колонки.Добавить("Характеристика",		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ВозвращаемоеЗначение.Колонки.Добавить("МестоХранения",		Новый ОписаниеТипов("СправочникСсылка.Склады, СправочникСсылка.ДоговорыКонтрагентов"));
	ВозвращаемоеЗначение.Колонки.Добавить("Коэффициент",		Новый ОписаниеТипов("Число"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Товары.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(Товары.НомерГТД КАК Справочник.НомераГТД) КАК НомерГТД,
	|	Товары.МестоХранения КАК МестоХранения
	|ПОМЕСТИТЬ ВТВходящаяТаблица
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.МестоХранения КАК МестоХранения,
	|	Товары.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	ВТВходящаяТаблица КАК Товары
	|ГДЕ
	|	ЕСТЬNULL(Товары.Номенклатура.КодТНВЭД.ПрослеживаемыйТовар, ЛОЖЬ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	МестоХранения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборДанных.Номенклатура КАК Номенклатура,
	|	НаборДанных.Характеристика КАК Характеристика,
	|	НаборДанных.МестоХранения КАК МестоХранения,
	|	НаборДанных.НомерГТД КАК НомерГТД,
	|	НаборДанных.КоличествоРНПТ / НаборДанных.Количество КАК Коэффициент
	|ПОМЕСТИТЬ ВТКоэффициентыПоДаннымТоваровОрганизаций
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОрганизацийОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыОрганизацийОстатки.Характеристика КАК Характеристика,
	|		ТоварыОрганизацийОстатки.Склад КАК МестоХранения,
	|		ТоварыОрганизацийОстатки.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизацийОстатки.КоличествоОстаток КАК Количество,
	|		ТоварыОрганизацийОстатки.КоличествоПоРНПТОстаток КАК КоличествоРНПТ
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.Остатки(
	|				,
	|				Организация = &Организация
	|					И (Номенклатура, Характеристика, Склад, НомерГТД) В
	|						(ВЫБРАТЬ
	|							ВТТовары.Номенклатура,
	|							ВТТовары.Характеристика,
	|							ВТТовары.МестоХранения,
	|							ВТТовары.НомерГТД
	|						ИЗ
	|							ВТТовары КАК ВТТовары)) КАК ТоварыОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыОрганизацийСторно.Номенклатура,
	|		ТоварыОрганизацийСторно.Характеристика,
	|		ТоварыОрганизацийСторно.Склад,
	|		ВТТовары.НомерГТД,
	|		-ТоварыОрганизацийСторно.Количество,
	|		-ТоварыОрганизацийСторно.КоличествоПоРНПТ
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизацийСторно
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ВТТовары
	|			ПО ТоварыОрганизацийСторно.Номенклатура = ВТТовары.Номенклатура
	|				И ТоварыОрганизацийСторно.Характеристика = ВТТовары.Характеристика
	|				И ТоварыОрганизацийСторно.Склад = ВТТовары.МестоХранения
	|				И ТоварыОрганизацийСторно.НомерГТД = ВТТовары.НомерГТД
	|	ГДЕ
	|		ТоварыОрганизацийСторно.Регистратор = &ИсключаемыйДокумент) КАК НаборДанных
	|ГДЕ
	|	НаборДанных.КоличествоРНПТ <> 0
	|	И НаборДанных.Количество <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.МестоХранения КАК МестоХранения,
	|	Товары.НомерГТД КАК НомерГТД,
	|	1 КАК Коэффициент
	|ПОМЕСТИТЬ ВТКоэффициентыДляТоваровБезОстатка
	|ИЗ
	|	ВТТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыПоДаннымТоваровОрганизаций КАК ДанныеТоваровОрганизаций
	|		ПО Товары.Номенклатура = ДанныеТоваровОрганизаций.Номенклатура
	|			И Товары.Характеристика = ДанныеТоваровОрганизаций.Характеристика
	|			И Товары.МестоХранения = ДанныеТоваровОрганизаций.МестоХранения
	|			И Товары.НомерГТД = ДанныеТоваровОрганизаций.НомерГТД
	|ГДЕ
	|	ДанныеТоваровОрганизаций.Номенклатура ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТоваровОрганизаций.Номенклатура КАК Номенклатура,
	|	ДанныеТоваровОрганизаций.Характеристика КАК Характеристика,
	|	ДанныеТоваровОрганизаций.МестоХранения КАК МестоХранения,
	|	ДанныеТоваровОрганизаций.НомерГТД КАК НомерГТД,
	|	ДанныеТоваровОрганизаций.Коэффициент КАК Коэффициент
	|ИЗ
	|	ВТКоэффициентыПоДаннымТоваровОрганизаций КАК ДанныеТоваровОрганизаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеТоваровБезОстатка.Номенклатура,
	|	ДанныеТоваровБезОстатка.Характеристика,
	|	ДанныеТоваровБезОстатка.МестоХранения,
	|	ДанныеТоваровБезОстатка.НомерГТД,
	|	ДанныеТоваровБезОстатка.Коэффициент
	|ИЗ
	|	ВТКоэффициентыДляТоваровБезОстатка КАК ДанныеТоваровБезОстатка";
	
	Запрос.УстановитьПараметр("ИсключаемыйДокумент",	ИсключаемыйДокумент);
	Запрос.УстановитьПараметр("Организация",			Организация);
	Запрос.УстановитьПараметр("Товары",					Товары);
	
	ВозвращаемоеЗначение = Запрос.Выполнить().Выгрузить();
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Склад.Магазин)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли