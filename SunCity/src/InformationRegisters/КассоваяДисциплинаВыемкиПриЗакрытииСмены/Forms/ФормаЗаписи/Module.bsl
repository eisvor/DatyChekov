#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияРТ.ПроверитьИспользованиеОрганизации(Отказ,,Запись.Организация);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		Элементы.Организация.Видимость = Ложь;
	КонецЕсли;
	
	АвтоматическаяВыемка = НЕ Запись.ТипВыемкиПриЗакрытииСмены = Перечисления.ТипыВыемкиПриЗакрытииСмены.ВыемкуНеПроизводить;
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если АвтоматическаяВыемка Тогда
		Если Запись.МаксимальнаяСуммаРазмена > 0 Тогда
			ТекущийОбъект.ТипВыемкиПриЗакрытииСмены = Перечисления.ТипыВыемкиПриЗакрытииСмены.ОставитьСуммуРазменаВКассеККМ;
		Иначе
			ТекущийОбъект.ТипВыемкиПриЗакрытииСмены = Перечисления.ТипыВыемкиПриЗакрытииСмены.ПроизводитьПолнуюВыемку;
		КонецЕсли;
	Иначе
		ТекущийОбъект.ТипВыемкиПриЗакрытииСмены = Перечисления.ТипыВыемкиПриЗакрытииСмены.ВыемкуНеПроизводить;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АвтоматическаяВыемкаПриИзменении(Элемент)
	
	Если НЕ АвтоматическаяВыемка Тогда
		Запись.МаксимальнаяСуммаРазмена = 0;
	КонецЕсли;
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.МаксимальнаяСуммаРазмена.Доступность = Форма.АвтоматическаяВыемка;

КонецПроцедуры

#КонецОбласти



