#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры); 
	
	ЗначенияЗаполнения 					= Параметры.ЗначенияЗаполнения;
	
	Элементы.Организация.Доступность 	= НЕ ЗначенияЗаполнения.Свойство("Организация");
	Элементы.Магазин.Доступность 		= НЕ ЗначенияЗаполнения.Свойство("Магазин");
	Элементы.Склад.Доступность 			= НЕ ЗначенияЗаполнения.Свойство("Склад");
	Элементы.ТоварнаяГруппа.Доступность = НЕ ЗначенияЗаполнения.Свойство("ТоварнаяГруппа");
	Элементы.Склад.ТолькоПросмотр		= НЕ (ЗначениеЗаполнено(Запись.Магазин) И ЗначениеЗаполнено(Запись.Организация));
	
	Если Параметры.Свойство("ВводСНООрганизации") Тогда
		ВводСНООрганизации					= Истина;
		ЗаголовокаОбъекта = Нстр("ru='Система налогообложения организации (%1)'");
		ЭтотОбъект.Заголовок 				= СтрШаблон(ЗаголовокаОбъекта, Запись.Организация);
		ЭтотОбъект.АвтоЗаголовок 			= Ложь;
		Элементы.Организация.Доступность	= Ложь;
		Элементы.Магазин.Видимость 			= Ложь;
		Элементы.Склад.Видимость 			= Ложь;
		Элементы.ТоварнаяГруппа.Видимость 	= Ложь;
	КонецЕсли;
	
	НастроитьСписокСНО();
	ЗаполнитьСНО();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	Если СистемаНалогообложенияСтрока = "ЕНВД" И Запись.Период < Дата('2021.01.01') Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ЕНВДВОрганизациях.Установить(Истина);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если ПереходСЕНВД Тогда
		
		РегистрыСведений.ПрименениеСистемНалогообложения.ПроверитьНаличиеЕНВДВОрганизациях();
		ПереходСЕНВД = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Запись.Склад) Тогда
		
		РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.Склад, "Организация, Магазин");
		
		Если РеквизитыСклада.Организация <> Запись.Организация Тогда
			ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Значение поля ""Склад"" не соответствует выбранной организации'"),,, "Запись.Склад");
			Отказ = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.Магазин) И РеквизитыСклада.Магазин <> Запись.Магазин Тогда
			ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Значение поля ""Склад"" не соответствует выбранному магазину'"),,, "Запись.Склад");
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.Магазин) Тогда
			ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'При заполненном значении поля ""Склад"" обязательно должно быть заполнено поле ""Магазин""'"),,, "Запись.Магазин");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "УСНОбъектНалогообложения");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент Тогда
		ТекущийОбъект.ДатаНачалаПатента = Дата(1,1,1);
		ТекущийОбъект.ДатаОкончанияПатента = Дата(1,1,1);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ДатаНачалаПатента) Тогда
		ТекущийОбъект.Период = НачалоМесяца(Запись.ДатаНачалаПатента);
	КонецЕсли;
	
	СписокСНОДляПроверки = Новый СписокЗначений;
	СписокСНОДляПроверки.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.ОСН);
	СписокСНОДляПроверки.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход);
	СписокСНОДляПроверки.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход);
	
	ДетализированнаяСНО = ЗначениеЗаполнено(Запись.Магазин)
			ИЛИ ЗначениеЗаполнено(Запись.Склад)
			ИЛИ ЗначениеЗаполнено(Запись.ТоварнаяГруппа);
	
	Если НЕ СписокСНОДляПроверки.НайтиПоЗначению(Запись.СистемаНалогообложения) = Неопределено Тогда
		СообщениеОбОшибке = РегистрыСведений.ПрименениеСистемНалогообложения.ПроверитьНаПересечениеСНО(Запись.Организация, Запись.СистемаНалогообложения, ДетализированнаяСНО, Запись.Период);
		Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			Отказ = Истина;
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = СообщениеОбОшибке;
			СообщениеПользователю.Сообщить();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ВводСНООрганизации Тогда
		Оповестить("ИзменениеСНООрганизации", Запись.Организация);
	КонецЕсли;
	
	БылаЗапись = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если БылаЗапись Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрЗакрытия = Новый Структура;
		ПараметрЗакрытия.Вставить("Организация", Запись.Организация);
		ПараметрЗакрытия.Вставить("Магазин", Запись.Магазин);
		ПараметрЗакрытия.Вставить("Склад", Запись.Склад);
		ПараметрЗакрытия.Вставить("ТоварнаяГруппа", Запись.ТоварнаяГруппа);
		ПараметрЗакрытия.Вставить("СистемаНалогообложения", Запись.СистемаНалогообложения);
		Закрыть(ПараметрЗакрытия);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура МагазинПриИзменении(Элемент)
	УстановитьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	ЗаполнитьОрганизациюМагазинИзСклада();
	
	Если ЗначениеЗаполнено(Запись.Склад) Тогда
		Элементы.Магазин.АвтоОтметкаНезаполненного 	= Истина;
	Иначе
		Элементы.Магазин.АвтоОтметкаНезаполненного 	= Ложь;
		Элементы.Магазин.ОтметкаНезаполненного		= Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ПроверитьПереходСЕНВД();
	
	УстановитьСНОВЗаписи();
	
	Если СистемаНалогообложенияСтрока <> "Общая" И СистемаНалогообложенияСтрока <> "ЕСН" Тогда
		Запись.ОсвобожденОтНДС = Истина;
	Иначе
		Запись.ОсвобожденОтНДС = Ложь;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура УСНОбъектНалогообложенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	УстановитьСНОВЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	НастроитьСписокСНО();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()
	
	Элементы.Склад.ТолькоПросмотр = НЕ (ЗначениеЗаполнено(Запись.Магазин) И ЗначениеЗаполнено(Запись.Организация));
	
	Элементы.ОсвобожденОтНДС.Доступность = СистемаНалогообложенияСтрока = "Общая"
														ИЛИ СистемаНалогообложенияСтрока = "ЕСН";

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСНО()
	
	Если ЗначениеЗаполнено(Запись.СистемаНалогообложения) Тогда
		Если Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход Тогда
			СистемаНалогообложенияСтрока 	= "УСН";
			УСНОбъектНалогообложения 		= "Доход";
		ИначеЕсли Запись.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
			СистемаНалогообложенияСтрока 	= "Общая";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСНОВЗаписи()
	
	Если СистемаНалогообложенияСтрока = "УСН" Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.УСНДоход");
	ИначеЕсли СистемаНалогообложенияСтрока = "Общая" Тогда
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ОСН"); 
	Иначе
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ПустаяСсылка");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПереходСЕНВД()
	
	Если Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ЕНВД") И СистемаНалогообложенияСтрока <> "ЕНВД" Тогда
		ПереходСЕНВД = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОрганизациюМагазинИзСклада()
	
	Если ЗначениеЗаполнено(Запись.Склад) И (НЕ ЗначениеЗаполнено(Запись.Организация) ИЛИ НЕ ЗначениеЗаполнено(Запись.Магазин)) Тогда
		
		РеквизитыСклада = ОбщегоНазначенияРТВызовСервера.ЗначенияРеквизитовОбъекта(Запись.Склад, Новый Структура("Организация, Магазин"));
		
		Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
			Запись.Организация = РеквизитыСклада.Организация;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.Магазин) Тогда
			Запись.Магазин = РеквизитыСклада.Магазин;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСписокСНО()
	
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Организация, "ЮрФизЛицо");
	Если ПолучитьФункциональнуюОпцию("ОтключенныйФункционал") Тогда
		СписокСНО = Элементы.СистемаНалогообложения.СписокВыбора;
		СНОПатент = СписокСНО.НайтиПоЗначению("Патент");
		
		Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
			
			Если СНОПатент = Неопределено Тогда
				СписокСНО.Добавить("Патент", НСтр("ru = 'Патентная система налогообложения'"));
			КонецЕсли; 
			
		Иначе
			
			Если СНОПатент <> Неопределено Тогда
				СписокСНО.Удалить(СНОПатент);
			КонецЕсли;
			
			Если СистемаНалогообложенияСтрока = "Патент" Тогда
				СистемаНалогообложенияСтрока = "";
			КонецЕсли; 
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти