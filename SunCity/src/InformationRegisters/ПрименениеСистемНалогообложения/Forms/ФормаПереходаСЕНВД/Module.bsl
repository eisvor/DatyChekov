#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЕНВД = ПолучитьСписокЕНВД();
	
	Для Каждого ОрганизацияЕНВД Из ТаблицаЕНВД Цикл
		
		НоваяСтрока = ОрганизацииСЕНВД.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОрганизацияЕНВД);
		НоваяСтрока.Результат = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		
	КонецЦикла;
	
	ТаблицаКассыСЕНВД = ПолучитьСписокКассЕНВД();
	
	Для Каждого КассаЕНВД Из ТаблицаКассыСЕНВД Цикл
		
		НоваяСтрока = КассыСЕНВД.Добавить();
		НоваяСтрока.КассаККМ = КассаЕНВД.Ссылка;
		
	КонецЦикла;
	
	Если ТаблицаКассыСЕНВД.Количество() = 0 Тогда
		Элементы.Группа2.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Завершить(Команда)
	
	ЗавершитьНаСервере();
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура ОрганизацииСЕНВДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Данные = Элемент.ТекущиеДанные;
	ПараметрыОткрытия = Новый Структура; 
	ПараметрыОткрытия.Вставить("Организация", Данные.Организация);
	ПараметрыОткрытия.Вставить("Магазин", Данные.Магазин);
	ПараметрыОткрытия.Вставить("Склад", Данные.Склад);
	ПараметрыОткрытия.Вставить("ТоварнаяГруппа", Данные.ТоварнаяГруппа);
	ПараметрыОткрытия.Вставить("Период", Данные.Период);
	
	КлючЗаписи = СоздатьКлючЗаписиРегистра(ПараметрыОткрытия);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИдентификаторСтроки", Данные.ПолучитьИдентификатор());
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьСНОЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("РегистрСведений.ПрименениеСистемНалогообложения.ФормаЗаписи", ПараметрыФормы,,,,,ОписаниеОповещения,);
КонецПроцедуры

&НаКлиенте
Процедура КассыСЕНВДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Данные = Элемент.ТекущиеДанные;
	ПараметрыФормы = Новый Структура("Ключ", Данные.КассаККМ);
	
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.ФормаЭлемента", ПараметрыФормы);
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСписокЕНВД()
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.Магазин, ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)) КАК Магазин,
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.ТоварнаяГруппа, ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)) КАК ТоварнаяГруппа,
	               |	ПрименениеСистемНалогообложения.СистемаНалогообложения КАК СистемаНалогообложения,
	               |	ПрименениеСистемНалогообложения.Период КАК Период
	               |ИЗ
	               |	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних КАК ПрименениеСистемНалогообложения
	               |ГДЕ
	               |	ПрименениеСистемНалогообложения.СистемаНалогообложения = &СистемаНалогообложения";
	Запрос.УстановитьПараметр("СистемаНалогообложения", Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

&НаСервере
Функция ПолучитьСписокКассЕНВД()
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПодключаемоеОборудование.Ссылка КАК Ссылка,
	               |	ПодключаемоеОборудованиеПараметрыРегистрации.НаименованиеПараметра КАК НаименованиеПараметра,
	               |	ПодключаемоеОборудованиеПараметрыРегистрации.ЗначениеПараметра КАК ЗначениеПараметра
	               |ИЗ
	               |	Справочник.ПодключаемоеОборудование.ПараметрыРегистрации КАК ПодключаемоеОборудованиеПараметрыРегистрации
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	               |		ПО ПодключаемоеОборудованиеПараметрыРегистрации.Ссылка = ПодключаемоеОборудование.Ссылка
	               |ГДЕ
	               |	ПодключаемоеОборудованиеПараметрыРегистрации.НаименованиеПараметра = ""КодыСистемыНалогообложения""
	               |	И ПодключаемоеОборудованиеПараметрыРегистрации.ЗначениеПараметра = ""3""";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

&НаСервере
Функция ЗавершитьНаСервере()
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("СистемаНалогообложения", ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ЕНВД"));
	Результат = ОрганизацииСЕНВД.Выгрузить(ПараметрыОтбора, "СистемаНалогообложения");
	
	Если Результат.Количество() = 0 Тогда
		УстановитьЗначениеКонстантыЕНВДОрганизаций();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция УстановитьЗначениеКонстантыЕНВДОрганизаций()
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.ЕНВДВОрганизациях.Установить(Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

&НаСервере
Функция СоздатьКлючЗаписиРегистра(ПараметрыОткрытия)
	
	Возврат РегистрыСведений.ПрименениеСистемНалогообложения.СоздатьКлючЗаписи(ПараметрыОткрытия);
	
КонецФункции

&НаКлиенте
Процедура ИзменитьСНОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = ОрганизацииСЕНВД.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
	
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока.Результат = БиблиотекаКартинок.ВыполненоУспешно;
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
