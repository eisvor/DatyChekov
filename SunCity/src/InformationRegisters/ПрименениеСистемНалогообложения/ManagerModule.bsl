#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет СНО для организации на указанную дату
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации -  организация, у которой необходимо определить СНО 
//  Дата		 - Дата - дата, на которую необходимо определит СНО организации.
//                        Если не указано, СНО определяется на текущую дату сеанса.
// 
// Возвращаемое значение:
//   Перечисление.ТипыСистемНалогообложенияККТ, Неопределено - система налогообложения организации. Если не удалось найти, возвращает Неопределено. 
//
Функция СистемаНалогообложенияОрганизации(Организация, Дата = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрименениеСистемНалогообложенияСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
		|	ПрименениеСистемНалогообложенияСрезПоследних.ОсвобожденОтНДС КАК ОсвобожденОтНДС,
		|	ПрименениеСистемНалогообложенияСрезПоследних.Период КАК Период
		|ПОМЕСТИТЬ ВТСрезПоследних
		|ИЗ
		|	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних(
		|			&Дата,
		|			Организация = &Организация
		|				И Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|				И Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				И ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
		|				И ВЫБОР
		|					КОГДА СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.ТипыСистемНалогообложенияККТ.Патент)
		|						ТОГДА &Дата >= ДатаНачалаПатента
		|									И &Дата <= ДатаОкончанияПатента
		|								ИЛИ ДатаНачалаПатента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|									И ДатаОкончанияПатента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ) КАК ПрименениеСистемНалогообложенияСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВТСрезПоследних.Период) КАК Период
		|ПОМЕСТИТЬ ВТПериод
		|ИЗ
		|	ВТСрезПоследних КАК ВТСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
		|	ВТСрезПоследних.ОсвобожденОтНДС КАК ОсвобожденОтНДС
		|ИЗ
		|	ВТСрезПоследних КАК ВТСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериод КАК ВТПериод
		|		ПО ВТСрезПоследних.Период = ВТПериод.Период";
	
	Запрос.УстановитьПараметр("Дата", 		НачалоДня(Дата));
	Запрос.УстановитьПараметр("Организация", 	Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСНО = РезультатЗапроса.Выбрать();
	
	Если ВыборкаСНО.Следующий() Тогда
		
		Результат = Новый Структура("СистемаНалогообложения, ОсвобожденОтНДС");
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаСНО);
		
		Возврат Результат;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Разрешено удалить.
//
// Параметры:
//  Период		 - Дата - дата записи, которую нужно проверить.
//  Организация	 - СправочникСсылка.Организации - организация, по которой проходит проверка.
// 
// Возвращаемое значение:
//  Булево - ксли истина, то разрешено удалять, Ложь - запрещено удалять. 
//
Функция РазрешеноУдалятьЗапись(Период, Организация) Экспорт
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		ОрганизацияПомеченаНаУдаление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ПометкаУдаления");
		
		Если ОрганизацияПомеченаНаУдаление Тогда
			Возврат Истина;
		КонецЕсли;
		
	#КонецЕсли
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрименениеСистемНалогообложения.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПрименениеСистемНалогообложения КАК ПрименениеСистемНалогообложения
	|ГДЕ
	|	ПрименениеСистемНалогообложения.Период <> &Период
	|	И ПрименениеСистемНалогообложения.Организация = &Организация
	|	И ПрименениеСистемНалогообложения.Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
	|	И ПрименениеСистемНалогообложения.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	И ПрименениеСистемНалогообложения.ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Период", 		Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РазрешеноУдалять = НЕ РезультатЗапроса.Пустой();
	
	Возврат РазрешеноУдалять;
	
КонецФункции

// Проверяет использование патентной системы налогообложения.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - организация, по которой проходит проверка.
// 
// Возвращаемое значение:
//  Булево - если Истина, то использует Патент, иначе Ложь - не использует патент
//
Функция ОрганизацияИспользуетПатент(Организация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрименениеСистемНалогообложения.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПрименениеСистемНалогообложения КАК ПрименениеСистемНалогообложения
	|ГДЕ
	|	ПрименениеСистемНалогообложения.Организация = &Организация
	|	И ПрименениеСистемНалогообложения.СистемаНалогообложения = &СистемаНалогообложения";
	
	Запрос.УстановитьПараметр("Организация", 			Организация);
	Запрос.УстановитьПараметр("СистемаНалогообложения", Перечисления.ТипыСистемНалогообложенияККТ.Патент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОрганизацияИспользуетПатент = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОрганизацияИспользуетПатент;
	
КонецФункции

// Возвращает систему налогообложения переданных документов
//
// Параметры:
//  МассивДокументов	 - Массив - массив документов у которых есть реквизит СистемаНалогообложения.
//							 		Могут быть переданы документы разных типов.
// 
// Возвращаемое значение:
//  Соответствие - соответствие документа и системы налогообложения.
//      * Ключ     - ссылка на документ.
//      * Значение - значение Системы налогообложения.
//
Функция СистемаНалогообложенияДокументов(МассивДокументов) Экспорт
	
	ЗначенияСНОДокументов = Новый Соответствие;
	
	#Если НЕ ТолстыйКлиентУправляемоеПриложение Тогда
		
		// Разделение документов по типам
		РазбивкаПоТипам = ОбщегоНазначенияРТ.СоответствиеМассивовПоТипамОбъектов(МассивДокументов);
		
		// Определение значений реквизита
		Для каждого ТипДокументов Из РазбивкаПоТипам Цикл
			
			Если НЕ ОбщегоНазначения.ЕстьРеквизитОбъекта(
					"СистемаНалогообложения", ТипДокументов.Значение[0].Метаданные()) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначенияСНО = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
				ТипДокументов.Значение, "СистемаНалогообложения");
			
			Для каждого Документ Из ЗначенияСНО Цикл
				
				ЗначенияСНОДокументов.Вставить(Документ.Ключ, Документ.Значение);
				
			КонецЦикла;
			
		КонецЦикла;
		
	#КонецЕсли
	
	Возврат ЗначенияСНОДокументов
	
КонецФункции

// Проверяет, использовала ли организация ЕНВД на 31.12.2020.
//
// Параметры:
//  ДатаПроверки - Дата - дата проверки использования;
//  Организация - СправочникСсылка.Организации - организация, по которой проходит проверка.
// 
// Возвращаемое значение:
//  Булево - если Истина, то использовала ЕНВД.
//
Функция ОрганизацияИспользовалаЕНВД(ДатаПроверки, Организация) Экспорт
	
	ОрганизацияИспользовалаЕНВД = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат ОрганизацияИспользовалаЕНВД;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаПроверки) ИЛИ ДатаПроверки < Дата("20210101") Тогда
		Возврат ОрганизацияИспользовалаЕНВД;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрименениеСистемНалогообложенияСрезПоследних.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.ТипыСистемНалогообложенияККТ.ЕНВД)) КАК ПрименениеСистемНалогообложенияСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Дата("20201231235959"));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОрганизацияИспользовалаЕНВД = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОрганизацияИспользовалаЕНВД;
	
КонецФункции

Процедура ПроверитьНаличиеЕНВДВОрганизациях() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.Магазин, ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)) КАК Магазин,
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
	               |	ЕСТЬNULL(ПрименениеСистемНалогообложения.ТоварнаяГруппа, ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)) КАК ТоварнаяГруппа,
	               |	ПрименениеСистемНалогообложения.СистемаНалогообложения КАК СистемаНалогообложения,
	               |	ПрименениеСистемНалогообложения.Период КАК Период
	               |ИЗ
	               |	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних КАК ПрименениеСистемНалогообложения
	               |ГДЕ
	               |	ПрименениеСистемНалогообложения.СистемаНалогообложения = &СистемаНалогообложения";
	Запрос.УстановитьПараметр("СистемаНалогообложения", Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД);
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Константы.ЕНВДВОрганизациях.Установить(Истина);
	Иначе
		Константы.ЕНВДВОрганизациях.Установить(Ложь);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПроверитьНаПересечениеСНО(Организация, СистемаНалогообложения, ДетализированнаяСНО, ДатаПроверки) Экспорт
	
	СообщениеОбОшибке = Неопределено;
	
	ТестЗапросаСНО = 
	"ВЫБРАТЬ
	|	ПрименениеСистемНалогообложенияСрезПоследних.Организация КАК Организация,
	|	ПрименениеСистемНалогообложенияСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
	|	ПрименениеСистемНалогообложенияСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ ВТСрезПоследних
	|ИЗ
	|	РегистрСведений.ПрименениеСистемНалогообложения.СрезПоследних(
	|			&Дата,
	|			Организация = &Организация
	|				И ВЫБОР
	|					КОГДА &ДетализированнаяСНО = ИСТИНА
	|						ТОГДА Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
	|								И Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|								И ТоварнаяГруппа = ЗНАЧЕНИЕ(Справочник.ТоварныеГруппы.ПустаяСсылка)
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|				И ВЫБОР
	|					КОГДА СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.ТипыСистемНалогообложенияККТ.Патент)
	|						ТОГДА &Дата >= ДатаНачалаПатента
	|									И &Дата <= ДатаОкончанияПатента
	|								ИЛИ ДатаНачалаПатента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|									И ДатаОкончанияПатента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ) КАК ПрименениеСистемНалогообложенияСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСрезПоследних.Организация КАК Организация,
	|	МАКСИМУМ(ВТСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ ВТПериод
	|ИЗ
	|	ВТСрезПоследних КАК ВТСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСрезПоследних.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСрезПоследних.Организация КАК Организация,
	|	ВТСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения
	|ИЗ
	|	ВТСрезПоследних КАК ВТСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериод КАК ВТПериод
	|		ПО ВТСрезПоследних.Период = ВТПериод.Период
	|			И ВТСрезПоследних.Организация = ВТПериод.Организация
	|ГДЕ
	|	НЕ ВТСрезПоследних.СистемаНалогообложения = &СистемаНалогообложения
	|	И ВТСрезПоследних.СистемаНалогообложения В (ЗНАЧЕНИЕ(Перечисление.ТипыСистемНалогообложенияККТ.ОСН), 
	|												ЗНАЧЕНИЕ(Перечисление.ТипыСистемНалогообложенияККТ.УСНДОХОД), 
	|												ЗНАЧЕНИЕ(Перечисление.ТипыСистемНалогообложенияККТ.УСНДОХОДРАСХОД))
	|	И ВЫБОР
	|		КОГДА &ДетализированнаяСНО = ИСТИНА ТОГДА
	|			ВТСрезПоследних.Период <= &Дата
	|		ИНАЧЕ ВТСрезПоследних.Период >= &Дата
	|	КОНЕЦ";

	Запрос = Новый Запрос(ТестЗапросаСНО);
	Запрос.УстановитьПараметр("Дата", НачалоДня(ДатаПроверки));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СистемаНалогообложения", СистемаНалогообложения);
	Запрос.УстановитьПараметр("ДетализированнаяСНО", ДетализированнаяСНО);
	ВыборкаСНО = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаСНО.Следующий() Цикл
		
		СообщениеОбОшибке = НСтр("ru='Для организации %1 уже используется ""%2"" система налогообложения'");
		СообщениеОбОшибке = СтрШаблон(СообщениеОбОшибке, Организация, ВыборкаСНО.СистемаНалогообложения);
		
	КонецЦикла;
	
	Возврат СообщениеОбОшибке;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Магазин)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли
