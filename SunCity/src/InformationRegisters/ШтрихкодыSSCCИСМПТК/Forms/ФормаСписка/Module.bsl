
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		ОрганизацияОтбор = Параметры.Отбор.Организация;
	КонецЕсли;
	
	ОбщегоНазначенияИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПечатьЭтикетокШтрихкода", "Доступность", ПравоДоступа("Просмотр", Метаданные.Обработки.ПечатьКодовМаркировкиИСМПТК));
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)	
	
	УстановитьБыстрыйОтбор("Организация", ОрганизацияОтбор);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьБыстрыйОтбор(ВидЭлемента, ЗначениеЭлемента)
	
	ОбщегоНазначенияИСМПТККлиентСервер.ИзменитьЭлементОтбораСписка(Список, ВидЭлемента, ЗначениеЭлемента, ЗначениеЗаполнено(ЗначениеЭлемента));
	
КонецПроцедуры

#КонецОбласти

#Область ПечатьSSCC

&НаКлиенте
Процедура ПечатьЭтикетокШтрихкода(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Штрихкод = ТекущиеДанные.Штрихкод;
	Если Не ЗначениеЗаполнено(Штрихкод) Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеВопросПечати = Новый ОписаниеОповещения("ОбработкаОтветаПользователяПриПечати", ЭтотОбъект);
	
	ПоказатьВопрос(ОповещениеВопросПечати, 
		НСтр("ru = 'Распечатать штрихкод?'"),
		РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаПользователяПриПечати(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПечати = ПодготовитьДанныеДляПечати();
	
	Если ДанныеПечати.ОбъектыПечати.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не обнаружено подходящих данных для печати этикетки транспортного кода.'");
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПечатьКодовМаркировкиИСМПТККлиент.НапечататьSSCC(ДанныеПечати, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте 
Функция ПодготовитьДанныеДляПечати()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ОбъектыПечати = Новый Массив;
	
	ПараметрыШтрихкода = ПечатьКодовМаркировкиИСМПТККлиентСервер.СтруктураПечатиЭтикеткиSSCC();
	
	ПараметрыШтрихкода.Организация        = ТекущиеДанные.Организация;
	ПараметрыШтрихкода.ЦифраРасширения    = ТекущиеДанные.ЦифраРасширения;
	ПараметрыШтрихкода.ПрефиксКомпанииGS1 = ТекущиеДанные.ПрефиксКомпанииGS1;
	ПараметрыШтрихкода.СерийныйНомерSSCC  = ТекущиеДанные.СерийныйНомерSSCC;
	ПараметрыШтрихкода.Штрихкод     	  = ТекущиеДанные.Штрихкод;
	ПараметрыШтрихкода.ТипШтрихкода       = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодовИСМПТК.SSCC");
	ПараметрыШтрихкода.ШаблонЭтикетки 	  = ПечатьКодовМаркировкиИСМПТККлиентСервер.ПолучитьШаблонЭтикеткиSSCC();
	ПараметрыШтрихкода.Количество         = 1;
	
	ОбъектыПечати.Добавить(ПараметрыШтрихкода);
	
	ИтоговаяСтруктура = Новый Структура;
	ИтоговаяСтруктура.Вставить("ОбъектыПечати",              ОбъектыПечати);
	ИтоговаяСтруктура.Вставить("КаждаяЭтикеткаНаНовомЛисте", Истина);
	ИтоговаяСтруктура.Вставить("ЕстьПечатьЭтикетокИЦенников", ПроверитьНаличиеМенеджераПечати());
	
	Возврат ИтоговаяСтруктура;
	
КонецФункции

&НаСервере
Функция ПроверитьНаличиеМенеджераПечати()
	
	Возврат ?(Метаданные.НайтиПоПолномуИмени("Обработка.ПечатьКодовМаркировкиИСМПТК") <> Неопределено, Истина, Ложь);
	
КонецФункции

#КонецОбласти

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ОбщегоНазначенияИСМПТККлиентПереопределяемый.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	
	ОбщегоНазначенияИСМПТКПереопределяемый.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти