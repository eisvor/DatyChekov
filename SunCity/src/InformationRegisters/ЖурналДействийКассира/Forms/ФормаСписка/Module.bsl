#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ИнтерфейсРМКСлужебный.УстановитьУсловноеОформлениеЖурналаДействийКассира(ЭтотОбъект);
	ОбщегоНазначенияРМК.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытиеУстановкиСтроки = "УстановитьСтроку";
	Если ИмяСобытия = СобытиеУстановкиСтроки И ЭтотОбъект = Источник Тогда
		УстановитьСтрокуПоОтборуНаСервере(Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодЗаписейПриИзменении(Элемент)
	
	ПараметрНачалоПериода = Список.Параметры.Элементы.Найти("НачалоПериода");
	ПараметрКонецПериода = Список.Параметры.Элементы.Найти("КонецПериода");
	Если ЗначениеЗаполнено(ПериодЗаписей.ДатаНачала) Тогда
		ПараметрНачалоПериода.Значение = ПериодЗаписей.ДатаНачала;
	КонецЕсли;
	ПараметрНачалоПериода.Использование = ЗначениеЗаполнено(ПериодЗаписей.ДатаНачала);
	Если ЗначениеЗаполнено(ПериодЗаписей.ДатаОкончания) Тогда
		ПараметрКонецПериода.Значение = ПериодЗаписей.ДатаОкончания;
	КонецЕсли;
	ПараметрКонецПериода.Использование = ЗначениеЗаполнено(ПериодЗаписей.ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура КассирОтборПриИзменении(Элемент)
	
	ПолеКомпоновкиКассир = Новый ПолеКомпоновкиДанных("Кассир");
	КоличествоЭлементов = Список.Отбор.Элементы.Количество();
	ИндексЭлемента = 0;
	ЭлементОтбораКассир = Неопределено;
	Пока ИндексЭлемента < КоличествоЭлементов И ЭлементОтбораКассир = Неопределено Цикл
		ЭлементОтбора = Список.Отбор.Элементы.Получить(ИндексЭлемента);
		Если ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиКассир Тогда
			ЭлементОтбораКассир = ЭлементОтбора;
		КонецЕсли;
		ИндексЭлемента = ИндексЭлемента + 1;
	КонецЦикла;
	Если ЭлементОтбораКассир = Неопределено И ЗначениеЗаполнено(КассирОтбор) Тогда
		ЭлементОтбораКассир = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораКассир.ЛевоеЗначение = ПолеКомпоновкиКассир;
		ЭлементОтбораКассир.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	Если Не ЭлементОтбораКассир = Неопределено Тогда
		ЭлементОтбораКассир.ПравоеЗначение = КассирОтбор;
		ЭлементОтбораКассир.Использование = ЗначениеЗаполнено(КассирОтбор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	ОткрытьФорму("Отчет.НезавершенныеДействияСЧекомРМК.Форма.ФормаОтчета", ,
		ЭтотОбъект, , , , ,РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСтрокуПоОтборуНаСервере(ПараметрыОтбора)
	
	ЗапросЗаписи = Новый Запрос;
	ЗапросЗаписи.Текст =
	"ВЫБРАТЬ
	|	ЖурналДействийКассира.ДатаВремяОперации КАК ДатаВремяОперации,
	|	ЖурналДействийКассира.РабочееМесто КАК РабочееМесто,
	|	ЖурналДействийКассира.ТорговыйОбъект КАК ТорговыйОбъект,
	|	ЖурналДействийКассира.ОперацияСЧеком КАК ОперацияСЧеком,
	|	ЖурналДействийКассира.Пользователь КАК Пользователь,
	|	ЖурналДействийКассира.Кассир КАК Кассир,
	|	ЖурналДействийКассира.НомерСеанса КАК НомерСеанса
	|ИЗ
	|	РегистрСведений.ЖурналДействийКассира КАК ЖурналДействийКассира
	|ГДЕ
	|	ЖурналДействийКассира.РабочееМесто = &РабочееМесто
	|	И ЖурналДействийКассира.ТорговыйОбъект = &ТорговыйОбъект
	|	И ЖурналДействийКассира.НомерСеанса = &НомерСеанса
	|	И ЖурналДействийКассира.ДатаВремяОперации = &ДатаВремяОперации";
	ЗапросЗаписи.УстановитьПараметр("РабочееМесто", ПараметрыОтбора.РабочееМесто);
	ЗапросЗаписи.УстановитьПараметр("ТорговыйОбъект", ПараметрыОтбора.ТорговыйОбъект);
	ЗапросЗаписи.УстановитьПараметр("НомерСеанса", ПараметрыОтбора.НомерСеанса);
	ЗапросЗаписи.УстановитьПараметр("ДатаВремяОперации", ПараметрыОтбора.ДатаВремяОперации);
	Выборка = ЗапросЗаписи.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Состав = "ДатаВремяОперации,РабочееМесто,ТорговыйОбъект,ОперацияСЧеком,Пользователь,Кассир,НомерСеанса";
		СтруктураКлюча = Новый Структура(Состав);
		ЗаполнитьЗначенияСвойств(СтруктураКлюча, Выборка);
		КлючЗаписи = РегистрыСведений.ЖурналДействийКассира.СоздатьКлючЗаписи(СтруктураКлюча);
		Элементы.Список.ТекущаяСтрока = КлючЗаписи;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти