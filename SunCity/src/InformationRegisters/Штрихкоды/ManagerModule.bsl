#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает данные по штрихкодам.
//
// Параметры:
//  Штрихкоды - ТаблицаЗначений - таблица штрихкодов.
//
// Возвращаемое значение:
//  ТаблицаЗначений - данные по штрихкодам.
//
Процедура ПолучитьДанныеПоШтрихкодам(КэшированныеЗначения, Штрихкоды) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	Штрихкоды.Владелец КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	&ОпределениеПризнакаМаркируемаяПродукция КАК МаркируемаяПродукция
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК Штрихкоды
	|ГДЕ
	|	Штрихкоды.Штрихкод В(&МассивШтрихкодов)
	|	И Штрихкоды.Владелец ССЫЛКА Справочник.Номенклатура");
	
	ОпределениеМаркируемаяПродукция = ИнтеграцияИСРТ.ОпределениеПризнакаМаркируемаяПродукцияТекстаЗапроса("Штрихкоды.Владелец");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОпределениеПризнакаМаркируемаяПродукция", ОпределениеМаркируемаяПродукция);
	
	МассивШтрихкодов = Новый Массив;
	
	Для Каждого ТекШтрихкод Из Штрихкоды Цикл
		Штрихкод = ВРег(СокрЛП(ТекШтрихкод));
		
		МассивШтрихкодов.Добавить(Штрихкод);
		
		ИнформацияПоШтрихкодам = ОбработкаТабличнойЧастиТоварыКлиентСервер.СтруктураКешируемойИнформацииПоШтрихкоду();
		ИнформацияПоШтрихкодам.Штрихкод = Штрихкод;
		КэшированныеЗначения.Штрихкоды.Вставить(Штрихкод, ИнформацияПоШтрихкодам);
		
	КонецЦикла;

	Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекДанные = КэшированныеЗначения.Штрихкоды[ВРег(СокрЛП(Выборка.Штрихкод))];
		ТекДанные.Номенклатура                    = Выборка.Номенклатура;
		ТекДанные.Характеристика                  = Выборка.Характеристика;
		ТекДанные.Упаковка                        = Выборка.Упаковка;
		ТекДанные.МаркируемаяПродукция            = Выборка.МаркируемаяПродукция;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
