#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры);
	
	Элементы.ТоварнаяГруппа.Доступность = НЕ ЗначениеЗаполнено(Запись.ТоварнаяГруппа);
	Элементы.Магазин.Доступность 		= НЕ ЗначениеЗаполнено(Запись.Магазин);
	
	ИспользуетсяНесколькоСкладов 		= Справочники.Склады.ИспользуетсяНесколькоСкладов(Запись.Магазин);
	ИспользоватьНесколькоОрганизаций 	= ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
	Элементы.СкладПродажи.Видимость 	= ИспользуетсяНесколькоСкладов;
	Элементы.СкладПоступления.Видимость = ИспользуетсяНесколькоСкладов;
	Элементы.Организация.Доступность	= ИспользоватьНесколькоОрганизаций;
	
	Если НЕ ИспользоватьНесколькоОрганизаций Тогда
		Запись.Организация = ПолучитьОрганизацию();
	КонецЕсли;
	
	Если Параметры.Свойство("СкрытьРабочееМесто") Тогда
		
		Элементы.РабочееМесто.Видимость = Ложь;
		
		Если Параметры.Свойство("РазныеСкладыПродажиПоступления", РазныеСкладыПродажиПоступления) Тогда
			Если НЕ РазныеСкладыПродажиПоступления Тогда
				Элементы.СкладПоступления.Видимость = Ложь;
				Элементы.СкладПродажи.Заголовок		= "Склад";
			КонецЕсли;
		Иначе
			РазныеСкладыПродажиПоступления = Истина; 
		КонецЕсли;
		
	Иначе //Для записи с указанным Рабочим местом нет необходимости вводить Склад поступления
		
		РазныеСкладыПродажиПоступления 		= Ложь;
		Элементы.СкладПоступления.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СкладПродажиПриИзменении(Элемент)
	
	Если НЕ РазныеСкладыПродажиПоступления Тогда
		Запись.СкладПоступления = Запись.СкладПродажи;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МагазинПриИзменении(Элемент)
	
	ИспользуетсяНесколькоСкладов = ИспользуетсяНесколькоСкладов(Запись.Магазин);
	
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СкладПродажи", 
		"Видимость", 
		ИспользуетсяНесколькоСкладов);
		
	ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СкладПоступления", 
		"Видимость", 
		ИспользуетсяНесколькоСкладов);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("РаспределениеНоменклатурыИзменениеЗаписи", Запись.Магазин);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИспользуетсяНесколькоСкладов(Магазин)

	Возврат Справочники.Склады.ИспользуетсяНесколькоСкладов(Магазин);

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОрганизацию()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.Ссылка;
		
	КонецЕсли;
	
КонецФункции // ПолучитьОрганизацию()


#КонецОбласти
