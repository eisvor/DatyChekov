#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДанныеКодовМаркировки(КодыМаркировки) Экспорт
	
	ИсходныеДанные = Новый ТаблицаЗначений;
	ИсходныеДанные.Колонки.Добавить("КодИдентификации",     Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(200)));
	ИсходныеДанные.Колонки.Добавить("ХешКодаИдентификации", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(50)));
	
	Для Каждого КодМаркировки Из КодыМаркировки Цикл
		
		НоваяСтрока = ИсходныеДанные.Добавить();
		НоваяСтрока.КодИдентификации     = КодМаркировки;
		НоваяСтрока.ХешКодаИдентификации = ОбщегоНазначенияИСМПТК.ХешированиеДанныхSHA256(КодМаркировки);
		
	КонецЦикла;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ИсходныеДанные.КодИдентификации КАК КодИдентификации,
	|	ИсходныеДанные.ХешКодаИдентификации КАК ХешКодаИдентификации
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&ИсходныеДанные КАК ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПулКодовМаркировкиСУЗИСМПТК.Организация               КАК Организация,
	|	ПулКодовМаркировкиСУЗИСМПТК.ЗаказНаЭмиссию            КАК ЗаказНаЭмиссию,
	|	ПулКодовМаркировкиСУЗИСМПТК.ХешСуммаКодаИдентификации КАК ХешСуммаКодаИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.Шаблон               	  КАК Шаблон,
	|	ПулКодовМаркировкиСУЗИСМПТК.GTIN               		  КАК GTIN,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодИдентификации          КАК КодИдентификации,
	|	ПулКодовМаркировкиСУЗИСМПТК.КодМаркировки             КАК КодМаркировки,
	|	ПулКодовМаркировкиСУЗИСМПТК.Статус            		  КАК Статус,
	|	ПулКодовМаркировкиСУЗИСМПТК.ДатаЭмиссии          	  КАК ДатаЭмиссии,
	|	ПулКодовМаркировкиСУЗИСМПТК.ДатаПечати    			  КАК ДатаПечати,
	|	ПулКодовМаркировкиСУЗИСМПТК.ДатаВыгрузки              КАК ДатаВыгрузки,
	|	ПулКодовМаркировкиСУЗИСМПТК.EAN              		  КАК EAN,
	|	ПулКодовМаркировкиСУЗИСМПТК.Номенклатура              КАК Номенклатура,
	|	ПулКодовМаркировкиСУЗИСМПТК.Характеристика       	  КАК Характеристика,
	|	ПулКодовМаркировкиСУЗИСМПТК.СпособВводаВОборот		  КАК СпособВводаВОборот,
	|	ПулКодовМаркировкиСУЗИСМПТК.ТипШтрихкода   			  КАК ТипШтрихкода,
	|	ПулКодовМаркировкиСУЗИСМПТК.ВидПродукции   			  КАК ВидПродукции
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ПулКодовМаркировкиСУЗИСМПТК
	|		ПО ИсходныеДанные.КодИдентификации = ПулКодовМаркировкиСУЗИСМПТК.КодИдентификации
	|			И ИсходныеДанные.ХешКодаИдентификации = ПулКодовМаркировкиСУЗИСМПТК.ХешСуммаКодаИдентификации");
	
	Запрос.УстановитьПараметр("ИсходныеДанные", ИсходныеДанные);
	
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	ДанныеПула = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		ДанныеКода = ОбщегоНазначенияИСМПТКПереопределяемый.СтрокаТаблицыЗначенийВСтруктуру(Выборка);
		ДанныеПула.Вставить(Выборка.КодМаркировки, ДанныеКода);
	КонецЦикла;
	
	Возврат ДанныеПула;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СтатусыВыведенИзОборота() Экспорт
	
	Статусы = Новый Массив();
	Статусы.Добавить(Перечисления.СтатусыКодовМаркировкиСУЗИСМПТК.Использован);
	Статусы.Добавить(Перечисления.СтатусыКодовМаркировкиСУЗИСМПТК.Отбракован);
	
	Возврат Статусы;
	
КонецФункции

//Возвращает структуру для наложения отборов на регистр ПулКодовМаркировкиСУЗ
// 
//Параметры:
//   Документ  - ДокументСсылка - источник отбора
//
//Возвращаемое значение:
//   Структура - Где ключ - это имя измерения регистра по типу документа, значение - ссылка на документ
//
Функция СтруктураОтбораДокументаПоТипуДокумента(Документ) Экспорт
	
	СтруктураОтбораДокументы = Новый Структура;
	
	ТипДокумента = ТипЗнч(Документ);
	Если ТипДокумента = Тип("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК") Тогда
		СтруктураОтбораДокументы.Вставить("ЗаказНаЭмиссию", Документ);
	КонецЕсли;
	
	Возврат СтруктураОтбораДокументы;
	
КонецФункции

// Процедура выполняет блокировку записей пула, выполняет печать этикеток и сохраняет дату печати
// 
// Параметры:
// 	ДанныеПечати - Структура - Входные данные печаети
// 	ТабличныйДокумент - ТабличныйДокумент - Табличный документ для вывода результатов
// 	СтруктураНастроек - Структура - Дополнительные настройки печати
Процедура РаспечататьЭтикеткиИЗафиксироватьДатуПечати(ДанныеПечати, ТабличныйДокумент, СтруктураНастроек = Неопределено, РезультатРезервирования = Неопределено, СтруктураМакетаШаблона = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документ                    = Неопределено;
	СтруктураОбъектовПечати     = Новый Структура();
	СоответствиеКодовМаркировки = Новый Соответствие();
	ОбъектыПечати               = Новый Массив();
	РежимПечати                 = "";
	
	Если ЭтоАдресВременногоХранилища(ДанныеПечати.АдресВХранилище) Тогда
		СтруктураОбъектовПечати = ПолучитьИзВременногоХранилища(ДанныеПечати.АдресВХранилище);
		ОбъектыПечати = СтруктураОбъектовПечати.ОбъектыПечати;
		СтруктураОбъектовПечати.Свойство("РежимПечати", РежимПечати);
	КонецЕсли;
	
	Если СтруктураНастроек = Неопределено Тогда
		СтруктураНастроек = ПечатьКодовМаркировкиИСМПТК.СтруктураНастроекЭтикеткаИСМП();
	КонецЕсли;
	
	Если ДанныеПечати.Свойство("КаждаяЭтикеткаНаНовомЛисте") Тогда
		СтруктураНастроек.Вставить("КаждаяЭтикеткаНаНовомЛисте", ДанныеПечати.КаждаяЭтикеткаНаНовомЛисте);
	КонецЕсли;
	
	Если СтруктураМакетаШаблона <> Неопределено Тогда
		СтруктураНастроек.Вставить("СтруктураМакетаШаблона", СтруктураМакетаШаблона);
	КонецЕсли;
	
	ИсходнаяТаблица = НоваяТаблицаДанныхДляПечатиЭтикеток();
	НомерСтроки = 1;
	
	Для Каждого ОбъектПечати Из ОбъектыПечати Цикл
		
		НоваяСтрока = ИсходнаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбъектПечати);
		НоваяСтрока.КодEAN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(НоваяСтрока.GTIN);
		НоваяСтрока.Порядок = НомерСтроки;
		
		НомерСтроки = НомерСтроки + 1;
		
		Если Не ЗначениеЗаполнено(НоваяСтрока.ХешСуммаКодаИдентификации) Тогда
			Если ЗначениеЗаполнено(НоваяСтрока.КодИдентификации) Тогда
				НоваяСтрока.ХешСуммаКодаИдентификации = ОбщегоНазначенияИСМПТК.ХэшСуммаСтроки(НоваяСтрока.КодИдентификации);
			ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.Штрихкод) Тогда
				НоваяСтрока.ХешСуммаКодаИдентификации = ОбщегоНазначенияИСМПТК.ХэшСуммаСтроки(НоваяСтрока.Штрихкод);
			КонецЕсли;
		КонецЕсли;
		СоответствиеКодовМаркировки.Вставить(НоваяСтрока.Штрихкод);
	КонецЦикла;
	
	//Печать из обработки печати КМ из загруженных файлов - упрощенный режим без использования Пула кодов
	Если ДанныеПечати.Свойство("Документ") Тогда
		Если ДанныеПечати.Документ = "ПечатьИзФайла" Тогда 
			
			НачатьТранзакцию();
	        Попытка
				Если РежимПечати = "РезервироватьСвободныеКоды" Тогда
					
					СтруктураДанныхПоДокументу = СтруктураДанныхДляПечатиКодовИзФайла(ИсходнаяТаблица);
					ВывестиВТабличныйДокументЭтикеткиОбувь(ТабличныйДокумент, СтруктураДанныхПоДокументу.ТаблицаПечати, СтруктураНастроек);
					
				Иначе
					ВызватьИсключение РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаПечатиНеУказанРежимПечати();
				КонецЕсли;
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
				ВызватьИсключение;
			КонецПопытки;
			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	//Стандартная печать КМ с использованием данных Пула кодов
	БлокировкаЗаписей = Новый БлокировкаДанных;
	
	Если ДанныеПечати.Свойство("Документ", Документ) Тогда
		СтруктураОтбораДокументы = СтруктураОтбораДокументаПоТипуДокумента(Документ);
		Для Каждого ПолеОтбора Из СтруктураОтбораДокументы Цикл
			ЭлементБлокировки = БлокировкаЗаписей.Добавить("РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК");
			ЭлементБлокировки.УстановитьЗначение(ПолеОтбора.Ключ, ПолеОтбора.Значение);
		КонецЦикла;
	Иначе
		Для Каждого КлючЗначение Из СоответствиеКодовМаркировки Цикл
			ЭлементБлокировки = БлокировкаЗаписей.Добавить("РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК");
			ЭлементБлокировки.УстановитьЗначение("КодИдентификации", КлючЗначение.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаЗаписей.Заблокировать();
		
		Если РежимПечати = "РезервироватьСвободныеКоды" Тогда
			
			СтруктураДанныхПоДокументу = СтруктураДанныхДляПечатиСвободныхКодов(ИсходнаяТаблица, СтруктураОтбораДокументы);
			ВывестиВТабличныйДокументЭтикеткиОбувь(ТабличныйДокумент, СтруктураДанныхПоДокументу.ТаблицаПечати, СтруктураНастроек);
			УстановитьСостояниеРаспечатанПоСтруктуреДокумента(СтруктураДанныхПоДокументу);
	
		Иначе
			ВызватьИсключение РаботаСТекстамиИСМПТККлиентСервер.ТекстСообщенияОшибкаПечатиНеУказанРежимПечати();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Удаляет использованные коды маркировки из пула в случаях:
//   * Все коды маркировки по основанию распечатаны, основание полностью оформлено (по основанию)
//   * Основание не указано или архивировано, коды маркировки распечатаны (по использованным кодам).
//
Процедура ОчисткаПулаКодовМаркировки() Экспорт
	
	СрокХраненияНапечатанныхКодов = Константы.СрокХраненияИспользованныхКодовМаркировкиИСМПТК.Получить();
	СрокХраненияВыгруженныхКодов  = Константы.СрокХраненияВыгруженныхКодовМаркировкиИСМПТК.Получить();
	
	Если СрокХраненияНапечатанныхКодов = 0 И СрокХраненияВыгруженныхКодов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДеньВСекундах = 86400;
	ДатаОчисткиНапечатанных = ?(СрокХраненияНапечатанныхКодов = 0, Дата(0001,01,01), НачалоДня(ТекущаяДатаСеанса()) - ДеньВСекундах * СрокХраненияНапечатанныхКодов);
	ДатаОчисткиВыгруженных 	= ?(СрокХраненияВыгруженныхКодов  = 0, Дата(0001,01,01), НачалоДня(ТекущаяДатаСеанса()) - ДеньВСекундах * СрокХраненияВыгруженныхКодов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаОчисткиНапечатанных", ДатаОчисткиНапечатанных);
	Запрос.УстановитьПараметр("ДатаОчисткиВыгруженных",  ДатаОчисткиВыгруженных);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПулКодовМаркировкиСУЗИСМПТК.ХешСуммаКодаИдентификации КАК ХешСуммаКодаИдентификации
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ПулКодовМаркировкиСУЗИСМПТК
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ПулКодовМаркировкиСУЗИСМПТК.ДатаПечати <> ДАТАВРЕМЯ(1, 1, 1)
	|					И ПулКодовМаркировкиСУЗИСМПТК.ДатаПечати < &ДатаОчисткиНапечатанных
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ПулКодовМаркировкиСУЗИСМПТК.ДатаВыгрузки <> ДАТАВРЕМЯ(1, 1, 1)
	|							И ПулКодовМаркировкиСУЗИСМПТК.ДатаВыгрузки < &ДатаОчисткиВыгруженных
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		КОНЕЦ
	|	И (ПулКодовМаркировкиСУЗИСМПТК.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКодовМаркировкиСУЗИСМПТК.Сгенерирован)
	|			ИЛИ ПулКодовМаркировкиСУЗИСМПТК.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКодовМаркировкиСУЗИСМПТК.Использован))";
	
	ВыборкаКоды = Запрос.Выполнить().Выбрать();
	
	Набор = РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.СоздатьНаборЗаписей();
	Пока ВыборкаКоды.Следующий() Цикл
		Набор.Отбор.ХешСуммаКодаИдентификации.Установить(ВыборкаКоды.ХешСуммаКодаИдентификации);
		Набор.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Используя объект платформы СхемаЗапроса добавляет условие по документу в зависимости от типа документа
// 
// Параметры:
// 	Запрос - Запрос - Экземпляр объекта запроса, текст которого необходимо модифицировать
// 	СтруктураОтбораДокументы - Структура - Описание:
// * Ключ - Строка - Имя поля, на значение которог необходимо установить отбор
// * Значение - ДокументСсылка - Значение отбора, которое необходимо установить
// 	ИндексПакета - Число, Неопределено - Индекс запроса в пакете, в который необходимо добавить условие,
//	если Неопределено - условие добавляется в последний запрос.
Процедура ДобавитьВЗапросПечатиУсловияПоДокументу(Запрос, СтруктураОтбораДокументы, ИндексПакета=Неопределено) Экспорт
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	
	Если ИндексПакета = Неопределено Тогда
		ПоследнийПакет = СхемаЗапроса.ПакетЗапросов.Получить(СхемаЗапроса.ПакетЗапросов.Количество() - 1);
	Иначе
		ПоследнийПакет = СхемаЗапроса.ПакетЗапросов.Получить(ИндексПакета);
	КонецЕсли;
	
	Операторы = ПоследнийПакет.Операторы.Получить(0);
	Для Каждого КлючЗначение Из СтруктураОтбораДокументы Цикл
		Операторы.Отбор.Добавить(СтрШаблон("%1 = &%2", КлючЗначение.Ключ, КлючЗначение.Ключ));
		Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если ПечатьКодовМаркировкиИСМПТКПереопределяемый.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭтикеткаКодМаркировкиИСМП") Тогда
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПулКодовМаркировкиСУЗИСМПТК_ЭтикеткаИСМП";
		
		ВходящиеДанныеПечати = ПолучитьИзВременногоХранилища(ПараметрыПечати.АдресВХранилище);
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("АдресВХранилище",            ПараметрыПечати.АдресВХранилище);
		ДанныеПечати.Вставить("Документ",                   ВходящиеДанныеПечати.Документ);
		ДанныеПечати.Вставить("КаждаяЭтикеткаНаНовомЛисте", ВходящиеДанныеПечати.КаждаяЭтикеткаНаНовомЛисте);
		ДанныеПечати.Вставить("РазрешитьПовторнуюПечать",   ОбщегоНазначенияИСМПТКПереопределяемый.РазрешенаПовторнаяПечатьКодовМаркировки());
		
		РаспечататьЭтикеткиИЗафиксироватьДатуПечати(ДанныеПечати, ТабличныйДокумент);
		
		ПечатьКодовМаркировкиИСМПТКПереопределяемый.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
															 "ЭтикеткаКодМаркировкиИСМП", 
															 НСтр("ru = 'Этикетки кодов маркировки'"),
															 ТабличныйДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит на печать этикетки
// 
// Параметры:
// 	ТабличныйДокумент - ТабличныйДокумент - Результат печати
// 	ТаблицаОбъектыПечати - ТаблицаЗначений - Исходные данные для печати
// 	СтруктураНастроек - Структура - (См. ПечатьЭтикетокИСМП.СтруктураНастроекЭтикеткаИСМП).
Процедура ВывестиВТабличныйДокументЭтикеткиОбувь(ТабличныйДокумент, ТаблицаОбъектыПечати, СтруктураНастроек) Экспорт
	
	СтандартнаяОбработка = Истина;
	ПечатьКодовМаркировкиИСМПТКПереопределяемый.ПечатьЭтикетокИСМП(ТаблицаОбъектыПечати, ТабличныйДокумент, СтруктураНастроек, СтандартнаяОбработка);

КонецПроцедуры

// Подготавливает данные для печати этикеток ИС
// 
// Параметры:
// 	ДанныеПечати - Структура - Данные для печати
// Возвращаемое значение:
// 	Структура, Неопределено - Описание:
// * РезультатРезервирования - Массив - Результат резервированных кодов маркировки
// * ТабличныйДокумент - ТабличныйДокумент - Напечатанные коды маркировки
Функция ПечатьЭтикетокСРезервированиемПоДокументу(ДанныеПечати) Экспорт
	
	СтруктураНастроек = ПечатьКодовМаркировкиИСМПТК.СтруктураНастроекЭтикеткаИСМП();
	СтруктураНастроек.КаждаяЭтикеткаНаНовомЛисте = ДанныеПечати.КаждаяЭтикеткаНаНовомЛисте;
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	РезультатРезервирования = Неопределено;
	Попытка
		РаспечататьЭтикеткиИЗафиксироватьДатуПечати(ДанныеПечати, ТабличныйДокумент, СтруктураНастроек, РезультатРезервирования);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("ТабличныйДокумент",       ТабличныйДокумент);
	СтруктураРезультат.Вставить("РезультатРезервирования", РезультатРезервирования);
	
	Возврат СтруктураРезультат;
	
КонецФункции

// Возвращает структуру данных для печати свободных кодов.
// 
// Параметры:
// 	ТаблицаНоменклатуры - ТаблицаЗначений - Исходные данныех для печати
// 	СтруктураОтбораДокументы - Структура - Описание:
// * ДокументОснование - ДокументСсылка - Документ-основание для резервирования свободных кодов
// Возвращаемое значение:
// 	Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - Данные для печати, по структуре частично повторяют ПулКодовМаркировки
// * Отбор - Структура - Структура с полями для отбора по документу, используется для передачи в 
//		(см. РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК.ДобавитьВЗапросПечатиУсловияПоДокументу)
Функция СтруктураДанныхДляПечатиСвободныхКодов(ТаблицаНоменклатуры, СтруктураОтбораДокументы) Экспорт

	//Формируем данные для отбора доступных КМ из Пула.
	//Отбирать данные из регистра нужно строго по связке параметров Заказ-GTIN,
	//т.к. GTIN может встречаться и в рамках других заказов, не выбранных пользователем для печати.
	//При этом таких комбинаций может быть сразу несколько, поэтому параметры придется устанавливать парно,
	//формируя несколько однотипных подзапросов.
	
	ТоварыДляОтбора = ТаблицаНоменклатуры.Скопировать(, "ЗаказКМ, GTIN, Количество");
	ТоварыДляОтбора.Свернуть("ЗаказКМ, GTIN", "Количество");
	
	Запрос = Новый Запрос;
	Запрос.Текст = РаботаСДокументамиИСМПТКПереопределяемый.ПолучитьТекстЗапросаДляПечатиКМ(ТоварыДляОтбора.Количество());
	
	ТекстОшибки = НСтр("ru = 'При формировании запроса данных по регистру сведений ""%НаименованиеРегистра%"" произошла ошибка. Процесс печати прерван.'");
	ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НаименованиеРегистра%", Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.Представление());
	
	Если Запрос.Текст = "" Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
		
	СчетчикЦикла = 1;
	Для Каждого СтрокаТоваров Из ТоварыДляОтбора Цикл
	
		ИмяПараметрGTIN = "GTIN" + Строка(СчетчикЦикла);
		ИмяПараметрКоличество = "%КоличествоКПечати" + Строка(СчетчикЦикла);
		ИмяПараметрЗаказНаЭмиссию = "ЗаказНаЭмиссию" + Строка(СчетчикЦикла);
		
		Запрос.УстановитьПараметр(ИмяПараметрЗаказНаЭмиссию, СтрокаТоваров.ЗаказКМ); 
		Запрос.УстановитьПараметр(ИмяПараметрGTIN, СтрокаТоваров.GTIN); 
		Запрос.Текст = СтрЗаменитьПервое(Запрос.Текст, ИмяПараметрКоличество, Формат(СтрокаТоваров.Количество, "ЧГ=0"));
		
		СчетчикЦикла = СчетчикЦикла + 1;
		
	КонецЦикла;	
	
	РазрешенаПовторнаяПечать = ОбщегоНазначенияИСМПТКПереопределяемый.РазрешенаПовторнаяПечатьКодовМаркировки();
	НедоступныеСтатусы =  СтатусыВыведенИзОборота();
	НедоступныеСтатусы.Добавить(Перечисления.СтатусыКодовМаркировкиСУЗИСМПТК.СгенерированНеНанесен);
	
	Запрос.УстановитьПараметр("НедоступныеСтатусы",       НедоступныеСтатусы);
	Запрос.УстановитьПараметр("РазрешенаПовторнаяПечать", РазрешенаПовторнаяПечать);
	
	СтруктураВозвращаемыхДанных = Новый Структура();
	СтруктураВозвращаемыхДанных.Вставить("Отбор", СтруктураОтбораДокументы);
	
	ТаблицаЗаписей = Запрос.Выполнить().Выгрузить();
	ТаблицаЗаписей.Индексы.Добавить("Организация, Номенклатура, Характеристика, Шаблон, СпособВводаВОборот, ДляПечати, ЗаказНаЭмиссию, КодИдентификации");
	
	ТаблицаЗаписей.Колонки.Добавить("ШаблонЭтикетки", 		Новый ОписаниеТипов("СправочникСсылка.ХранилищеШаблоновИСМПТК"));
	ТаблицаЗаписей.Колонки.Добавить("Количество", 			ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаЧисло(10));
	ТаблицаЗаписей.Колонки.Добавить("СодержимоеКоличество", ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаЧисло(10));
	ТаблицаЗаписей.Колонки.Добавить("НомерВГруппе", 		ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаЧисло(10));
	ТаблицаЗаписей.Колонки.Добавить("КодEAN", 				ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаСтрока(0));
	ТаблицаЗаписей.Колонки.Добавить("ДатаПечати",			ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	
	Для Каждого СтрокаТаблицы Из ТаблицаНоменклатуры Цикл
		                                                             
		НайденныеСтрокиКМ = ТаблицаЗаписей.НайтиСтроки(Новый Структура("ЗаказНаЭмиссию, GTIN", СтрокаТаблицы.ЗаказКМ, СтрокаТаблицы.GTIN));
		Для Каждого КодМаркировки Из НайденныеСтрокиКМ Цикл
			КодМаркировки.ШаблонЭтикетки = СтрокаТаблицы.ШаблонЭтикетки;
			КодМаркировки.Количество 	 = 1;
			КодМаркировки.СодержимоеКоличество = 1;
			КодМаркировки.НомерВГруппе 	 = СтрокаТаблицы.НомерВГруппе;
			КодМаркировки.Штрихкод 		 = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.Base64ВШтрихкод(КодМаркировки.Штрихкод);
			КодМаркировки.КодEAN 		 = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(КодМаркировки.GTIN);
			КодМаркировки.ДатаПечати	 = ТекущаяДатаСеанса();
		КонецЦикла;
		
	КонецЦикла;
	
	СтруктураВозвращаемыхДанных.Вставить("ТаблицаПечати", ТаблицаЗаписей);
	
	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

Функция СтрЗаменитьПервое(Знач ИсходнаяСтрока, Знач ПодстрокаПоиска, Знач ПодстрокаЗамены) Экспорт
	
	ПозицияЗамены = Найти(ИсходнаяСтрока, ПодстрокаПоиска);
	Если ПозицияЗамены > 0 Тогда
		ВтораяЧасть    = Сред(ИсходнаяСтрока, ПозицияЗамены + СтрДлина(ПодстрокаПоиска));
		ИсходнаяСтрока = Лев(ИсходнаяСтрока,  ПозицияЗамены - 1) + ПодстрокаЗамены + ВтораяЧасть;
	КонецЕсли; 
	
	Возврат ИсходнаяСтрока;
	
КонецФункции

// Возвращает структуру данных для печати конкретного кода маркировки.
// 
// Параметры:
// 	ТаблицаНоменклатуры - ТаблицаЗначений - Исходные данные
// Возвращаемое значение:
// 	Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - ТаблицаЗначений - Данные для печати
Функция СтруктураДанныхДляПечатиВыборочно(ТаблицаНоменклатуры, РазрешитьПовторнуюПечать=Ложь) Экспорт

	СтруктураВозвращаемыхДанных = Новый Структура();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.КодИдентификации          КАК КодИдентификации,
		|	ИсходнаяТаблица.ХешСуммаКодаИдентификации КАК ХешСуммаКодаИдентификации,
		|	ИсходнаяТаблица.ТипШтрихкода              КАК ТипШтрихкода,
		|	ИсходнаяТаблица.Номенклатура              КАК Номенклатура,
		|	ИсходнаяТаблица.Характеристика            КАК Характеристика,
		|	ИсходнаяТаблица.ШаблонЭтикетки            КАК ШаблонЭтикетки,
		|	ИсходнаяТаблица.Количество                КАК КоличествоПечать,
		|	ИсходнаяТаблица.Количество                КАК Количество
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК ИсходнаяТаблица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодИдентификации,
		|	ХешСуммаКодаИдентификации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДанныеРегистра.КодИдентификации, ТаблицаНоменклатуры.КодИдентификации) КАК КодИдентификации,
		|	ЕСТЬNULL(ДанныеРегистра.ХешСуммаКодаИдентификации, ТаблицаНоменклатуры.ХешСуммаКодаИдентификации) КАК ХешСуммаКодаИдентификации,
		|	ЕСТЬNULL(ДанныеРегистра.КодИдентификации, ТаблицаНоменклатуры.КодИдентификации) КАК Штрихкод,
		|	ЕСТЬNULL(ДанныеРегистра.КодМаркировки, ТаблицаНоменклатуры.КодИдентификации)    КАК КодМаркировки,
		|	ДанныеРегистра.ЗаказНаЭмиссию,
		|	ДанныеРегистра.Статус,
		|	ДанныеРегистра.Организация,
		|	ЕСТЬNULL(ДанныеРегистра.Номенклатура,   ТаблицаНоменклатуры.Номенклатура)    КАК Номенклатура,
		|	ЕСТЬNULL(ДанныеРегистра.Характеристика, ТаблицаНоменклатуры.Характеристика)  КАК Характеристика,
		|	ДанныеРегистра.GTIN,
		|	ДанныеРегистра.ДатаПечати,
		|	ТаблицаНоменклатуры.ТипШтрихкода Как ТипШтрихкода,
		|	ВЫБОР
		|		КОГДА  ДанныеРегистра.ДатаПечати = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА 
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ДляЗаписи,
		|	ИСТИНА КАК ЭтоКодМаркировки,
		|	ТаблицаНоменклатуры.ШаблонЭтикетки,
		|	0 КАК НомерВГруппе,
		|	0 КАК СодержимоеКоличество,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.ДатаПечати = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА 
		|		КОГДА &РазрешенаПовторнаяПечать
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК МожноПечататьПовторно,
		|	ТаблицаНоменклатуры.КоличествоПечать Как Количество
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗИСМПТК КАК ДанныеРегистра
		|		ПО ДанныеРегистра.КодИдентификации = ТаблицаНоменклатуры.КодИдентификации
		|		И ДанныеРегистра.ХешСуммаКодаИдентификации = ТаблицаНоменклатуры.ХешСуммаКодаИдентификации
		|";
	РазрешенаПовторнаяПечать = ОбщегоНазначенияИСМПТКПереопределяемый.РазрешенаПовторнаяПечатьКодовМаркировки();
	Запрос.УстановитьПараметр("РазрешенаПовторнаяПечать", РазрешенаПовторнаяПечать);
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",      ТаблицаНоменклатуры);
	
	ТаблицаПечати = Запрос.Выполнить().Выгрузить();
	
	Отказ = Ложь;
	
	Для Каждого СтрокаТЧ Из ТаблицаПечати Цикл
		
		СтрокаТЧ.Штрихкод = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.КодМаркировки);
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Штрихкод) Тогда
			СтрокаТЧ.Штрихкод = СтрокаТЧ.КодМаркировки;
		КонецЕсли;
		
		Если Не РазрешитьПовторнуюПечать И Не СтрокаТЧ.МожноПечататьПовторно Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка печати этикетки для кода маркировки ""%КодИдентификации%"": данный код уже был распечатан ранее.'")
						   + Символы.ПС
						   + НСтр("ru = 'Повторная печать кодов маркировки недоступна!'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КодИдентификации%", СтрокаТЧ.КодИдентификации);
			ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ТаблицаПечати.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'В регистре ""%НаименованиеРегистра%"" не найдено подходящих данных для печати.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НаименованиеРегистра%", Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.Представление());
		ОбщегоНазначенияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка печати этикеток.'");
	КонецЕсли;
	
	СтруктураВозвращаемыхДанных.Вставить("ТаблицаПечати", ТаблицаПечати);

	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

// Устанавливает признак печати кода маркировки в регистре ПулКодовМаркировкиСУЗИСМПТК текущей универсальной датой
// 
// Параметры:
// 	СтруктураДанныхПоДокументу - Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - Список записей, для которых нужно установить признак печати
// * Отбор - Структура - Структура данных для отбора и установки блокировки по документу
Процедура УстановитьСостояниеРаспечатанПоСтруктуреДокумента(СтруктураДанныхПоДокументу, ПечатьИзОбработки = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПечатьИзОбработки Тогда
		МассивКМДляПечати = СтруктураДанныхПоДокументу.ВыгрузитьКолонку("ХешСуммаКодаИдентификации");
	Иначе
		МассивКМДляПечати = СтруктураДанныхПоДокументу.ТаблицаПечати.ВыгрузитьКолонку("ХешСуммаКодаИдентификации");
	КонецЕсли;
	
	Для Каждого Код Из МассивКМДляПечати Цикл
		
		НаборДанныхДляОбновления = РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.СоздатьНаборЗаписей();
		НаборДанныхДляОбновления.Отбор.ХешСуммаКодаИдентификации.Установить(Код);
		НаборДанныхДляОбновления.Прочитать();
		
		ДатаПечати = ТекущаяДатаСеанса();
		
		НаборДанныхДляОбновления[0].ДатаПечати = ДатаПечати;
		НаборДанныхДляОбновления[0].Статус 	   = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиСУЗИСМПТК.Использован");
		
		НаборДанныхДляОбновления.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает новую таблицу для печати этикеток
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Новая пустая таблица для заполнения данными печати
Функция НоваяТаблицаДанныхДляПечатиЭтикеток() Экспорт
	
	НаборВременный = РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.СоздатьНаборЗаписей();
	
	ОписаниеТипаСтрока = Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.Ресурсы.КодИдентификации.Тип;
	ОписаниеТипаЧисло  = ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаЧисло(10);
	ТаблицаИтог        = НаборВременный.Выгрузить(,"Номенклатура, Характеристика");
	
	ТаблицаИтог.Колонки.Добавить("Серия",                ОписаниеТипаСтрока);
	ТаблицаИтог.Колонки.Добавить("Количество",           ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("КоличествоПечать",     ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("Порядок",              ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("Штрихкод",             ОписаниеТипаСтрока);
	ТаблицаИтог.Колонки.Добавить("ТипШтрихкода",         Новый ОписаниеТипов("ПеречислениеСсылка.ТипыШтрихкодовИСМПТК"));
	ТаблицаИтог.Колонки.Добавить("КодИдентификации",     ОписаниеТипаСтрока);
	ТаблицаИтог.Колонки.Добавить("НомерВГруппе",         ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("СодержимоеКоличество", ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("ШаблонЭтикетки",       Новый ОписаниеТипов("СправочникСсылка.ХранилищеШаблоновИСМПТК"));
	ТаблицаИтог.Колонки.Добавить("Шаблон", 			     Новый ОписаниеТипов("ПеречислениеСсылка.ШаблоныКодовМаркировкиСУЗИСМПТК"));
	ТаблицаИтог.Колонки.Добавить("СпособВводаВОборот",   Новый ОписаниеТипов("ПеречислениеСсылка.СпособыВводаВОборотСУЗИСМПТК"));
	ТаблицаИтог.Колонки.Добавить("ЭтоКодМаркировки",     Новый ОписаниеТипов("Булево"));
	ТаблицаИтог.Колонки.Добавить("Организация",          Метаданные.ОпределяемыеТипы.ОрганизацияИСМПТК.Тип);
	ТаблицаИтог.Колонки.Добавить("ВидПродукции",         Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПродукцииИСМПТК"));
	ТаблицаИтог.Колонки.Добавить("ХешСуммаКодаИдентификации", Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗИСМПТК.Измерения.ХешСуммаКодаИдентификации.Тип);
	ТаблицаИтог.Колонки.Добавить("GTIN",        		 ОписаниеТипаСтрока);
	ТаблицаИтог.Колонки.Добавить("КодEAN",        		 ОписаниеТипаСтрока);
	ТаблицаИтог.Колонки.Добавить("ЗаказКМ",  			 Новый ОписаниеТипов("ДокументСсылка.ЗаказКодовМаркировкиСУЗИСМПТК"));
		
	МассивТиповПризнакаМаркировкиОстатков = Новый Массив();
	МассивТиповПризнакаМаркировкиОстатков.Добавить(Тип("Булево"));
	МассивТиповПризнакаМаркировкиОстатков.Добавить(Тип("ПеречислениеСсылка.ВидыПродукцииИСМПТК"));
	ТаблицаИтог.Колонки.Добавить("МаркировкаОстатков",   Новый ОписаниеТипов(МассивТиповПризнакаМаркировкиОстатков));
	
	Возврат ТаблицаИтог;
	
КонецФункции

#КонецОбласти

Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	РаботаСДокументамиИСМПТКПереопределяемый.ПриЗаполненииОграниченияДоступа(Ограничение, "ПулКодовМаркировкиСУЗИСМПТК");

КонецПроцедуры

#Область ПечатьКМИзЗагруженныхФалов

Функция СтруктураДанныхДляПечатиКодовИзФайла(ТаблицаНоменклатуры) Экспорт

	СтруктураВозвращаемыхДанных = Новый Структура();
	ТаблицаПечати = ТаблицаНоменклатуры.Скопировать();
	ТаблицаПечати.Колонки.Добавить("ДатаПечати", ОбщегоНазначенияИСМПТКПереопределяемый.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	
	Для Каждого СтрокаТЧ Из ТаблицаПечати Цикл
		СтрокаТЧ.Штрихкод = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.Штрихкод);
		СтрокаТЧ.ДатаПечати = ТекущаяДатаСеанса();
	КонецЦикла;	
	
	ТаблицаПечати.Индексы.Добавить("КодИдентификации");
	СтруктураВозвращаемыхДанных.Вставить("ТаблицаПечати", ТаблицаПечати);
	
	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли